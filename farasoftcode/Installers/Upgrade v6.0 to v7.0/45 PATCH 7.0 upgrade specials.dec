&MAX_AGE [install_aliasdb(FS3SKILL_CGDEMO)]=75
&MIN_AGE [install_aliasdb(FS3SKILL_CGDEMO)]=11

+jobs/create CODE=Age Limits/The chargen Demographics commands object now uses configurable min and max ages.  These have been set by default to 75 and 11.  Feel free to change them if you don't like them.

@mvattr [install_aliasdb(GLOBFUN_DB)]/fun_ictime=[install_aliasdb(GLOBFUN_DB)]/OLD_ICTIME

+jobs/create CODE=Upgrade ICTime/Previously the IC Time Function was part of the global functions module.  Now it has been moved to the IC Time Module.  Your old ictime function has been moved to an attribute named OLD_ICTIME on the global functions object as part of the upgrade. %R%RYou can use that when configuring the new IC Time module.  If you're not using the new module, you can simply move the old ictime function to a new object and leave it as-is.

@wipe [install_aliasdb(FS3SKILL_CGINFO)]/FUN_DISPLAY_BIRTHDATE
@tr [install_aliasdb(ICTIME_CMD)]/STARTUP

+jobs/create CODE=Birthdate Attrs/In previous versions, character age was stored relative to the RL date.  So if your game was set in 1848 and your character was 20 years old, your birthdate attribute would say 1991 (20 years before 2011).  This was goofy.%R%RBeginning in v7.0, ages are stored relative to IC time.  So your 20-year-old will have a birthdate of 1828, as you would probably expect.%R%RThe age\() function now expects for the birthdate attribute to be in an IC time format like Jan 01, 2145.%R%RIf your IC time is the same as OOC time but with different years, you can run the +ageupgrade command (on the special 7.0 upgrade object) to automatically convert everyone's ages.  Otherwise you'll probably have to do some custom coding.

+jobs/create CODE=Defensive Weapons/Any weapons that used the "Defensive" weapon type (like ECM jammers) should be renamed to "Suppressive" instead.

+jobs/create CODE=Range/The "range" weapon stat used to mean max effective range, but now it means the weapon's "short" range.  You may want to adjust your range stats accordingly.

+jobs/create CODE=Initiative/The ability to use for initiative is now configurable.  It defaults to Reactive, but you can change that if you desire.

think set(install_aliasdb(FS3COMBAT_PREFS),Initiative_Ability:Reactive)

+jobs/create CODE=Ability Room Desc/Since chargen is not patched, you'll have to manually update the room desc of the chargen ability room to reflect the new commands.  Here is what it should say: %R%RHere you will set up your character's Abilities, including Attributes, Action Skills, Background Skills, Language Skills and Quirks.   See [ansi(hg,+help FS3_Chargen)] for help with Chargen.%R%R[space(5)][ansi(h,+reset)] - Starts setting abilities, erasing any current ones.  [ansi(hr,*Do this first!*)]%R[space(5)][ansi(h,+sheet)] - Views your character sheet%R[space(5)][ansi(h,+points)] - Views your points remaining%R[space(5)][ansi(h,+limits)] - Views limits on what you can spend.%R%R[space(5)][ansi(h,+abilities)] - Views all abilities%R[space(5)][ansi(h,+raise <ability>)] and [ansi(h,+lower <ability>)]%R[space(5)][ansi(h,+ability <ability>=<level>)]%R%R[space(5)][ansi(h,+lang/add <language>)] and [ansi(h,+lang/delete <language>)]%R[space(5)][ansi(h,+quirk/add <quirk>)] and [ansi(h,+quirk/delete <quirk>)]%R%R[space(5)]You may also want to configure ruling attributes for your%R[space(5)]background skills.  See +help +rulingattr

&FIELDS [install_aliasdb(JOBS_DB)]=Number:#:5 Category:Cat:5 Title:Title:20 Submitter:Submitter:10 Handler:Handler:10 DueDate:Due:10 Status:Status:10

@create 7.0 Upgrade Utils
@set 7.0=WIZARD
@set 7.0=!NO_COMMAND

&CMD-+AGEUPGRADE 7.0=$+ageupgrade:think pemit(%#,switch(0,isstaff(%#),ansi(hr,This command is only for staff.),[ansi(hg,You upgrade everyone's ages.  Their old birthdate is stored on the old_birthdate attribute if you need it back.)][iter(real_players(),[setq(0,xget(##,birthdate))][set(##,old_birthdate:%q0)][set(##,birthdate:[switch(%q0,,,[setq(m,Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec)][extract(%qm,extract(%q0,1,1,/),1)] [extract(%q0,2,1,/)]\, [sub(last(ictime()),sub(last(time()),extract(%q0,3,1,/)))])])])]))

&CMD-+ATTRUPGRADE 7.0=$+attrupgrade:think pemit(%#,switch(0,isstaff(%#),ansi(hr,This command is only for staff.),ansi(hg,You convert everyone's attrs.  Their old attrs are store on the old_attributes attr if you need them back.)[iter(real_players(),u(fun_convert_attrs,##))]))

&FUN_CONVERT_ATTRS 7.0=localize([setq(0,0)][set(%0,old_attributes:[xget(%0,attributes)])][set(%0,attributes:[iter(xget(%0,attributes),[setq(1,after(##,:))][before(##,:)]:[switch(%q1,<3,1[setq(0,add(sub(%q1,1),%q0))],<6,2[setq(0,add(sub(%q1,4),%q0))],<9,3[setq(0,add(sub(%q1,7),%q0))],4[setq(0,add(sub(%q1,10),%q0))])])])][set(%0,refund_attributes:%q0)])

&CMD-+LANGUPGRADE 7.0=$+langupgrade:think pemit(%#,switch(0,isstaff(%#),ansi(hr,This command is only for staff.),ansi(hg,You convert everyone's languages.  Their old languages are store on the old_langskills attr if you need them back.)[iter(real_players(),u(fun_convert_langs,##))]))

&FUN_CONVERT_LANGS 7.0=localize([setq(0,0)][setq(1,xget(%0,langskills))][set(%0,old_langskills:%q1)][set(%0,langskills:[iter(%q1,[before(##,:)][setq(0,add(%q0,after(##,:)))],,|)])][set(%0,refund_langskills:[sub(sub(%q0,5),mul(2,words(%q1)))])])

&CMD-+RENAMEATTRS 7.0=$+renameattrs:think pemit(%#,switch(0,isstaff(%#),ansi(hr,This command is only for staff.),ansi(hg,You rename the attributes.)[iter(real_players(),u(fun_rename_attrs,##))][u(fun_update_combatskills)]))

&FUN_RENAME_ATTRS 7.0=[setq(0,add(ability_level(%0,academic),ability_level(%0,creative),ability_level(%0,technical)))][set(%0,attributes:Mind:[div(%q0,3)] Body:[ability_level(%0,Athletic)] Reaction:[ability_level(%0,Reactive)] Presence:[ability_level(%0,Social)])][u(fun_update_combatskills)][u(fun_update_abilitylist)]

&FUN_UPDATE_COMBATSKILLS 7.0=[setq(0,install_aliasdb(FS3COMBAT_PREFS))][set(%q0,INITIATIVE_ABILITY:Reaction)][set(%q0,TOUGHNESS_SKILL:Body)][setq(1,install_aliasdb(FS3COMBAT_DB_EQUIP))][iter(lattr(%q1/weapon_*),set(%q1,##:[edit(xget(%q1,##),Reactive,Reaction,Athletic,Body)]))]

&FUN_UPDATE_ABILITYLIST 7.0=[setq(0,install_aliasdb(FS3SKILL_DB))][iter(lattr(%q0/ATTRIBUTE_*),wipe(%q0/##))][set(%q0,ATTRIBUTE_MIND:Mental fitness - Intelligence and Creativity)][set(%q0,ATTRIBUTE_BODY:Physical fitness - Strength\, Constitution and Agility)][set(%q0,ATTRIBUTE_REACTION:Reflexes - Dexterity\, Hand-Eye Coordination and Wits)][set(%q0,ATTRIBUTE_Presence:Presence - Charisma and Will)]


+jobs/create CODE=FS3 Abilities/Two major changes to the FS3 system were:  Attributes are now rated from 1-4 instead of 1-12, and languages don't have ratings.  To convert all your old characters over to the new system, simply execute the +langupgrade and +attrupgrade commands (on the special 7.0 upgrade object).%R%RAfter running these commands, their old stats will be in old_attributes and old_langskills.  Also, refund_attributes and refund_langskills will give you an idea how many points they "lost" due to the conversion. You could consider refunding part or all of these in the form of bonus skill points or XP.

+jobs/create CODE=FS3 Chargen Points/Because there are fewer things to spend points on, you should reduce your chargen point allotment accordingly.  The exact numbers will vary based on your skill configuration, but for reference:  the "out-of-the-box" FS3 config was reduced from 80 points to 60, and this was vetted against existing characters on three separate games.

+jobs/create CODE=Attribute Names/After much feedback, I concluded that the attribute names in FS3 were confusing.  People didn't understand where "Academic" left off and actual academic skills took over.  So I have changed the default attributes in the "out-of-the-box" FS3 configuration to: Mind, Body, Presence, Reaction. %R%RYou don't *have* to do this.  In fact, if you have an existing game you're probably better off NOT doing it because players will nitpick how their points ended up.  But if you do choose to switch over to the new names, the conversion should be Mind=Average of Academic+Creativity+Technical, Presence=Social,Reaction=Reactive,Body=Athletic.  Use the +renameattrs command (on the special 7.0 upgrade object) to do this.

&XP_PER_LANG [install_aliasdb(FS3XP_DB)]=3

