@@ Faraday's Version of Myrddin's BBS
@@ Author:  Myrddin (original), Linda O'Meara (modifications)
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hg,Beginning +help Installation.   Please wait until the Installation Complete message appears...)]%R

think install_create(BBS_CMD,BBS - Myrddin's Global BBS,1,0,5.0,Global BBS System.)
think install_create(BBS_POCKET,bbpocket,0,0,5.0,BBS Storage.,BBS_CMD)

@pemit/silent %#=[ansi(hg,Setting Object Attributes...)]%R


@lock bbpocket=me
&BBTIME bbpocket=[extract(time(), 2, 2)] [last(time())]
&GET_GROUP bbpocket=switch(isnum(%0),1,extract(v(groups),%0,1),locate(#BBS_POCKET,%0,ni))
&VALID_GROUPS bbpocket=iter(v(groups),switch(and(u(##/can%1,%0),not(member(get(%0/bb_omit),##))),1,##))
&ISGROUPREAD bbpocket=switch(setdiff(%1,get(%0/bb_read)),,,U)
&UNREADNUMS bbpocket=sort(iter(setdiff(get(%1/mess_lst),get(%0/bb_read)),member(get(%1/mess_lst),##)))
&FN_MAKELIST bbpocket=map(me/fn_maplist,secure(%0))
&FN_MAPLIST bbpocket=switch(strmatch(%0,?*-*),1,lnum(before(%0,-),after(%0,-)),%0)
&FN_MAPLIST_2 bbpocket=switch(strmatch(%0,*-*),1,u(fn_lnum,before(%0,-),after(%0,-)),%0)
&FN_UNREAD_LIST bbpocket=edit(sort(iter(%0,member(%1,##))),%b,\,%b)
&FN_LNUM bbpocket=iter(lnum(add(sub(%1,%0),1)),add(##,%0))


@switch lnum(1,2)=1 2,{&FN_MAPLIST_2 bbpocket; &FN_LNUM bbpocket},{@mvattr bbpocket=FN_MAPLIST_2,FN_MAPLIST}

&ADDEM bbpocket=add(%0,%1)
&B36_DIGITS bbpocket=0 1 2 3 4 5 6 7 8 9 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z
&B36_POWS bbpocket=1 36 1296 46656 1679616 60466176
&FN_B36_MAGS bbpocket=revwords(iter(v(b36_pows),switch([div(##,%0)]:[eq(##,%0)],0:*,##,*:1,##)))
&FN_B36-TO-B10 bbpocket=[setq(0,ucstr(%0))][setq(1,strlen(%q0))][fold(me/addem,[iter(lnum(%q1),mul(sub(member(v(b36_digits),mid(%q0,##,1)),1),power(36,sub(sub(%q1,##),1))))],0)]
&FN_B10-TO-B36 bbpocket=[setq(0,%0)][edit(iter(u(fn_b36_mags,%q0),[extract(v(b36_digits),add(div(%q0,##),1),1)][setq(0,sub(%q0,mul(##,div(%q0,##))))]),%b,)]
&FN_INC_NEXT_MESS bbpocket=u(fn_b10-to-b36,add(u(fn_b36-to-b10,%0),1))
&VALID_CONFIGS bbpocket=anonymous timeout
&VALID_GLOBAL_CONFIGS bbpocket=timeout autotimeout
&TR_TIMEOUT bbpocket=@dolist v(groups)={@trigger me/tr_timeout_group=##}
&TR_TIMEOUT_GROUP bbpocket=@dolist get(%0/mess_lst)={@switch and(extract(get(%0/hdr_##),5,1,|),gt(secs(),extract(get(%0/hdr_##),5,1,|)))=1,{&hdr_## %0; &bdy_## %0; &mess_lst %0=[remove(get(%0/mess_lst),##)]; &master_lst #BBS_POCKET=[remove(get(#BBS_POCKET/master_lst),##)]}}
&DO_TIMEOUTS bbpocket=@trigger me/tr_timeout; @wait 86400={@trigger me/do_timeouts}
&TR_CONFIG_ANONYMOUS bbpocket=&anonymous %0=[ifelse([%1],[%1],)]; @pemit %2=Posters on the [name(%0)] group will show up as [ifelse([%1],['%1'],themselves)].
&TR_CONFIG_TIMEOUT bbpocket=&config_timeout %0=[mul(%1,86400)]; @pemit %2=New messages for group '[name(%0)]' have [ifelse(%1,a %1 day,no)] timeout.
&TR_GCONFIG_TIMEOUT bbpocket=&config_timeout me=[mul(%0,86400)]; @pemit %1=BB global config parameter 'timeout': [ifelse(%0,%0 days,none)].%rAny new groups will have this value set as their default timeout.
&TR_GCONFIG_AUTOTIMEOUT bbpocket=@switch [eq(hasattr(me,startup),setr(0,switch(%0,on,1,0)))]:%0=1:*,{@pemit %1=BB autotimeout is already [ifelse(%q0,on,off)].},0:on,{@startup me=@trigger me/do_timeouts; @trigger me/do_timeouts; @pemit %1=BB autotimeout turned on.},{@startup me; @pemit %1=BB autotimeout turned off.; @halt me}
&FN_MSG_FLAGS bbpocket=switch([member(get(%0/bb_read),%2)]:[u(fn_timeout_close,index(get(%1/hdr_%2),|,5,1))],0:*,U,*:1,T)
&TR_POST_NOTIFY bbpocket=@switch hasflag(%0,DARK)=0,{@pemit/list iter(remove(lwho(),%0),switch(or(member([get(##/bb_omit)] [get(##/bb_silent)],%1),not(u(%1/canread,##))),0,##))=(New BB message ([member(v(groups),%1)]/%2) posted to '[name(%1)]' by [ifelse(hasattr(%1,anonymous),get(%1/anonymous),name(%0))]: %3)}
&FN_TIMEOUT_CLOSE bbpocket=switch(%0:[lte(sub(%0,secs()),259200)],0:*,0,*:1,1,0)
&FN_HASCONFIG bbpocket=or(hasattr(%0,anonymous),neq(get(%0/config_timeout),get(#BBS_POCKET/config_timeout)))
@set bbpocket=!HALT
@set bbpocket=INHERIT
@set bbpocket=SAFE

&TR-UNINSTALL BBS - Myrddin's Global BBS={@dolist remove(lcon(me),#BBS_POCKET)=@set ##=!safe;@nuke ##}

@switch strlen(repeat(.,7998))=7998,&BUFFER_SIZE bbpocket=8000,&BUFFER_SIZE bbpocket=4000

@lock BBS - Myrddin's Global BBS=me
@Fail BBS - Myrddin's Global BBS=It's bolted down... you aint budgin' it.
&CMD_+BBNEWGROUP BBS - Myrddin's Global BBS=$+bbnewgroup *:@switch 0=hasflag(%#,wizard), {@pemit %#=You can't add groups to the message base.}, [not(num(%0))], {@pemit %#=There is already a group with that name.}, {@create %0; @wait 1={@switch [setr(0,num(%0))]=#-1,{@pemit %#=That's not a good name for a group.},{&groups #BBS_POCKET=[switch(words(get(#BBS_POCKET/groups)),0,,[get(#BBS_POCKET/groups)]%b)]%q0; &own %q0=%#; @set %q0=safe; @set %q0=inherit; &last_mod %q0=[u(#BBS_POCKET/bbtime)]; @DESCRIBE %q0=BBS; &CANREAD %q0=1; &CANWRITE %q0=1; &config_timeout %q0=[get(#BBS_POCKET/config_timeout)]; @pemit %#=Group number [member(get(#BBS_POCKET/groups),%q0)] added as '%0'. Messages will have [ifelse(get(%q0/config_timeout),a [div(get(%q0/config_timeout),86400)] day,no)] timeout.}}}
&CMD_+BBLOCK BBS - Myrddin's Global BBS=$+bblock *=*/*:@switch hasflag(%#,wizard)=1,{@switch member(get(#BBS_POCKET/groups),setr(0,u(#BBS_POCKET/get_group,%0)))=0,{@pemit %#=No such group as '%0'.},{@switch %1=flag,{&CANREAD %q0=\[or(hasflag(\%0,%2),hasflag(\%0,wizard))]},{&CANREAD %q0=\[or(match(get(\%0/%1),%2),hasflag(\%0,wizard))]}; @wait 1={&CANWRITE %q0=[get(%q0/CANREAD)]}; @pemit %#=Group '%0' locked. Only people with %1=%2 can access.}},{@pemit %#=You cannot lock message groups.}
&CMD_+BBCLEARGROUP BBS - Myrddin's Global BBS=$+bbcleargroup *:@switch and(hasflag(%#, wizard),member(get(#BBS_POCKET/groups),setr(0,u(#BBS_POCKET/get_group,%0))))=1, {&delete_grp #BBS_POCKET=%q0; @pemit %#=%r[space(15)]******** Warning ********%r%rDeleting Group #%0 will also delete the [words(get(%q0/mess_lst))] messages in that group.%rIf you still wish to proceed, type '+bbconfirm %0'%r%r[space(15)][repeat(*,25)]%r}, {&delete_grp #BBS_POCKET; @pemit %#=You can't remove that group (either that group does not exist, or you are not the owner).}
&DO_CLEARGROUP BBS - Myrddin's Global BBS=$+bbconfirm *:@switch get(#BBS_POCKET/DELETE_GRP)=[setr(0,u(#BBS_POCKET/get_group,%0))], {&groups #BBS_POCKET=[remove(get(#BBS_POCKET/groups),%q0)]; &master_lst #BBS_POCKET=[setdiff(get(#BBS_POCKET/master_lst),get(%q0/mess_lst))]; @set %q0=!safe; @nuke %q0; &delete_grp #BBS_POCKET; @pemit %#=Group number %0 removed.}, {&delete_grp #BBS_POCKET; @pemit %#=That group has not been marked for deletion. Use '+bbcleargroup <#>' to mark a group for deletion.}
&CMD_+BBPOST BBS - Myrddin's Global BBS=$+bbpost */*=*:@switch member(u(#BBS_POCKET/valid_groups,%#,write),setr(0,u(#BBS_POCKET/get_group,%0)))=0, {@pemit %#=Either you do not subscribe to Group #%0, or are unable to post to it.},{&mess_lst %q0=[cat(get(%q0/mess_lst),setr(1,get(#BBS_POCKET/nxt_mess)))]; &nxt_mess #BBS_POCKET=[ulocal(#BBS_POCKET/fn_inc_next_mess,%q1)]; &master_lst #BBS_POCKET=[cat(get(#BBS_POCKET/master_lst),%q1)]; &hdr_%q1 %q0=[mid(%1, 0, 34)]|[u(#BBS_POCKET/bbtime)]|[edefault(%q0/anonymous,mid(name(%#),0,24))]|[owner(%#)]|[ifelse(get(%q0/config_timeout),add(get(%q0/config_timeout),secs()),0)]; &last_mod %q0=[u(#BBS_POCKET/bbtime)]; &bdy_%q1 %q0=[%2][ifelse(and(hasattr(%#,bb_sig),not(hasattr(%q0,anonymous))),{%r[ulocal(%#/bb_sig)]},)]; @pemit %#=You post your note about '%1' in group [member(get(#BBS_POCKET/groups),%q0)] ([name(%q0)]) as message #[member(get(%q0/mess_lst),%q1)][ifelse(and(hasattr(%#,bb_sig),hasattr(%q0,anonymous)),{%b%bThis is an anonymous group, your BB_SIG was -not- appended.},)]; think [u(FUN_UPDATE_BBREAD,%#,%q1)]; @trigger #BBS_POCKET/tr_post_notify=%#,%q0,[member(get(%q0/mess_lst),%q1)],[mid(%1, 0, 34)]}

&CMD_+BBREMOVE BBS - Myrddin's Global BBS=$+bbremove */*:@dolist [setq(0,u(#BBS_POCKET/get_group,%0))][revwords(sort(u(#BBS_POCKET/fn_makelist,%1)))]={@switch or(strmatch(index(get(%q0/HDR_[setr(1,extract(get(%q0/MESS_LST),##,1))]),|,4, 1),%#), hasflag(%#,wizard))=1, {&HDR_%q1 %q0; &BDY_%q1 %q0; &MESS_LST %q0=[remove(get(%q0/MESS_LST),%q1)]; &MASTER_LST #BBS_POCKET=[remove(get(#BBS_POCKET/MASTER_LST),%q1)]; @pemit %#=Message ## removed from group #[member(get(#BBS_POCKET/groups),%q0)] ([name(%q0)]).}, {@pemit %#=Either message ## does not exist, or you were not the original poster.}}


&CMD_+BBREAD2 BBS - Myrddin's Global BBS=$+bbread *:@switch strmatch([%0],*/*)=0,{@switch member(u(#BBS_POCKET/valid_groups,%#,read),setr(0,u(#BBS_POCKET/get_group,%0)))=0,{@pemit %#=[switch(%q0,#-2,That Group name is not specific enough.,You do not subscribe to that Group.)]},{@pemit %#=[line_with_text(name(%q0))]%r[space(8)]Message[space(28)]Posted[space(8)]By%r[line2()]; @dolist get(%q0/MESS_LST)={@pemit %#=[ljust([member(get(#BBS_POCKET/groups),%q0)]/[member(get(%q0/MESS_LST),##)],6)][ljust(u(#BBS_POCKET/fn_msg_flags,%#,%q0,##),2)][ljust(index(setr(1,get(%q0/HDR_##)),|,1,1),35)][ljust(index(%q1,|,2,1),14)][mid([index(%q1,|,3,1)][ifelse(and(hasattr(%q0,anonymous),hasflag(%#,wizard)),%b\([name(index(%q1,|,4,1))]\),)],0,21)][switch(member(get(%q0/MESS_LST),##),words(get(%q0/MESS_LST)),%r[line()])]}}},{@switch member(u(#BBS_POCKET/valid_groups,%#,read),setr(0,u(#BBS_POCKET/get_group,setr(2,first(%0,/)))))=0,{@pemit %#=[switch(%q0,#-2,That Group name is not specific enough.,You do not subscribe to that Group.)]},{@dolist [setq(4,member(get(#BBS_POCKET/groups),%q0))][switch(rest(%0,/),u,u(#BBS_POCKET/unreadnums,%#,%q0),u(#BBS_POCKET/fn_makelist,rest(%0,/)))]={@switch [and(lte(##,words(get(%q0/MESS_LST))),isnum(##),gt(##,0))]=0,{@pemit %#=Message %q4/## ([name(%q0)]/##) does not exist.},{@pemit %#=%r[line_with_text(name(%q0))]%r[ljust(Message: %q4/##[switch(u(#BBS_POCKET/fn_timeout_close,index(setr(3,get(%q0/HDR_[extract(get(%q0/MESS_LST),##,1)])),|,5,1)),1,{%b(timeout warning)})],35)]Posted[space(8)]Author%r[ljust(index(%q3,|,1,1),35)][ljust(index(%q3,|,2,1),14)][mid([index(%q3,|,3,1)][ifelse(and(hasattr(%q0,anonymous),hasflag(%#,wizard)),%b\([name(index(%q3,|,4,1))]\),)],0,29)]%r[line2()]%r[get(%q0/BDY_[extract(get(%q0/MESS_LST),##,1)])]%r[line()]; think [u(FUN_UPDATE_BBREAD,%#,[extract(get(%q0/MESS_LST),##,1)])]}}; think [u(FUN_UPDATE_BBREAD,%#,[extract(get(%q0/MESS_LST),##,1)])]}}

&CMD_+BBEDIT BBS - Myrddin's Global BBS=$+bbedit */*=*/*:@switch strmatch(index(get([setq(0,u(#BBS_POCKET/get_group,%0))]%q0/HDR_[setq(1,extract(get(%q0/MESS_LST),%1,1))]%q1),|,4, 1),%#)=1,{&BDY_%q1 %q0=[edit(get(%q0/BDY_%q1),{%2},{%3})]; @pemit %#=[line()]%rMessage [member(get(#BBS_POCKET/groups),%q0)]/%1 ([name(%q0)]/%1) now reads:%r[line2()]%r[get(%q0/BDY_%q1)]%r[line()]}, {@pemit %#=Either that message does not exist, or you were not the original poster.}
&CMD_+BBLEAVE BBS - Myrddin's Global BBS=$+bbleave *:@switch member(u(#BBS_POCKET/valid_groups,%#,read),[setq(0,u(#BBS_POCKET/get_group,%0))]%q0)=0,{@pemit %#=You aren't currently subscribing to Board #%0. No ommission necessary.},{&bb_omit %#=[setunion(get(%#/bb_omit),%q0)]; @pemit %#=You have removed yourself from the [name(%q0)] board.}
&CMD_+BBJOIN BBS - Myrddin's Global BBS=$+bbjoin *:@switch member(NULL [get(%#/bb_omit)],[setq(0,u(#BBS_POCKET/get_group,%0))]%q0)=0,{@pemit %#=[switch(member(u(#BBS_POCKET/valid_groups,%#,read),%q0),0,{Sorry, you don't have access to that board.},{You are already a member of the [name(%q0)] board.})]},{&bb_omit %#=[setdiff(get(%#/bb_omit),%q0)]; @pemit %#=You have joined the [name(%q0)] board.}

&CMD_+BBSCAN BBS - Myrddin's Global BBS=$+bbscan: @pemit %#=[switch(hasattr(#BBS_POCKET,BB_POST_HDR_%#),1,** BB Warning: You are in the middle of writing a bbpost. **%r)][switch(setdiff(iter(setr(0,u(#BBS_POCKET/VALID_GROUPS,%#,read)),get(##/MESS_LST)),setr(1,get(%#/BB_READ))),,There are no unread postings on the Global Bulletin Board.,[center(- Unread Postings on the Global Bulletin Board -,78,-)][setq(9,get(#BBS_POCKET/GROUPS))][trim(iter(%q0,switch(setr(2,setdiff(setr(3,get(##/MESS_LST)),%q1)),,,%r[name(##)] \(#[member(%q9,##)]\): [words(%q2)] unread \([u(#BBS_POCKET/FN_UNREAD_LIST,%q2,%q3)]\))),b)]%r[center(- BBS at [round(mul(fdiv(strlen(get(#BBS_POCKET/master_lst)),get(#BBS_POCKET/buffer_size)),100),1)]\% capacity -,78,-)])]

&CMD_+BBLIST BBS - Myrddin's Global BBS=$+bblist:@pemit %#=[line()]%r[ljust(Available Bulletin Board Groups,37)]Member?[space(8)]Timeout (in days)%r[line2()]%r%b[iter(get(#BBS_POCKET/groups),switch(u(##/canread,%#),1,{%r%b[trim([ljust(member(get(#BBS_POCKET/groups),##),5)][ljust(name(##),32)][ljust(switch(member(get(%#/bb_omit),##),0,Yes,No),19)][rjust(ifelse(get(##/config_timeout),div(get(##/config_timeout),86400),none),4)])]}))]%r[line2()]%rTo join groups, type '+bbjoin <group number or name>'%r[line()]
&CREDITS BBS - Myrddin's Global BBS=Myrddin's BBS was written by Myrddin@Witchcraft/Dreaming/Elysium (email: merlin@firstmagic.com) and then modified by Faraday (http://www.wordsmyth.org/faraday). The most recent version of the official branch of the BBS code can be found at http://www.firstmagic.com/~merlin/mushcode, or via anonymous ftp from ftp.firstmagic.com in the /pub/merlin directory.  Faraday's branched version can be found at http://code.google.com/p/faramushcode/   Please refer interested parties to myself or those addresses, thank you. :)  
@set BBS - Myrddin's Global BBS/CREDITS = no_command
&CMD_+BBCATCHUP BBS - Myrddin's Global BBS=$+bbcatchup *:@switch lcstr(%0)=all,{think u(FUN_UPDATE_BBREAD,%#,[get(#BBS_POCKET/master_lst)]); @pemit %#=All postings on all boards marked as read.},{@dolist [setq(1,u(#BBS_POCKET/valid_groups,%#,read))][u(#BBS_POCKET/fn_makelist,%0)]={@switch member(%q1,[setq(0,u(#BBS_POCKET/get_group,##))]%q0)=0,{@pemit %#=You do not subscribe to Group ##.},{ think [u(FUN_UPDATE_BBREAD,%#,get(%q0/MESS_LST))]; @pemit %#=All postings on Group #[member(get(#BBS_POCKET/groups),%q0)] ([name(%q0)]) marked as read.}}}
&CMD_+BBMOVE BBS - Myrddin's Global BBS=$+bbmove */* to *:@switch and(member(u(#BBS_POCKET/valid_groups,%#,read),[setq(0,u(#BBS_POCKET/get_group,%0))]%q0),member(u(#BBS_POCKET/valid_groups,%#,write),[setq(2,u(#BBS_POCKET/get_group,%2))]%q2),not(strmatch(%q0,%q2)))=0,{@pemit %#=One of the selected groups is not valid (either it doesn't exist, you can't write to it, or you tried to 'move' to the same group).},{@switch or(strmatch(index(get(%q0/HDR_[setq(1,extract(get(%q0/MESS_LST),%1,1))]%q1),|,4, 1),%#), and(hasflag(%#,wizard),member(get(%q0/MESS_LST),%q1)))=1,{&HDR_%q1 %q2=[get(%q0/HDR_%q1)]; &BDY_%q1 %q2=[get(%q0/BDY_%q1)]; &MESS_LST %q2=[cat(get(%q2/mess_lst),%q1)]; &MESS_LST %q0=[remove(get(%q0/MESS_LST),%q1)]; &HDR_%q1 %q0; &BDY_%q1 %q0; @pemit %#=Message '%1' removed from group '%0' and added to group '%2' as message #[member(get(%q2/mess_lst),%q1)]},{@pemit %#=Not a valid message number for that group.}}
&CMD_+BBWRITELOCK BBS - Myrddin's Global BBS=$+bbwritelock *=*/*:@switch hasflag(%#,wizard)=1,{@switch member(get(#BBS_POCKET/groups),[setq(0,u(#BBS_POCKET/get_group,%0))]%q0)=0,{@pemit %#=No such group as '%0'.},{@switch %1=flag,{&CANWRITE %q0=\[or(hasflag(\%0,%2),hasflag(\%0,wizard))]},{&CANWRITE %q0=\[or(match(get(\%0/%1),%2),hasflag(\%0,wizard))]}; @pemit %#=Group '%0' locked. Only people with %1=%2 can write.}},{@pemit %#=You cannot lock message groups.}
&CMD_+BBPOST2 BBS - Myrddin's Global BBS=$+bbpost */*:@switch hasattr(#BBS_POCKET,bb_post_hdr_%#)=1,{@pemit %#=You are already in the middle of writing a bbpost.},{@switch strmatch(%1,*=*)=0,{@switch member(u(#BBS_POCKET/valid_groups,%#,write),[setq(0,u(#BBS_POCKET/get_group,%0))]%q0)=0,{@pemit %#=Either you do not subscribe to Group #%0 or are unable to post to it.},{&bb_post_hdr_%# #BBS_POCKET=%q0|[mid(%1,0,34)]; @pemit %#=%rYou start your posting to Group #[member(get(#BBS_POCKET/groups),%q0)] ([name(%q0)]).%rYou can now compose the body of the post by using '+bbwrite <text>'%ror '+bb <text>'.  When you are finished, type '+bbpost' by itself.[ifelse(and(hasattr(%#,bb_sig),hasattr(%q0,anonymous)),{%b%bThis is an anonymous group. Your BB_SIG will -not- be appended.},)]}}}
&CMD_+BBWRITE BBS - Myrddin's Global BBS=$+bbwrite *:@switch hasattr(#BBS_POCKET,bb_post_hdr_%#)=0,{@pemit %#=You do not have a bbpost in progress.},{&bb_post_bdy_%# #BBS_POCKET=[get(#BBS_POCKET/bb_post_bdy_%#)]%0; @pemit %#=Text added to bbpost.}
&CMD_+BBPROOF BBS - Myrddin's Global BBS=$+bbproof:@switch hasattr(#BBS_POCKET,bb_post_hdr_%#)=0,{@pemit %#=You do not have a bbpost in progress.},{@pemit %#=[center(= BB Post in Progress =,78,=)]%rGroup: %b[name(index(get(#BBS_POCKET/bb_post_hdr_%#),|,1,1))]%rTitle: %b[index(get(#BBS_POCKET/bb_post_hdr_%#),|,2,1)]%r[line2()]%r[trim(get(#BBS_POCKET/bb_post_bdy_%#))]%r[line()]}
&CMD_+BBPOST-POST BBS - Myrddin's Global BBS=$+bbpost:@switch [hasattr(#BBS_POCKET,bb_post_hdr_%#)]:[hasattr(#BBS_POCKET,bb_post_bdy_%#)]=0:0,{@pemit %#=You do not have a bbpost in progress.},1:0,{@pemit %#=Your post is empty. Please add text with the '+bbwrite <text>' command or discard the posting with the '+bbtoss' command.},{&mess_lst [setr(0,index(get(#BBS_POCKET/bb_post_hdr_%#),|,1,1))]=[cat(get(%q0/mess_lst),setr(1,get(#BBS_POCKET/nxt_mess)))]; &nxt_mess #BBS_POCKET=[ulocal(#BBS_POCKET/fn_inc_next_mess,%q1)]; &master_lst #BBS_POCKET=[cat(get(#BBS_POCKET/master_lst),%q1)]; &hdr_%q1 %q0=[index(get(#BBS_POCKET/bb_post_hdr_%#),|,2,1)]|[u(#BBS_POCKET/bbtime)]|[edefault(%q0/anonymous,mid(name(%#),0,24))]|[owner(%#)]|[ifelse(get(%q0/config_timeout),add(get(%q0/config_timeout),secs()),0)]; &last_mod %q0=[u(#BBS_POCKET/bbtime)]; &bdy_%q1 %q0={[trim(get(#BBS_POCKET/bb_post_bdy_%#))][ifelse(and(hasattr(%#,bb_sig),not(hasattr(%q0,anonymous))),{%r[ulocal(%#/bb_sig)]},)]}; @pemit %#=You post your note about '[index(get(#BBS_POCKET/bb_post_hdr_%#),|,2,1)]' in group '[name(%q0)]' as message #[member(get(%q0/mess_lst),%q1)]; think [u(FUN_UPDATE_BBREAD,%#,%q1)]; @trigger #BBS_POCKET/tr_post_notify=%#,%q0,[member(get(%q0/mess_lst),%q1)],[index(get(#BBS_POCKET/bb_post_hdr_%#),|,2,1)]; &bb_post_bdy_%# #BBS_POCKET; &bb_post_hdr_%# #BBS_POCKET}
&CMD_+BBREAD BBS - Myrddin's Global BBS=$+bbread:@pemit %#=[line()]%r[space(7)]Group Name[space(20)]Last Post[space(6)]# of messages%r[line2(-,78)][iter(u(#BBS_POCKET/valid_groups,%#,read),%r[rjust(member(get(#BBS_POCKET/groups),##),2)][center([switch([get(##/CANREAD)]:[get(##/CANWRITE)]:[u(##/CANWRITE,%#)],1:1:1,,1:*:1,{(-)},1:*:0,-,*)],5)][ljust(name(##),30)][ljust(get(##/LAST_MOD),20)][rjust(words([setq(0,get(##/MESS_LST))]%q0),3)]%b[ljust(u(#BBS_POCKET/isgroupread,%#,%q0),2)])]%r[line2()]%r[center('*' = restricted[space(5)]'-' = read only[space(5)]'\(-\)' = read only\, but you can write,78)]%r[line_with_text( [switch(hasflag(%#,wizard),1,{= BBS at [round(mul(fdiv(strlen(get(#BBS_POCKET/master_lst)),get(#BBS_POCKET/buffer_size)),100),1)]\% capacity =},=)])]
&CMD_+BBTOSS BBS - Myrddin's Global BBS=$+bbtoss:@switch hasattr(#BBS_POCKET,bb_post_hdr_%#)=0,{@pemit %#=You do not have bbpost in progress.},{&bb_post_hdr_%# #BBS_POCKET; &bb_post_bdy_%# #BBS_POCKET; @pemit %#=Your bbpost has been discarded.}
&CMD_+BB BBS - Myrddin's Global BBS=$+bb *:@switch hasattr(#BBS_POCKET,bb_post_hdr_%#)=0,{@pemit %#=You do not have a bbpost in progress.},{&bb_post_bdy_%# #BBS_POCKET=[get(#BBS_POCKET/bb_post_bdy_%#)]%b%0; @pemit %#=Text added to bbpost.}
&CMD_+BBEDIT2 BBS - Myrddin's Global BBS=$+bbedit *=*/*:@switch strmatch([setq(0,ucstr(%0))]%q0,*/*)=0,{@switch %q0=TEXT,{@edit #BBS_POCKET/bb_post_bdy_%#={%1},{%2}},TITLE,{&bb_post_hdr_%# #BBS_POCKET=[index(get(#BBS_POCKET/bb_post_hdr_%#),|,1,1)]|[edit(index(get(#BBS_POCKET/bb_post_hdr_%#),|,2,1),{%1},{%2})]}; @wait 0={@pemit %#=[center(= BB Post in Progress =,78,=)]%rGroup: %b[name(index(get(#BBS_POCKET/bb_post_hdr_%#),|,1,1))]%rTitle: %b[index(get(#BBS_POCKET/bb_post_hdr_%#),|,2,1)]%r[line2()]%r[trim(get(#BBS_POCKET/bb_post_bdy_%#))]%r[line()]}}
&CMD_+bbpocket BBS - Myrddin's Global BBS=$+bbpocket:@pemit %#=Please use '+help bb'.
&CMD_+BBCONFIG BBS - Myrddin's Global BBS=$+bbconfig:@pemit %#=[center(= Myrddin's Global BBS v[get(#BBS_POCKET/version)] =,78,=)]%rGlobal Config Parameters:%r%r[rjust(timeout:,15)] [div(get(#BBS_POCKET/config_timeout),86400)] days%r[rjust(autotimeout:,15)] [ifelse(hasattr(#BBS_POCKET,startup),on,off)]%r[line2()]%rBoard Config Parameters:%r%r%b[iter(filter(#BBS_POCKET/fn_hasconfig,get(#BBS_POCKET/groups)),%b%b[name(##)]%r[ifelse(hasattr(##,anonymous),[space(5)]anonymous: [get(##/anonymous)]%r,)][ifelse(neq(get(##/config_timeout),get(#BBS_POCKET/config_timeout)),[space(5)]timeout: [div(get(##/config_timeout),86400)] days%r,)]%r)][line()]
&CMD_+BBCONFIG2 BBS - Myrddin's Global BBS=$+bbconfig *=*:@switch [strmatch(%0,*/*)]:[hasflag(%#,wizard)]=1:*,{},0:0,{@pemit %#=Huh?%b%b(Type "help" for help.)},{@switch [member(get(#BBS_POCKET/valid_global_configs),lcstr(%0))]=0,{@pemit %#=Valid global config parameters are: [get(#BBS_POCKET/valid_global_configs)]},{@trigger #BBS_POCKET/tr_gconfig_%0=%1,%#}}
&CMD_+BBCONFIG3 BBS - Myrddin's Global BBS=$+bbconfig */*=*:@switch/first hasflag(%#,wizard)=0,{@pemit %#=Huh?%b%b(Type "help" for help.)},{@switch [setr(0,u(#BBS_POCKET/get_group,%0))]:[member(get(#BBS_POCKET/valid_configs),lcstr(%1))]=#-1:*,{@pemit %#='%0' is not a valid group name.},#-2:*,{@pemit %#='%0' is not specific enough.},*:0,{@pemit %#=Valid config parameters are: [get(#BBS_POCKET/valid_configs)]},{@trigger #BBS_POCKET/tr_config_%1=%q0,%2,%#}}
&CMD_+BBTIMEOUT BBS - Myrddin's Global BBS=$+bbtimeout */*=*:@dolist [sort(u(#BBS_POCKET/fn_makelist,%1))][setq(0,u(#BBS_POCKET/get_group,%0))]={@switch [or(strmatch(index(get(%q0/HDR_[setq(1,extract(get(%q0/MESS_LST),##,1))]%q1),|,4, 1),%#), hasflag(%#,wizard))]:[and(isnum(%2),or(and(gt(%2,0),lte(mul(%2,86400),get(%q0/config_timeout))),and(not(get(%q0/config_timeout)),gte(%2,0)),hasflag(%#,wizard)))]=1:1, {&hdr_%q1 %q0=[replace(get(%q0/hdr_%q1),5,ifelse(%2,add(secs(),mul(%2,86400)),0),|)]; @pemit %#=Message ## in group '[name(%q0)]' has [ifelse(%2,a %2 day,no)] timeout.}, 0:*,{@pemit %#=Either message ## does not exist, or you were not the original poster.},*:0,{@pemit %#=Sorry, '%2' is not a valid number of days for a timeout on the '[name(%q0)]' board. [ifelse(get(%q0/config_timeout),Maximum timeout is [div(get(%q0/config_timeout),86400)] days.,)]}}
&CMD_+BBSEARCH BBS - Myrddin's Global BBS=$+bbsearch */*:@switch [member(u(#BBS_POCKET/valid_groups,%#,write),setr(0,u(#BBS_POCKET/get_group,%0)))]:[hasattr(%q0,anonymous)]:[isdbref(setr(1,locate(%#,*%1,p)))]=0:*:*,{@pemit %#=BB Group '%0' is either an invalid Group, an unreadable Group, or a Group you do not subscribe to.},1:1:*,{@pemit %#=BB Group '[name(%q0)]' is an anonymous group.},1:0:0,{@pemit %#=No such player '%1'.},{@switch words(setr(2,grep(%q0,hdr_*,|%q1|)))=0,{@pemit %#=[name(%q1)] hasn't posted any messages to the '[name(%q0)]' Group.},{@pemit %#=%r[line_with_text(name(%q0))]%r[space(8)]Message[space(28)]Posted[space(8)]By%r[line2()]; @dolist %q2={@pemit %#=[ljust([member(get(#BBS_POCKET/groups),%q0)]/[member(get(%q0/mess_lst),last(##,_))],6)][ljust(u(#BBS_POCKET/fn_msg_flags,%#,%q0,last(##,_)),2)][ljust(index([setr(3,get(%q0/##))],|,1,1),35)][ljust(index(%q3,|,2,1),14)][mid([index(%q3,|,3,1)],0,21)][switch(member(%q2,##),words(%q2),%r[line()])]}}}
&CMD_+BBSEARCH_NOGREP BBS - Myrddin's Global BBS=$+bbsearch */*:@switch [member(u(#BBS_POCKET/valid_groups,%#,write),setr(0,u(#BBS_POCKET/get_group,%0)))]:[hasattr(%q0,anonymous)]:[isdbref(setr(1,locate(%#,*%1,p)))]=0:*:*,{@pemit %#=You do not subscribe to that Group.},1:1:*,{@pemit %#=BB Group '[name(%q0)]' is an anonymous group.},1:0:0,{@pemit %#=No such player '%1'.},{@switch words(setr(2,u(#BBS_POCKET/fn_grep,%q0,hdr_*,|%q1|)))=0,{@pemit %#=[name(%q1)] hasn't posted any messages to the '[name(%q0)]' Group.},{@pemit %#=%r[line_with_text,name(%q0))]%r[space(8)]Message[space(28)]Posted[space(8)]By%r[line2()]; @dolist %q2={@pemit %#=[ljust([member(get(#BBS_POCKET/groups),%q0)]/[member(get(%q0/mess_lst),last(##,_))],6)][ljust(u(#BBS_POCKET/fn_msg_flags,%#,%q0,last(##,_)),2)][ljust(index([setr(3,get(%q0/##))],|,1,1),35)][ljust(index(%q3,|,2,1),14)][mid([index(%q3,|,3,1)],0,21)][switch(member(%q2,##),words(%q2),%r[line()])]}}}
&CMD_+BBNOTIFY BBS - Myrddin's Global BBS=$+bbnotify *=*:@switch/first [member(u(#BBS_POCKET/valid_groups,%#,read),setr(0,u(#BBS_POCKET/get_group,%0)))]:[member(on off,lcstr(%1))]:[lcstr(%1)]=0:*:*,{@pemit %#=[switch(%q0,#-2,That Group name is not specific enough.,You do not subscribe to that Group.)]},*:0:*,{@pemit %#=Invalid choice '%1'. Choices are 'on' or 'off'.},*:*:off,{&bb_silent %#=[setunion(get(%#/bb_silent),%q0)]; @pemit %#=Post notification for BB Group '[name(%q0)]' turned off. You will no longer be notified of new postings to that Group.},{&bb_silent %#=[setdiff(get(%#/bb_silent),%q0)]; @pemit %#=Post notification for BB Group '[name(%q0)]' turned on. You will now be notified of new postings to that Group.}
@set BBS - Myrddin's Global BBS=!HALT
@set BBS - Myrddin's Global BBS=INHERIT
@set BBS - Myrddin's Global BBS=SAFE

&cmd-+bbclear BBS - Myrddin's Global BBS=$+bbclear *=*-*:@switch and(hasflag(%#, wizard),member(get(#BBS_POCKET/groups),setr(0,u(#BBS_POCKET/get_group,%0))))=1,{@pemit %#=ansi(hr,Clearing BBS - please wait.);@dolist lnum(%1,%2)=@wait #@={@fo %#=+bbremove %0/%1;@fo %#=think Removing message #@ of [words(lnum(%1,%2))]}},@pemit %#=This command is restricted to staff.

&CMD-+BBARCHIVE BBS - Myrddin's Global BBS=$+bbarchive */*:@switch 0=member(u(#BBS_POCKET/valid_groups,%#,read),setr(0,u(#BBS_POCKET/get_group,%0))),{@pemit/silent %#=[switch(%q0,#-2,That Group name is not specific enough.,You do not subscribe to that Group.)]},member(v(archive_formats),%1),@pemit/silent %#={That is not a valid archive option.  Use one of these:  [v(archive_formats)].},{@pemit/silent %#=[u(fun_archive_header_[trim(%1)],%q0)];@dolist [xget(%q0,mess_lst)]=@wait add(#@,3)=@pemit/silent %#=[u(fun_archive_msg_[trim(%1)],%q0,##)]}


&ARCHIVE_FORMATS BBS - Myrddin's Global BBS=wiki html plain
&FUN_ARCHIVE_HEADER_wiki BBS - Myrddin's Global BBS===[name(%0)] \([time()]\)==%R[iter(xget(%0,mess_lst),%R* [extract(xget(%0,hdr_##),1,1,|)]<BR>)]%R
&FUN_ARCHIVE_HEADER_HTML BBS - Myrddin's Global BBS=<h1>[name(%0)] \([time()]\)</h1>%R<P><HR><P>%R<UL>[iter(xget(%0,mess_lst),%R<LI><a href="\#[##]">[extract(xget(%0,hdr_##),1,1,|)]</a>)]%R</UL>
&FUN_ARCHIVE_HEADER_PLAIN BBS - Myrddin's Global BBS=[name(%0)] \([time()]\)%R[line2()]%R[iter(xget(%0,mess_lst),%R* [extract(xget(%0,hdr_##),1,1,|)])]%R

&FUN_ARCHIVE_MSG_wiki BBS - Myrddin's Global BBS=%R<P><HR><P>%R<B>[extract(xget(%0,hdr_1),1,1,|)]</B> \(<I>[switch(hasattr(%0,anonymous),1,xget(%0,anonymous),extract(xget(%0,hdr_%1),3,1,|))] - [extract(xget(%0,hdr_%1),2,1,|)]</I>\)%R%R[xget(%0,bdy_%1)]
&FUN_ARCHIVE_MSG_HTML BBS - Myrddin's Global BBS=%R<P><HR><P>%R<a name="%1"><B>[extract(xget(%0,hdr_1),1,1,|)]</B> \(<I>[switch(hasattr(%0,anonymous),1,xget(%0,anonymous),extract(xget(%0,hdr_%1),3,1,|))] - [extract(xget(%0,hdr_%1),2,1,|)]</I>\)%R%R<P>[edit(xget(%0,bdy_%1),<,&lt\;,>,&gt\;,%r,%R</P><P>)]
&FUN_ARCHIVE_MSG_PLAIN BBS - Myrddin's Global BBS=%R%R[line2()]%R[extract(xget(%0,hdr_1),1,1,|)]  \([switch(hasattr(%0,anonymous),1,xget(%0,anonymous),extract(xget(%0,hdr_%1),3,1,|))] - [extract(xget(%0,hdr_%1),2,1,|)]\)%R%R[xget(%0,bdy_%1)]


&CMD-+BBNEW BBS - Myrddin's Global BBS=$+bbnew:@switch setdiff(iter(setr(0,u(#BBS_POCKET/VALID_GROUPS,%#,read)),get(##/MESS_LST)),setr(1,get(%#/BB_READ)))=,think pemit(%#,There are no more bulletin board posts to read.),@fo %#=+bbread [iter(revwords(%q0),switch(setr(2,setdiff(setr(3,get(##/MESS_LST)),%q1)),,,setq(4,##)))][setq(2,setdiff(setr(3,get(%q4/MESS_LST)),%q1))][setq(1,first(sort(iter(%q2,member(%q3,##)))))][name(%q4)]/%q1

&CMD-+BBNEXT BBS - Myrddin's Global BBS=$+bbnext:@fo %#=+bbnew

&fun_update_bbread BBS - Myrddin's Global BBS=iter(%0 [u(#BBS_POCKET/fun_find_alts,%N)],set(##,bb_read:[setinter(setunion(get(##/bb_read),%1),get(#BBS_POCKET/master_lst))]))

&cmd_+bbreply BBS - Myrddin's Global BBS=$+bbreply */*=*:@switch 0=[t(member(u(#BBS_POCKET/valid_groups,%#,write),[setq(0,u(#BBS_POCKET/get_group,%0))][setq(1,extract(get(%q0/MESS_LST),%1,1))]%q0))],{@pemit %#=Either you do not subscribe to Group #%0 or are unable to post to it.},t(%q1),@pemit %#=That is not a valid post number.,{&BDY_%q1 %q0=[get(%q0/BDY_%q1)]%R%R[ansi(r,-- Reply by %N at [time()] -- )]%R{%2}; @dolist [setdiff(lsearch(all,type,player),%# [u(#BBS_POCKET/fun_find_alts,%N)])]={&bb_read ##=[remove(xget(##,bb_read),%q1)]}; @pemit %#=You reply to message %0/%1.;@trigger #BBS_POCKET/tr_post_reply=%#,%q0,[member(get(%q0/mess_lst),%q1)],[extract(get(%q0/hdr_%q1),1,1,|)]}


&tr_post_reply bbpocket=@switch hasflag(%0,DARK)=0,{@pemit/list iter(remove(lwho(),%0),switch(or(member([get(##/bb_omit)] [get(##/bb_silent)],%1),not(u(%1/canread,##))),0,##))=(New BB reply ([member(v(groups),%1)]/%2) posted to '[name(%1)]' by [ifelse(hasattr(%1,anonymous),get(%1/anonymous),name(%0))]: %3)}

&FN_IFELSE bbpocket=switch({%0},0,{%2},,{%2},{%1})
&FN_SETR bbpocket=[setq(%0,%1)][s(\%q%0)]
&FN_GREP bbpocket=iter(lattr(%0/%1),switch(strmatch(get(%0/##),*%2*),1,##))

@@ WEB OFF
@switch ifelse(1,1,0)=1,{&FN_IFELSE bbpocket;},{@pemit/silent %#=BBS INSTALLER> You don't have an ifelse() func. That's ok, I'll make my own.%r[space(15)]IMPORTANT: #1 Needs to execute the following command AND put%r[space(15)]it in its @startup:%r[space(15)]@function/priv ifelse=<dbref of bbpocket>/fn_ifelse}
@switch setr(0,1)=1,{&FN_SETR bbpocket;},{@pemit/silent %#=BBS INSTALLER> You don't have an setr() func. That's ok, I'll make my own.%r[space(15)]IMPORTANT: #1 Needs to execute the following command AND put%r[space(15)]it in its @startup:%r[space(15)]@function/priv setr=<dbref of bbpocket>/fn_setr}
@switch [first(grep(me,*,a))]=#-1,{@mvattr BBS - Myrddin's Global BBS=CMD_+BBSEARCH_NOGREP,CMD_+BBSEARCH},{&FN_GREP bbpocket; &CMD_+BBSEARCH_NOGREP BBS - Myrddin's Global BBS}
@@ WEB ON


@switch version()=TinyMUSH *version 3.*,{@set BBS - Myrddin's Global BBS=COMMANDS; @set BBS - Myrddin's Global BBS=STOP},TinyMUSH version 2.2.*,{@set BBS - Myrddin's Global BBS=COMMANDS; @set BBS - Myrddin's Global BBS=STOP},PennMUSH*,{@edit BBS - Myrddin's Global BBS/CMD_+BBNEWGROUP=inherit,wizard; @set bbpocket=!INHERIT; @set bbpocket=WIZARD; @set BBS - Myrddin's Global BBS=!INHERIT; @set BBS - Myrddin's Global BBS=WIZARD; @set BBS - Myrddin's Global BBS=!NO_COMMAND; @power bbpocket=see_all; @edit BBS - Myrddin's Global BBS/CMD_+BBNEWGROUP={&CANREAD},{@set %q0=NO_WARN; &CANREAD}}

@@
@@ The following insure that @attribute/access works
@@
&bb_read me=1
&bb_read me
&bb_omit me=1
&bb_omit me
&bb_silent me=1
&bb_silent me



&help_BBS bbpocket=[space(16)]Commands for Myrddin's Bulletin Board 4.0.6%r[line2()]%r[space(5)]Requiem's BBS is a global, multi-threaded board with a rich set of%r[space(5)]features. To see help on a particular topic, type '+help <topic>'%r[space(5)](Example: +help bbread).%r%r[space(5)]TOPIC[space(17)]DESCRIPTION%r[space(5)]~~~~~[space(17)]~~~~~~~~~~~%r[space(5)]bbread[space(16)]Reading bulletin board messages.%r[space(5)]bbpost[space(16)]Posting bulletin board messages.%r[space(5)]bbmisc[space(16)]Other commands (removing messages, unsubscribing%r[space(29)]groups, resubscribing to groups, etc)%r%r[space(5)]bbtimeout[space(13)]Expanded help on the topic of message timeouts.
&MORE_HELP_BBread bbpocket=[space(18)]Commands for Myrddin's Bulletin Board 4.0.6%r[line2()]%R[space(5)]+bbread[space(25)]Scans joined bulletin board groups.%r[space(5)]+bbread <#>[space(21)]Scans messages in group <#>.%r[space(5)]+bbread <#>/<list>[space(14)]Reads message(s). <list> can be a single%r[space(40)]number, multiple numbers, or a range%r[space(40)]of numbers (ie. 1-6), or any combo.%r[space(5)]+bbread <#>/u[space(19)]Reads all unread messages in group <#>.%r[space(5)]+bbcatchup <#>[space(18)]Marks all messages in group <#> as read.%r[space(40)]You can use multiple group #'s/names%r[space(40)]or may use the word 'all' to catchup%r[space(40)]on all messages on all boards.%r%r[space(5)]+bbscan[space(8)]Totals unread postings (if any) in each joined group. You%r[space(20)]can place this command in your @aconnect, and it's%r[space(20)]recommended you do so.%r%r[space(5)]+bbnew[space(8)]Reads the next available unread BBS message.  You can use this%R[space(20)]to 'scroll' through all unread messages.%r%rNote: You can use the boards name (or abbreviation) in place of it's number.%r[line2()]%rSee also: +help bbpost, +help bbmisc
&MORE_HELP_BBpost bbpocket=[space(18)]Commands for Myrddin's Bulletin Board 4.0.6%r[line2()]%r[space(5)]+bbpost <#>/<title>[space(13)]This starts a post to group <#>.%r[space(5)]+bbwrite <text>[space(17)]This adds text to an already started post.%r[space(5)]+bb <text>[space(22)]Same as +bbwrite.%r[space(5)]+bbedit <area>=<old>/<new>[space(6)]Edits your post in progress. Valid areas%r[space(39)]are: text, title%r[space(5)]+bbproof[space(24)]Displays your current post in progress.%r[space(5)]+bbtoss[space(25)]Discards your current post in progress.%r[space(5)]+bbpost[space(25)]This will post your current post in%r[space(39)]progress.%r%r[space(5)]+bbpost <#>/<subject>=<body>[space(4)]Posts a message to group <#>. This is a%r[space(39)]quick way of posting a message with%r[space(39)]one command.%r[space(5)]+bbedit <#>/<#>=<old>/<new>[space(5)]Edits one of your posted messages.%R[space(5)]+bbreply <#>/<#>=<reply>[space(8)]Replies to a message.%r%rNote: You can use the boards name (or abbreviation) in place of it's number.%r[line2()]%rSee also: +help bbread, +help bbmisc
&MORE_HELP_BBmisc bbpocket=[space(18)]Commands for Myrddin's Bulletin Board 4.0.6%r[line2()]%r[space(5)]+bbremove <#>/<list>[space(12)]Removes a message by you. <list> can be a%r[space(39)]single number, a group of numbers, or a%r[space(39)]range (10-14).%r[space(5)]+bbmove <#>/<#> to <#>[space(10)]Moves one of your messages to a new group.%r[space(5)]+bbleave <#>[space(20)]Unsubscribe from group <#>.%r[space(5)]+bbjoin <#>[space(21)]Joins a group you've previously 'left'.%r[space(5)]+bblist[space(25)]Listing of all groups available to you%r[space(39)]along with their timeout values.%r[space(5)]+bbsearch <#>/<name>[space(12)]Shows you a list of <name>'s postings on%r[space(39)]group <#>.%r[space(5)]+bbtimeout <#>/<#>=<days>[space(7)]Changes timeout for a message to <days>.%r[space(5)]+bbnotify <#>=<on|off>[space(10)]Turn post notification for group <#> on%r[space(39)]or off.%r%r[space(2)]%rFor a '.sig', set an attribute on you called BB_SIG, and place your%rsignature in that.%r%rNote: You can use the board's name (or abbreviation) in place of its number.%r[line2()]%rSee also: +help bbread, +help bbpost, +help bbtimeout, +help bbchanges
&MORE_HELP_BBtimeout bbpocket=[space(3)]Expanded Help on Message Timeouts for Myrddin's Bulletin Board (1 of 2)%r[line2()]%rThere's only a couple of things that need to be understood in order to take%rfull advantage of message timeouts.%r%r1: Once a day, the TR_TIMEOUT attribute on the bbpocket should be triggered.%r[space(3)]Some games already have a way of accomplishing this. For games that do not,%r[space(3)]simply have a wizard execute set the 'autotimeout' global parameter:%r[space(6)]+bbconfig autotimeout=on%r[space(3)]Turning that parameter on sets up the board to take care of its own daily%r[space(3)]triggering.[space(2)]Setting the paramenter to 'off' turns off the BBS's internal%r[space(3)]triggering.%r%r[line2()]%rConcluded in: +help bbtimeout2
&MOMRE_HELP_BBtimeout2 bbpocket=[space(3)]Expanded Help on Message Timeouts for Myrddin's Bulletin Board (2 of 2)%r[line2()]%r%r2: There are 3 different timeout values:%r%r[space(3)]The global parameter 'timeout' (set via the '+bbconfig timeout=<days>'%r[space(3)]command) sets a default timeout that will be set on any new bb groups%r[space(3)]created after the execution of this command.%r%r[space(3)]The group parameter 'timeout' (set via the '+bbconfig <#>/timeout=<days>'%r[space(3)]command) sets a default timeout for the group <#>. Any new messages%r[space(3)]posted to this group will inherit that timeout.%r%r[space(3)]The user command '+bbtimeout <group>/<message list>=<days>' will set the%r[space(3)]specified timeout on individual messages. Players can only modify their%r[space(3)]own messages, wizards can modify any messages. Players can only set%r[space(3)]timeouts equal to or less than the default timeout for the board, wizards%r[space(3)]can set any timeout.%r%r[space(3)]In all cases, a timeout of '0' indicates no timeout.
&MORE_HELP_BBwiz bbpocket=[space(15)]Wizard Commands for Myrddin's Bulletin Board 4.0.6%r[line2()]%r[space(5)]+bbnewgroup <title>[space(13)]Creates a new group.%r[space(5)]+bbcleargroup <#>[space(15)]Clears group <#> and all it's messages.%R[space(5)]+bbclear <#>=<#>-<#>[space(12)]Clears specific messages in a group.%r[space(5)]+bblock <#>=<lock>/<key>[space(8)]Locks group <#>. Lock can be 'flag' or%r[space(39)]an attribute name.%r[space(39)]Examples: +bblock 4=flag/wizard%r[space(49)]+bblock 5=race/were%r[space(5)]+bbwritelock <#>=<lock>/<key>[space(3)]Same as above, but controls who may write%r[space(39)]to the group.%r[space(5)]+bbconfig[space(23)]Shows values of all config parameters.%r[space(39)]See '+help bbconfig' for help on%r[space(39)]config parameters and how to set them.%R[space(5)]+bbarchive <#>/<type>[space(11)]Prints an archive of the board suitable for logging.%R[space(39)]Type can be 'html' 'wiki' or 'plain'%r%RYou can set up the FUN_FIND_ALTS method on the bbpocket to list a players' alts (DB#'s) so messages will automatically be marked as read on all alts.%R[line2()]%rSee also: +help bbconfig, +help bblock (contains expanded help for locks)
&MORE_HELP_BBlock bbpocket=[space(11)]Expanded Help on Group Locks for Myrddin's Bulletin Board%r[line2()]%rA few notes about locking BB Groups.%r%rThe +bblock/+bbwritelock commands are only meant to take care of the simplest%rof locks.[space(2)]There is simply no way to write one bblock'ing command that can%rdeal with all the different ways people want to lock their BB Groups, and it%rcertainly isn't practical to keep adding different locking commands for each%rlocking method I come across.%r%rCoders can set up locks on Groups that are as customized or exotic as you like%rquite easily, however.[space(2)]Each Group has its own object.[space(2)]If you look inside%rthe global BBS object, you'll see them.[space(2)]Each Group object has a pair of%rattributes that control who may read and who may write to that Group.[space(2)]The%rattributes are named CANREAD and CANWRITE, respectively.[space(2)]The dbref of the%rplayer attempting to read/write is passed to the appropriate attribute as \%0.%rAs long as the attribute returns a non-zero result, the player can read/write.%r%rCANREAD Examples:%r[space(3)]CANREAD: 1[space(31)]Anyone can read%r[space(3)]CANREAD: hasflag(\%0,wizard)[space(14)]Only wizards can read%r[space(3)]CANREAD: strmatch(name(\%0),*z*)[space(10)]Only characters with a 'z' in%r[space(46)]their name can read
&MORE_HELP_BBconfig bbpocket=[space(10)]Configuration Parameters for Myrddin's Bulletin Board 4.0.6%r[line2()]%rThere are two types of configuration parameters: global and group.[space(2)]Global%rconfiguration parameters affect the BB system as a whole, while group %rconfiguration parameters affect a single Group.%rTo set a global parameter:%r%r[space(5)]+bbconfig <param>=<value>[space(7)]Sets global config parameter.%r%rGlobal Parameters:%r%r[space(5)]timeout[space(12)]This sets the default timeout (in days) for any new%r[space(26)]groups created. A timeout of '0' means messages%r[space(26)]won't timeout. (ex. +bbconfig timeout=30)%r[space(5)]autotimeout[space(8)]If you don't have a way to trigger the TR_TIMEOUT%r[space(26)]attribute on the bbpocket (daily) to clean out%r[space(26)]expired messages, you can use the autotimeout param%r[space(26)]instead. Setting this param 'on' enables the BB's%r[space(26)]built in timer. Set this param to 'off' to disable.%r[line2()]%rSee '+help bbconfig2' for info on Group parameters
&MORE_HELP_BBconfig2 bbpocket=[space(10)]Configuration Parameters for Myrddin's Bulletin Board 4.0.6%r[line2()]%rTo set a Group parameter:%r%r[space(5)]+bbconfig <#>/<param>=<value>[space(3)]Sets config parameter for board <#>.%r%rGroup Parameters:%r%r[space(5)]anonymous[space(10)]Setting this value turns the Group 'anonymous'.[space(2)]This%r[space(26)]means that any future posting's 'Author' field will%r[space(26)]have the value of this param instead of the name of%r[space(26)]the author. Wizards will also see the actual name of%r[space(26)]author.[space(2)](examples:%r[space(26)]+bbconfig newspaper/anonymous=NewsPaper Staff,%r[space(26)]+bbconfig house rules/anonymous=The Staff)%r[space(5)]timeout[space(12)]Sets the default timeout value for the Group.[space(2)]Any new%r[space(26)]messages will have this new value.[space(2)]A value of '0'%r[space(26)]means messages won't timeout.
&SHELP_BBS bbpocket=See +help bbwiz

&HATTR_BB_READ bbpocket=
&HATTR_BB_OMIT bbpocket=


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       DATA SECTION           *****
@@    *****   CHECK BEFORE UPGRADING     *****
@@    *****   REMOVE OF MODIFY AS NEEDED *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


&FUN_FIND_ALTS bbpocket=iter(alts(%0),pmatch(##))
&NXT_MESS bbpocket=1
&CONFIG_TIMEOUT bbpocket=2592000
@fo %#=+jobs/create CODE=BBS Config/Create and lock a staff jobs BBS.

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       END DATA SECTION      *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


@pemit/silent %#=[ansi(hg,Setting up database numbers...)]%R

@wait 20=+install/updategroup BBS

@wait 20=@pemit/silent me=%rBulletin Board construction complete.  Enjoy!%r[line()]

@wait 20=@set me=!quiet

@switch/first %#:[first(version())]=#1:Tiny*,{@attribute/access bb_read=hidden wizard; @attribute/access bb_omit=hidden wizard},%#:Penn*,,*:Tiny*,{@pemit/silent me=Reminder! Make sure #1 executes the following:%r%b%b@attribute/access bb_read=hidden wizard%r%b%b@attribute/access bb_omit=hidden wizard%r%b%b@attribute/access bb_silent=hidden wizard},{@pemit/silent me=Reminder! Make sure #1 sets the BBS WIZARD}




