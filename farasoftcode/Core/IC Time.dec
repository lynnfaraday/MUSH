@@ Faraday's IC Time System
@@ Author:  Linda O'Meara
@@ Website: http://www.wordsmyth.org/aresmush/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hg,Beginning IC Time Installation.   Please wait until the Installation Complete message appears...)]%R

think install_create(ICTIME_CMD,IC Time,1,0,7.0,IC time system.)

@pemit/silent %#=[ansi(hg,Setting Object Attributes...)]%R

&CMD-+TIME IC Time=$+time:think pemit(%#,[line()]%R[center(ansi(h,IC Time),78)]%R%R[ansi(hc,padstr(IC Time:,20))] [ictime()] %r%R[center(------------------,78)]%R[center(ansi(h,OOC Time),78)]%R%R[ansi(hc,MUSH Time \(EST\):)][space(5)][time()][space(5)]\(MUSH+0\)%R[ansi(hc,Local Time:)][space(10)][switch(hasattr(%#,time_offset),1,[convsecs(add(secs(),mul(3600,xget(%#,time_offset))))][space(5)]\(MUSH[switch(xget(%#,time_offset),<0,,+)][xget(%#,time_offset)]\),See [ansi(hg,+help +time)] for info on setting up your local time)]%R[line()])
&CMD-+TIME-OFFSET IC Time=$+time/offset *:think switch(0,and(isnum(%0),lte(%0,24),gte(%0,-24)),pemit(%#,ansi(hr,That is not a valid time offset.)),[pemit(%#,ansi(hg,You set your local time offset to %0.  This value will be added to or subtracted from the current MUSH Time to determine the time where you are.  Don't forget to update it for Daylight Savings Time, if your Daylight Savings is different than the Eastern US Daylight Savings.))][set(%#,time_offset:[trim(%0)])])
&DATE_FORMAT IC Time=$a $b $d, $Y
&FUN_ICDATE_TO_SECS IC Time=add(fdiv(sub(sub(convtime(u(fun_offset_icdate_year,%0 %1)),v(date_offset)),v(start_secs)),v(ratio)),v(start_secs))
&FUN_ICTIME IC Time=u(FUN_SECS_TO_ICDATE,secs())
&FUN_IC_YEAR_TO_RL_YEAR IC Time=[extract(%0,1,sub(words(%0),1))] [sub(last(%0),v(year_offset))]
&FUN_OFFSET_ICDATE_YEAR IC Time=rest(u(fun_ic_year_to_rl_year,edit(%0,\,,))
&FUN_OFFSET_IC_SECS IC Time=add(%0,fdiv(%1,v(ratio))
&FUN_RL_SECS_TO_IC_SECS IC Time=add(add(mul(sub(%0,v(start_secs)),v(ratio)),v(start_secs)),v(date_offset))
&FUN_RL_YEAR_TO_IC_YEAR IC Time=[extract(%0,1,sub(words(%0),1))] [add(last(%0),v(year_offset))]
&FUN_SECS_TO_ICDATE IC Time=u(fun_rl_year_to_ic_year,timefmt(v(date_format),u(FUN_RL_SECS_TO_IC_SECS,%0)))
&HELP_+TIME IC Time=The +time command helps you keep track of time on the MUSH. +time displays the IC time and OOC time.%R%R[ansi(h,+time)] - Displays the current IC time, plus the current OOC time in the MUSH's timezone ("MUSH time") and in your timezone.%R%R[ansi(h,+time/offset <hours to add/subtract>)] - This allows you to set the  difference between your own location's timezone and MUSH time, so +time will  display the correct local time for you. Note that this only affects +time -  it does not affect other commands.
&SHELP_ICTIME IC Time=IC time allows you to represent IC time with a flexible year and IC:OOC time ratio. ictime\() - Returns current IC date.%R%RRATIO - IC:OOC time ratio.%R%TExample: Use 2 for a 2:1 ratio, or 0.5 for a 1:2 ratio.%RYEAR_OFFSET - The value to add/subtract from the current year to get the desired IC year.%R%TExample: Use -163 to make the year 1848 (if it's 2011 today)%RSTART_SECS - The OOC secs\() value from which you start calculating the IC time offset.%R%TSet this whenever you change the ratio.%RDATE_OFFSET - An extra number of seconds to add to OOC time to get IC time.%R%TExample: If it's October in RL and you want the game to start in March, %R%Tset the DATE_OFFSET to 86400*30*5 to shift the date by 5 months.%R%Ricconvsecs(<secs>) - Given a secs() value (past, present or future), converts to an IC time.%Ricconvtime(<ictime>) - Given an IC date, converts to a RL secs() value.%Rictimeoffset(<secs>,<offset>) - Adds the specified *IC* time offset to the secs() value.  For example, to find "IC Tomorrow" you could do ictimeoffset(secs(),86400).  The 86400 is scaled to account for the IC/OOC time ratio.%R%RNOTE:  This system does some fudging with the years, so days of the week, leap years, etc. will not necessarily match their true historic values.
@STARTUP IC Time=@fun ICTIME=me,fun_ictime;@fun ICCONVTIME=me,FUN_ICDATE_TO_SECS;@fun ICCONVSECS=me,FUN_SECS_TO_ICDATE;@fun ICTIMEOFFSET=me,FUN_OFFSET_IC_SECS;@fun AGE=me/FUN_GET_AGE;@fun BIRTHDATE=me/FUN_GET_BIRTHDATE

&CMD-+AGEUPGRADE IC Time=$+ageupgrade:think pemit(%#,switch(0,isstaff(%#),ansi(hr,This command is only for staff.),[ansi(hg,You upgrade everyone's ages.  Their old birthdate is stored on the old_birthdate attribute if you need it back.)][iter(real_players(),[setq(0,xget(##,birthdate))][set(##,old_birthdate:%q0)][set(##,birthdate:[switch(%q0,,,[setq(m,Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec)][extract(%qm,extract(%q0,1,1,/),1)] [extract(%q0,2,1,/)]\, [sub(last(ictime()),sub(last(time()),extract(%q0,3,1,/)))])])])]))
&FUN_GET_AGE IC Time=localize([setq(0,xget(%0,birthdate))][switch(%q0,,0,[setq(m,Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec)][setq(1,sub(last(ictime()),last(%q0)))][setq(2,match(%qm,extract(%q0,1,1)))][setq(3,match(%qm,extract(ictime(),2,1)))][setq(4,edit(extract(%q0,2,1),\,,))][setq(5,edit(extract(ictime(),3,1),\,,))][setq(6,switch(1,lt(%q2,%q3),0,eq(%q2,%q3),switch(%q4,>%q5,-1,0),-1))][add(%q1,%q6)])])
&FUN_GET_BIRTHDATE IC Time=xget(%0,birthdate)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       DATA SECTION           *****
@@    *****   CHECK BEFORE UPGRADING     *****
@@    *****   REMOVE OF MODIFY AS NEEDED *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&DATE_OFFSET IC Time=0
&RATIO IC Time=1
@fo me=&START_SECS IC Time=[secs()]
&YEAR_OFFSET IC Time=-163

+jobs/create CODE=Configure the IC time system.  See +shelp ICTIME.

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       END DATA SECTION      *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

@pemit/silent %#=[ansi(hg,Setting up database numbers...)]%R

@wait 2=+install/updategroup ICTIME

@wait 5=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!)]%R

@wait 5=@set me=!quiet
