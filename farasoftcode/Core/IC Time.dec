@@ Faraday's IC Time System
@@ Website: http://www.aresmush.com/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hg,Beginning IC Time Installation.   Please wait until the Installation Complete message appears...)]%R

think install_create(ICTIME_CMD,IC Time,1,0,8.1,IC time system.)

@pemit/silent %#=[ansi(hg,Setting Object Attributes...)]%R

&CMD-+TIME IC Time=$+time:think pemit(%#,[line()]%R[center(ansi(h,IC Time),78)]%R%R[ansi(hc,padstr(IC Time:,20))] [ictime()] %r%R[center(------------------,78)]%R[center(ansi(h,OOC Time),78)]%R%R[ansi(hc,MUSH Time \(EST\):)][space(5)][time()][space(5)]\(MUSH+0\)%R[ansi(hc,Local Time:)][space(10)][switch(hasattr(%#,time_offset),1,[convsecs(add(secs(),mul(3600,xget(%#,time_offset))))][space(5)]\(MUSH[switch(xget(%#,time_offset),<0,,+)][xget(%#,time_offset)]\),See [ansi(hg,+help +time)] for info on setting up your local time)]%R[line()])
&CMD-+TIME-OFFSET IC Time=$+time/offset *:think switch(0,and(isnum(%0),lte(%0,24),gte(%0,-24)),pemit(%#,ansi(hr,That is not a valid time offset.)),[pemit(%#,ansi(hg,You set your local time offset to %0.  This value will be added to or subtracted from the current MUSH Time to determine the time where you are.  Don't forget to update it for Daylight Savings Time, if your Daylight Savings is different than the Eastern US Daylight Savings.))][set(%#,time_offset:[trim(%0)])])
&HELP_+TIME IC Time=The +time command helps you keep track of time on the MUSH. +time displays the IC time and OOC time.%R%R[ansi(h,+time)] - Displays the current IC time, plus the current OOC time in the MUSH's timezone ("MUSH time") and in your timezone.%R%R[ansi(h,+time/offset <hours to add/subtract>)] - This allows you to set the  difference between your own location's timezone and MUSH time, so +time will  display the correct local time for you. Note that this only affects +time -  it does not affect other commands.
&STARTUP IC Time=@fun ICTIME=me,fun_ictime;@fun AGE=me/FUN_AGE;@fun BIRTHDATE=me/FUN_BIRTHDATE;@fun RANDOM_BDAY=me/FUN_RANDOM_BIRTHDATE
&FUN_AGE IC Time=switch(xget(%0,birthdate),,0,add(sub(last(time()),add(after(after(xget(%0,birthdate),/),/),1)),switch(match(v(months),extract(time(),2,1)),<[before(xget(%0,birthdate),/)],0,[before(xget(%0,birthdate),/)],switch(gte(extract(time(),3,1),before(after(xget(%0,birthdate),/),/)),1,1,0),1))
&FUN_BIRTHDATE IC Time=[extract(v(months),first(xget(%0,birthdate),/))] [extract(xget(%0,birthdate),2,1,/)]
&MONTHS IC Time=Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec
&FUN_RANDOM_BIRTHDATE IC Time=[rand(1,12)]/[rand(1,28)]/[sub(last(time()),%0)]

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       DATA SECTION           *****
@@    *****   CHECK BEFORE UPGRADING     *****
@@    *****   REMOVE OF MODIFY AS NEEDED *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_ICTIME IC Time=[timefmt($a $b $d)]\, [sub(last(time()),v(YEAR_OFFSET))]
&YEAR_OFFSET IC Time=100


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       END DATA SECTION      *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

@pemit/silent %#=[ansi(hg,Setting up database numbers...)]%R

@wait 2=+install/updategroup ICTIME

@wait 5=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!)]%R

@wait 5=@set me=!quiet
