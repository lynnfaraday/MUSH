@@ Faraday's Map System
@@ Website: http://www.aresmush.com/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hg,Beginning Map System Installation.  Please wait until you see the 'Installation Complete' message...)]%R

@pemit/silent %#=[ansi(hg,Creating Objects...)]%R

think install_create(MAP_CMD,Map Commands,1,0,8.2,Map commands.)
think install_create(MAP_DB,Map DB,0,1,8.2,Map data storage.)

&FUN_MAP_FOR_AREA Map Commands=localize([setq(0,unprettify(%0))][switch(hasattr(#MAP_DB,map_%q0),0,ansi(hr,No map available for this area.),u(#MAP_DB/map_%q0))])

&FUN_LEGEND_FOR_AREA Map Commands=localize([setq(0,unprettify(%0))][switch(hasattr(#MAP_DB,legend_%q0),0,ansi(hr,No legend available for this area.),u(#MAP_DB/legend_%q0))])

&FUN_HAS_LEGEND Map Commands=hasattr(#MAP_DB,legend_[unprettify(%0)])

&CMD-+MAP Map Commands=$+map:think pemit(%#,[setq(0,prettify(xget(%l,area)))][line_with_text(%q0)]%r[u(fun_map_for_area,%q0)][switch(u(FUN_HAS_LEGEND,%q0),1,%r%rType [ansi(h,+map/legend)] for a legend to the numbers or abbreviations on the map.)]%r[line()])

&CMD-+MAP-AREA Map Commands=$+map *:think pemit(%#,[setq(0,prettify(%0))][line_with_text(%q0)]%r[u(fun_map_for_area,%q0)][switch(u(FUN_HAS_LEGEND,%q0),1,%r%rType [ansi(h,+map/legend)] for a legend to the numbers or abbreviations on the map.)]%r[line()])

&CMD-+MAP-AREA-LEGEND Map Commands=$+map/legend *:think [setq(0,prettify(%0))]pemit(%#,[line()]%r[ansi(h,Map Legend For %q0)]%R%R[u(fun_legend_for_area,%q0)]%r[line()])

&CMD-+MAP-AREAS Map Commands=$+map/areas:think pemit(%#,[line()]%R[ansi(hg,Areas covered by +map include:)]%r[iter(sort(lattr(#MAP_DB/map_*)),after(##,_))]%R%RType [ansi(g,+map <area>)] for a map of a particular area.%R[line()])

&CMD-+MAP-legend Map Commands=$+map/legend:think [setq(0,prettify(xget(%l,area)))]pemit(%#,[line()]%r[ansi(h,Map Legend For %q0)]%R%R[u(fun_legend_for_area,%q0)]%r[line()])

&HELP_+MAP Map Commands=The +map system helps you find your way around the MUSH.%R%R[ansi(h,+map)] - Views a map for your current area.%R[ansi(h,+map/legend)] - Views a legend for your current area's map, if there is one.%R[ansi(h,+map/areas)] - Views a list of other areas that have +maps.%R[ansi(h,+map <area>)] - Views another area's map.%R[ansi(h,+map/legend <area>)] - Views another area's legend.

&SHELP_+MAP Map Commands=Maps are stored on object #MAP_DB in attributes named MAP_AREA and LEGEND_AREA.  A legend is not required.  You only need to use it if you want supplemental information to appear.  The system expects the area to match an AREA attribute on the room.%R%RFor instance, if you have an area named "Downtown", you would mark all the rooms in that area with &AREA here=Downtown, and then you'd have a MAP_DOWNTOWN and LEGEND_DOWNTOWN (optional) attribute on the map DB with the map and legend for the downtown area.

@STARTUP Map Commands=@set me=!no_command


@pemit/silent %#=[ansi(hg,Setting up database numbers...)]%R

@wait 2=+install/updategroup MAP

@wait 5=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!)]

@wait 5=@set me=!quiet


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       DATA SECTION           *****
@@    *****   CHECK BEFORE UPGRADING     *****
@@    *****   REMOVE OF MODIFY AS NEEDED *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


@wait 7=+jobs/create CODE=Maps/Create maps for +map.  See +shelp +map

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       END DATA SECTION      *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@



