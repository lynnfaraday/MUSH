@@ Faraday's FS3 System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet
@pemit/silent %#=%R%R[ansi(hg,Installing FS3 Combat NPC module.)]

think install_create(FS3COMBAT_CMD_NPC,FS3Combat NPC Commands,1,0,5.0,FS3 NPC Commands.  See the FS3Combat Action Commands object for important copyright/license information.)
think install_create(FS3COMBAT_DB_NPC,FS3Combat NPC DB,0,1,5.0,FS3Combat NPC DB.  See the FS3Combat Action Commands object for important copyright/license information.,FS3_STORAGE)

&NPCSTATS FS3Combat NPC Commands=skill owner notes temp

&CMD-+NPCS FS3Combat NPC Commands=$+npcs:@pemit %#=[line()]%R[ansi(h,Name)][space(16)][ansi(h,Owner)][space(15)][ansi(h,Damage Status)][space(7)][ansi(h,Skill)][iter(sort(lattr(#FS3COMBAT_DB_NPC/npc_*)),[setq(0,after(##,_))]%R[padstr(prettify(lcstr(%q0)),20)][padstr(npcstat(%q0,owner),20)][padstr(u(#FS3COMBAT_CMD_DMG/fun_display_restricted,%q0),20)][switch(u(fun_controls_npc,%0,%#),1,npcstat(%q0,skill),--)])]%R%RUse [ansi(h,+npc <name>)] to view details.%R[line()]

&CMD-+NPC FS3Combat NPC Commands=$+npc *:@pemit %#=switch(0,isnpc(%0),ansi(hr,There is no NPC by that name.),[setq(0,unprettify(%0))][setq(1,damagemod(%0))][line()]%R[ansi(h,Name:)] [prettify(%0)]%R[ansi(h,Owner:)] [npcstat(%q0,owner)][switch(u(fun_controls_npc,%0,%#),1,%R[ansi(h,Skill:)] [npcstat(%q0,skill)])]%R[ansi(h,Damage Modifier:)] %q1 [u(#FS3COMBAT_CMD_DMG/fun_damage_restricted,%q0)]%R[ansi(h,Notes:)]%R[replace_cr(npcstat(%q0,notes))]%R[line()]

&CMD-+NPC-CREATE FS3Combat NPC Commands=$+npc/create *=*:@pemit %#=[setq(0,u(fun_create_npc,%0,%1,0))]switch(%q0,1,ansi(hg,You create NPC %0.),ansi(hr,Error creating NPC: [after(%q0,|)]))

&CMD-+NPC-DELETE FS3Combat NPC Commands=$+npc/delete *:@pemit %#=switch(0,isnpc(%0),ansi(hr,There is no NPC by that name.),u(fun_controls_npc,%0,%#),ansi(hr,Only staff or the owner can delete an NPC.),ansi(hg,You delete NPC %0)[u(fun_delete_npc,%0)])

&CMD-+NPC-SKILL FS3Combat NPC Commands=$+npc/skill *=*:@pemit %#=switch(0,isnpc(%0),ansi(hr,There is no NPC by that name.),u(fun_controls_npc,%0,%#),ansi(hr,Only staff or the owner can change a NPC's skill.),u(fun_is_valid_skill,%1),ansi(hr,%1 is not a valid skill.),ansi(hg,You change NPC %0's skill to %1.)[u(fun_mod_npcstat,%0,skill,trim(%1))])

&CMD-+NPC-NOTES FS3Combat NPC Commands=$+npc/notes *=*:@pemit %#=switch(0,isnpc(%0),ansi(hr,There is no NPC by that name.),u(fun_controls_npc,%0,%#),ansi(hr,Only staff or the owner can change a NPC's notes.),not(match(%1,*|*)),ansi(hr,Notes may not contain the '|' character.),ansi(hg,You change NPC %0's notes to %1.)[u(fun_mod_npcstat,%0,notes,escape_cr(%1))])

&CMD-+NPC-ADDNOTE FS3Combat NPC Commands=$+npc/addnote *=*:@pemit %#=switch(0,isnpc(%0),ansi(hr,There is no NPC by that name.),u(fun_controls_npc,%0,%#),ansi(hr,Only staff or the owner can change a NPC's notes.),not(match(%1,*|*)),ansi(hr,Notes may not contain the '|' character.),ansi(hg,You add a note to NPC %0.)[u(fun_mod_npcstat,%0,notes,[npcstat(%0,notes)]%R[escape_cr(%1)])])

&CMD-+NPC-PURGETEMP FS3Combat NPC Commands=$+npc/purgetemp:@pemit %#=ansi(hg,You purge all temporary NPCs.)[iter(lattr(#FS3COMBAT_DB_NPC/npc_*),[setq(0,after(##,_))]switch(npcstat(%q0,temp),1,u(fun_delete_npc,%q0)))]

@@ @@@@@@@@@@@@@@@@@
@@ FUN_CONTROLS_NPC
@@  %0 = NPC name
@@  %1 = Player DB
@@  Returns 1 if they're staff or owner
@@ @@@@@@@@@@@@@@@@@
&FUN_CONTROLS_NPC FS3Combat NPC Commands=or(strmatch(npcstat(unprettify(%0),owner),name(%1)),isstaff(%1)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_MOD_NPCSTAT
@@  Modifies a NPC statistic.
@@  %0 = NPC name
@@  %1 = The stat to set, from NPCSTATS.
@@  %2 = The new value.
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_MOD_NPCSTAT FS3Combat NPC Commands=set(#FS3COMBAT_DB_NPC,npc_[unprettify(%0)]:[replace(xget(#FS3COMBAT_DB_NPC,npc_[unprettify(%0)]),match(v(NPCSTATS),%1),%2,|)])

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_IS_VALID_SKILL
@@  Tell is a skill # is valid.
@@  %0 = Skill number
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_IS_VALID_SKILL FS3Combat NPC Commands=and(isnum(%0),lte(%0,12),gte(%0,1))

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_NPCSTAT
@@  Gets a NPC stat.
@@  %0 = NPC Name
@@  %1 = Stat to get
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_NPCSTAT FS3Combat NPC Commands=extract(xget(#FS3COMBAT_DB_NPC,npc_[unprettify(%0)]),match(v(npcstats),%1),1,|)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_ISNPC
@@  Tells if someone is a NPC.
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_ISNPC FS3Combat NPC Commands=hasattr(#FS3COMBAT_DB_NPC,npc_[unprettify(%0)])

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_CREATE_NPC
@@  Creates a NPC.  
@@  %0 = Name
@@  %1 = Skill
@@  %2 = Temporary flag (1=temp)
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_CREATE_NPC FS3Combat NPC Commands=switch(1,isnpc(%0),0|There is already a NPC by that name.,isplayer(%0),0|There is already a player by that name.,isvehicle(%0),0|There is already a vehicle by that name.,strmatch(%0,*|*),0|NPC names may not contain the '|' character.,not(u(fun_is_valid_skill,%1)),0|%1 is not a valid skill level.,1[set(#FS3COMBAT_DB_NPC,npc_[unprettify(%0)]:[trim(%1)]|%N||%2)])

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DELETE_NPC
@@  Deletes a NPC.  
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_DELETE_NPC FS3Combat NPC Commands=switch(0,isnpc(%0),0|There is no NPC by that name.,wipe(#FS3COMBAT_DB_NPC/npc_[unprettify(%0)])[u(#FS3COMBAT_CMD_DMG/fun_clear_damage,%0)])

@STARTUP FS3Combat NPC Commands=@set me=!no_command;@fun npcstat=me,fun_npcstat,2,2,admin;@fun isnpc=me,fun_isnpc

@wait 10=@set me=!quiet
@wait 10=@pemit/silent %#=%R%R[ansi(hg,Installation complete.)]
