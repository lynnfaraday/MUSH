@@ Faraday's IC Com System
@@ Coded by Faraday
@@
@@ Requires: Global Functions, Job System
@@

@set me=quiet

@pemit/silent %#=[ansi(hr,Beginning Com Installation...)]%R

@pemit/silent %#=[ansi(hr,Creating Objects...)]%R

@create Com System
@set Com System = WIZARD
@set Com System = !NO_COMMAND
@set Com System = SAFE
@ACONNECT Com System=@switch [setq(0,0)][iter(xget(#COMMDB,board_list),setq(0,add(%q0,u(#COMMFUN/fun_unread_board,%#,##))))][add(%q0,u(#COMMFUN/fun_unread_messages,%#,%l))]=0,@pemit %#=There are no new Com messages.,@fo %#=+com;@switch orflags(%#,WrJ)=1,think switch(words(lattr(#COMMDB/submit*)),>0,pemit(%#,ansi(hr,There are new Com submissions waiting to be approved.))
@describe Com System=IC communications system supporting phone, message boards, news, and job listings.%R%R[u(credits)]
&CMD-+com Com System=$+com:think pemit(%#,[u(#COMMDB/Com_header)]%R[ansi(h,Com Main Menu)]%R%R- News[space(28)][ansi(g,[u(#COMMFUN/fun_unread_board,%#,news)] New Articles)][space(11)][ansi(h,+com/news)]%R- Messages[space(24)][ansi(g,[u(#COMMFUN/fun_unread_messages,%#,%l)] New Messages)][space(11)][ansi(h,+com/msg)]%R- Phone Communications[space(37)][ansi(h,+com/com)]%R- Jobs[space(53)][ansi(h,+com/jobs)]%R[u(#COMMDB/bottom_line)]
&CMD-+com-ANSWER Com System=$+com/answer *:think switch(0,match(xget(#COMMDB,pending_calls),*:%l),pemit(%#,ansi(hr,There is no call to answer!)),[set(#COMMDB,est_calls:[xget(#COMMDB,active_calls)]%B[u(#COMMFUN/fun_find_call,%l,pending)])][set(#COMMDB,pending_calls:[remove(xget(#COMMDB,pending_calls),u(#COMMFUN/fun_find_call,%l,pending))])][pemit(%#,u(#COMMDB/Com_header))][u(#COMMFUN/fun_com_message,%l,The call goes through.)][u(#COMMFUN/fun_com_message,%l,[parse_pose(%0,[name(%#)])])][u(#COMMFUN/fun_com_message,[u(#COMMFUN/fun_find_target,%l,est)],The call goes through.)][u(#COMMFUN/fun_com_message,[u(#COMMFUN/fun_find_target,%l,est)],[parse_pose(%0,[name(%#)])])]))
&CMD-+com-ANSWER-SHORT Com System=$+com/answer:@fo %#=+com/answer :answers the call.
&CMD-+com-BOARD Com System=$+com/*:think switch(1,u(#COMMFUN/fun_common_cmd_prefix,%0),,gt(words(after(%0,)),0),,not(u(#COMMFUN/fun_is_valid_board,%0)),pemit(%#,ansi(hr,That is not a valid Com board.)),[setq(0,trim(%0))][pemit(%#,[u(#COMMDB/%q0_banner,ictime())][iter(sort(lattr(#COMMDB/%q0_1_*),n),%R[padstr(#@.[switch(match(xget(%#,read_%q0),after(after(##,_),_)),0,%BU%B)],5)] [padstr(u(#COMMFUN/fun_board_title,##),48)][padstr(u(#COMMFUN/fun_board_date,##),24)])]%R%R'U' denotes an unread article.%R[u(#COMMDB/bottom_line)])
&CMD-+com-BOARD-ARTICLE Com System=$+com/* *:think switch(1,u(#COMMFUN/fun_common_cmd_prefix,%0),,not(u(#COMMFUN/fun_is_valid_board,%0)),pemit(%#,ansi(hr,That is not a valid Com board.)),[setq(0,trim(%0))]switch(%1,submit*,,catchup,,switch(0,isnum(%1),pemit(%#,ansi(hr,That is not a valid article number.)),lte(%1,words(lattr(#COMMDB/%q0_1_*))),pemit(%#,ansi(hr,There aren't that many articles!)),pemit(%#,[setq(1,extract(sort(lattr(#COMMDB/%q0_1_*),n),[trim(%1)],1))][set(%#,read_%q0:[setunion(xget(%#,read_%q0),after(after(%q1,_),_))])][u(#COMMDB/%q0_banner,ictime())]%r[center(ansi(h,u(#COMMFUN/fun_board_title,%q1)),78)]%r%r[ansi(h,Dateline: )]%b[u(#COMMFUN/fun_board_date,%q1)]%r%r[u(#COMMFUN/fun_board_text,%q1)]%R[u(#COMMDB/bottom_line)])))
&CMD-+com-BOARD-CATCHUP Com System=$+com/* catchup:think switch(1,u(#COMMFUN/fun_common_cmd_prefix,%0),,not(u(#COMMFUN/fun_is_valid_board,%0)),pemit(%#,ansi(hr,That is not a valid Com board.)),[setq(0,trim(%0))]pemit(%#,ansi(hm,You mark all [ucstr(%0)] Articles as read.))[set(%#,read_%q0:[xget(%#,read_%q0)]%b[setdiff(iter(lattr(#COMMDB/%q0_*),after(after(##,_),_)),xget(%#,read_%q0))])])
&CMD-+com-CALL Com System=$+com/call *:@switch 0=u(#COMMFUN/fun_is_valid_comcode,%0),@pemit %#=ansi(hr,That is not a valid comcode!),u(#COMMFUN/fun_is_valid_comcode,u(#COMMFUN/fun_get_comcode,%l)),@pemit %#=ansi(hr,Com is not available in this area.),not(or(u(#COMMFUN/fun_is_call_est,%l),u(#COMMFUN/fun_is_call_pending,%l))),@pemit %#=ansi(hr,The Com unit is already in use!  If it looks like no one is using it, type +com/end to hang up and then try again.),not(or(u(#COMMFUN/fun_is_call_est,#[after(%0,-)]),u(#COMMFUN/fun_is_call_pending,#[after(%0,-)]))),@pemit %#=ansi(hr,That number is busy.  Please try your call again later.),{@pemit %#=u(#COMMDB/Com_header);@remit %l=ansi(r,[name(%#)] punches a comcode into the Com unit.);think [u(#COMMFUN/fun_com_message,#[after(%0,-)],[beep(1)]A pleasant beep indicates an incoming call.  The caller is identified as: [u(#COMMFUN/fun_directory,[u(#COMMFUN/fun_get_comcode,%l)])]%B\[Comcode [u(#COMMFUN/fun_get_comcode,%l)]\])][set(#COMMDB,pending_calls:[xget(#COMMDB,pending_calls)]%b%l:#[after(%0,-)])];@dolist 1 2 3 4 5={@wait [mul(15,##)]=@switch/first 1=and(match(##,5),match(xget(#COMMDB,pending_calls),%l:*)),{think [u(#COMMFUN/fun_com_message,#[after(%0,-)],The beeping stops.)][u(#COMMFUN/fun_com_message,%l,The Com unit chirps to indicate the call attempt has failed.)][u(#COMMFUN/fun_answering_machine_message,%0,%#)][set(#COMMDB,pending_calls:[remove(xget(#COMMDB,pending_calls),%l:#[after(%0,-)])])]},match(xget(#COMMDB,pending_calls),%l:*),{think [u(#COMMFUN/fun_com_message,#[after(%0,-)],[beep(1)]The beeping continues - indicating an incoming call.)];think u(#COMMFUN/fun_com_message,%l,The call is still going through.  The party at the other end has not answered.)]}}}
&CMD-+com-COM Com System=$+com/com:think pemit(%#,[u(#COMMDB/Com_header)]%R[u(#COMMDB/help_com)]%R[u(#COMMDB/bottom_line)])
&CMD-+com-DIR Com System=$+com/dir *:think pemit(%#,[u(#COMMDB/Com_header)]%R[center(ansi(h,Com Directory - Search for %0),78)]%R[ansi(h,Search By Name:)]%R[switch(and(pmatch(%0),u(#COMMFUN/fun_is_valid_comcode,119-[after(pmatch(%0),#)])),1,[padstr(119-[after(pmatch(%0),#)],10)][name(pmatch(%0))],None)]%R%R[ansi(h,Search By Keyword:)][switch(words(grepi(#COMMDB,dir-*,%0)),0,%rNone,iter(grepi(#COMMDB,dir-*,%0),%r[padstr(after(##,-),10)][xget(#COMMDB,##)]))]%R%R[ansi(h,Search By Number:)]%R[switch(1,hasattr(#COMMDB,dir-[trim(%0)]),[padstr(trim(%0),10)][xget(#COMMDB,dir-[trim(%0)])],and(match(%0,119-*),pmatch(#[after(%0,-)])),[padstr(trim(%0),10)][name(#[after(%0,-)])],None)]%R[u(#COMMDB/bottom_line)]
&CMD-+com-END Com System=$+com/end:think switch(1,u(#COMMFUN/fun_is_call_pending,%l),[u(#COMMFUN/fun_com_message,%l,[name(%#)] closes the connection.)][u(#COMMFUN/fun_com_message,[u(#COMMFUN/fun_find_target,%l,pending)],The call has been disconnected.)][set(#COMMDB,pending_calls:[remove(xget(#COMMDB,pending_calls),u(#COMMFUN/fun_find_call,%l,pending))])],u(#COMMFUN/fun_is_call_est,%l),[u(#COMMFUN/fun_com_message,%l,[name(%#)] closes the connection.)][u(#COMMFUN/fun_com_message,[u(#COMMFUN/fun_find_target,%l,est)],[name(%#)] closes the connection.)][set(#COMMDB,est_calls:[remove(xget(#COMMDB,est_calls),u(#COMMFUN/fun_find_call,%l,est))])],pemit(%#,ansi(hr,There is no call to end!)))
&CMD-+com-board-SUBMIT Com System=$+com/* submit=*/*:think switch(1,u(#COMMFUN/fun_common_cmd_prefix,%0),,not(u(#COMMFUN/fun_is_valid_board,%0)),pemit(%#,ansi(hr,That is not a valid Com board.)),[setq(0,trim(%0))]pemit(%#,switch(1,t(match(%1 %2,|)),ansi(hr,Please do not use the "|" character in your title or text.),ansi(hm,You submit an [ucstr(%0)] article entitled "%1". It must be reviewed by staff before it will appear on the article list.)[set(#COMMDB,SUBMIT_%q0_1_[xget(#COMMDB,next_%q0)]:%1|[ictime()]|[escape_cr(%2)]|[name(%#)])][create_job(COM,Com Article,%N has submitted a %0 article.,%#)][set(#COMMDB,next_%q0:[inc(xget(#COMMDB,next_%q0))])])
&CMD-+com-JOBS Com System=$+com/jobs:think pemit(%#,[u(#COMMDB/Com_header)]%R[ansi(h,Organizations Hiring)]%R[iter(xget(#COMMDB,list_orgs),%r[padstr(#@.,4)][xget(#COMMDB,##)])]%R[u(#COMMDB/bottom_line)])
&CMD-+com-JOBS-ALL Com System=$+com/jobs *:think switch(%0,*/*,,submit*,,delete*,,pemit(%#,switch(0,isnum(%0),ansi(hr,You must enter a number.),lte(%0,words(xget(#COMMDB,list_orgs))),ansi(hr,There aren't that many organizations!),[u(#COMMDB/Com_header)]%R[ansi(h,[xget(#COMMDB,extract(xget(#COMMDB,list_orgs),%0,1))])]%R%R[switch(words(lattr(#COMMDB/[extract(xget(#COMMDB,list_orgs),%0,1)]_*)),0,Sorry - there are no job openings at this time.%R,[ansi(h[rand_color()],#.%B%BJob Title[space(33)])][iter(sort(lattr(#COMMDB/[extract(xget(#COMMDB,list_orgs),%0,1)]_*)),%R[padstr(#@.,4)][u(#COMMFUN/fun_get_job_title,##)])])]%r%RPlease note that only the primary jobs for each organization are listed.  There are usually others available.  Please contact staff if you have an idea for a character that does not fit in one of these job descriptions.%R[u(#COMMDB/bottom_line)])))
&CMD-+com-JOBS-DELETE Com System=$+com/jobs delete=*:think pemit(%#,switch(0,isnum(%0),ansi(hr,You must specify a number.),lte(%0,words(lattr(#COMMDB/other_*))),ansi(hr,There aren't that many job postings!),match(%#,u(#COMMFUN/fun_get_job_openings,[extract(sort(lattr(#COMMDB/other_*)),%0,1)])),ansi(hr,Only the submitter of a job can delete it!),[ansi(hg,You delete job %0.)][wipe(#COMMDB/[extract(sort(lattr(#COMMDB/other_*)),%0,1)])]))
&CMD-+com-JOBS-ORG Com System=$+com/jobs */*:think switch(%0,submit*,,delete*,,pemit(%#,switch(0,isnum(%0),ansi(hr,You must enter a number.),isnum(%1),ansi(hr,You must enter a number.),lte(%0,words(xget(#COMMDB,list_orgs))),ansi(hr,There aren't that many organizations!),lte(%1,words(lattr(#COMMDB/[extract(xget(#COMMDB,list_orgs),%0,1)]_*))),ansi(hr,That is not a valid job number.),[u(#COMMDB/Com_header)]%R[setq(0,extract(xget(#COMMDB,list_orgs),%0,1))][ansi(h,[xget(#COMMDB,%q0)])]%R%R[ansi(h,Job Title:)]%B[u(#COMMFUN/fun_get_job_title,[extract(sort(lattr(#COMMDB/%q0_*)),%1,1)])]%R%R[u(#COMMFUN/fun_get_job_desc,[extract(sort(lattr(#COMMDB/%q0_*)),%1,1)])]%R%RContact [ansi(hg,[xget(#COMMDB,contact_%q0)])] if you are interested in this position or have questions.%R[u(#COMMDB/bottom_line)])))
&CMD-+com-JOBS-SUBMIT Com System=$+com/jobs submit=*/*:think pemit(%#,switch(or(match(%0 %1,*|*),match(%0 %1,*\[*)),1,ansi(hr,Please do not use the | character or MUSH functions in your Com job postings.),[ansi(hg,You create a job posting with title %0.)][set(#COMMDB,OTHER_[xget(#COMMDB,next_other)]:%0|%#|%1%R%RPosted by %N)][set(#COMMDB,next_other:[inc(xget(#COMMDB,next_other))])])
&CMD-+com-MSG Com System=$+com/msg:think pemit(%#,[u(#COMMDB/Com_header)]%r[space(9)][ansi(h,Time[space(26)]To[space(21)]From)][iter(sortby(#COMMFUN/fun_sort_messages,[lattr(%#/msg_*)]%B[lattr(%#/oldmsg_*)]%B[lattr(%l/msg_*)]%B[lattr(%l/oldmsg_*)]),%r[padstr(#@.,5)] [switch(##,old*,%B%B%B,U%B%B)][extract(xget([switch(hasattr(%#,##),1,%#,%l)],##),1,1,|)][space(6)][padstr(switch(hasattr(%#,##),1,[name(%#)],ComCode\[[u(#COMMFUN/fun_get_comcode,%l)]\]),23)][extract(xget([switch(hasattr(%#,##),1,%#,%l)],##),2,1,|)])]%r%RU denotes an unread message.%r[u(#COMMDB/bottom_line)])
&CMD-+com-MSG-DELETE Com System=$+com/msg delete=*:think pemit(%#,switch(0,isnum(%0),ansi(hr,That is not a valid message number.),lte(%0,add(words(lattr(%#/msg_*)),words(lattr(%#/oldmsg_*)),words(lattr(%l/msg_*)),words(lattr(%l/oldmsg_*)))),ansi(hr,There aren't that many messages!),ansi(hg,You delete message %0.)[setq(0,extract(sortby(#COMMFUN/fun_sort_messages,[lattr(%#/msg_*)]%B[lattr(%#/oldmsg_*)]%B[lattr(%l/msg_*)]%B[lattr(%l/oldmsg_*)]),%0,1,))][wipe([switch(hasattr(%#,%q0),1,%#,%l)]/%q0)]))
&CMD-+com-MSG-SEND Com System=$+com/msg send=*/*:think switch(or(u(#COMMFUN/fun_is_valid_comcode,%0),pmatch(%0)),0,pemit(%#,ansi(hr,That is not a valid player or comcode.)),t(match(%1,*\[*)),pemit(%#,ansi(hr,Your message could not be completed for security reasons.  Do not use MUSH functions in your message.)),[pemit(%#,ansi(hg,You send a message to %0))][u(#COMMFUN/fun_send_msg,%0,%1,%N))
&CMD-+com-MSG-VIEW Com System=$+com/msg *:think switch(%0,send*,,delete*,,switch(0,isnum(%0),pemit(%#,ansi(hr,That is not a valid message number.)),lte(%0,add(words(lattr(%#/msg_*)),words(lattr(%#/oldmsg_*)),words(lattr(%l/msg_*)),words(lattr(%l/oldmsg_*)))),pemit(%#,ansi(hr,There aren't that many messages!)),pemit(%#,[setq(0,extract(sortby(#COMMFUN/fun_sort_messages,[lattr(%#/msg_*)]%B[lattr(%#/oldmsg_*)]%B[lattr(%l/msg_*)]%B[lattr(%l/oldmsg_*)]),[trim(%0)],1,))][u(#COMMDB/Com_header)]%r[ansi(h,Time:)] [extract(xget([switch(hasattr(%#,%q0),1,%#,%l)],%q0),1,1,|)]%r[ansi(h,To:)] [switch(hasattr(%#,%q0),1,[name(%#)],Comcode\[[u(#COMMFUN/fun_get_comcode,%l)]\])]%r[ansi(h,From:)] [extract(xget([switch(hasattr(%#,%q0),1,%#,%l)],%q0),2,1,|)]%R%R[ansi(h,Message:)]%r[extract(u([switch(hasattr(%#,%q0),1,%#,%l)]/%q0),3,1,|)]%r[u(#COMMDB/bottom_line)][switch(%q0,old*,,set([switch(hasattr(%#,%q0),1,%#,%l)],old%q0:[xget([switch(hasattr(%#,%q0),1,%#,%l)],%q0)])[wipe([switch(hasattr(%#,%q0),1,%#,%l)]/%q0)])]))
&CMD-+com-NUM Com System=$+com/num:think switch(u(#COMMFUN/fun_is_valid_comcode,u(#COMMFUN/fun_get_comcode,%l)),0,pemit(%#,ansi(hr,Com is not available in this area.)),pemit(%#,[u(#COMMDB/Com_header)]%r[ansi(hr,Terminal Comcode:)] [u(#COMMFUN/fun_get_comcode,%l)]%R[u(#COMMDB/bottom_line)]))
&CMD-+com-TALK Com System=$+com *:think switch(0,u(#COMMFUN/fun_is_call_est,%l),pemit(%#,ansi(hr,There is no call established!)),[u(#COMMFUN/fun_com_message,%l,[parse_pose(%0,%N)])][u(#COMMFUN/fun_com_message,[u(#COMMFUN/fun_find_target,%l,est)],[parse_pose(%0,%N)])])
&CMD-+LINK-ANSWER Com System=$+link/answer *:think switch(0,match(xget(#COMMDB,pending_calls),*:%#),pemit(%#,ansi(hr,There is no call to answer!)),[set(#COMMDB,est_calls:[xget(#COMMDB,active_calls)]%B[u(#COMMFUN/fun_find_call,%#,pending)])][set(#COMMDB,pending_calls:[remove(xget(#COMMDB,pending_calls),u(#COMMFUN/fun_find_call,%#,pending))])][pemit(%#,u(#COMMDB/Com_header))][u(#COMMFUN/fun_com_message,%#,The call goes through.)][u(#COMMFUN/fun_com_message,%#,[parse_pose(%0,[name(%#)])])][u(#COMMFUN/fun_com_message,[u(#COMMFUN/fun_find_target,%#,est)],The call goes through.)][u(#COMMFUN/fun_com_message,[u(#COMMFUN/fun_find_target,%#,est)],[parse_pose(%0,[name(%#)])])]))
&CMD-+LINK-ANSWER-SHORT Com System=$+link/answer:@fo %#=+link/answer :answers the call.
&CMD-+LINK-CALL Com System=$+link/call *:@switch 0=u(#COMMFUN/fun_is_valid_comcode,%0),@pemit %#=ansi(hr,That is not a valid comcode!),u(#COMMFUN/fun_is_valid_comcode,u(#COMMFUN/fun_get_comcode,%#)),@pemit %#=ansi(hr,Com is not available in this area.),not(or(u(#COMMFUN/fun_is_call_est,%#),u(#COMMFUN/fun_is_call_pending,%#))),@pemit %#=ansi(hr,The Com unit is already in use!  If it looks like no one is using it, type +com/end to hang up and then try again.),not(or(u(#COMMFUN/fun_is_call_est,#[after(%0,-)]),u(#COMMFUN/fun_is_call_pending,#[after(%0,-)]))),@pemit %#=ansi(hr,That number is busy.  Please try your call again later.),{@pemit %#=u(#COMMDB/Com_header);@remit %#=ansi(r,[name(%#)] punches a comcode into the Com unit.);think [u(#COMMFUN/fun_com_message,#[after(%0,-)],[beep(1)]A pleasant beep indicates an incoming call.  The caller is identified as: [u(#COMMFUN/fun_directory,[u(#COMMFUN/fun_get_comcode,%#)])]%B\[Comcode [u(#COMMFUN/fun_get_comcode,%#)]\])][set(#COMMDB,pending_calls:[xget(#COMMDB,pending_calls)]%b%#:#[after(%0,-)])];@dolist 1 2 3 4 5={@wait [mul(15,##)]=@switch/first 1=and(match(##,5),match(xget(#COMMDB,pending_calls),%#:*)),{think [u(#COMMFUN/fun_com_message,#[after(%0,-)],The beeping stops.)][u(#COMMFUN/fun_com_message,%#,The Com unit chirps to indicate the call attempt has failed.)][u(#COMMFUN/fun_answering_machine_message,%0,%#)][set(#COMMDB,pending_calls:[remove(xget(#COMMDB,pending_calls),%#:#[after(%0,-)])])]},match(xget(#COMMDB,pending_calls),%#:*),{think [u(#COMMFUN/fun_com_message,#[after(%0,-)],[beep(1)]The beeping continues - indicating an incoming call.)];think u(#COMMFUN/fun_com_message,%#,The call is still going through.  The party at the other end has not answered.)]}}}
&CMD-+LINK-DIR Com System=$+link/dir *:@fo %#=+com/dir %0
&CMD-+LINK-END Com System=$+link/end:think switch(1,u(#COMMFUN/fun_is_call_pending,%#),[u(#COMMFUN/fun_com_message,%#,[name(%#)] closes the connection.)][u(#COMMFUN/fun_com_message,[u(#COMMFUN/fun_find_target,%#,pending)],The call has been disconnected.)][set(#COMMDB,pending_calls:[remove(xget(#COMMDB,pending_calls),u(#COMMFUN/fun_find_call,%#,pending))])],u(#COMMFUN/fun_is_call_est,%#),[u(#COMMFUN/fun_com_message,%#,[name(%#)] closes the connection.)][u(#COMMFUN/fun_com_message,[u(#COMMFUN/fun_find_target,%#,est)],[name(%#)] closes the connection.)][set(#COMMDB,est_calls:[remove(xget(#COMMDB,est_calls),u(#COMMFUN/fun_find_call,%#,est))])],pemit(%#,ansi(hr,There is no call to end!)))
&CMD-+LINK-NUM Com System=$+link/num:think switch(u(#COMMFUN/fun_is_valid_comcode,u(#COMMFUN/fun_get_comcode,%#)),0,pemit(%#,ansi(hr,You do not have a commlink.)),pemit(%#,[u(#COMMDB/Com_header)]%r[ansi(hr,Your Commlink's Comcode:)] [u(#COMMFUN/fun_get_comcode,%#)]%R[u(#COMMDB/bottom_line)])))
&CMD-+LINK-TALK Com System=$+link *:think switch(0,u(#COMMFUN/fun_is_call_est,%#),pemit(%#,ansi(hr,There is no call established!)),[u(#COMMFUN/fun_com_message,%#,[parse_pose(%0,%N)])][u(#COMMFUN/fun_com_message,[u(#COMMFUN/fun_find_target,%#,est)],[parse_pose(%0,%N)])])
@STARTUP Com System=@set me=!no_command
&CREDITS Com System=Version [v(version)]%R%RCoded by Faraday ~ http://www.aresmush.com
&VERSION Com System=4.0


@create Com DB
@set Com DB = WIZARD
@set Com DB = SAFE
@set Com DB = NO_COMMAND
@describe Com DB=Database for the +com system.
&BOARD_LIST Com DB=news
&EST_CALLS Com DB=
&HELP_+COM Com DB=Each section of the Com system has a help page devoted to its commands.  They are:%R%T[ansi(h,PHONE)] - %BTelephone communication services%R%T[ansi(h,NEWS)] %B%B- News%R%T[ansi(h,MSG)] %B%B%B- Message Center%R%T[ansi(h,JOBS)] %B%B- Job Postings%R%T[ansi(h,LINK)] %B%B- Commlinks%R%RSee the help topic [ansi(h,+COM_<topic>)] for more details.
&MORE_HELP_+COM_PHONE Com DB=The Com system is used to make and receive phone calls using the Com terminals present in various rooms.%R%R[ansi(hg,Phone Call Commands:)]%r[ansi(h,+com/call <comcode>)] -- Makes a phone call%R%r[ansi(h,+com/answer)] -- Answers a phone call%R[ansi(h,+com/answer <greeting>)] -- Answers with a greeting pose or say.%r%R[ansi(h,+com <speech> :<pose> ;<semipose> or .<emit>)] -- Speaks, poses or emits on the phone.%R%R[ansi(h,+com/end)] -- Hangs up.%R%R[ansi(h,+com/num)] -- Gets the comcode for this terminal.%R[ansi(h,+com/dir <search string>)] - Searches the Com directory (like the yellow pages).  It will give you results if a match by name, comcode, or keyword is found.  To add an entry to the directory, contact staff.%R%RNOTE:  To make or receive a call using your commlink, you must use the +link command.  See [ansi(h,+com_+link)] help file for details.
&MORE_SHELP_+COM_CUSTOM Com DB=[ansi(hg,Customizing the Com System)]%RYou can customize the Com System based on your theme by modifying several attributes.  They are:%R%R[ansi(h,#COMMDB/COM_HEADER)] - This is the generic header that appears at the top of all Com displays.%R[ansi(h,#COMMDB/NEWS_HEADER)] - This is the header that appears at the top of the news sections.  The parameter \%0 is passed in, and usually shows the IC date.%R[ansi(h,#COMMDB/BOTTOM_LINE)] - The line that appears at the bottom of all Com displays.  If you change this, please make sure a reference to +help +com appears in there somewhere.%R%R[ansi(hg,Continued in +COM_CUSTOM2
&MORE_SHELP_+COM_CUSTOM2 Com DB=[ansi(hg,Customizing Boards)]%R[ansi(h,#COMMDB/board_list)] - By default, this attribute defines a NEWS board, which corresponds to the +com/news family of commands.  You can add additional boards to this list (space-separated) to add new bulletin-board-like systems, like perhaps +com/rumours.  All boards have the same set of commands as news (list articles, read article, etc.) %R%RCreating a new board involves some additional steps:%R%R- Modify #488/cmd-+com to add your new board to the main menu.%R%R- Modify #COMMDB/help_+com to add a help topic for your new board.%R%R- Copy #COMMDB/more_help_+com_news to a new attribute more_help_+com_<boardname> (i.e. more_help_+com_rumours).  Edit this new attribute to describe the purpose of your new board and replace "news" with the new board name in all the commands.%R%R- Set #COMMDB/next_<boardname> to 1.%R%R- Add a <boardname>_banner attribute to #COMMDB.
&MORE_HELP_+COM_NEWS Com DB=The Interstellar Network News covers stories from across the galaxy.%R%R[ansi(hg,News Commands:)]%R[ansi(h,+com/news)] - Lists all articles.%R[ansi(h,+com/news <article#>)] - Reads an article.%R%R[ansi(h,+com/news catchup)] -- Mark all articles as read.%R[ansi(h,+com/news submit=<title>/<article text>)] -- Submits a news article.%R[space(5)]The IC date of your submission will automatically be appended.
&MORE_HELP_+COM_JOBS Com DB=The Jobs database helps you find employment.  It lists not only jobs available in organizations (such as station personnel or alien embassies) but also in private businesses.  Select an organization to view job listings in that area, and then select a particular job for detailed information.%R%R[ansi(hg,Job Commands)]%R[ansi(h,+com/jobs)] - Views the main screen with job listings by employer.%R[ansi(h,+com/jobs <employer#>)] - Views the job listings for a particular employer.%R[ansi(h,+com/jobs <employer#>/<job#>)] - Views a particular job.%R%R[ansi(h,+com/jobs submit=<title>/<description>)] - Submits a job for the "Other" category.%R[ansi(h,+com/jobs delete=<job#>)] - Deletes a job in the "Other" category.  You can only do this if you submitted the job.
&MORE_HELP_+COM_LINK Com DB=The +link command is used to make and receive phone calls from a portable commlinks.  Note that only the phone call commands work with +link.  For everything else related to the Com system use the +com command.  This was done to keep one set of commands wherever possible, to avoid confusion.  The only reason the +link phone commands are separate is to allow you to move from room to room while carrying on a commlink conversation.%R%R[ansi(hg,Phone Call Commands:)]%r[ansi(h,+link/call <comcode>)] -- Makes a phone call%R%r[ansi(h,+link/answer)] -- Answers a phone call%R[ansi(h,+link/answer <greeting>)] -- Answers with a greeting pose or say.%r%R[ansi(h,+link <speech> :<pose> ;<semipose> or .<emit>)] -- Speaks, poses or emits on the phone.%R%R[ansi(h,+link/end)] -- Hangs up.%R%R[ansi(h,+link/num)] -- Gets the comcode for your link unit.%R[ansi(h,+link/dir <search string>)] - Searches the Com directory (like the yellow pages).  It will give you results if a match by name, comcode, or keyword is found.  If you would like to add an entry to the directory, contact staff.
&MORE_HELP_+COM_MSG Com DB=The Com network allows you access to electronic mail services.  You may send IC messages via Com instead of using the OOC mail system.  Mailing lists will [ansi(h,not)] work with the Com messages system.  Answering machine messages left after a failed Com call will also show up on this messages system.%R%R[ansi(hg,Message Commands:)]%R[ansi(h,+com/msg)] - Lists your own personal messages as well as any messages saved on the Com terminal in your current location.%R[ansi(h,+com/msg <#>)] - Reads a message on the list.%R%R[ansi(h,+com/msg delete=<#>)] - Deletes a message.%R[ansi(h,+com/msg send=<recipient>/<text>)] - Sends a message.  Recipient can either be a player's name or a comcode.
&SHELP_+COM Com DB=[ansi(hg,Phone Commands)]%R[ansi(h,+com/disable <room db#>)] - Marks the room as a location where Com is unavailable.%R[ansi(h,+com/enable <room db#>)] - When Com has been diabled in a room, this command will re-enable it.%R%R[ansi(h,+com/addlink <player name>)] - Issues a commlink%R[ansi(h,+com/dellink <player name>)] - UNissues a commlink%R[ansi(h,+com/listlink)] - Shows a list of all players with commlinks.%R%R[ansi(h,+com/adddir <comcode>=<directory listing>)] - Adds a listing to the directory for a comcode.%R[ansi(h,+com/deldir <comcode>)] - Removes a comcode directory listing.%R%R[ansi(hg,Job Commands)]%R[ansi(h,+com/addorg <org abbreviation>=<full org title>/<contact person>)] - Adds an organization to the jobs database.  One of these organizations should have the abbreviation "OTHER" so players can post their own jobs to it.  You can make its full org title whatever you want, only the abbreviation is required to say OTHER.%R[ansi(h,+com/delorg <org#>)] - Deletes an organization from the jobs database.%R%R[ansi(h,+com/addjob <org#>=<job title>/<full job description>)] - Adds a job to the jobs database.%R[ansi(h,+com/deljob <org#>/<job#>)] - Deletes a job from the jobs database.%R%R[ansi(hg,Continued in +COM2)]
&MORE_SHELP_+COM2 Com DB=[ansi(hg,Board Commands)]%RThe board commands work on all the other misc. Com boardS.%R%R[ansi(h,+com/review <board>)] - Gives a summary of pending article submissions.%R[ansi(h,+com/review <board>/<#>)] - Reviews a submission in detail.%R%R[ansi(h,+com/approve <board>/<#>)] - Approves an article and adds it to the database.%R[ansi(h,+com/reject <board>/<#>)] - Rejects an article submission.%R%R[ansi(h,+com/remove <board>/<article#>)] - Deletes an already-approved article from the database.%R%R[ansi(h,+com/archive <board>)] - Spews out HTML code suitable for archiving news articles.%R%R[ansi(hg,Message Commands)]%R[ansi(h,+com/msgcheck)] - Displays a list of all rooms that have Com messages so you can see if any are wasting DB space needlessly.
&news_1_3 Com DB=Test3|Sun Jan 06 07:10:09 2265|This is my third test article.|Faraday
&LINK_HOLDERS Com DB=
&LIST_ORGS Com DB=
&NEXT_news Com DB=1
&NEXT_JOB Com DB=1
&Com_HEADER Com DB=[u(#COMMDB/top_line)]%R%B%B%B%B____ %b___ %b ____ %b__________ %b__ %b___%r%B%B%B%B%B__ \)/ %b | %b __ \)/ ____/ __ \\/ %b|/ %b/%r%B%B%B%B__ %b/ /| | %b__ %b/ / %b / / / / /|_/ /%r%B%B%B/_/ / %b- %b| /_/ / /___/ /_/ / / %b/ /%r%B_____/_____/_____/\\____/\\____/_/ %b/_/[space(15)][ictime()]%r[u(#COMMDB/top_line)]
&BOTTOM_LINE Com DB=[line_with_text(+help +com)]
&news_BANNER Com DB=[u(#COMMDB/top_line)]%r[setq(9,rand_color())]%B*%b=*********\\[space(8)]InterStellar%r *%b**%b%b%b%b*=%b%b*[space(8)]Network%r *%b****=%b*=\\%b*[space(8)]News%r *%b%b%b***%b**\**%r /*****%b%b**%b\\*[space(39)]%b%0%b%r [ansi(%q9,==--------------------------------------------------------------------------==)]
&PENDING_CALLS Com DB=
&ROOMS_WITHOUT_Com Com DB=
&SUBMIT_news_1_4 Com DB=Test4|Sun Jan 06 07:55:12 2265|This is another test.|Faraday
&TOP_LINE Com DB=[ansi(rand_color(),+==~~~~~====~~~~====~~~~====~~~~=====~~~~=====~~~~====~~~~====~~~~====~~~~~==+)]


@create Com Functions
@describe Com Functions=Functions for the +com system.
@set Com Functions = WIZARD
@set Com Functions = NO_COMMAND
&FUN_ANSWERING_MACHINE_MESSAGE Com Functions=pemit(%1,[u(#COMMDB/Com_header)]%R[switch(hasattr(#[after(%0,-)],com_message),0,A computerized voice says: Your call to Comcode %0 has failed.  You may leave a message or try your call again later.,Your call to Comcode %0 has failed.  The answering machine picks up and says: [u(#[after(%0,-)]/com_message)])]%R%RTo leave a message, type [ansi(hg,+com/msg send=<comcode>/<message>)]%R[u(#COMMDB/bottom_line)]
&FUN_BOARD_DATE Com Functions=extract(xget(#COMMDB,trim(%0)),2,1,|)
&FUN_BOARD_SUBMITTER Com Functions=extract(xget(#COMMDB,trim(%0)),4,1,|)
&FUN_BOARD_TEXT Com Functions=replace_cr(extract(xget(#COMMDB,trim(%0)),3,1,|))
&FUN_BOARD_TITLE Com Functions=extract(xget(#COMMDB,trim(%0)),1,1,|)
&FUN_COMMON_CMD_PREFIX Com Functions=t(match(com jobs answer end num dir msg review approve reject remove archive disable enable addlink dellink listlink adddir deldir addorg delorg addjob msgcheck deljob,before(%0,)))
&FUN_COM_MESSAGE Com Functions=switch(isplayer(%0),1,[remit(loc(%0),[ansi(hb,\[-[name(%0)]'s Link-\])]%B%1)],[remit(%0,[ansi(hb,\[-Com Unit-\])]%B%1)])
&FUN_DIRECTORY Com Functions=switch(1,hasattr(#COMMDB/dir-%0),xget(#COMMDB,dir-%0),name(#[after(%0,-)]))
&FUN_FIND_CALL Com Functions=switch(match(xget(#COMMDB,%1_calls),%0:*),0,grab(xget(#COMMDB,%1_calls),*:%0),grab(xget(#COMMDB,%1_calls),%0:*))
&FUN_FIND_TARGET Com Functions=switch(match(xget(#COMMDB,%1_calls),%0:*),0,before(grab(xget(#COMMDB,%1_calls),*:%0),:),after(grab(xget(#COMMDB,%1_calls),%0:*),:))
&FUN_GET_CHANNEL_NAME Com Functions=switch(isnum(%0),0,%0,extract(xget(#COMMDB,channel_list),%0,1))
&FUN_GET_CHANNEL_NUMBER Com Functions=switch(isnum(%0),1,%0,match(xget(#COMMDB,channel_list),%0))
&FUN_GET_COMCODE Com Functions=switch(1,isplayer(%0),119-[after(%0,#)],110-[after(%0,#)])
&FUN_GET_JOB_DESC Com Functions=extract(replace_cr(xget(#COMMDB,%0)),3,1,|)
&FUN_GET_JOB_OPENINGS Com Functions=extract(xget(#COMMDB,%0),2,1,|)
&FUN_GET_JOB_TITLE Com Functions=extract(xget(#COMMDB,%0),1,1,|)
&FUN_IS_CALL_EST Com Functions=or(match(xget(#COMMDB,est_calls),%0:*),match(xget(#COMMDB,est_calls),*:%0))
&FUN_IS_CALL_PENDING Com Functions=or(match(xget(#COMMDB,pending_calls),%0:*),match(xget(#COMMDB,pending_calls),*:%0))
&FUN_IS_CELLPHONE_COMCODE Com Functions=switch(%0,119-*,1,0)
&FUN_IS_VALID_BOARD Com Functions=t(match(xget(#COMMDB,board_list),%0))
&FUN_IS_VALID_CHANNEL Com Functions=switch(isnum(%0),1,and(neq(%0,0),lte(%0,words(xget(#COMMDB,channel_list)))),t(match(xget(#COMMDB,channel_list),%0)))
&FUN_IS_VALID_COMCODE Com Functions=switch(1,and(t(match(%0,110-*)),not(match(xget(#COMMDB,rooms_without_Com),#[after(%0,-)])),not(isplayer(#[after(%0,-)]))),1,t(match(%0,119-*)),and(isplayer(#[after(%0,-)]),match(xget(#COMMDB,link_holders),#[after(%0,-)])),0)
&FUN_SEND_MSG Com Functions=switch(1,u(#COMMFUN/fun_is_valid_comcode,%0),set(#[after(%0,-)],msg_[secs()]:[ictime()]|%2|%1),t(pmatch(%0)),set(pmatch(%0),msg_[secs()]:[ictime()]|%2|%1))
&FUN_SORT_MESSAGES Com Functions=comp(after(%0,_),after(%1,_))
&FUN_UNREAD_BOARD Com Functions=words(trim(iter(lattr(#COMMDB/%1_1_*),switch(t(match(xget(%0,read_%1),after(after(##,_),_))),0,##))))
&FUN_UNREAD_MESSAGES Com Functions=[add(words(lattr(%0/msg*)),words(lattr(%1/msg*)))]

@create Com Staff Commands
@describe Com Staff Commands=Staff commands for the +com system.
@lock/Use Com Staff Commands=ISSTAFF/1
@set Com Staff Commands = WIZARD
@set Com Staff Commands = !NO_COMMAND
@startup Com Staff Commands = @set me=!NO_COMMAND
&CMD-+com-ADDDIR Com Staff Commands=$+com/adddir *=*:think pemit(%#,switch(0,u(#COMMFUN/fun_is_valid_comcode,%0),ansi(hr,That is not a valid comcode.),[ansi(hg,You add a directory listing for comcode %0.)][set(#COMMDB,dir-[trim(%0)]:%1)]))
&CMD-+com-ADDJOB Com Staff Commands=$+com/addjob *=*/*:think pemit(%#,switch(0,isnum(%0),ansi(hr,You must enter a number.),lte(%0,words(xget(#COMMDB,list_orgs))),ansi(hr,There aren't that many organizations!),ansi(hg,You add a job to org %0.)[set(#COMMDB,[extract(xget(#COMMDB,list_orgs),%0,1)]_[xget(#COMMDB,next_job)]:%1||[escape_cr(%2)])][set(#COMMDB,next_job:[inc(xget(#COMMDB,next_job))])]))
&CMD-+com-ADDLINK Com Staff Commands=$+com/addlink *:think [setq(0,pmatch(%0))]pemit(%#,switch(0,t(%q0),ansi(hr,That is not a player.),not(match(xget(#COMMDB,link_holders),%q0)),ansi(hr,That person already has a commlink.),[ansi(hg,You give %0 a commlink.)][set(#COMMDB,link_holders:[xget(#COMMDB,link_holders)]%B%q0)]))
&CMD-+com-ADDORG Com Staff Commands=$+com/addorg *=*/*:think pemit(%#,switch(0,eq(words(%0),1),ansi(hr,Org abbreviations may only have one word in them.),not(hasattr(#COMMDB,%0)),ansi(hr,There is already an org with that keyword.),ansi(hg,You add organization %0.)[set(#COMMDB,list_orgs:[xget(#COMMDB,list_orgs)][trim(%0)]%B)][set(#COMMDB,[trim(%0)]:%1)][set(#COMMDB,contact_[trim(%0)]:%2)]))
&CMD-+com-APPROVE Com Staff Commands=$+com/approve */*:think [setq(0,trim(%0))]switch(0,lte(%1,words(sort(lattr(#COMMDB/submit_%q0_*)))),pemit(%#,ansi(hr,No such article.)),pemit(%#,ansi(c,You approve %q0 submission %1.))[setq(1,extract(sort(lattr(#COMMDB/submit_%q0_*)),trim(%1),1))][set(#COMMDB,[after(%q1,_)]:[xget(#COMMDB,%q1)])][wipe(#COMMDB/%q1)])
&CMD-+com-ARCHIVE Com Staff Commands=$+com/archive *:think [setq(0,trim(%0))]pemit(%#,<table>[iter(sort(lattr(#COMMDB/%q0_1_*)),%R<TR><TD><a href="#[after(##,_)]">[extract(xget(#COMMDB,##),1,1,|)]</a></td><td>[extract(xget(#COMMDB,##),2,1,|)]</td></tr>)]</table>%R%R%R);@dolist sort(lattr(#COMMDB/%q0_1_*))=think pemit(%#,%R%R[setq(1,xget(#COMMDB,##))][setq(1,edit(%q1,<,\[))][setq(1,edit(%q1,>,\]))][setq(1,edit(%q1,\\\%r,<BR>))][setq(1,edit(%q1,\\\%R,<BR>))]<P><HR><P><a name="[after(##,_)]"><CENTER><B>[extract(%q1,1,1,|)]</B></CENTER><BR><BR><B>Dateline:</B>%b[extract(%q1,2,1,|)]<BR><BR>[extract(%q1,3,1,|)])])
&CMD-+com-DELDIR Com Staff Commands=$+com/deldir *:think pemit(%#,switch(0,hasattr(#COMMDB,dir-[trim(%0)]),ansi(hr,There is no such directory listing.),[ansi(hg,You remove the directory listing for comcode %0.)][wipe(#COMMDB/dir-[trim(%0)])]))
&CMD-+com-DELJOB Com Staff Commands=$+com/deljob */*:think [setq(0,[extract(xget(#COMMDB,list_orgs),%0,1)])][pemit(%#,switch(0,isnum(%0),ansi(hr,You must enter a number.),isnum(%1),ansi(hr,You must enter a number.),lte(%0,words(xget(#COMMDB,list_orgs))),ansi(hr,There aren't that many organizations!),lte(%1,words(lattr(#COMMDB/%q0_*))),ansi(hr,That is not a valid job number.),[wipe(#COMMDB/[extract(sort(lattr(#COMMDB/%q0_*)),%1,1)])][ansi(hg,You delete job %1 from org %0.)]))]
&CMD-+com-DELLINK Com Staff Commands=$+com/dellink *:think [setq(0,pmatch(%0))]pemit(%#,switch(0,t(%q0),ansi(hr,That is not a player.),match(xget(#COMMDB,link_holders),%q0),ansi(hr,That person doesn't have a commlink.),[ansi(hg,You remove %0's commlink.)][set(#COMMDB,link_holders:[remove(xget(#COMMDB,link_holders),%q0)])]))
&CMD-+com-DELORG Com Staff Commands=$+com/delorg *:think [setq(0,[extract(xget(#COMMDB,list_orgs),%0,1)])][pemit(%#,switch(0,isnum(%0),ansi(hr,You must enter a number.),isnum(%1),ansi(hr,You must enter a number.),lte(%0,words(xget(#COMMDB,list_orgs))),ansi(hr,There aren't that many organizations!),ansi(hg,You delete organization %0.)[set(#COMMDB,list_orgs:[remove(xget(#COMMDB,list_orgs),%q0)])][wipe(#COMMDB/%q0)][wipe(#COMMDB/contact_%q0)][iter(lattr(#COMMDB/%q0_*),wipe(#COMMDB/##))]))
&CMD-+com-DISABLE Com Staff Commands=$+com/disable *:think pemit(%#,switch(0,hastype(%0,room),ansi(hr,That is not a room!),not(match(xget(#COMMDB,rooms_without_Com),%0)),ansi(hr,That room already has Com disabled.),[ansi(hg,You disable Com in room %0.)][set(#COMMDB,rooms_without_Com:[xget(#COMMDB,rooms_without_Com)]%B%0)]))
&CMD-+com-ENABLE Com Staff Commands=$+com/enable *:think pemit(%#,switch(0,hastype(%0,room),ansi(hr,That is not a room!),match(xget(#COMMDB,rooms_without_Com),%0),ansi(hr,That room already has Com enabled.),[ansi(hg,You enable Com in room %0.)][set(#COMMDB,rooms_without_Com:[remove(xget(#COMMDB,rooms_without_Com),%0)])]))
&CMD-+com-LISTLINK Com Staff Commands=$+com/listlink:think pemit(%#,[u(#COMMDB/Com_header)]%R[ansi(h,Commink Holders)]%R[iter(xget(#COMMDB,link_holders),%R[name(##)])]%R[u(#COMMDB/bottom_line)]
&CMD-+com-MSGCHECK Com Staff Commands=$+com/msgcheck:@pemit %#=[ansi(h,[padstr(Rooms with messages,60)]%B[padstr(DB#,5)]%B[padstr(Msgs,10)])]%R[repeat(-,78)];@dolist lsearch(all,type,rooms)={@switch add(words(lattr(##/msg*)),words(lattr(##/oldmsg*)))=>0,@pemit %#=[padstr(name(##),60)]%B[padstr(##,5)]%B[padstr(add(words(lattr(##/msg*)),words(lattr(##/oldmsg*))),10)]}
&CMD-+com-REJECT Com Staff Commands=$+com/reject */*:think [setq(0,trim(%0))]switch(0,lte(%1,words(sort(lattr(#COMMDB/submit_%q0_*)))),pemit(%#,ansi(hr,No such article.)),pemit(%#,ansi(c,You reject %0 submission %1.  Remember to send the author an mail explaining why the article was rejected.))[setq(1,extract(sort(lattr(#COMMDB/submit_%q0_*)),trim(%1),1))][wipe(#COMMDB/%q1)])
&CMD-+com-REMOVE Com Staff Commands=$+com/remove */*:think [setq(0,trim(%0))]switch(0,isnum(%1),pemit(%#,ansi(hr,That is not a valid article number.)),lte(%1,words(lattr(#COMMDB/%q0_1_*))),pemit(%#,ansi(hr,There aren't that many articles!)),pemit(%#,[wipe(#COMMDB/[extract(sort(lattr(#COMMDB/%q0_1_*),n),%1,1)])]You remove %0 article %1.)
&CMD-+com-REVIEW Com Staff Commands=$+com/review:think switch(%0,*/*,,pemit(%#,[u(#COMMDB/Com_header)][iter(xget(#COMMDB,board_list),[setq(0,##)]%R%R[ansi(h,%q0 Articles Awaiting Approval)]%r[iter(sort(lattr(#COMMDB/submit_%q0_*)),%r[inum(0)]. [u(#COMMFUN/fun_board_title,itext(0))])])]%r%rType +com/review <board>/<#> to view an article.%R[u(#COMMDB/bottom_line)])
&CMD-+com-REVIEW-DETAIL Com Staff Commands=$+com/review */*:think [setq(0,trim(%0))]switch(0,lte(%1,words(sort(lattr(#COMMDB/submit_%q0_*)))),pemit(%#,ansi(hr,No such article.)),pemit(%#,[u(#COMMDB/Com_header)]%r[ansi(h,Article %1)]%r[setq(1,extract(sort(lattr(#COMMDB/submit_%q0_*)),trim(%1),1))]"[u(#COMMFUN/fun_board_title,%q1)]" - submitted by [u(#COMMFUN/fun_board_submitter,%q1)]%r%r%t[u(#COMMFUN/fun_board_date,%q1)]%r%r[u(#COMMFUN/fun_board_text,%q1)]%r[u(#COMMDB/bottom_line)]))
&ISSTAFF Com Staff Commands=isstaff(%#)


@pemit/silent %#=[ansi(hr,Setting up database numbers...)]%R

@fo me=@edit Com Staff Commands/*=#COMMFUN,[num(Com Functions)]
@fo me=@edit Com System/*=#COMMFUN,[num(Com Functions)]
@fo me=@edit Com DB/*=#COMMFUN,[num(Com Functions)]
@fo me=@edit Com Functions/*=#COMMFUN,[num(Com Functions)]

@fo me=@edit Com Staff Commands/*=#COMMDB,[num(Com DB)]
@fo me=@edit Com System/*=#COMMDB,[num(Com DB)]
@fo me=@edit Com DB/*=#COMMDB,[num(Com DB)]
@fo me=@edit Com Functions/*=#COMMDB,[num(Com DB)]


@wait 4=@pemit/silent %#=[ansi(hr,Copying help files...)]%R
@wait 4=@fo %#=+copyhelp [num(Com DB)]

@wait 5=@pemit/silent %#=[ansi(hr,Moving objects to their appropriate places...)]%R

@wait 6=@tel Com Functions=Com System
@wait 7=@tel Com DB=Com System
@wait 8=@tel Com Staff Commands=#2
@wait 9=@tel Com System=#2

@fo %#=+jobs/newcat COM

@wait 10=@pemit/silent %#=%R%R[ansi(hr,Installation Complete!!)]

@wait 11=@set me=!quiet

