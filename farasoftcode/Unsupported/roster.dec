@@ Faraday's Roster System
@@ Requires: Global Functions, Jobs, Help

@set me=quiet

@pemit/silent %#=[ansi(hr,Beginning Roster System Installation.  Please wait until the Installation Complete message appears...)]%R

@pemit/silent %#=[ansi(hr,Creating Objects...)]%R


@create Roster System
@set Roster System = WIZARD
@set Roster System = SAFE
@set Roster System = !NO_COMMAND
&CMD-+ROSTER-ADD Roster System=$+roster/add *:think pemit(%#,[setq(0,pmatch(%0))]switch(0,t(%q0),ansi(hr,That is not a valid player.),isstaff(%#),ansi(hr,This command is restricted to staff only.),not(hasflag(%q0,unregistered)),ansi(hr,You can only add characters that have gone through chargen and been approved.),not(match(v(roster_db),%q0)),ansi(hr,That player is already on the list.),ansi(hg,You add %0 to the list of available players.)[set(me,roster_db:[v(roster_db)]%q0%B)]))
&CMD-+ROSTER-CLAIM Roster System=$+roster/claim *=*:think pemit(%#,[setq(0,pmatch(%0))]switch(0,t(%q0),ansi(hr,That is not a valid player.),match(v(roster_db),%q0),ansi(hr,That player is not on the list.),not(xget(%q0,roster_claimed)),ansi(hr,That character has already been claimed by someone else.),ansi(hg,You apply for %0.  You can check the status of your application using +request.)[set(%q0,roster_claimed:1)][set(%#,roster_choice:%q0)][create_job(APP,Roster Claim - [capstr(%0)],[capstr(%0)] has been claimed.%R%R[ansi(h,Application:)]%R%R{%1},%#)]))
&CMD-+ROSTER-APPROVE Roster System=+roster/approve *:think pemit(%#,[setq(1,0)][setq(0,pmatch(%0))]switch(0,t(%q0),ansi(hr,That is not a valid player.),match(v(roster_db),%q0),ansi(hr,That player is not on the list.),isstaff(%#),ansi(hr,This command is restricted to staff.),ansi(hg,You release %0.)[set(%q0,roster_claimed:0)][set(me,roster_db:[remove(v(roster_db),%q0)])][setq(2,rand(5000))][setq(1,1)]));@switch %q1=0,,1,{@newpassword %0=welcome%q2;@pemit %#=[ansi(hg,Send a mail to the player telling them that the password is welcome%q2 so they can log in.)]}
&CMD-+ROSTER-BG Roster System=$+roster/bg *:think pemit(%#,switch(0,not(match(%0,*/*)),,u(fun_display_bg,%0,1)))
&CMD-+ROSTER-BG-PAGE Roster System=$+roster/bg */*:think pemit(%#,u(fun_display_bg,%0,%1))
&HELP_+ROSTER Roster System=The roster system allows players to view a list of available characters and claim the one you want.  Staff members will be notified of the claim and review your application.  %R%RThe exact application process will vary by game.  It may be as simple as "I want to play this character" or it may require a lengthy application letter and/or RP tests or other things.  Review the policies on your game for details.%R%R[ansi(h,+roster)] - Views the first page of the roster.%R[ansi(h,+roster <page>)] - Views other pages of the roster.%R[ansi(h,+roster/claim <character>=<application>)] - Claims a character.%R[ansi(h,+roster/bg <character>)] - Views the first page of a character's background.%R[ansi(h,+roster/bg <character>/<page>)] - Views other pages of a character's background.info.%R[ansi(h,+roster/sheet <character>)] - Views a character sheet of someone on the roster.
&SHELP_+ROSTER Roster System=[ansi(h,+roster/add <character>)] - Adds a character to the roster list.%R[ansi(h,+roster/remove <character>)] - Removes a character from the roster list.%R[ansi(h,+roster/release <character>)] - Releases a claim on a character.
&CMD-+ROSTER-LIST Roster System=$+roster:think pemit(%#,[setq(0,add(div(words(v(roster_db)),25),1))][u(fun_display_roster_page,1,%q0)])
&CMD-+ROSTER-LIST2 Roster System=$+roster *:think pemit(%#,[setq(0,add(div(words(v(roster_db)),25),1))]switch(0,isnum(%0),ansi(hr,That is not a valid number.),gt(%0,0),ansi(hr,That is not a valid number.),lte(%0,%q0),ansi(hr,There aren't that many players.),u(fun_display_roster_page,%0,%1)))
&CMD-+ROSTER-RELEASE Roster System=$+roster/release *:think pemit(%#,[setq(0,pmatch(%0))]switch(0,t(%q0),ansi(hr,That is not a valid player.),match(v(roster_db),%q0),ansi(hr,That player is not on the list.),isstaff(%#),ansi(hr,This command is restricted to staff.),ansi(hg,You release %0.)[set(%q0,roster_claimed:0)]))
&CMD-+ROSTER-REMOVE Roster System=$+roster/remove *:think pemit(%#,[setq(0,pmatch(%0))]switch(0,t(%q0),ansi(hr,That is not a valid player.),isstaff(%#),ansi(hr,This command is restricted to staff only.),match(v(roster_db),%q0),ansi(hr,That player is not on the list.),ansi(hg,You remove %0 from the list of available players.)[set(me,roster_db:[remove(v(roster_db),%q0)])]))
&CMD-+ROSTER-SHEET Roster System=$+roster/sheet *:think pemit(%#,[setq(0,pmatch(%0))]switch(0,t(%q0),ansi(hr,That is not a valid player.),match(v(roster_db),%q0),ansi(hr,That player is not on the list.),u(fun_display_sheet,%q0)))
@set Roster System/DESCRIBE=no_command visual prefixmatch public nearby
&FUN_DISPLAY_BG Roster System=[setq(0,pmatch(%0))][setq(1,words(lattr(%q0/bg*)))]switch(0,t(%q0),ansi(hr,That is not a valid player.),match(v(roster_db),%q0),ansi(hr,That player is not on the list.),lte(%1,%q1),ansi(hr,That is not a valid background section.),gt(%1,0),ansi(hr,That is not a valid background section.),isnum(%1),ansi(hr,That is not a valid background section.),[line_with_text(Roster System)]%R[ansi(h,[capstr(%0)]'s Background - Page %1 of [words(lattr(%q0/bg*))])]%R[repeat(-,78)]%R[u(%q0/[extract(lattr(%q0/bg*),%1,1)])]%R[line_with_text(+help +roster)])
&FUN_DISPLAY_ROSTER_PAGE Roster System=[line_with_text(Roster System)]%R[ansi(h,Available Characters)] - [ansi(hg,Page %0)]%R[iter(extract(v(roster_db),add(1,mul(sub(%0,1),25)),25),%R[switch(xget(##,roster_claimed),1,[ansi(hg,\(C\))]%B,%B%B%B%B)][ljust(name(##),25)][mid(xget(##,concept),0,50)])]%R%R[ansi(hg,\(C\) = Claim Pending)][switch(1,gt(%q0,%0),[rjust(Continued in [ansi(h,+roster [inc(%0)])],55)])]%R[line_with_text(+help +roster)]
&ROSTER_DB Roster System=
&FUN_DISPLAY_SHEET Roster System=ansi(hr,Character Sheet Not Implemented)
&VERSION Roster System=4.0
&CREDITS Roster System=Version [v(version)]%R%RCoded by Faraday ~ http://www.aresmush.com
@DESCRIBE Roster System=Commands for a basic character roster.%R%R[u(credits)]

@wait 2=@pemit/silent %#=[ansi(hr,Setting up database numbers...)]%R

@wait 3=@pemit/silent %#=[ansi(hr,Copying help files...)]%R
@wait 3=@fo %#=+copyhelp [num(Roster System)]


@pemit/silent %#=[ansi(hr,Moving objects to their appropriate places...)]%R

@wait 5=@tel Roster System=#2

@wait 5=@pemit/silent %#=%R%R[ansi(hr,Installation Complete!!)]%R

@wait 5=@set me=!quiet

