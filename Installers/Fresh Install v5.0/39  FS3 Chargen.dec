@@ Faraday's FS3 System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.


@@ !!!!!!!!!!!!!!!!!!!! IMPORTANT NOTE !!!!!!!!!!!!!!!!!!!!!!
@@ Unlike the other systems, this one does not automatically do upgrades since all it does is create rooms.

@set me=quiet

@pemit/silent %#=[ansi(hr,Installing FS3 Chargen rooms.  Please wait...)]%R

@@ ========================
@@ BEGIN OUTSIDE CG ROOM
@@ ========================

@dig Chargen=CG;chargen,B;back

@lock Chargen=ISOK/1
@power Chargen = See_All
&CHARGEN_OPEN Chargen=1
@FAILURE Chargen=switch(v(chargen_open),0,Chargen isn't open yet.,Only new players may enter chargen.)
@set Chargen/FAILURE=no_command prefixmatch
&ISOK Chargen=or(isstaff(%#),and(orflags(%#,?),not(haspower(%#,guest)),v(chargen_open)))

CG

@@ ========================
@@ IN CHARGEN ROOM
@@ ========================

@set here = NO_COMMAND
&CG here=1
@DESCRIBE here=The next few rooms will guide you through the process of creating your character.  Each room will give you instructions about what to set.  Before starting, you should have a good idea of the character you wish to create.  You can always go back to a previous room if you need to change something (without losing your other stats).  If you have any questions, ask on the Newbie channel by typing [ansi(hg,+n <message>)].%R%RIf this is an alt character, be sure to register them by using the [ansi(hc,+alts)] command.

@dig Demographics=N;next,B;back

N

@@ ========================
@@ IN DEMOGRAPHICS ROOM
@@ ========================

&CG here=1
@DESCRIBE here=In this room, you will set up some basic info about your character's background - how old he is, his job, etc.  This is all subject to approval - you can make yourself an Admiral in chargen, but you won't be approved!%R%R%T[ansi(h,+fullname <full name>)]%R%T[ansi(h,+age <age>)]%R%T[ansi(h,@sex <male or female>)]%R%T[ansi(h,+faction <faction>)] (See +factions) %R%T[ansi(h,+position <position>)] (See +positions)%R%T[ansi(h,+org <organization>)] (see +orgs)%R%T[ansi(h,+rank <rank>)] (see +ranks - military chars only)%R%RIf you have an actor in mind for your character, you can set them using the [ansi(hc,+actors)] command.
@set here/DESCRIBE=no_command visual prefixmatch public nearby

@dig Abilities=N;next,B;back

@parent N=[install_aliasdb(FS3SKILL_CGEXIT)]
@lock N=ISDONE/1
@power Next = See_All
&OTHER_CHECKS Next=switch(match(xget(%#,faction),Military),1,t(words(rank(%#))),1)
&OTHER_ERRORS Next=Military characters must have a rank.
&REQUIRED_ATTRS Next=faction org position birthdate fullname sex


@@ ========================
@@ In ABILITIES ROOM
@@ ========================

@tel loc(N)

&CG here=1
@DESCRIBE here=Here you will set up your character's Abilities, including Attributes, Action Skills, Background Skills, Language Skills and Quirks.   See [ansi(hg,+help FS3_Chargen)] for help with Chargen.%R%R%T[ansi(h,+reset)] - Starts setting abilities, erasing any current ones.%R%T[ansi(h,+sheet)] - Views your character sheet%R%T[ansi(h,+points)] - Views your points remaining%R%T[ansi(h,+limits)] - Views limits on what you can spend.%R%R%T[ansi(h,+abilities)] - Views all abilities%R%T[ansi(h,+raise <type>/<ability>)] and [ansi(h,+lower <type>/<ability>)]%R%T[ansi(h,+ability <type>/<ability>=<level>)]%R%T- type must be one of attr, action, bg, lang%R%T- type may be omitted for action skills and attributes%R%R%T[ansi(h,+quirk/add <quirk>)] and [ansi(h,+quirk/delete <quirk>)]%R%R%TYou may also want to configure ruling attributes for your%R%Tbackground skills.  See +help +rulingattr

@dig Background=N;next,B;back

@parent N=[install_aliasdb(FS3SKILL_CGEXIT)]
@lock N=ISDONE/1
@power Next=see_all
&OTHER_CHECKS Next=strmatch(xget(%0,cg_errors),1*)
&OTHER_ERRORS Next=after(xget(%0,cg_errors),|)

@tel loc(N)

@@ ========================
@@ In BACKGROUND ROOM
@@ ========================

&CG here=1
@DESCRIBE here=This is the room where you set up your character's backstory, or background.  Make sure that your background matches your character's skills and other information.%R%RYour background is just another MUSH attribute on yourself, like your description. You can set it by typing [ansi(hg,&bg me=<your background text>)]. You can use the built-in MUSH editing commands (@edit, @dec, etc.) to edit it.%R%RIf your background is particularly long, you can split it into multiple attributes, like:%R%T[ansi(h,&BG1 me=This is the first part of my bg.)]%R%T[ansi(h,&BG2 me=This is the second part of my bg.)]%R%Tetc.%R%RThe [ansi(h,+bg)] command will let you view your background.  Use [ansi(hg,+bg/submit)] to submit it when you've finished.

@dig Approval=N;next,B;back

@parent N=[install_aliasdb(FS3SKILL_CGEXIT)]
@lock N=ISDONE/1
@power Next = See_All
&OTHER_CHECKS Next=or(hasattr(%0,bg),hasattr(%0,bg1))
&OTHER_ERRORS Next=- You must set your background to proceed.

@tel loc(N)

@@ ========================
@@ In APPROVAL ROOM
@@ ========================

@DESCRIBE here=If you're finished with everything, type [ansi(hg,+bg/submit)] to submit your character for approval.  While you're waiting to be approved, you can hang out in the OOC Center.  Just go through the OOC exit.  If there are any problems with your character, you will be able to re-enter chargen later to fix them.%R%RIf you realize you forgot something, you can use [ansi(h,+bg/unsubmit)] to withdraw your application and go back to make changes.

@open Done;D;N;next=#0
@parent D=[install_aliasdb(FS3SKILL_CGEXIT)]
@lock D=ISDONE/1
@power Done = See_All
&OTHER_CHECKS Done=or(t(xget(%0,approval_job)),not(hasflag(%0,unregistered)))
&OTHER_ERRORS Done=- You must submit your character for approval first.

@lock Back=ISSUBMITTED/0
@power Back = See_All
@FAILURE Back=You must unsubmit your background first.
&ISSUBMITTED Back=t(xget(%#,approval_job))


+jobs/create BUILD=Chargen OOC Exit/Link the 'done' exit from chargen to the appropriate room in the OOC area.

@wait 5=@set me=!quiet
@wait 5=@pemit/silent %#=%R%R[ansi(hg,FS3 Chargen rooms installed.)]