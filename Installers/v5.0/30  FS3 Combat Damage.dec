@@ Faraday's FS3 System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet
@pemit/silent %#=%R%R[ansi(hg,Installing FS3 Combat Damage module.)] [ansi(hc,NOTE: This may take a minute.)]

think install_create(FS3COMBAT_CMD_DMG,FS3Combat Damage Commands,1,0,5.0,FS3 Damage Commands.  See the FS3Combat Action Commands object for important copyright/license information.)
think install_create(FS3COMBAT_DB_DMG,FS3Combat Damage Database,0,1,5.0,FS3Combat Damage Database. See the FS3Combat Action Commands object for important copyright/license information.,FS3_STORAGE)

@@  @@@@@@@@@@@@@@@@@@@@@@
@@  @ DAMAGE VIEW COMMANDS
@@  @@@@@@@@@@@@@@@@@@@@@@

&CMD-+DAMAGE FS3Combat Damage Commands=$+damage:@pemit %#=[line()]%R[ansi(hr,Damage Report for %N)]%R[u(fun_dmglist,%N)]%R%R[ansi(h,Total Wound Modifier:)] -[damagemod(%N)] [u(fun_display_restricted,%N)][space(15)][ansi(h,Healed By:)] [u(fun_healed_by,%N)]%RSee [ansi(h,+damage <#>)] for details on a given wound.%R[line()]

&CMD-+DAMAGE-DETAIL FS3Combat Damage Commands=$+damage *:@pemit %#=switch(0,isnum(%0),,gt(%0,0),ansi(hr,That is not a valid wound number.),lte(%0,words(u(FUN_ALL_DMG,%N))),ansi(hr,You don't have that many wounds.),[line()]%R[ansi(hr,Damage Report for %N wound %0)]%R[u(fun_dmg_detail,u(fun_get_dmgnum,%N,%0))]%R[line()]

&CMD-+DAMAGE-PERSON FS3Combat Damage Commands=$+damage *:@pemit %#=switch(1,isnum(%0),,strmatch(%0,*/*),,eq(words(u(FUN_ALL_DMG,%0)),0),ansi(hr,No damage report available.  Make sure %0 is a valid PC\, NPC\, or vehicle name.  It could be they have never taken any damage.),[line()]%R[ansi(hr,Damage Report for [prettify(%0)])]%R[u(fun_dmglist,%0)]%R%R[ansi(h,Total Wound Modifier:)] -[damagemod(%0)] [u(fun_display_restricted,%0)][space(15)][ansi(h,Healed By:)] [u(fun_healed_by,%0)]%RSee [ansi(h,+damage <name>/<#>)] for details on a given wound.%R[line()]

&CMD-+DAMAGE-PERSON-DETAIL FS3Combat Damage Commands=$+damage */*:@pemit %#=switch(0,isnum(%1),ansi(hr,That is not a valid wound number.),gt(%1,0),ansi(hr,That is not a valid wound number.),lte(%1,words(u(FUN_ALL_DMG,%0))),ansi(hr,%0 doesn't have that many wounds.),[line()]%R[ansi(hr,Damage Report for [prettify(%0)] wound %1)]%R[u(fun_dmg_detail,u(fun_get_dmgnum,%0,%1))]%R[line()]

@@  @@@@@@@@@@@@@@@@@@@@@@
@@   DAMAGE MOD COMMANDS
@@  @@@@@@@@@@@@@@@@@@@@@@

&CMD-+DAMAGE-MODLIST FS3Combat Damage Commands=$+damage/mod:@pemit %#=[line()]%R[ansi(h,Damage values that can be modified:)]%R[ljust(real,12)] 1 for real, 0 for mock%R[ljust(hitloc,12)] Hit location%R[ljust(weapon,12)] Weapon used%R[ljust(type,12)] Physical or stun%R[ljust(initsev,12)] Initial severity (C/S/M/L/H)%R[ljust(currsev,12)] Current severity (C/S/M/L/H)%R[ljust(healing,12)] Healing points%R[ljust(lasttreat,12)] Last treated (secs)%R[ljust(notes,12)] Wound notes%R%RC=Critical, S=Serious, M=Moderate, L=Light, H=Healed%RUse [ansi(h,+damage/mod <name>/<#>=<stat, from list above>/<newvalue>)]%R[line()]

&CMD-+DAMAGE-MOD FS3Combat Damage Commands=$+damage/mod */*=*/*:@pemit %#=[setq(0,u(fun_get_dmgnum,%0,%1))]switch(0,isstaff(%#),ansi(hr,This command is for staff only.),t(%q0),ansi(hr,%0/%1 is not a valid damage value.),match(v(dmgstats),trim(%2)),ansi(hr,%2 is not a valid damage stat to modify.),u(fun_valid_dmgstat_[trim(%2)],%3),ansi(hr,%3 is not a valid value for the %2 statistic.),ansi(hg,You modify [prettify(%0)]'s damage.)[u(fun_mod_dmgstat,%q0,trim(%2),prettify(escape_cr(%3)))])

&CMD-+DAMAGE-MOVE FS3Combat Damage Commands=$+damage/move *=*:@pemit %#=[setq(0,u(fun_get_dmgnum,%N,%0))][switch(0,t(%q0),ansi(hr,That is not a valid damage number.),gt(xget(%#,luck_points),0),ansi(hr,You do not have any luck points.  Contact staff.),lte(u(fun_minutes_since_injury,%q0),10),ansi(hr,You only have 10 minutes to move a wound using this command.  Contact staff.),ansi(hg,You spend a luck point to move wound %0 to hit location %1.)[set(%#,luck_points:[sub(xget(%#,luck_points),1)])][u(fun_mod_dmgstat,%q0,hitloc,prettify(%1))])]

&CMD-+DAMAGE-NOTES FS3Combat Damage Commands=$+damage/notes *=*:@pemit %#=[setq(0,u(fun_get_dmgnum,%N,%0))][switch(0,t(%q0),ansi(hr,That is not a valid damage number.),ansi(hg,You modify the notes on wound %0.)[u(fun_mod_dmgstat,%q0,notes,escape_cr(prettify(%1)))])]

&CMD-+DAMAGE-DELETE FS3Combat Damage Commands=$+damage/delete */*:@pemit %#=[setq(0,u(fun_get_dmgnum,%0,%1))]switch(0,isstaff(%#),ansi(hr,This command is for staff only.),t(%q0),ansi(hr,%0/%1 is not a valid damage value.),ansi(hg,You delete [prettify(%0)]'s damage.)[wipe(#FS3COMBAT_DB_DMG/%q0)])

&CMD-+DAMAGE-CLEAR FS3Combat Damage Commands=$+damage/clear *:@pemit %#=switch(0,isstaff(%#),ansi(hr,This command is for staff only.),gt(words(u(FUN_ALL_DMG,%0)),0),ansi(hr,No damage report available.  Make sure %0 is a valid PC\, NPC\, or vehicle name.  It could be they have never taken any damage.),ansi(hg,You clear all of [prettify(%0)]'s damage.)[u(fun_clear_damage,%0)])

@@  %0 = Name 
@@  %1 = Real (1) or Mock (0) Damage
@@  %2 = Hit location
@@  %3 = Weapon
@@  %4 = Physical or Stun damage
@@  %5 = Severity (C/S/M/L), applied to both initial and current severity

&CMD-+DAMAGE-INFLICT FS3Combat Damage Commands=$+damage/inflict *=*/*/*/*/*:@pemit %#=switch(0,isstaff(%#),ansi(hr,This command is only for staff.),match(real mock,%1),ansi(hr,Damage type must be mock or real.),match(physical stun,%4),ansi(hr,Damage type must be physical or stun.),match(C S M L,%5),ansi(hr,Damage severity must be C S M L.),ansi(hg,You inflict damage upon %0)[u(fun_do_damage,%0,[switch(%1,real,1,0)],prettify(%2),prettify(%3),prettify(%4),capstr(%5))])


&CMD-+DAMAGE-INFLICT-CATCH FS3Combat Damage Command=$+damage/inflict *=*:@pemit %#=switch(words(%1,/),5,,[ansi(h,<COMBAT>)] [ansi(hr,The syntax is +damage/inflict <name>=<real or mock>/<hit location>/<weapon>/<physical or stun>/<C S M or L for severity>)])


@@  @@@@@@@@@@@@@@@@@@@@@@
@@   HEALING COMMANDS
@@  @@@@@@@@@@@@@@@@@@@@@@

&CMD-+DAMAGE-SCAN FS3Combat Damage Commands=$+damage/scan:@pemit %#=[line()]%R[ansi(h,Injured Characters In [name(%l)])]%R[ansi(h,Name[space(16)]Can Treat[space(4)]Healed By[space(5)]Damage Mod[space(5)]Status)][iter(lcon(%l),[setq(0,name(##))]switch(0,or(isplayer(##),isvehicle(%q0)),,damagemod(%q0),,%R[ljust(name(##),20)][ljust(switch(u(fun_any_can_be_treated,##),1,+,-),10)][ljust(u(fun_healed_by,##),15)][ljust(-[damagemod(%q0)],15)][u(fun_display_restricted,%q0)]))]%R%RSee [ansi(h,+damage <name>)] for a detailed damage report.%R[line()]

&CMD-+TREAT FS3Combat Damage Commands=$+treat *:@pemit %#=[setq(0,u(fun_can_treat,%0,%N))]switch(0,eq(%q0,1),ansi(hr,after(%q0,|)),ansi(hg,You treat %0.)[remit(%l,[ansi(hc,%N treats %0:)][u(fun_treat,%0,%N)])])


@@ %q0 = skill
@@ %q1 = HEAL or REPAIR for attribute name
&CMD-+HEAL FS3Combat Damage Commands=$+heal *:@pemit %#=[setq(0,switch(1,isvehicle(%0),xget(#FS3COMBAT_PREFS,repair_skill)[setq(1,repair)],xget(#FS3COMBAT_PREFS,medicine_skill)[setq(1,heal)]))][switch(0,or(isnpc(%0),isplayer(%0),isvehicle(%0)),ansi(hr,%0 is not a person or vehicle.),not(match(%0,%N)),ansi(hr,Physcian cannot heal thyself.),not(match(xget(#FS3COMBAT_DB_DMG,%q1_%N),unprettify(%0))),ansi(hr,You are already healing %0.),words(u(fun_healable_wounds,%0)),ansi(hr,%0 has no healable wounds.),ability_level(%#,%q0),ansi(hr,You don't have the %q0 skill.),lt(words(xget(#FS3COMBAT_DB_DMG,%q1_%N)),ceil(fdiv(ability_level(%#,%q0),2))),ansi(hr,You have reached your maximum number of [switch(isvehicle(%0),1,vehicles,patients.)]),[pemit(%#,ansi(hg,You will be healing %0 until they are fully healed or until you use +stopheal.))][append(#FS3COMBAT_DB_DMG,%q1_%N,[unprettify(%0)])])

&CMD-+STOPHEAL FS3Combat Damage Commands=$+stopheal *:@pemit %#=[switch(1,isvehicle(%0),[setq(0,repair)],[setq(0,heal)])]switch(0,match(xget(#FS3COMBAT_DB_DMG,%q0_%N),unprettify(%0)),ansi(hr,You are not healing %0.),ansi(hg,You stop healing %0.)[set(#FS3COMBAT_DB_DMG,%q0_%N:[remove(xget(#FS3COMBAT_DB_DMG,%q0_%N),unprettify(lcstr(%0)))])])

&CMD-+HEALING FS3Combat Damage Commands=$+healing:@pemit %#=[line()]%R[ansi(h,Current Patients)][space(15)][ansi(h,Damage Modifier)][space(4)][ansi(h,Status)]%R[line2()][iter(xget(#FS3COMBAT_DB_DMG,heal_%N),%R[padstr(prettify(##),35)][ljust(-[damagemod(##)],15)][u(fun_display_restricted,##)])]%R%R[ansi(h,Current Vehicles)][space(15)][ansi(h,Damage Modifier)][space(4)][ansi(h,Status)]%R[line2()][iter(xget(#FS3COMBAT_DB_DMG,repair_%N),%R[padstr(prettify(##),35)][ljust(-[damagemod(##)],15)][u(fun_display_restricted,##)])]%R[line()]

&CMD-+REPAIR-ERROR FS3Combat Damage Commands=$+repair*:@pemit %#=ansi(hm,The +repair command is obsolete.  Use +heal instead.)

&CMD-+HEAL-ERROR FS3Combat Damage Commands=$+heal */*:@pemit %#=ansi(hm,+heal no longer requires a specific wound number.)

&CMD-+TREAT-ERROR FS3Combat Damage Commands=$+treat */*:@pemit %#=ansi(hm,+treat no longer requires a specific wound number.)

@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_CAN_TREAT
@@  %0 = target
@@  %1 = healer
@@  Returns 1 or 0|Reason
@@    %q0 = skill required, based on target type
@@    %q1 = healer skill level, based on PC or NPC
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


&FUN_CAN_TREAT FS3Combat Damage Commands=localize([setq(0,switch(1,isvehicle(%0),xget(#FS3COMBAT_PREFS,juryrig_skill),isnpc(%0),xget(#FS3COMBAT_PREFS,firstaid_skill),isplayer(%0),xget(#FS3COMBAT_PREFS,firstaid_skill),#-1))][setq(1,switch(isplayer(%1),1,ability_level(pmatch(%1),%q0),npcstat(%1,skill)))][switch(0,t(%1),0|Invalid healer.,t(%q0),0|This command may only be used on people or vehicles.,%q1,0|You don't have the [prettify(%q0)] skill.,u(fun_any_can_be_treated,%0),0|[prettify(%0)] has no treatable wounds.,1)])

@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_TREAT
@@  Treats all available wounds on a person or vehicle.
@@  %0 = target
@@  %1 = healer
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


&FUN_TREAT FS3Combat Damage Commands=iter(u(fun_all_dmg,%0),switch(u(fun_can_be_treated,##),1,u(fun_treat_wound,##,%1)))

@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_TREAT_WOUND
@@  Treats an individual wound.
@@  %0 = dmg attr
@@  %1 = healer
@@    %q0 = skill required, based on target type
@@    %q1 = healer skill level, based on PC or NPC
@@    %q2 = skill roll
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


&FUN_TREAT_WOUND FS3Combat Damage Commands=localize([setq(0,switch(isvehicle(%0),1,xget(#FS3COMBAT_PREFS,juryrig_skill),xget(#FS3COMBAT_PREFS,firstaid_skill)))][setq(1,switch(isplayer(%1),1,ability_level(pmatch(%1),%q0),npcstat(%1,skill)))][setq(2,roll_ability(pmatch(%1),%q1,xget(#FS3COMBAT_PREFS,hero_factor)))][u(fun_mod_dmgstat,%0,lasttreat,secs())][u(fun_mod_dmgstat,%0,healing,switch(%q2,-2,-0.5,-1,-0.25,0,0,1,0.5,2,0.5,3,1,0))]%R< [u(fun_dmgstat,%0,hitloc)] \([u(fun_wound_name,u(fun_dmgstat,%0,currsev))]\): [switch(%q2,>0,ansi(hg,successful),ansi(hr,unsuccessful))])

@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_HEAL
@@  Treats all wounds
@@  %0 = patient
@@  %1 = healer
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


&FUN_HEAL FS3Combat Damage Commands=iter(u(fun_healable_wounds,%0),u(fun_heal_wound,##,%1))
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_HEAL_WOUND
@@  Treats an individual wound.
@@  %0 = dmg attr
@@  %1 = healer
@@    %q0 = skill required, based on target type
@@    %q1 = healer skill level, based on PC or NPC
@@    %q2 = skill roll
@@    %q3 = current healpoints
@@    %q4 = points just healed
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


&FUN_HEAL_WOUND FS3Combat Damage Commands=localize([setq(0,switch(isvehicle(%0),1,xget(#FS3COMBAT_PREFS,repair_skill),xget(#FS3COMBAT_PREFS,medicine_skill)))][setq(1,switch(isplayer(%1),1,ability_level(pmatch(%1),%q0),npcstat(%1,skill)))][setq(2,roll_ability(pmatch(%1),%q1,xget(#FS3COMBAT_PREFS,hero_factor)))][setq(3,u(fun_dmgstat,%0,healing))][setq(4,switch(%q2,-2,-1,-1,-0.5,0,0,1,1.75,2,2.25,3,2.75,0))][u(fun_mod_dmgstat,%0,healing,add(%q3,%q4))][u(fun_healing_log,%1 healed %0: old=%q3 plus=%q4)])


&FUN_HEALED_BY FS3Combat Damage Commands=prettify(lcstr(iter(lattr(#FS3COMBAT_DB_DMG/[switch(isvehicle(%0),1,repair,heal)]_*),switch(1,t(match(xget(#FS3COMBAT_DB_DMG,##),unprettify(%0))),after(##,_))))


&FUN_HEALING_LOG FS3Combat Damage Commands=switch(v(debug_healing_log),1,[setq(0,secs())]set(#FS3COMBAT_DB_DMG,heallog_%q0:[xget(#FS3COMBAT_DB_DMG,heallog_%q0)]%0|))

@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@   TR_DAILY_HEALING
@@   Goes through every unhealed wound in the database and applies daily healing points.
@@   This is based on a 'toughness' skill roll, with a bonus if you're in a designated
@@   hospital.  NPCs roll their rating.  Vehicles don't have a rating so they just use the
@@   default NPC rating.
@@ 
@@  This is done in seventeen thousand steps because of the goofiness of nested dolists
@@  and the fact that lattr conks out with even a moderately sized list of damage attrs.
@@  Argh.  
@@    Step 1 = Apply healing points from docs (uses TR_HEAL_PATIENTS for each doc)
@@    Step 2 = Determine how many groups of 100 damage attrs we have. 
@@    Step 3 = Triggers TR_HEAL_ATTR_GROUP for each group of 100 damage attrs.
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&TR_DAILY_HEALING FS3Combat Damage Commands=@dolist [lattr(#FS3COMBAT_DB_DMG/heal_*)] [lattr(#FS3COMBAT_DB_DMG/repair_*)] NEXTHEALING={@switch ##=NEXTHEALING,@tr me/TR_DAILY_HEALING2,@tr me/TR_HEAL_PATIENTS=##}

&TR_HEAL_PATIENTS FS3Combat Damage Commands=@dolist [xget(#FS3COMBAT_DB_DMG,%0)]=think [u(fun_heal,##,after(%0,_))]

&TR_DAILY_HEALING2 FS3Combat Damage Commands=@dolist [lnum(1,ceil(fdiv(nattr(#FS3COMBAT_DB_DMG/DMG_*),100)))]=@tr me/TR_DAILY_HEALING3=##

&TR_DAILY_HEALING3 FS3Combat Damage Commands=@tr me/TR_HEAL_ATTR_GROUP=xattr(#FS3COMBAT_DB_DMG/DMG_*,add(1,mul(sub(%0,1),100)),100)
@@     %q0 = patient name
@@     %q1 = toughness ability level (or npc rating)
@@     %q2 = current heal points
@@     %q3 = new healing points
@@     %q4 = healing points required for the wound to heal 1 lvl
@@     %q5 = current sev
@@     %q6 = 1 if a wound was totally healed, 0 otherwise.  used to cut down on iterations
&TR_HEAL_ATTR_GROUP FS3Combat Damage Commands=@dolist %0=think [setq(5,u(fun_dmgstat,##,currsev))][switch(%q5,H,,[setq(0,edit(before(after(##,_),_),.,_))][setq(1,switch(1,isplayer(%q0),ability_level(pmatch(%q0),xget(#FS3COMBAT_PREFS,toughness_skill)),isnpc(%q0),npcstat(%q0,skill),5))][setq(2,u(fun_dmgstat,##,healing))][setq(3,add(%q2,switch(roll_ability(NoPlayer,%q1,xget(#FS3COMBAT_PREFS,hero_factor)),-2,0.75,-1,1,0,1,1,1,2,1.25,3,1.5,1),switch(0,match(xget(#FS3COMBAT_PREFS,hospitals),loc(pmatch(%q0))),0,0.5)))][setq(4,u(FUN_HEALPOINTS_WOUND,##))][u(fun_mod_dmgstat,##,healing,%q3)][setq(6,0)][switch(gte(%q3,%q4),1,[u(fun_mod_dmgstat,##,currsev,switch(%q5,C,S,S,M,M,L,L,H[setq(6,1)]))][u(fun_mod_dmgstat,##,healing,sub(%q3,%q4))][switch(%q6,1,[switch(words(u(fun_healable_wounds,%q0)),0,u(fun_clear_healing,%q0))][switch(isplayer(%q0),0,wipe(#FS3COMBAT_DB_DMG/##))])])][u(fun_healing_log,%q0 healed ##: sev:%q5 old:%q2 new:%q3 healedDown:[gte(%q3,%q4)] healedCompletely:%q6)]


@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  @@  NOTE: Most +damage functions expect to be passed in the damage attribute name  @@@@
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&SEVERITIES FS3Combat Damage Commands=C S M L H


@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@   FUN_ALL_DMG
@@   Gets a list of attr names for all the damage suffered by a char/vehicle.
@@   Damage is sorted by date/time, oldest first.
@@   %0 = Name, may be PC, NPC or Vehicle
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_ALL_DMG FS3Combat Damage Commands=sortkey(KEY_SORT_DMG,lattr(#FS3COMBAT_DB_DMG/DMG_[trim(edit(%0,_,.,%B,.))]_*))

&KEY_SORT_DMG FS3Combat Damage Commands=after(after(%0,_),_)


@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@   FUN_GET_DMGNUM
@@  Gets a specific damage attr name from the 'all damage' list for a char/vehicle.
@@  %0 = Name
@@  %1 = Index, 1-based
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_GET_DMGNUM FS3Combat Damage Commands=extract(u(FUN_ALL_DMG,%0),%1,1)


@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@   FUN_CLEAR_MOCK
@@  Clears out any mock damage suffered by someone.  Should be done after a combat. 
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_CLEAR_MOCK FS3Combat Damage Commands=iter(u(fun_all_dmg,%0),switch(u(fun_dmgstat,##,real),0,wipe(#FS3COMBAT_DB_DMG/##)),,)

@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@   FUN_CLEAR_HEALING
@@  Clears out anyone who's marked as healing someone because they're ok now.
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


&FUN_CLEAR_HEALING FS3Combat Damage Commands=iter([lattr(#FS3COMBAT_DB_DMG/heal_*)] [lattr(#FS3COMBAT_DB_DMG/repair_*)],set(#FS3COMBAT_DB_DMG,##:[remove(xget(#FS3COMBAT_DB_DMG,##),unprettify(lcstr(%0)))]))
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_CLEAR_DAMAGE
@@  Clears out ALL damage suffered by someone.
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_CLEAR_DAMAGE FS3Combat Damage Commands=iter(u(fun_all_dmg,%0),wipe(#FS3COMBAT_DB_DMG/##),,)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DMGSTAT
@@  Gets a piece of damage info from a given damage attribute.
@@  %0 = Damage attr name 
@@  %1 = The stat to get, from DMGSTATS.
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&DMGSTATS FS3Combat Damage Commands=real hitloc weapon type initsev currsev healing lasttreat obsolete1 notes

&FUN_DMGSTAT FS3Combat Damage Commands=extract(xget(#FS3COMBAT_DB_DMG,%0),match(v(DMGstats),%1),1,|)


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_VALID_DMGSTAT_xxx
@@  Tells whether a value is valid for the xxx damage statistic.
@@  %0 = The value
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_VALID_DMGSTAT_REAL FS3Combat Damage Commands=match(0 1,%0)

&FUN_VALID_DMGSTAT_HITLOC FS3Combat Damage Commands=1

&FUN_VALID_DMGSTAT_WEAPON FS3Combat Damage Commands=1

&FUN_VALID_DMGSTAT_TYPE FS3Combat Damage Commands=match(physical stun,%0)

&FUN_VALID_DMGSTAT_INITSEV FS3Combat Damage Commands=match(v(severities),%0)

&FUN_VALID_DMGSTAT_CURRSEV FS3Combat Damage Commands=match(v(severities),%0)

&FUN_VALID_DMGSTAT_HEALING FS3Combat Damage Commands=isnum(%0)

&FUN_VALID_DMGSTAT_LASTTREAT FS3Combat Damage Commands=isnum(%0)

&FUN_VALID_DMGSTAT_LASTHEAL FS3Combat Damage Commands=isnum(%0)

&FUN_VALID_DMGSTAT_NOTES FS3Combat Damage Commands=1


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_MOD_DMGSTAT
@@  Modifies a piece of damage info from a given damage attribute.  This is stored back 
@@  on the damage database.
@@  %0 = Damage attr name 
@@  %1 = The stat to set, from DMGSTATS.
@@  %2 = The new value.
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_MOD_DMGSTAT FS3Combat Damage Commands=set(#FS3COMBAT_DB_DMG,%0:[replace(xget(#FS3COMBAT_DB_DMG,%0),match(v(DMGstats),%1),%2,|)])


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_NAME_FROM_ATTR
@@  Gets the person/vehicle name from a damage attr.
@@  %0 = Damage attr name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_NAME_FROM_ATTR FS3Combat Damage Commands=unprettify(before(after(%0,_),_))


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_MINUTES_SINCE_INJURY
@@  How long it's been since the injury occurred.
@@  %0 = Damage attr name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_MINUTES_SINCE_INJURY FS3Combat Damage Commands=div(sub(secs(),extract(%0,3,1,_)),60)


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_CAN_BE_TREATED
@@  Whether the char/vehicle can be treated right now.  True if they haven't already been treated,
@@  and the damage is not more than an hour old.
@@  %0 = Damage attr name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_CAN_BE_TREATED FS3Combat Damage Commands=not(or(u(FUN_DMGSTAT,%0,lasttreat),gt(u(FUN_MINUTES_SINCE_INJURY,%0),60)))

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_ANY_CAN_BE_TREATED
@@  Checks if any of the char/vehicle's injuries can be treated.
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_ANY_CAN_BE_TREATED FS3Combat Damage Commands=localize([setq(0,0)][iter(u(fun_all_dmg,%0),setq(0,or(%q0,u(fun_can_be_treated,##))),,)]%q0)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_HEALABLE_WOUNDS
@@  Whether the person has any healable wounds.
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


&FUN_HEALABLE_WOUNDS FS3Combat Damage Commands=[squish(iter(u(fun_all_dmg,%0),switch(u(FUN_DMGSTAT,##,currsev),H,,##)))]

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DISPLAY_TIME
@@  Shows a damage time, -- if not set.
@@  %0 = Seconds
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_DISPLAY_TIME FS3Combat Damage Commands=switch(%0,,--,convsecs(%0))


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_HEALED_TODAY
@@  Whether the char/vehicle was already healed today.
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_HEALED_TODAY FS3Combat Damage Commands=switch(sub(secs(),u(FUN_DMGSTAT,%0,lastheal)),>86400,0,1)


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DMG_DETAIL
@@  Shows detail for a given damage element.
@@  %0 = Damage attr name.
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_DMG_DETAIL FS3Combat Damage Commands=%R[ansi(h,Wound)]: [u(fun_dmgstat,%0,weapon)] to [prettify(u(fun_dmgstat,%0,hitloc))] [switch(u(fun_dmgstat,%0,real),0,%R[ansi(hg,*** MOCK DAMAGE ***)] %R\(will be removed when combat ends\)%R)]  %R[ansi(h,Time of Injury)]: [u(fun_display_time,before(after(after(%0,_),_),_))]%R[ansi(h,Type)]: [u(fun_dmgstat,%0,type)] %R[ansi(h,Initial Severity)]: [u(fun_print_wound,u(fun_dmgstat,%0,initsev))] %R[ansi(h,Current Severity)]: [u(fun_print_wound,u(fun_dmgstat,%0,currsev))] %R[ansi(h,Healing Points)]: [u(fun_dmgstat,%0,healing)] %R[ansi(h,Last Treated)]: [u(fun_display_time,u(fun_dmgstat,%0,lasttreat))] %R[ansi(h,Last Healed)]: [u(fun_display_time,u(fun_dmgstat,%0,lastheal))] %R[ansi(h,Notes)]:%R[replace_cr(u(fun_dmgstat,%0,notes))]


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DMGLIST
@@  Lists damage summary for a char/vehicle.
@@  %0 = Name
@@
@@ %q0 = current severity, %q1 = time to heal
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_DMGLIST FS3Combat Damage Commands=[ansi(h,%b%b%b%b%bSeverity[space(41)]Can)]%r[ansi(h,[space(5)]\(Current\)[space(6)]Type[space(6)]Wound[space(18)]Treat%B%BEst Heal)][iter(u(FUN_ALL_DMG,%0),[setq(0,u(FUN_DMGSTAT,##,currsev))[setq(1,u(FUN_TIME_TO_HEAL,##))]]%R[ljust(#@.,4)] [ljust(ansi(u(fun_wound_color,%q0),[u(fun_wound_name,%q0)]),14)] [ljust(u(FUN_DMGSTAT,##,type),10)][padstr([u(FUN_DMGSTAT,##,weapon)] to [prettify(u(FUN_DMGSTAT,##,hitloc))],25)][switch(u(FUN_CAN_BE_TREATED,##),1,+,-)]%B%B%B%B[switch(%q1,0,-,%q1 days)])]


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DMGMOD_TOTAL
@@  Total damage modifier for a char/vehicle, counting all wounds.
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_DMGMOD_TOTAL FS3Combat Damage Commands=localize([setq(0,0)][iter(u(FUN_ALL_DMG,%0),setq(0,add(%q0,u(FUN_DMGMOD_WOUND,##))),,)][min(%q0,95)])


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DMGMOD_WOUND
@@  Damage modifier for an individual wound.
@@  Modifier is 1/2 for wounds that have been treated/healed and another 1/2 for stun.
@@  This is cumulative.
@@  %0 = Damage attr
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&WOUND_DMGMOD FS3Combat Damage Commands=L:5 M:10 S:50 C:80

&FUN_DMGMOD_WOUND FS3Combat Damage Commands=localize([setq(0,after(grab(v(WOUND_DMGMOD),[u(FUN_DMGSTAT,%0,currsev)]:*),:))][switch(u(FUN_DMGSTAT,%0,healing),>0,setq(0,div(%q0,2)))][switch(u(FUN_DMGSTAT,%0,type),stun,setq(0,div(%q0,2)))]%q0)


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_PRINT_WOUND
@@  FUN_WOUND_COLOR
@@  FUN_WOUND_NAME
@@  These functions are used to show a pretty printed version of a wound.
@@  %0 = Wound level (C/S/M/L)
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_PRINT_WOUND FS3Combat Damage Commands=ansi(u(FUN_WOUND_COLOR,%0),u(FUN_WOUND_NAME,%0))

&FUN_WOUND_COLOR FS3Combat Damage Commands=switch(%0,C,hR,S,hr,M,hy,L,hg,hx)

&FUN_WOUND_NAME FS3Combat Damage Commands=switch(%0,C,Critical,S,Serious,M,Moderate,L,Light,Healed)


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DO_DAMAGE
@@  Inflicts damage upon a char/vehicle.
@@  %0 = Name 
@@  %1 = Real (1) or Mock (0) Damage
@@  %2 = Hit location
@@  %3 = Weapon
@@  %4 = Physical or Stun damage
@@  %5 = Severity (C/S/M/L), applied to both initial and current severity
@@  Healing points defaulted to 0, notes to empty, and last treated/healed to empty
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_DO_DAMAGE FS3Combat Damage Commands=set(#FS3COMBAT_DB_DMG,DMG_[edit(%0,_,.,%B,.)]_[secs()]_[rjust(u(fun_rand_second),4,0)]:%1|%2|%3|%4|%5|%5|0|||)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_RAND_SECOND
@@  Randomizes a suffix to the damage in case multiple bits of damage
@@  happen at the same moment.
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_RAND_SECOND FS3Combat Damage Commands=[setq(0,xget(#FS3COMBAT_DB_DMG,randsec_[secs()]))][wipe(#FS3COMBAT_DB_DMG/randsec_*)][set(#FS3COMBAT_DB_DMG,randsec_[secs()]:[inc(%q0)])][switch(%q0,,0,%q0)]

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_HEALPOINTS_WOUND
@@  How many healing points are required to heal a particular wound down to the next
@@  lower level.  
@@  %0 = Damage attr name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&HEALING_POINTS FS3Combat Damage Commands=C:12 S:8 M:6 L:4 H:0

&FUN_HEALPOINTS_WOUND FS3Combat Damage Commands=u(fun_healpoints_sev,u(fun_name_from_attr,%0),[u(fun_dmgstat,%0,currsev)],u(fun_dmgstat,%0,type))


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_HEALPOINTS_TOTAL
@@  How many healing points are required to heal a particular wound down to 0.
@@  lower level.  
@@  %0 = Damage attr name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_HEALPOINTS_TOTAL FS3Combat Damage Commands=localize([setq(0,0)][iter(switch([u(fun_dmgstat,%0,currsev)],C,C S M L,S,S M L,M,M L,L,L,0),setq(0,add(%q0,u(fun_healpoints_sev,u(FUN_NAME_FROM_ATTR,%0),##,u(fun_dmgstat,%0,type)))),,)]%q0)


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ FUN_HEALPOINTS_SEV
@@ How many healing points are required to heal a wound of a given severity/type.
@@ %0 = name
@@ %1 = severity
@@ %2 = type (stun or physical)
@@  Note that stun wounds heal 5x faster and vehicles heal 5x faster.
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_HEALPOINTS_SEV FS3Combat Damage Commands=div(after(grab(v(healing_points),%1:*),:),switch(1,isvehicle(%0),5,strmatch(%2,Stun),5,1))


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ FUN_IS_RESTRICTED
@@ Determine whether someone is hospitalized (2), restricted duty (1) or OK (0).
@@ Thresholds are somewhat arbitrary.  Keeping in mind that L5,M10,S50,C80 but halved 
@@ for wounds that are healed, they will be restricted if they have anything at mod or
@@ higher (or enough light added up), and hospitalized if they have anything serious or 
@@ higher (or enough mods added up).
@@ %0 = name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_IS_RESTRICTED FS3Combat Damage Commands=switch(damagemod(%0),>24,2,>4,1,0)


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ FUN_DISPLAY_RESTRICTED
@@ Displays an appropriate message based on someone's restricted status.
@@ %0 = name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_DISPLAY_RESTRICTED FS3Combat Damage Commands=switch(u(FUN_IS_RESTRICTED,%0),0,[ansi(hg,<FIT FOR DUTY>)],1,[ansi(hm,switch(isvehicle(%0),0,<LIGHT DUTY>,<DOWN FOR REPAIRS>))],2,[ansi(hr,switch(isvehicle(%0),0,<HOSPITALIZED>,<DOWN FOR REPAIRS>))])


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_CHECK_KO
@@  Determines if someone should be ko'd by rolling a random # and comparing to their wound
@@  level.  They get a bonus/penalty based on their 'toughness' stat.  PCs get an additional
@@  bonus based on the Hero Factor.  (Note this does not apply to vehicles piloted by PCs.)
@@ 
@@  They must roll OVER the specified target number, which will never be lower than 2 
@@  or higher than 95.  Thus there's always a chance of getting KO'd or staying OK no
@@  matter how hurt you are.
@@
@@  Returns 1 if they FAIL their KO check.
@@  %0 = name
@@  %1 = combat #
@@
@@ %q0=damagemod    %q1=ability level  %q2=random roll  %q3=hero  %q4=ability mod
@@ %q5=roll target  %q6=result
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_CHECK_KO FS3Combat Damage Commands=localize([setq(0,damagemod(%0))][setq(1,switch(1,isnpc(%0),npcstat(%0,skill),isvehicle(%0),vehtypestat(vehiclestat(%0,type),toughness),isplayer(%0),ability_level(pmatch(%0),xget(#FS3COMBAT_PREFS,toughness_skill)),5))][setq(2,rand(100))][setq(3,switch(isplayer(%0),1,xget(#FS3COMBAT_PREFS,hero_factor),0))][setq(4,mul(sub(%q1,5),5))][setq(5,max(min(sub(sub(%q0,%q4),%q3),95),2))][setq(6,switch(1,eq(%q0,0),0,lte(%q2,%q5),1,0))][u(%1/fun_combat_log,%0 checking KO : dmgmod=%q0 toughness=%q1 toughmod=%q4 randroll=%q2 hero=%q3 rolltarget=%q5%B%BKOd:%q6)]%q6)


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_TIME_TO_HEAL
@@  Calculates estimated time to heal, cumulative for the current level + all under.
@@  %0 = damage attr
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_TIME_TO_HEAL FS3Combat Damage Commands=max(round(sub(u(FUN_HEALPOINTS_TOTAL,%0),u(fun_dmgstat,%0,healing)),0),0)

@STARTUP FS3Combat Damage Commands=@set me=!no_command;@fun damagemod=me,fun_dmgmod_total


&CRON_TIME_DAILYHEAL FS3Combat Damage Commands=|||23|57|

&CRON_JOB_DAILYHEAL FS3Combat Damage Commands=@tr #FS3COMBAT_CMD_DMG/tr_daily_healing

@wait 10=@set me=!quiet
@wait 10=@pemit/silent %#=%R%R[ansi(hg,Installation complete.)]
