@@ Faraday's Player Setup System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hg,Beginning Player Setup Object Installation.  Please wait until you see the 'Installation Complete' message...)]%R

@pemit/silent %#=[ansi(hg,Creating Objects...)]%R


think install_create(SETUP_CMD,Player Setup Object,1,0,5.0,Sets up attributes on new players.)
think install_create(SETUP_DB,Player Setup Data,1,1,5.0,Stores player setup data.)


@pemit/silent %#=[ansi(hg,Setting Object Attributes...)]%R

@set Player Setup Object = DARK
@set Player Setup Object = MONITOR
@startup Player Setup Object = @set me=!NO_COMMAND
@AHEAR Player Setup Object=@switch 0=hasattr(%#,status),{@tr #SETUP_DB/ADDITIONAL_SETUP=%#;think [iter(lattr(#SETUP_DB/hattr_*),[setq(0,after(##,_))][set(%#,%q0:[u(#SETUP_DB/##,%#)])][set(%#/%q0,mortal_dark)][atrlock(%#/%q0,on)])][iter(lattr(#SETUP_DB/vattr_*),[setq(0,after(##,_))][set(%#,%q0:[u(#SETUP_DB/##,%#)])])]}
&FUN_SETUP_ATTR Player Setup Object=switch(isstaff(%#),0,ansi(hr,This command is for staff only.),switch(%0,h,[set(#SETUP_DB,HATTR_%1:%2)][iter(lsearch(all,type,player),[switch(hasattr(##,%1),0,set(##,%1:[u(#SETUP_DB/HATTR_%1)]))][set(##/%1,mortal_dark)][atrlock(##/%1,on)])][ansi(h,%RYou set up a Hidden Locked attribute named %1)],v,[set(#SETUP_DB,VATTR_%1:%2)][iter(lsearch(all,type,player),[switch(hasattr(##,%1),0,set(##,%1:[u(#SETUP_DB/VATTR_%1)]))])][ansi(h,%RYou set up a Visible Unlocked attribute named %1)],[ansi(hb,%0 is not a valid setting.)]))
@LISTEN Player Setup Object=* has connected.
&SHELP_PLAYER_SETUP Player Setup Object=In the welcome room (#0), there is a dark "Player Setup Object" (#SETUP_DB) that does initial player setup. It initializes things like their starting IC location, status, etc. If any coded systems require attributes to be locked/initialized, add an attribute to the setup object. Use HATTR_<name> for attributes that should be hidden and locked, and VATTR_<name> for attributes that should be visible and unlocked.%R%RFor example:%R%R%T&HATTR_ICLOC Player Setup=#66%R%RThis will set an "ICLOC" attribute on every player with a starting value of #66. That attribute will be locked and invisible to players.%R%R%T&VATTR_ADESC Player Setup=think <OOC> %N looked at you.%R%RThis will set up an @adesc attribute that the player can see and change.%R%RNote: The player's DB# is passed to each attribute as \%0.

@startup Player Setup Object=@set me=!NO_COMMAND;@fun setup_attr=me,fun_setup_attr


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       DATA SECTION           *****
@@    *****   CHECK BEFORE UPGRADING     *****
@@    *****   REMOVE OF MODIFY AS NEEDED *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&VATTR_ADESCRIBE Player Setup Data=think <OOC> \%N looked at you.
&HATTR_CREATE_DATE Player Setup Data=[time()]
&HATTR_STATUS Player Setup Data=NEW
&ADDITIONAL_SETUP Player Setup Data=@fo %0=@chan/on newbie;@fo %0=@chan/on public;@dolist lwho()=@pemit ##=[ansi(hg,<Newbie>)] [name(%0)] has just created a character!  Welcome to [mudname()]!;@set %0=unregistered;@fo %0=@lock me=me

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       END DATA SECTION      *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

@pemit/silent %#=[ansi(hg,Setting up database numbers...)]%R

@wait 2=+install/updategroup SETUP

@wait 5=@pemit/silent %#=[ansi(hg,Moving objects to their appropriate places...)]%R

@wait 6=@tel [install_aliasdb(SETUP_CMD)]=#0



