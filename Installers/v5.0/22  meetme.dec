@@ Faraday's Meetme System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hg,Beginning Meetme Installation.  Please wait until the Installation Complete message appears...)]%R

@pemit/silent %#=[ansi(hg,Creating Objects...)]%R

think install_create(MEETME_CMD,Meetme System,1,0,5.0,Meetme System.)

&CMD-+MDECLINE Meetme System=$+mdecline:@switch [setq(0,xget(%#,meetme))][t(%q0)]=0,@pemit %#=ansi(hr,No one has invited you to meet them.),{@pemit %q0=ansi(hy,%N has declined your invitation.);@pemit %#=ansi(hy,You decline [name(%q0)]'s invitation.);think set(%#,meetme:)}
&CMD-+MEETME Meetme System=$+meetme *:think [setq(0,pmatch(%0))]pemit(%#,switch(0,t(%q0),ansi(hr,That is not a valid player.),u(fun_both_ic,%q0,%#),ansi(hr,You cannot invite someone unless you both are IC.),u(FUN_IS_IN_OBJECT,%q0),ansi(hr,You can't invite someone who is inside another object.),ansi(hg,You invite [name(%q0)] to meet you for RP.)[set(%q0,meetme:%#)][pemit(%q0,ansi(hg,<OOC> %N has invited you to meet them for RP at [xget(%l,area)] - [name(%l)].  You can +mjoin to join them, +msummon to bring them to you, or +mdecline to decline the invitation.))]))
&CMD-+MJOIN Meetme System=$+mjoin:@switch/first [setq(0,xget(%#,meetme))]0=[t(%q0)],@pemit %#=ansi(hr,No one has invited you to meet them.),u(fun_both_ic,%#,%q0),@pemit %#=ansi(hr,You cannot meet someone unless you are both IC.),u(FUN_IS_IN_OBJECT,%#),@pemit %#=ansi(hr,You cannot join someone while inside another object.),{@remit loc(%q0)=ansi(hy,<OOC> %N is about to join your location.);@remit %l=ansi(hy,<OOC> %N is being summoned away.);think set(%#,meetme:);@wait 5=@tel %#=loc(%q0)}
&CMD-+MSUMMON Meetme System=$+msummon:@switch/first [setq(0,xget(%#,meetme))]0=t(%q0),@pemit %#=ansi(hr,No one has invited you to meet them.),u(fun_both_ic,%#,%q0),@pemit %#=ansi(hr,You cannot meet someone unless you are both IC.),u(FUN_IS_IN_OBJECT,%q0),@pemit %#=ansi(hr,You can't summon someone who is inside another object.),{@remit %l=ansi(hy,<OOC> [name(%q0)] is about to join your location.);@remit loc(%q0)=ansi(hy,<OOC> [name(%q0)] is being summoned away.);think set(%#,meetme:);@wait 5=@tel %q0=%l}
&HELP_+MEETME Meetme System=This command will invite someone for RP.  They will have the option of coming to your location or bringing you to them.  They can, of course, decline the invitation entirely.  You must both be IC for this to work, and it won't let either of you leave the inside of another object.%R%R[ansi(h,+meetme <player>)] - Invites a player to meet you for RP.%R[ansi(h,+mjoin)] - Joins a player who has invited you to RP.%R[ansi(h,+msummon)] - Summons a player to you if they have invited you to RP.%R[ansi(h,+mdecline)] - Declines a RP invitation.
&FUN_BOTH_IC Meetme System=and(match(xget(%0,status),IC),match(xget(%1,status),IC))
&FUN_IS_IN_OBJECT Meetme System=t(hastype(loc(%0),room))
@STARTUP Meetme System=@set me=!no_command
&HATTR_MEETME Meetme System=

@pemit/silent %#=[ansi(hg,Setting up database numbers...)]%R

@wait 2=+install/updategroup MEETME

@wait 5=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!)]

@wait 5=@set me=!quiet
