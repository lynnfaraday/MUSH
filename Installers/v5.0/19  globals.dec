@@ Faraday's Misc Globals
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=QUIET

@pemit/silent %#=[ansi(hg,Beginning Globals System Installation.  Please wait until you see the 'Installation Complete' message...)]%R

@pemit/silent %#=[ansi(hg,Creating Objects...)]%R

think install_create(GLOBALS_CMD,Misc Globals,1,0,5.0,Miscellaneous global functions.)
think install_create(GLOBALS_FUN,Misc Globals Functions,1,1,5.0,Functions for the misc globals.)
think install_create(GLOBALS_DB,Misc Globals Data,0,1,5.0,Data storage for the misc globals.)

@pemit/silent %#=[ansi(hg,Setting Up Object Attributes...)]%R

@aconnect Misc Globals=think [switch(gt(words(mwho()),xget(#GLOBALS_DB,who_record)),1,[set(#GLOBALS_DB,who_record:[words(mwho())])][pemit(lwho(),[ansi(h[rand_color()],New Online Record - [words(mwho())] players!)])])]
&CMD-+DETAIL Misc Globals=$+detail*:think switch(%0,,pemit(%#,[line()]%r[ansi(hy,Detail Views Available:)]%R%R[ansi(hg,NAME)][space(18)][ansi(hg,AVAILABLE VIEWS)][iter(%l [lcon(%l)],u(#GLOBALS_FUN/fun_detail,##))]%r%rType +detail <name>/<view> to look at a detail view.%r[ansi(y,line())]),pemit(%#,[ansi(y,line())]%r[ansi(hy,Detail View for [ucstr(trim(before(%0,/)))]/[ucstr(after(%0,/))]:)]%r[setq(0,matchcon(switch(trim(before(%0,/)),here,[name(%l)],trim(before(%0,/))),%l))][setq(1,detail_[trim(after(%0,/))])][u(%q0/%q1)]%r[line()]))
&CMD-+GUESTBOOT Misc Globals=$+guestboot *=*:@switch/first [setq(0,pmatch(%0))]0=t(%q0),@pemit %#=That is not a valid player!,not(hasflag(%#,unreg)),@pemit %#=Only approved characters may use this command.,match(name(%q0),guest-*),@pemit %#=This command may only be used to boot guests!,{think [create_job(MISC,Guest Booted!,%N booted a Guest from IP Address: [xget(%q0,lastip)] for the following reason:%R%R%1,%#)];@pemit %#=You have booted %0.;@pemit %q0=[line()]%R[ansi(h,A player has booted you from the game for the following reason: "%1".  If you feel that you have been unjustly booted, please send the staff a message (+mail staff=Subject/Message) explaining what happened so we can deal with it.)]%R[line()];@boot %q0;@fo %q0=home}
&CMD-+LAST Misc Globals=$+last *:think pemit(%#,switch(0,t(pmatch(%0)),ansi(hr,That is not a valid player.),match(mwho(),pmatch(%0)),ansi(hy,[capstr(trim(%0))] was last online [xget(pmatch(%0),last)].),ansi(hg,[capstr(trim(%0))] is currently CONNECTED.)))
&CMD-+SHOUT Misc Globals=$+shout *:think switch(1,t(match(xget(%l,area),OOC)),pemit(%#,Sorry, that command doesn't work in the OOC Nexus. :\)),[iter(lexits(%l),remit(room(##),From [ansi(r,trim(before(name(%l),--)))] you hear shouting: %0))][remit(%l,You hear shouting: %0)])
&CMD-+FULLNAME Misc Globals=$+fullname *:think pemit(%#,switch(0[setq(0,pmatch(%0))],t(%q0),ansi(hr,That is not a valid player.),hasattr(%q0,fullname),ansi(hr,No fullname set.),ansi(hg,Fullname: [switch(xget(%q0,rank),,,[xget(%q0,rank)]%B)][xget(%q0,fullname)][switch(xget(%q0,callsign),,,%B\("[xget(%q0,callsign)]"\))])))
&CMD-+STAFF Misc Globals=$+staff:@fo %#=+list staff
&CMD-+TIME Misc Globals=$+time:think pemit(%#,[line()]%R[center(ansi(h,IC Time),78)]%R%R[ansi(hc,padstr(IC Time:,20))] [ictime()] %r%R[center(------------------,78)]%R[center(ansi(h,OOC Time),78)]%R%R[ansi(hc,MUSH Time \(EST\):)][space(5)][time()][space(5)]\(MUSH+0\)%R[ansi(hc,Local Time:)][space(10)][switch(hasattr(%#,time_offset),1,[convsecs(add(secs(),mul(3600,xget(%#,time_offset))))][space(5)]\(MUSH[switch(xget(%#,time_offset),<0,,+)][xget(%#,time_offset)]\),See [ansi(hg,+help +time)] for info on setting up your local time)]%R[line()])
&CMD-+TIME-OFFSET Misc Globals=$+time/offset *:think switch(0,and(isnum(%0),lte(%0,24),gte(%0,-24)),pemit(%#,ansi(hr,That is not a valid time offset.)),[pemit(%#,ansi(hg,You set your local time offset to %0.  This value will be added to or subtracted from the current MUSH Time to determine the time where you are.  Don't forget to update it for Daylight Savings Time, if your Daylight Savings is different than the Eastern US Daylight Savings.))][set(%#,time_offset:[trim(%0)])])
&CMD-+WHO Misc Globals=$+who:think pemit(%#,u(#GLOBALS_FUN/fun_who,who))
&CMD-WHO Misc Globals=$who:think pemit(%#,u(#GLOBALS_FUN/fun_who,who))
&CMD-+WHERE Misc Globals=$+where:think pemit(%#,u(#GLOBALS_FUN/fun_who,where))
&CMD-+WHO-AGE Misc Globals=$+who/age:think pemit(%#,[line()]%R[ansi(h,Top 25 Oldest Characters)]%R[setq(0,extract(sort(iter(real_players(),switch(hasflag(##,?),1,%q0,%q0 [convtime(ctime(##))]:##))),1,25))][iter(%q0,%R[ljust(#@.,3)] [name(after(##,:))] - [convsecs(before(##,:))])]%R[line()])
&CMD-OOC Misc Globals=$ooc *:think remit(%L,[ansi(hb,<OOC>)]%B[parse_pose({%0},%N)])
&cmd-+rpsearch Misc Globals=$+rpsearch:think pemit(%#,switch(1,xget(%#,rpsearch),ansi(hg,You take yourself off the RP Search list.)[set(%#,rpsearch:0)],ansi(hg,You put yourself on the RP Search list.)[set(%#,rpsearch:1)]))
&CMD-+afk Misc Globals=$+afk:think switch(xget(%#,status),NEW,pemit(%#,ansi(hr,Unapproved players may not use this command.)),IC,[remit(%l,ansi(h[rand_color()],<OOC> %N steps out for a breath of fresh O2 and takes a break from RP.))][set(%#,status:OOC)][set(%#,icloc:%l)][safe_tel(%#,xget(#GLOBALS_DB,IDLE_LOUNGE))],OOC,[remit(%l,ansi(h[rand_color()],<OOC> %N steps out for a breath of fresh O2 and takes a break from RP.))][safe_tel(%#,xget(#GLOBALS_DB,IDLE_LOUNGE))])]
&CMD-+IC Misc Globals=$+ic:think switch(xget(%#,status),NEW,pemit(%#,ansi(hr,Unapproved players may not use this command.)),IC,pemit(%#,ansi(hr,You are already IC.)),[remit(%l,ansi(h[rand_color()],<OOC> An off-stage voice hollars "Action" and %N steps to %p mark and gets ready to RP.))][remit(xget(%#,icloc),ansi(h[rand_color()],<OOC> An off-stage voice hollars "Action" and %N steps to %p mark and gets ready to RP.))][safe_tel(%#,xget(%#,icloc))][set(%#,status:IC)])]
&CMD-+OOC Misc Globals=$+ooc:think switch(xget(%#,status),NEW,pemit(%#,ansi(hr,Unapproved players may not use this command.)),IC,[remit(%l,ansi(h[rand_color()],<OOC> %N steps out for a breath of fresh O2 and takes a break from RP.))][set(%#,status:OOC)][set(%#,icloc:%l)][remit(xget(#GLOBALS_DB,OOC_CENTER),ansi(h[rand_color()],<OOC> %N steps out for a breath of fresh O2 and takes a break from RP.))][safe_tel(%#,xget(#GLOBALS_DB,OOC_CENTER))],pemit(%#,ansi(hr,You are already OOC.)))
&CMD-+GLANCE Misc Globals=$+glance:think pemit(%#,[line()]%R[ansi(hg,Quick Glance)][iter(lvplayers(loc(%#)),%R%R[setq(1,xget(##,rank))][setq(2,xget(##,fullname))][setq(3,xget(##,callsign))][switch(words(%q1),>0,[ansi(hr,%q1)]%B)][switch(words(%q2),>0,%q2,[name(##)])][switch(words(%q3),>0,%B\("[ansi(h,%q3)]"\))]%R%B%B%B[age(##)] year old [lcstr(xget(##,sex))].  [u(##/desc_short)]%R[line()])
&CMD-+KNOCK Misc Globals=$+knock *:think [setq(0,iter(lexits(%l),switch(strmatch(fullname(##),*%0*),1,##),,))][pemit(%#,switch(%q0,,ansi(hr,I don't see an exit with that name.),*#*#*,ansi(hr,I can't tell which exit you mean.  Be more specific.),[ansi(hg,You knock on the door to [name(%q0)].)][remit(loc(%q0),ansi(hg,Someone is knocking at the door.))]))]
&CMD-+KNOCK-EMIT Misc Globals=$+knock/emit *=*:think [setq(0,iter(lexits(%l),switch(strmatch(fullname(##),*%0*),1,##),,))][pemit(%#,switch(%q0,,ansi(hr,I don't see an exit with that name.),*#*#*,ansi(hr,I can't tell which exit you mean.  Be more specific.),[remit(%l,[ansi(hg,To [name(loc(%q0))]:)]%R%1)][remit(loc(%q0),[ansi(hg,From [name(%l)]:)]%R%1)]))]
&CMD-+KNOCK-LOOK Misc Globals=$+knock/look *:think [setq(0,iter(lexits(%l),switch(strmatch(fullname(##),*%0*),1,##),,))][pemit(%#,switch(%q0,,ansi(hr,I don't see an exit with that name.),*#*#*,ansi(hr,I can't tell which exit you mean.  Be more specific.),[ansi(hg,You see: [iter(lcon(loc(%q0)),switch(1,and(isplayer(##),conn(##)),name(##)))])]))]
@STARTUP Misc Globals=@set me=!no_COMMAND
@ADISCONNECT Misc Globals=+rpsearch/off


&HELP_+DETAIL Misc Globals Functions=Sometimes you'll want to add special details about a room, an object, or your  own character without cluttering up the main description. The +detail command  allows you to do this without having to create a bunch of objects to carry  around. It is perfect for jewelry, tattoos, or other aspects of your  character that are worth describing, but only if someone makes a point of  looking at them.%R%RRooms will automatically alert you to details by having +detail in the special notes section of the room description. It is suggested that if you have details on your character, you put (+detail) at the end of your  description to let other players know.  The +desc command will do this for you automatically.%R%R[ansi(h,+detail)] - Lists available details in the room and everything (and everyone) in it.%R[ansi(h,+detail <object>/<detail name>)] - views a detail.       <object> must be enough of a name to identify the player or object. Use "here" to view details in a room.%R[ansi(h,&detail_<name> <object>=<detail text>)] - Sets a detail view on yourself  or one of your objects. To set one on another player's object (like a public room), contact staff.
&HELP_+FULLNAME Misc Globals Functions=This command lets you see, at a glance, the full name (and callsign) of a character.%R%R[ansi(h,+fullname <character>)]
&HELP_+GUESTBOOT Misc Globals Functions=This command allows any player the ability to @boot a Guest. You must have a very good reason for booting a Guest - "He was annoying" is not sufficient. This power should only be used in cases of spammers or people who are being abusive. It only works on Guests, not on regular players, and all uses of this command are logged and will be reviewed by the staff.%R%R[ansi(h,+guestboot <guest name>=<reason>)] - Boots a Guest.
&HELP_+LAST Misc Globals Functions=This command shows the last time a player was logged in.%R%R[ansi(h,+last <player>)] - Views last online time.
&HELP_+SHOUT Misc Globals Functions=This command is used when you want to shout something to adjacent rooms. The text that you shout is used like an @emit, since usually the people who hear it wouldn't know who or what exactly was shouting, they'd only hear it. For example: +shout A woman screams, "Help! Thief!"%R%RShouting cannot be 'directed' to a particular exit. You shout in a room and all connected rooms will hear the shouting. If you want to send something only to a particular player in another room (such as someone whispering to them from a dark alley, "Psst, hey Mister") then check out help @pemit. That will let you do what you're looking for.%R%R[ansi(h,+shout <@emit to shout>)] - Shouts a message
&HELP_+STAFF Misc Globals Functions=This command shows all staff members and their duties.%R%R[ansi(h,+staff)]
&HELP_+TIME Misc Globals Functions=The +time command helps you keep track of time on the MUSH. It is important to note that time on the MUSH passes in a 1-to-1 time scale. What that means is that for every day that passes in real life, one day passes in the IC world.   +time displays the IC time and OOC time.%R%R[ansi(h,+time)] - Displays the current IC date and time, plus the current OOC time in the MUSH's timezone ("MUSH time") and in your timezone.%R%R[ansi(h,+time/offset <hours to add/subtract>)] - This allows you to set the  difference between your own location's timezone and MUSH time, so +time will  display the correct local time for you. Note that this only affects +time -  it does not affect other commands.
&HELP_+WHERE Misc Globals Functions=[ansi(h,+where)] is a general command to list connected players.  It shows their current locations, idle time, and whether they are available for RP. Information on player locations is only to be used OOCly. Do not go  somewhere unless your character has a reason to go there, no matter who might  be at that location. Even if your character does have a reason to wander by,  be courteous and ask the other players in a location if they mind you joining  in.  You can hide your location in +who by typing @set me=Unfindable.
&HELP_+WHO Misc Globals Functions=[ansi(h,+who)] is a general command to list connected players and their positions.%R%RIn addition to showing who's currently online, the +who command keeps track of the MUSH's oldest players (those who have been playing the longest).  You can view this through the [ansi(h,+who/age)] command.
&HELP_OOC Misc Globals Functions=This command is used to make quick Out Of Character remarks while on the IC grid.  It is not necessary to use it while speaking in the OOC grid.  Please refrain from using this for lenghty conversations - take those to pages or channels.%R%R[ansi(h,ooc <pose, say, or semipose>)] - Speaks OOCly%R%RNote:  Use the [ansi(h,+OOC)] command to travel to the OOC area.
&HELP_+afk Misc Globals Functions=Use the [ansi(h,+afk)] command to travel to the AFK Lounge when you need to idle.  You can return to RP using the +RP command.
&HELP_+IC Misc Globals Functions=The [ansi(h,+IC)] command will take you to the In-Character area of the game.  Use +OOC to get back out again.
&HELP_+OOC Misc Globals Functions=The [ansi(h,+OOC)] command will take you to the Out-Of-Character area of the game.  Use +IC to get back again.%R%RNote: The [ansi(h,ooc)] command is used to speak OOCly.
&HELP_+RPSEARCH Misc Globals Functions=This is a command designed to help connect people for RP. Sometimes even when people are hanging out on the IC grid, it's not clear whether they're already involved in a scene, or just hanging out waiting for something/someone, or only on for a few minutes, or whatever. +rpsearch lets you say: "HEY! COME RP!"%R%R[ansi(h,+rpsearch)] - Toggles yourself on and off the RP Search List.%R%RNote: You are automatically taken off the RP Search list on disconnect.
&SHELP_GLOBALS Misc Globals Functions=Object #GLOBALS_DB stores miscellaneous data used by the globals.  Things you may want to edit:%R%R[ansi(h,IDLE_LOUNGE)] - DB# of the room where idle players go.%R[ansi(h,OOC_CENTER)] - DB# of the OOC room.%R[ansi(h,TP_ROOM_PARENT)] - DB# of the TP Room Parent (if used).%R[ansi(h,MUSH_NAME)] - The way you want your MUSH name displayed on +who.%R[ansi(h,WHO_COLOR)] - Color for the side bars on +who.
&HELP_+KNOCK Misc Globals Functions=The +knock command lets you knock on a door.  Anyone in the room on the other side will see the knocking, but not who it came from.%R%R[ansi(h,+knock <exit name>)] - Knocks on the door.%R[ansi(h,+knock/emit <exit name>)] - Emits through an exit, to pose opening the door.%R[ansi(h,+knock/look <exit name>)] - Looks through a peephole.%R%RNOTE: Not all MUSH exits are doors, so please use these commands only when it is ICly appropriate to do so.
&HELP_+GLANCE Misc Globals Functions=This command shows you a quick summary of the players in the room.  Bear in mind that some information (like their exact age and full name) may not be known to you ICly.  You can set the description that shows up on +glance by creating a 'short' +desc, or a DESC_SHORT attribute.%R%R[ansi(h,+glance)] - Views nearby players.

&CRON_JOB_FORCEIDLE Misc Globals Functions=@dolist lwho()=@switch [and(gt(idlesecs(##),1800),match(xget(##,status),IC))]=1,{@pemit ##=[ansi(hr,Please do not idle on the IC grid.)];@fo ##=+afk}
&CRON_TIME_FORCEIDLE Misc Globals Functions=||||00 10 20 30 40 50|


&FUN_WHO_RPOK Misc Globals Functions=add(%0,switch(xget(%1,status),IC,1,0))
&FUN_SORT_AGE Misc Globals Functions=switch(1,eq(convtime(ctime(%0)),convtime(ctime(%1))),0,gt(convtime(ctime(%0)),convtime(ctime(%1))),1,-1)
&FUN_DETAIL Misc Globals Functions=[switch(words(lattr(%0/detail_*)),>0,%r[padstr([switch(hastype(%0,room),1,Here,name(%0))],20)]%b%b[iter(lattr(%0/detail_*),after(##,_))])]

&HATTR_ICLOC Misc Globals Functions=\[xget([num(Misc Globals Data)],IC_STARTING_LOC)\]
&HATTR_STATUS Misc Globals Functions=NEW

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       DATA SECTION           *****
@@    *****   CHECK BEFORE UPGRADING     *****
@@    *****   REMOVE OF MODIFY AS NEEDED *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


&FUN_WHO Misc Globals Functions=[ansi(u(#GLOBALS_DB/who_color),line_no_color())]%R[ansi([u(#GLOBALS_DB/who_color)],==-)]%B[center(u(#GLOBALS_DB/MUSH_NAME),71)][ansi([u(#GLOBALS_DB/who_color)],-==)]%R[ansi([u(#GLOBALS_DB/who_color)],==-)][repeat(-,72)][ansi([u(#GLOBALS_DB/who_color)],-==)][iter(sortby(fun_sortwho,switch(isstaff(%0),1,lwho(),mwho())),%r[ansi([u(#GLOBALS_DB/who_color)],==-)]%B[switch(1,match(loc(##),xget(#GLOBALS_DB,IDLE_LOUNGE)),[ansi(hx,AFK)]%B%B,isstaff(##),[ansi(hb,STF)]%B%B,match(parent(loc(##)),xget(#GLOBALS_DB,TP_ROOM_PARENT)),[ansi(hm,IC)]%B%B%B,match(xget(##,status),NEW),[ansi(hg,NEW)]%B%B,match(xget(##,status),IC),switch(xget(##,rpsearch),1,[ansi(hR,IC)]%B%B%B,[ansi(hr,IC)]%B%B%B),[ansi(h,OOC)]%B%B)]%B%B[padstr(name(##),20)]%b[padstr(switch(%0,where,switch(hasflag(##,unfindable),1,UNFINDABLE,[name(loc(##))] [switch(xget(loc(##),area),,,- [xget(loc(##),area)])]),[switch(xget(##,rank),,,[xget(##,rank)] --%B)][xget(##,position)]),37)]%b[padstr(first(timestring(idlesecs(##))),4)]%B[ansi([u(#GLOBALS_DB/who_color)],-==)])]%r[ansi([u(#GLOBALS_DB/who_color)],==-)][repeat(-,72)][ansi([u(#GLOBALS_DB/who_color)],-==)]%R[ansi([u(#GLOBALS_DB/who_color)],==-)][ljust(ansi(m,%B[switch(isstaff(%0),1,words(lwho()),words(mwho()))] Online),24)][center(ansi(r,[fold(fun_who_rpok,mwho(),0)] IC),24)][rjust(ansi(g,[xget(#GLOBALS_DB,who_record)] Record%B),24)][ansi([u(#GLOBALS_DB/who_color)],-==)]%R[ansi(u(#GLOBALS_DB/who_color),line_no_color())]
&FUN_SORTWHO Misc Globals Functions=[comp([xget(loc(%0),area)] [name(loc(%0))],[xget(loc(%1),area)] [name(loc(%1))])]

&IDLE_LOUNGE Misc Globals Data=#-1
&OOC_CENTER Misc Globals Data=#-1
&IC_STARTING_LOC Misc Globals Data=#-1
&TP_ROOM_PARENT Misc Globals Data=#-1
&MUSH_NAME Misc Globals Data=[ansi(h,MY MUSH)]
&WHO_RECORD Misc Globals Data=1
&WHO_COLOR Misc Globals Data=rand_color()
@wait 6=+jobs/create CODE=Globals/Set up the room DB#'s and other info for the globals.  See +shelp globals

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       END DATA SECTION      *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


@pemit/silent %#=[ansi(hg,Setting up database numbers...)]%R

@wait 2=+install/updategroup GLOBALS

@wait 5=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!)]

@wait 5=@set me=!quiet

