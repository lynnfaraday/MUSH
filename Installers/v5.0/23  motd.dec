@@ Faraday's MOTD System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.
 
@set me=quiet

@pemit/silent %#=[ansi(hg,Beginning MOTD System Installation.  Please wait until the Installation Complete message appears...)]%R


@pemit/silent %#=[ansi(hg,Creating Objects...)]%R

think install_create(MOTD_CMD,MOTD System,1,0,5.0,Message of the day commands.)
think install_create(MOTD_DB,MOTD Database,0,1,5.0,Message of the day data storage.)


@ACONNECT MOTD System=@fo %#=+motd
&CMD-+MOTD MOTD System=$+motd:@pemit %#=[line()][switch(hasattr(#MOTD_DB,motd),1,%R[center(ansi(xget(#MOTD_DB,color),Important Message!),78)]%r[center(u(#MOTD_DB/motd),78)]%r)]%R[center(ansi(xget(#MOTD_DB,color),Global BBS System - Unread Messages \(+bbread\)),78)][u(fun_get_bbs,%#)]%R[center(ansi(xget(#MOTD_DB,color),Upcoming Events \(+events\)),78)][event_reminders()]%R%r[center(ansi(xget(#MOTD_DB,color),Mail Report \([u(fun_mail_command)]\)),78)]%r[u(fun_get_mail,%#)]%R[line()]
&CMD-+MOTD-CATCHUP MOTD System=$+motd/catchup:@tr me/TR_CATCHUP=%#
&CMD-+MOTD-CLEAR MOTD System=$+motd/clear:think pemit(%#,switch(0,isstaff(%#),ansi(hr,This command is for staff members only.),ansi(hg,You clear the message of the day.)[wipe(#MOTD_DB/motd)])
&CMD-+MOTD-OPTION MOTD System=$^\+motd/(on|off)$:think switch(%1,on,pemit(%#,ansi(hy,You will now see the message of the day when you connect.))[set(%#,tips:1)],off,pemit(%#,ansi(hy,You will no longer see the message of the day when you connect.))[set(%#,tips:0)],pemit(%#,ansi(hy,That is not a valid option.  Use "on" or "off".)))
@set MOTD System/CMD-+MOTD-OPTION=regexp
&CMD-+MOTD-SET MOTD System=$+motd/set *:think pemit(%#,switch(0,isstaff(%#),ansi(hr,This command is for staff members only.),ansi(hg,You set the message of the day to: %0)[set(#MOTD_DB,motd:%0)])
&FUN_GET_BBS MOTD System=[switch(hasattr(xget(#MOTD_DB,BBPOCKET),BB_POST_HDR_%#),1,%R[center(** BB Warning: You are in the middle of writing a bbpost. **,78)])][switch(setdiff(iter(setr(0,u([xget(#MOTD_DB,BBPOCKET)]/VALID_GROUPS,%#,read)),get(##/MESS_LST)),setr(1,get(%#/BB_READ))),,%r[center(None.,78)]%r,[setq(9,get([xget(#MOTD_DB,BBPOCKET)]/GROUPS))][trim(iter(%q0,switch(setr(2,setdiff(setr(3,get(##/MESS_LST)),%q1)),,,%R[center([ansi(h,[name(##)] \(#[member(%q9,##)]\):%b)][words(%q2)] unread \([u([xget(#MOTD_DB,BBPOCKET)]/FN_UNREAD_LIST,%q2,%q3)]\),78)]),,),b)]%R)]
&FUN_GET_MAIL MOTD System=[center([ansi(h,[u(fun_unread_mail,%0)] Unread)] -- [u(fun_total_mail,%0)] Total,78)][switch([extract(mail(%#),1,1)],>25,ansi(r,%r[center(You have more than 25 mail messages.,78)]%r[center(Please archive and erase ones that you do not need!,78)]))]
&FUN_GET_TIP MOTD System=u(#MOTD_DB/[extract(lattr(#MOTD_DB/tip_*),add(1,rand(words(lattr(#MOTD_DB/tip_*)))),1)])

&HATTR_MOTD MOTD System=1
&HATTR_TIPS MOTD System=1

&FUN_UNREAD_MAIL MOTD System=switch(xget(#MOTD_DB,MAILSYS),builtin,extract(mail(%0),2,1),faramail,unread_mail(%0),ERROR)
&FUN_TOTAL_MAIL MOTD System=switch(xget(#MOTD_DB,MAILSYS),builtin,[add(extract(mail(%0),1,1),extract(mail(%0),2,1))],faramail,total_mail(%0),ERROR)
&FUN_MAIL_COMMAND MOTD System=switch(xget(#MOTD_DB,MAILSYS),builtin,@mail,faramail,+mail,ERROR)

@STARTUP MOTD System=@set me=!no_COMMAND
&TR_CATCHUP MOTD System=@fo %0=+bbcatchup all
&HELP_+MOTD MOTD System=Whenever you login, the Message Of The Day screen will be presented to you automatically. It shows any important news for the day, as well as a summary of unread items (mail, bulletin boards, events, etc.). %r%rThere is a "catchup" command for the MOTD. This will catch up on all unread items at once (except mail).%r%r[ansi(h,+motd)] - Views the welcome screen. %R[ansi(h,+motd/<on or off>)]- Sets whether to show the screen when you connect. %r[ansi(h,+motd/catchup)] - Catches up on posts.
&SHELP_+MOTD MOTD System=Staff members can change the message of the day, and also add and edit tips that show up randomly on the "tip of the day" section.%R%R[ansi(h,+motd/set <message>)] - Set the important message of the day.%R[ansi(h,+motd/clear)] - Clears the important message.%R%RIf you want integration with Myrddin's BBS to show unread BBS postings, you need to configure the BBPOCKET attribute on #MOTD_DB to be the DB# of the BBS "bbpocket" object.%R%RIf using FaraMail, set the MAILSYS attribute on #MOTD to faramail (this is the default).  Otherwise use builtin for @mail.


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       DATA SECTION           *****
@@    *****   CHECK BEFORE UPGRADING     *****
@@    *****   REMOVE OF MODIFY AS NEEDED *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


&COLOR MOTD Database=hb

&MOTD MOTD Database=[center(This is an important notice for today!,78)]%R[center(NOTE: There may not always be an important message.,78)]
&BBPOCKET MOTD Database=#0
&MAILSYS MOTD Database=faramail

@wait 5=+jobs/create CODE=MOTD BBS/Set BBPOCKET attribute for the MOTD object.  See +shelp +motd.
@wait 5=+jobs/create CODE=MOTD/Set the important message (if there is one).  See +shelp +motd.
@wait 5=+jobs/create CODE=MOTD Mail/Configure which mail system you're using for the MOTD system.  See +shelp +motd.


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       END DATA SECTION      *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

@pemit/silent %#=[ansi(hg,Setting up database numbers...)]%R

@wait 2=+install/updategroup MOTD

@wait 5=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!)]

@wait 5=@set me=!quiet





