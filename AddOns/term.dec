@@ Faraday's Data Terminal
@@ Coded by Linda Naughton O'Meara
@@ http://www.wordsmyth.org/faraday
@@
@@ ===================
@@ OVERVIEW
@@ ===================
@@
@@ This system is a standard Data Terminal. You can use it for anything from personal journal 
@@ storage to criminal records
@@
@@ ===================
@@ FEATURES
@@ ===================
@@
@@ * Create, view, edit and delete 'files' on the system.
@@ 
@@ +==~~~~~====~~~~====~~~~===~~~~ Medical Records ~~~~==~~~~====~~~~====~~~~~==+
@@ #     File                                            Date
@@ ------------------------------------------------------------------------------
@@ 1.  U Test25                                          Thu Jan 31 14:52:26 2265 
@@ 2.  U Test24                                          Thu Jan 31 14:52:25 2265 
@@ 3.  U Test23                                          Thu Jan 31 14:52:24 2265 
@@ 4.  U Test22                                          Thu Jan 31 14:52:23 2265 
@@ 5.    Test21                                          Thu Jan 31 14:52:22 2265 
@@ 6.    Test20                                          Thu Jan 31 14:52:21 2265 
@@ 7.    Test19                                          Thu Jan 31 14:52:20 2265 
@@ 
@@ ~ Files 1-7 of 7 ~                                    'U' - Unread File
@@ +==~~~~~====~~~~====~~~~===~~~~~~ +term/help ~~~~~~~==~~~~====~~~~====~~~~~==+
@@ 
@@ +==~~~~~====~~~~====~~~~===~~~~ Medical Records ~~~~==~~~~====~~~~====~~~~~==+
@@ Author: Faraday
@@ Date:   Thu Jan 31 14:52:26 2265
@@ File:   Test25
@@ ------------------------------------------------------------------------------
@@ This is my test 25.
@@ +==~~~~~====~~~~====~~~~===~~~~~~ +term/help ~~~~~~~==~~~~====~~~~====~~~~~==+
@@ 
@@
@@ ===================
@@ REQUIREMENTS
@@ ===================
@@
@@ * PennMUSH
@@
@@ ===================
@@ INSTALLATION
@@ ===================
@@ 
@@ Simply /quote this script to your MUSH via a MUSH client.  You can also cut and paste, but beware
@@ of linebreaks.  There is no need to create any objects or do any manual editing of this script unless
@@ your master room is something other than #2.
@@
@@
@@ ===================
@@ SETUP
@@ ===================
@@
@@ After installation, the terminal should be configured with a title and an access list 
@@ (if the terminal is restricted). These activities can only be performed by the terminal's 
@@ owner. By default the terminal has only one owner (the person who created it), but you can 
@@ manually modify the OWNER_LIST attribute on the terminal database object to add other owners.
@@
@@ You should also give the terminal a description, and can change its name if you want.
@@
@@
@@ ===================
@@ USAGE
@@ ===================
@@ 
@@ Look at the object for usage instructions.
@@
@@ ===================
@@ UPDATES
@@ ===================
@@
@@ Join the mailing list (http://groups.yahoo.com/group/faradaycode) to keep up with the latest news.
@@
@@     * Version 1.1 - 6/12/04 - Added a separate credits command and cleaned up install file.

@set me=quiet

@pemit/silent %#=[ansi(hr,Beginning Data Terminal Installation...)]%R

@pemit/silent %#=[ansi(hr,Creating Objects...)]%R

@create Data Terminal
@set Data Terminal = WIZARD
@set Data Terminal = SAFE
@set Data Terminal = !NO_COMMAND
@set Data Terminal = OPAQUE
@lock Data Terminal = me
@DESCRIBE Data Terminal=A standard data terminal.
@ADESCRIBE Data Terminal=@fo %#=+term
@STARTUP Data Terminal=@set me=!no_command
&CMD-+TERM Data Terminal=$+term:think pemit(%#,switch(u(#TERMDB/fun_has_access,%#),0,,[setq(0,u(#TERMDB/fun_list_files))][u(#TERMDB/fun_display_files,1,[switch(gte(words(%q0),20),1,20,words(%q0))],%#)]))
&CMD-+TERM-CATCHUP Data Terminal=$+term/catchup:think pemit(%#,switch(0,u(#TERMDB/fun_has_access,%#),,ansi(hg,You mark all files as read.))[set(%#,read_#TERMDB:[iter(lattr(#TERMDB/file_*),after(##,_))])]))
&CMD-+TERM-DEL Data Terminal=$+term/del *:think [setq(0,u(#TERMDB/fun_list_files))][setq(1,extract(%q0,%0,1,))]pemit(%#,switch(0,u(#TERMDB/fun_has_access,%#),,isnum(%0),ansi(hr,That is not a valid number.),lte(%0,words(%q0)),ansi(hr,There aren't that many files.),gte(%0,1),ansi(hr,That is not a valid number.),ansi(hg,You delete file number %0.[wipe(#TERMDB/%q1)])))
&CMD-+TERM-EDIT Data Terminal=$+term/edit *=*/*:think [setq(0,u(#TERMDB/fun_list_files))][setq(1,extract(%q0,%0,1,))]pemit(%#,switch(0,u(#TERMDB/fun_has_access,%#),,isnum(%0),ansi(hr,That is not a valid number.),lte(%0,words(%q0)),ansi(hr,There aren't that many files.),gte(%0,1),ansi(hr,That is not a valid number.),ansi(hg,You edit file number %0.)[set(#TERMDB,%q1:[edit(xget(#TERMDB,%q1),%1,%2)])]))
&CMD-+TERM-LIST Data Terminal=$+term *-*:think [setq(0,words(u(#TERMDB/fun_list_files)))]pemit(%#,switch(0,u(#TERMDB/fun_has_access,%#),,isnum(%0),ansi(hr,That is not a valid number.),isnum(%1),ansi(hr,That is not a valid number.),gte(%0,1),ansi(hr,That is not a valid number.),gte(%1,1),ansi(hr,That is not a valid number.),lte(%0,%q0),ansi(hr,There aren't that many files.),lte(%1,%q0),ansi(hr,There aren't that many files.),lte(%0,%1),ansi(hr,Your numbers are backwards.),lte(sub(%1,%0),25),ansi(hr,You may only display a maximum of 20 files at a time.),[u(#TERMDB/fun_display_files,%0,%1,%#)]))
&CMD-+TERM-NEW Data Terminal=$+term/new *=*:think switch(0,u(#TERMDB/fun_has_access,%#),,[pemit(%#,ansi(hg,You create a new file with title %0.))][set(#TERMDB,file_[xget(#TERMDB,next_file)]:%0|%N|[ictime()]|[escape_cr(%1)])][set(#TERMDB,next_file:[inc(xget(#TERMDB,next_file))])]))
&CMD-+TERM-VIEW Data Terminal=$+term *:think [setq(0,u(#TERMDB/fun_list_files))][setq(1,extract(%q0,%0,1,))]pemit(%#,switch(0,u(#TERMDB/fun_has_access,%#),,not(match(%0,*-*)),,isnum(%0),ansi(hr,That is not a valid number.),lte(%0,words(%q0)),ansi(hr,There aren't that many files.),gte(%0,1),ansi(hr,That is not a valid number.),[set(%#,read_#TERMDB:[xget(%#,read_#TERMDB)]%B[after(%q1,_)])][line_with_text(xget(#TERMDB,title))]%R[ansi(g,Author:)] [u(#TERMDB/fun_get_author,%q1)]%R[ansi(g,Date:)]%B%B%B[u(#TERMDB/fun_get_time,%q1)]%R[ansi(g,File:)]%B%B%B[ansi(h,u(#TERMDB/fun_get_title,%q1))]%R[repeat(-,78)]%R[u(#TERMDB/fun_get_text,%q1)]%R[line_with_text(+term/help)]))
&CMD-+TERM-SEARCH Data Terminal=$+term/search *:think pemit(%#,switch(0,u(#TERMDB/fun_has_access,%#),,[line_with_text(xget(#TERMDB,title))][setq(0,u(#TERMDB/fun_list_files))][setq(1,grepi(#TERMDB,file_*,%0))]%R[ansi(h,#%B%B%B%B%BSearch Results: "%0")]%R[repeat(-,78)][iter(extract(sortby(#TERMDB/fun_sortby_recent,%q1),1,20),%R[ljust([match(%q0,##)].,3)]%B[switch(u(#TERMDB/fun_is_unread_file,%#,##),1,U%B,%B%B)][ljust(u(#TERMDB/fun_get_title,##),48)][u(#TERMDB/fun_get_time,##)])][switch(1,gt(words(%q1),25),%R%R[ansi(hr,Too many search results.  Try a search term that isn't so broad.)])]%R[line_with_text(+term/help)]))
&CMD-+TERM-ACCESS Data Terminal=$+term/access:think pemit(%#,switch(match(xget(#TERMDB,owner_list),%#),0,ansi(hr,This command is restricted to the terminal owner.),[line_with_text(xget(#TERMDB,title))]%R[ansi(h,Access List:)]%R[iter(xget(#TERMDB,access_list),name(##))]%R[line_with_text(+term/help)]))
&CMD-+TERM-ADDACCESS Data Terminal=$+term/addaccess *:think [setq(0,pmatch(%0))]pemit(%#,switch(0,match(xget(#TERMDB,owner_list),%#),ansi(hr,This command is restricted to the terminal owner.),t(%q0),ansi(hr,That is not a valid player.),not(match(xget(#TERMDB,access_list),%q0)),ansi(hr,That person already has access.),ansi(hg,You add %0 to the access list.)[set(#TERMDB,access_list:[xget(#TERMDB,access_list)]%B%q0)]))
&CMD-+TERM-DELACCESS Data Terminal=$+term/delaccess *:think [setq(0,pmatch(%0))]pemit(%#,switch(0,match(xget(#TERMDB,owner_list),%#),ansi(hr,This command is restricted to the terminal owner.),t(%q0),ansi(hr,That is not a valid player.),match(xget(#TERMDB,access_list),%q0),ansi(hr,That person doesn't have access.),ansi(hg,You remove %0 from the access list.)[set(#TERMDB,access_list:[remove(xget(#TERMDB,access_list),%q0)])]))
&CMD-+TERM-HELP Data Terminal=$+term/help:think pemit(%#,[line_with_text(xget(#TERMDB,title))]%RThis terminal is used to store data in files.  Each file can be given a title, and is automatically stamped with the date and time when it was created.  Files are displayed with the newest first, and there is an indication of whether you've read the file or not.  Access is restricted, and may only be granted by staff.%R%R[ansi(hg,Viewing Commands)]%R[ansi(h,+term)] - Lists all files%R[ansi(h,+term <#>)] - Views a file.%R[ansi(h,+term <#>-<#>)] - Lists only files in a certain range%R[ansi(h,+term/catchup)] - Marks all files as read.%R[ansi(h,+term/search <search term>)] - Lists files containing the search term.%R%R[ansi(hg,Editing Commands)]%R[ansi(h,+term/new <title>=<file data>)] - Creates a new file%R[ansi(h,+term/edit <#>=<text to find>/<text to replace with>)] - Edits an existing file%R[ansi(h,+term/del <#>)] - Deletes a file.[switch(t(match(xget(#TERMDB,owner_list),%#)),1,%R%R[ansi(hg,Owner-Only Commands)]%R[ansi(h,+term/title <title>)] - Changes the title of the terminal.%R[ansi(h,+term/access)] - Shows who has access to the terminal.%R[ansi(h,+term/addaccess <player>)] - Gives a player access to the terminal.%R[ansi(h,+term/delaccess <player>)] - Removes a player's terminal access.)]%R%R--Terminal System coded by [ansi(hr,Faraday)] (http://www.wordsmyth.org)--%R[ansi(g,Version [xget(#TERMDB,version)])]%R[line_with_text(+term/help)]
&CMD-+TERM-TITLE Data Terminal=$+term/title *:think switch(0,match(xget(#TERMDB,owner_list),%#),pemit(%#,ansi(hr,This command is restricted to the terminal owner.)),pemit(%#,ansi(hg,You set the title of the computer to %0.)[set(#TERMDB,title:%0)]))
@create Data Terminal DB
@set Data Terminal DB = NO_COMMAND
@set Data Terminal DB = SAFE
&ACCESS_LIST Data Terminal DB=
&FUN_DISPLAY_FILES Data Terminal DB=[setq(9,u(#TERMDB/fun_list_files))][line_with_text(xget(#TERMDB,title))]%R[ansi(h,#%B%B%B%B%BFile[space(44)]Date)]%R[repeat(-,78)][iter(extract(%q9,%0,%1),%R[ljust([add(%0,#@,-1)].,3)]%B[switch(u(#TERMDB/fun_is_unread_file,%2,##),1,U%B,%B%B)][ljust(u(#TERMDB/fun_get_title,##),48)][u(#TERMDB/fun_get_time,##)])]%R%R[ansi(h,[switch(words(%q9),0,~ No Files ~,~ Files %0-%1 of [words(%q9)] ~)][space(35)]'U' - Unread File)]%R[line_with_text(+term/help)]
&FUN_GET_AUTHOR Data Terminal DB=extract(xget(#TERMDB,%0),2,1,|)
&FUN_GET_TEXT Data Terminal DB=extract(replace_cr(xget(#TERMDB,%0)),4,1,|)
&FUN_GET_TIME Data Terminal DB=extract(xget(#TERMDB,%0),3,1,|)
&FUN_GET_TITLE Data Terminal DB=extract(xget(#TERMDB,%0),1,1,|)
&FUN_HAS_ACCESS Data Terminal DB=switch(1,orflags(%#,Wr),1,t(match(xget(#TERMDB,access_list),%0)),1,0[pemit(%0,ansi(hr,Sorry - You do not have access to this terminal.))])
&FUN_IS_UNREAD_FILE Data Terminal DB=not(match(xget(%0,read_#TERMDB),after(%1,_))
&FUN_LIST_FILES Data Terminal DB=sort_recent(lattr(#TERMDB/file_*))
&NEXT_FILE Data Terminal DB=1
&TITLE Data Terminal DB=Data Terminal
&VERSION Data Terminal DB=1.1

@pemit/silent %#=[ansi(hr,Setting up database numbers...)]%R

@fo me=&owner_list Data Terminal DB=%#
@fo me=@edit Data Terminal/*=#TERMDB,[num(Data Terminal DB)]
@fo me=@edit Data Terminal DB/*=#TERMDB,[num(Data Terminal DB)]

@wait 5=@pemit/silent %#=[ansi(hr,Moving objects to their appropriate places...)]%R

@wait 6=@tel Data Terminal DB=Data Terminal

@wait 7=@pemit/silent %#=%R%R[ansi(hr,Installation Complete!!  You may now change the name of the terminal object and edit its description if you like.  You should also set its title and access list using the commands in +term/help.)]%R

@wait 8=@set me=!quiet

