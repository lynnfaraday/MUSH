

@create FS3Combat Instance Parent
@set FS3Combat Instance Parent = WIZARD
@set FS3Combat Instance Parent = SAFE
@set FS3Combat Instance Parent = !NO_COMMAND


@@ DB# of organizer.
&ORGANIZER FS3Combat Instance Parent=

@@ Team targets. By default only 1 and 2 have target specifications.
&TEAM1_TARGET FS3Combat Instance Parent=2
&TEAM2_TARGET FS3Combat Instance Parent=1

@@ List of players who entered their action.
&ACTED FS3Combat Instance Parent=

@@ Tracks turn #
&TURN FS3Combat Instance Parent=0

@@ Defaults to real combat
&REAL FS3Combat Instance=1

@@ STAT_<name>`<stat> stores all combat stats.


@@ ================================================================================================================
@@ ---------------   Attribute Management -----------------
@@ ================================================================================================================

@@ --------------------------
@@ FUN_COMBATSTAT
@@ Gets a combat stat.  Blank if not in combat.
@@ %0 = name
@@ %1 = stat
@@ --------------------------

&COMBATSTATS FS3Combat Instance Parent=type team vehicle weapon stance armor mods luck lastaim suppress action target actoptions hitthisturn ko subduedby ammo 
&FUN_COMBATSTAT FS3Combat Instance Parent=[v(stat_[unprettify(%0)]`[unprettify(%1)]

@@ --------------------------
@@  FUN_MOD_COMBATSTAT
@@  Modifies one of the combat stats.
@@  %0 = Name
@@  %1 = The stat to set, from COMBATSTATS.
@@  %2 = The new value.
@@ --------------------------
&FUN_MOD_COMBATSTAT FS3Combat Instance Parent=set(me,stat_[unprettify(%0)]`[unprettify(%1)]:%2)

@@ --------------------------
@@ FUN_INIT_COMBATSTAT
@@ %0 = name
@@ %1 = type
@@ %2 = vehicle
@@ %3 = weapon
@@ Note: 
@@ - PCs automatically put on team 1, and NPCs on team 2.
@@ --------------------------
&FUN_INIT_COMBATSTAT  FS3Combat Instance Parent=iter(v(combatstats),u(fun_mod_combatstat,%0,##,[u(init_##,%0,%1,%2,%3)]))

&INIT_TYPE FS3Combat Instance Parent=[lcstr(trim(%1))]
&INIT_TEAM FS3Combat Instance Parent=[switch(%1,observer,,npcmaster,,switch(isplayer(%0),1,1,2))]
&INIT_VEHICLE FS3Combat Instance Parent=[prettify(%2)]
&INIT_WEAPON FS3Combat Instance Parent=%3
&INIT_STANCE FS3Combat Instance Parent=normal
&INIT_ARMOR FS3Combat Instance Parent=
&INIT_MODS FS3Combat Instance Parent=0
&INIT_LUCK FS3Combat Instance Parent=
&INIT_LASTAIM FS3Combat Instance Parent=
&INIT_SUPPRESS FS3Combat Instance Parent=0
&INIT_ACTION FS3Combat Instance Parent=pass
&INIT_TARGET FS3Combat Instance Parent=
&INIT_ACTOPTIONS FS3Combat Instance Parent=
&INIT_HITTHISTURN FS3Combat Instance Parent=0
&INIT_KO FS3Combat Instance Parent=0
&INIT_SUBDUEDBY FS3Combat Instance Parent=
&INIT_AMMO FS3Combat Instance Parent=[weaponstat(%3,ammo)]

@@ --------------------------
@@ FUN_REMOVE
@@ Removes someone from combat by erasing all their combat attributes.
@@ %0 = name
@@ --------------------------
&FUN_REMOVE FS3Combat Instance Parent=wipe(me/stat_[unprettify(%0)]**)

@@ ================================================================================================================
@@ ---------------  Utilities -----------------
@@ ================================================================================================================

@@ --------------------------
@@ FUN_DAMAGEMOD
@@ Gets the damage modifier, which is for the person (if a soldier) or them + their vehicle (for pilot/passenger)
@@ %0 = name
@@ --------------------------
&FUN_DAMAGEMOD FS3Combat Instance Parent=switch(u(fun_is_in_vehicle,%0),1,add(damagemod(%0),damagemod(u(fun_combatstat,%0,vehicle))),damagemod(%0))

@@ --------------------------
@@ FUN_WEAPONSTAT
@@ Shortcut to get a particular weapon stat for someone's weapon.
@@ %0 = name
@@ %1 = weapon stat
@@ --------------------------
&FUN_WEAPONSTAT FS3Combat Instance Parent=weaponstat(u(fun_combatstat,%0,weapon),%1)

@@ --------------------------
@@ FUN_VEHICLESTAT
@@ Shortcut to get a particular vehicle stat for someone's vehicle.
@@ %0 = name
@@ %1 = vehicle stat
@@ --------------------------
&FUN_VEHICLESTAT FS3Combat Instance Parent=vehtypestat(vehiclestat(u(fun_combatstat,%0,vehicle),type),%1)



@@ ================================================================================================================
@@ ---------------   Participants and Messages -----------------
@@ ================================================================================================================

@@ --------------------------
@@ FUN_IS_PARTICIPANT
@@ FUN_IS_COMBATANT
@@ FUN_IS_TARGETABLE
@@ Given a name, returns whether they're a combatant, participant or targetable.
@@ 'participant' refers to everyone who's joined the combat 
@@ 'combatant' refers to everyone EXCEPT observers and npcmasters 
@@ 'targetable' refers to everyone EXCEPT observers npcmasters and passengers and folks in the 'outofsight' stance.
@@ 'actors' refers to everyone EXCEPT observers
@@ --------------------------
&FUN_IS_PARTICIPANT FS3Combat Instance Parent=t(u(fun_combatstat,%0,type))
&FUN_IS_COMBATANT FS3Combat Instance Parent=switch(u(fun_combatstat,%0,type),,0,observer,0,npcmaster,0,1)
&FUN_IS_TARGETABLE FS3Combat Instance Parent=and(switch(u(fun_combatstat,%0,type),,0,observer,0,passenger,0,npcmaster,0,1),not(strmatch(u(fun_combatstat,%0,stance),outofsight)))
&FUN_IS_ACTOR FS3Combat Instance Parent=switch(u(fun_combatstat,%0,type),,0,observer,0,1)

@@ --------------------------
@@ FUN_PARTICIPANTS
@@ FUN_COMBATANTS
@@ FUN_TARGETABLE
@@ Gets an (unpretty) list of the participants, combantants or targetable folks
@@ --------------------------
&FUN_PARTICIPANTS FS3Combat Instance Parent=iter(lattr(me/stat_*),after(##,_))
&FUN_COMBATANTS FS3Combat Instance Parent=iter(lattr(me/stat_*),switch(u(fun_is_combatant,after(##,_)),1,after(##,_)))
&FUN_TARGETS FS3Combat Instance Parent=iter(lattr(me/stat_*),switch(u(fun_is_targetable,after(##,_)),1,after(##,_)))
&FUN_ACTORS FS3Combat Instance Parent=iter(lattr(me/stat_*),switch(u(fun_is_actor,after(##,_)),1,after(##,_)))

@@ --------------------------
@@ FUN_IS_IN_VEHICLE
@@ Tells if you're in a vehicle or not.
@@ %0 = name
@@ --------------------------
&FUN_IS_IN_VEHICLE FS3Combat Instance Parent=switch(u(fun_combatstat,%0,type),pilot,1,passenger,1,0)

@@ --------------------------
@@ FUN_COMBAT_MSG
@@ Sends a message to all participants.
@@ %0 = message
@@ --------------------------
&FUN_COMBAT_MSG FS3Combat Instance Parent=iter(u(fun_participants),switch(isplayer(##),1,pemit(pmatch(##),[ansi(hb,<COMBAT>)] %0)),,)

@@ --------------------------
@@ FUN_ORG_MSG
@@ Sends a message to only the organizer.
@@ %0 = message
@@ --------------------------
&FUN_ORG_MSG FS3Combat Instance Parent=pemit(v(organizer),[ansi(hg,<COMBAT>)] %0)


@@ ================================================================================================================
@@ ---------------  Actions -----------------
@@ ================================================================================================================


@@ --------------------------
@@ FUN_DO_ACTION
@@ Triggers a character's combat action.
@@ %0 = name
@@ --------------------------
&FUN_DO_ACTION  FS3Combat Instance Parent=u(fun_action_[u(fun_combatstat,%0,action)],%0,u(fun_combatstat,%0,target),u(fun_combatstat,%0,actoptions))


@@ --------------------------
@@ All the action functions take:
@@ %0 = name
@@ %1 = target
@@ %2 = action options
@@ --------------------------

@@ TODO: Add headers to all these.
&FUN_ACTION_PASS FS3Combat Instance Parent=[u(fun_combat_msg,[prettify(lcstr(%0))] passes.)]

&FUN_ACTION_RELOAD FS3Combat Instance Parent=[u(fun_combat_msg,[prettify(lcstr(%0))] reloads.)][u(fun_mod_combatstat,%0,ammo,u(fun_weaponstat,%0,ammo))]

@@ TODO
&FUN_ACTION_ESCAPE FS3Combat Instance Parent=[u(fun_combat_msg,[prettify(lcstr(%0))] attempts to escape but Faraday hasn't coded that yet.)]

@@ TODO
&FUN_ACTION_SUBDUE FS3Combat Instance Parent=[u(fun_combat_msg,[prettify(lcstr(%0))] attempts to subdue [prettify(lcstr(%1))] but Faraday hasn't coded that yet.)]

@@ %q0 = attack roll result
@@ %q1 = defense roll result
&FUN_ACTION_SUPPRESS FS3Combat Instance Parent=localize([u(fun_combat_msg,[prettify(lcstr(%0))] suppresses [prettify(lcstr(%1))] with [prettify(u(fun_combatstat,%0,weapon))].%B[setq(0,u(fun_roll_attack,%0,%1))][setq(1,u(fun_roll_defense,%0,%1))][switch(and(gte(%q0,%q1),gt(%q0,0)),0,<unsuccessful>,<successful>[u(fun_mod_combatstat,%1,suppress,add(%q0,u(fun_combatstat,%1,suppress)))])])

&FUN_ACTION_AIM FS3Combat Instance Parent=[u(fun_combat_msg,[prettify(lcstr(%0))] takes careful aim at [prettify(lcstr(%1))].)]

@@ %q0 = attack roll result
@@ %q1 = defense roll result
@@ %q2 = random hit location
@@ %q3 = random damage
@@ %q4 = weapon
@@ TODO: Fix all this
&FUN_ACTION_ATTACK  FS3Combat Instance Parent=localize([setq(0,u(fun_roll_attack,%0,%1))][setq(1,u(fun_roll_defense,%0,%1))][setq(2,first(shuffle(v(hitlocs_[switch(u(fun_combatstat,%1,type),soldier,person,plane)]))))][setq(3,u(random_damage,sub(%q0,%q1),%q2,u(fun_weaponstat,%0,lethality)))][setq(4,u(fun_combatstat,%0,weapon))][u(fun_combat_msg,[prettify(lcstr(%0))] attacks [prettify(lcstr(%1))] with [prettify(%q4)] and [switch(1,lte(%q0,0),MISSES!,lt(%q0,%q1),%1 DODGES!,HITS! [switch(%q3,C,[ansi(hr,Critical)],S,[ansi(hy,Serious)],M,[ansi(hb,Moderate)],[ansi(hg,Light)])] wound to %q2.[u(fun_mod_combatstat,%1,hitthisturn,1)][dodamage(switch(u(fun_is_in_vehicle,%1),1,u(fun_combatstat,%1,vehicle),%1),v(real),%q2,%q4,weaponstat(%q4,dmgtype),%q3)])])
&hitlocs_person FS3Combat Instance Parent=Head Head Chest Chest Chest Chest Chest  Chest Chest Chest Chest Chest Abdomen Abdomen Abdomen Abdomen RightArm RightArm RightArm RightHand LeftArm LeftArm LeftArm LeftHand RightLeg RightLeg Rightfoot LeftLeg LeftLeg LeftFoot
&hitlocs_plane FS3Combat Instance Parent=Body Body Body Body Body Body Body Body Cockpit Cockpit Engine Engine Engine Weapon LandingGear Nose Tail Controls RightWing LeftWing
@@ %0 = attack - def roll
@@ %1 = hit loc
@@ %2 = weapon lethal
&random_damage FS3Combat Instance Parent=extract(L L L L L L L L L L L L L L L L L L L L M M M M M M M M M M M M M M M M M M M M M M M M M M M M M M M M M M M S S S S C,min(add(rand(1,50),max(%0,2),switch(%1,head,2,chest,1,abdomen,1,0),div(%2,10)),50)

@@ %0 = attacker
@@ %1 = defender
&FUN_ROLL_ATTACK FS3Combat Instance Parent=[setq(9,u(fun_weaponstat,%0,atkskill))][setq(8,u(fun_attack_mods,%0,%1))][roll_ability(pmatch(%0),%q9,%q8)][pemit(*Faraday,%q9 %q8)]

@@ --------------------------
@@ FUN_DEFENSE_MODS
@@ %0 = attacker
@@ %1 = defender
@@ Attack mods include:
@@ - Damage modifier
@@ - Suppression value x 10
@@ - Weapon accuracy
@@ - Special modifiers
@@ - Aim modifier x 15
@@ - Stance modifier
@@ --------------------------
&FUN_ATTACK_MODS FS3Combat Instance Parent=add(-[u(fun_damagemod,%0)],-[mul(u(fun_combatstat,%0,suppress),10)],u(fun_weaponstat,%0,accuracy),u(fun_combatstat,%0,mods),switch(u(fun_combatstat,%0,lastaim),%1,15,0),u(fun_stance_mod,%0,attacker))

@@ %0 = attacker
@@ %1 = defender
&FUN_ROLL_DEFENSE FS3Combat Instance Parent=roll_ability(pmatch(%1),u(fun_weaponstat,%0,defskill),[u(fun_defense_mods,%0)])

@@ --------------------------
@@ FUN_DEFENSE_MODS
@@ %0 = attacker
@@ %1 = defender
@@ Defense mods include:
@@ - Damage modifier
@@ - Suppression value x 10
@@ - Special modifiers
@@ - Stance mods
@@ --------------------------
&FUN_DEFENSE_MODS FS3Combat Instance Parent=add(-[u(fun_damagemod,%1)],-[mul(u(fun_combatstat,%0,suppress),10)],u(fun_combatstat,%0,mods),u(fun_stance_mod,%1,defender))


@@ --------------------------
@@ FUN_STANCE_MOD
@@ Gets someone's stance modifier.
@@ %0 = name
@@ %1 = attacker or defender
@@ --------------------------
&FUN_STANCE_MOD FS3Combat Instance Parent=after(grab(v(stancemod_%1),[u(fun_combatstat,%0,stance)]:*),:)

@@ Note: Cover and out of sight are handled specially so they don't have direct modifiers
&STANCEMOD_ATTACKER FS3Combat Instance Parent=normal:0 banzai:+30 evade:-50 cautious:-15 cover:0 outofsight:0
&STANCEMOD_DEFENDER FS3Combat Instance Parent=normal:0 banzai:-50 evade:+30 cautious:+15 cover:0 outofsight:0

@@ ================================================================================================================
@@ ---------------  Turns -----------------
@@ ================================================================================================================

@@ --------------------------
@@ POSE_HOOK
@@ This can be installed as an @hook after pose/say/semipose/emit to trigger combat notification when all actors have posed.
@@ --------------------------
&POSE_HOOK FS3Combat Instance Parent=[setq(c,xget(%#,combat))][switch(t(%qc),1,[set(%qc,posed:[setdiff([xget(%qc,posed)] %N,)])][u(%qc/fun_pose_trigger)])]
&FUN_POSE_TRIgGER FS3Combat Instance Parent=switch(u(FUN_EVERYONE_POSED),1,u(fun_org_msg,ansi(hc,Everyone has posed.)))

@@ --------------------------
@@ FUN_EVERYONE_ACTED
@@ Tells whether all players involved have registered an action.
@@ --------------------------
&FUN_EVERYONE_ACTED FS3Combat Instance Parent=not(strmatch(iter(u(fun_actors),switch(isplayer(##),1,t(match(v(acted),##)),1),,),*0*))


@@ --------------------------
@@ FUN_EVERYONE_POSED
@@ Tells whether all players involved have registered an action.
@@ --------------------------
&FUN_EVERYONE_POSED FS3Combat Instance Parent=not(strmatch(iter(u(fun_actors),switch(isplayer(##),1,t(match(v(posed),##)),1),,),*0*))

[switch(t(%qc),1,[set(%qc,posed:[setdiff([xget(%qc,posed)] %N,)])][u(%qc/fun_pose_trigger)])]

@@ --------------------------
@@ FUN_TURN_NUM
@@ Combat turn #
@@ --------------------------
&FUN_TURN_NUM FS3Combat Instance Parent=v(turn)

@@ --------------------------
@@ FUN_NEW_TURN
@@ Starts a new turn
@@ - Increment turn #
@@ - Wipe acted list
@@ foreach combatant...
@@   - Check for KO if hit. Note: if vehicle KO'd everyone in it is KO'd as well.
@@   - Reduce suppression effects by 1/2.  Cap at 3.
@@   - Reset luck expenditures
@@   - Set lastaim (to target if aiming, to blank if not)
@@   - reset 'hit this turn'
@@ --------------------------
&FUN_NEW_TURN FS3Combat Instance Parent=[u(fun_combat_msg,[ansi(hc,%N has started a new turn.  Pose and choose your action.)])][set(me,turn:[inc(v(turn))])][set(me,acted:)][set(me,posed:)][iter(u(fun_combatants),[u(fun_check_for_ko,##)][u(fun_reduce_suppress,##)][u(fun_reset_aim,##)][u(fun_reset_luck,##)][u(fun_reset_hit,##)])]

@@ --------------------------
@@ These are helper functions for FUN_NEW_TURN just to keep things less messy.  
@@ %0 = name
@@ --------------------------
&FUN_CHECK_FOR_KO FS3Combat Instance Parent=localize([setq(0,u(fun_combatstat,%0,vehicle))][switch(u(fun_combatstat,%0,hitthisturn),1,switch(checkko(switch(t(%q0),1,%q0,%0)),1,[u(fun_ko,%0)][switch(t(%q0),1,iter(remove(u(fun_combatants),%0),switch(u(fun_combatstat,##,vehicle),%q0,u(fun_ko,##))))]))])
&FUN_REDUCE_SUPPRESS FS3Combat Instance Parent=u(fun_mod_combatstat,%0,suppress,min(sub(u(fun_combatstat,%0,suppress),2),3))
&FUN_RESET_LUCK FS3Combat Instance Parent=u(fun_mod_combatstat,%0,luck,)
&FUN_RESET_AIM FS3Combat Instance Parent=u(fun_mod_combatstat,%0,lastaim,switch(u(fun_combatstat,%0,action),aim,u(fun_combatstat,%0,target),))
&FUN_RESET_HIT FS3Combat Instance Parent=u(fun_mod_combatstat,%0,hitthisturn,0)

@@ --------------------------
@@ FUN_KO
@@ Handles knocking someone out.  Players are set 'ko' but NPCs are removed from combat.
@@ %0 = name
@@ --------------------------
&FUN_KO FS3Combat Instance Parent=u(fun_combat_msg,[prettify(lcstr(%0))] has been [ansi(hr,**KO'd**)]!)[switch(isplayer(%0),1,u(fun_mod_combatstat,%0,ko,1),u(fun_remove,%0))]

@@ --------------------------
@@ FUN_INITIATIVE
@@ Determines initiative order.
@@ %0 = name
@@ --------------------------
&FUN_INITIATIVE FS3Combat Instance Parent=iter(revwords(sort(iter(u(fun_combatants),[roll_ability(pmatch(##),Reactive)]:##),n)),after(##,:))

@@ ================================================================================================================
@@ ---------------  Teams -----------------
@@ ================================================================================================================

@@ --------------------------
@@ FUN_TEAM_TARGETS
@@ Limits targets to only ones that are valid based on your team. (unpretty list)
@@ %0 = your team (will be excluded)
@@ %1 = if specified, will limit list to ONLY targets on these teams
@@ --------------------------

&FUN_TEAM_TARGETS FS3Combat Instance Parent=[iter(regraball(iter(u(fun_targets),[u(fun_combatstat,##,team)]:##),\[[edit(remove(switch(%1,,lnum(1,9),%1),%0),%B,|)]\]:),after(##,:))]

@@ --------------------------
@@ FUN_TEAM_MEMBERS
@@ Gets unpretty list of names in a team.
@@ %0 = team
@@ --------------------------
&FUN_TEAM_MEMBERS FS3Combat Instance Parent=[squish(iter(u(fun_combatants),switch(u(fun_combatstat,##,team),%0,##)))]

@@ --------------------------
@@ FUN_ACTIVE_TEAMS
@@ Gets list of teams with people in them.
@@ --------------------------
&FUN_ACTIVE_TEAMS FS3Combat Instance Parent=[sort(setdiff(iter(u(fun_combatants),u(fun_combatstat,##,team)),))]

@@ --------------------------
@@ FUN_SET_TEAM_TARGET
@@ Sets a team's target teams
@@ %0 = team to set
@@ %1 = list of teams to target
@@ --------------------------
&FUN_SET_TEAM_TARGET FS3Combat Instance Parent=set(me,team[trim(%0)]_target:[trim(%1)])

@@ --------------------------
@@ FUN_PICK_RANDOM_TARGET
@@ Picks a random target from the appropriate team target list.
@@ %0 = name
@@ --------------------------
&FUN_PICK_RANDOM_TARGET FS3Combat Instance Parent=localize([setq(0,u(fun_combatstat,%0,team))][first(shuffle(u(fun_team_targets,%q0,v(team[%q0]_target))))])


@@ --------------------------
@@ FUN_CAN_ATTACK
@@ Checks if they have an attack-ok weapon (i.e. not a defensive one).
@@ %0 = name
@@ --------------------------
&FUN_CAN_ATTACK FS3Combat Instance Parent=switch(u(fun_weaponstat,%0,wpntype),defensive,0,1)

@@ --------------------------
@@ FUN_SET_ACTION
@@ Sets up action attributes.
@@ %0 = name
@@ %1 = action 
@@ %2 = target
@@ %3 = options
@@ --------------------------
&FUN_SET_ACTION FS3Combat Instance Parent=[u(fun_mod_combatstat,%0,action,[lcstr(%1)])][u(fun_mod_combatstat,%0,target,[unprettify(%2)])][u(fun_mod_combatstat,%0,actoptions,%3)][u(fun_combat_msg,[prettify(lcstr(%0))] will [lcstr(%1)][switch(%2,,,%B[prettify(lcstr(%2))])] this turn.[u(fun_add_id,%0)])][set(me,acted:[setdiff([v(acted)] %N,)])][switch(u(fun_everyone_acted),1,u(fun_org_msg,[ansi(hc,Everyone has entered their actions.)]))]


@@ --------------------------
@@ FUN_ADD_ID
@@ Adds an identifier after a command if it's done on a NPC.
@@ %0=target of the command.
@@ --------------------------
&FUN_ADD_ID FS3Combat Instance Parent=switch(%0,%N,,%B\(%N\))


@startup FS3Combat Instance Parent=@set me=!no_command



