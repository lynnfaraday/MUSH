@@ Faraday's Pose Autospacer System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hr,Beginning Autospacer Installation.  Please wait until the Installation Complete message appears...)]%R

@pemit/silent %#=[ansi(hr,Creating Objects...)]%R

@create Pose Autospacer
@set Pose Autospacer = WIZARD
@set Pose Autospacer = SAFE
@set Pose Autospacer = !NO_COMMAND

&CMD-+AUTOSPACE Pose Autospacer=$+autospace:think [pemit(%#,switch(xget(%#,autospace),1,ansi(hg,You turn autospacing OFF.),ansi(hg,You turn autospacing ON.)))][set(%#,autospace:[not(xget(%#,autospace))])]
&HELP_+AUTOSPACE Pose Autospacer=This MUSH is set up to put blank lines before each pose, say or emit automatically. You can turn this on or off using the +autospace command.  Just type [ansi(h,+autospace)] and it will toggle the spaces on and off.
&LINE_HOOK Pose Autospacer=iter(lplayers(loc(%#)),switch(xget(##,autospace),0,,pemit(##,%B)))
@STARTUP Pose Autospacer=@hook/before pose=[num(me)],LINE_HOOK;@hook/before say=[num(me)],LINE_HOOK;@hook/before @emit=[num(me)],LINE_HOOK;@hook/before semipose=[num(me)],LINE_HOOK;@set me=!no_command


@DESCRIBE Pose Autospacer=Automatically puts spaces between poses.%R[u(credits)]
&CREDITS Pose Autospacer=Version [v(version)]%R%RCoded by Faraday ~ http://www.wordsmyth.org/faraday
&VERSION Pose Autospacer=4.xx

@wait 4=@pemit/silent %#=[ansi(hr,Copying help files...)]%R
@wait 4=@fo %#=+copyhelp [num(Pose Autospacer)]
@wait 4=think setup_attr(h,autospace,1)
@wait 4=@trigger Pose Autospacer/startup

@wait 5=@pemit/silent %#=[ansi(hr,Moving objects to their appropriate places...)]%R
@wait 5=@tel Pose Autospacer=#2

@wait 6=@pemit/silent %#=%R%R[ansi(hr,Installation Complete!!)]

@wait 6=@set me=!quiet

