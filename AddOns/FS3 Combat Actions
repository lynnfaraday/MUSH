@@ TODO: Store the default infantry weapon somewhere.

@create FS3Combat Instance Parent
@set FS3Combat Instance Parent = WIZARD
@set FS3Combat Instance Parent = SAFE
@set FS3Combat Instance Parent = !NO_COMMAND
&ORGANIZER FS3Combat Instance Parent=

@@ @@@@@@@@@@@@@@@@@@@@@
@@ FUN_COMBATSTAT
@@ Gets a combat stat.  Blank if not in combat.
@@ %0 = name
@@ %1 = stat
@@ @@@@@@@@@@@@@@@@@@@@@

&COMBATSTATS FS3Combat Instance Parent=type vehicle weapon stance armor mods luck lastaim suppress action acttarget actoptions
&FUN_COMBATSTAT FS3Combat Instance Parent=extract(u(fun_combat_attr,match(v(combatstats),%1),1,|)

@@ @@@@@@@@@@@@@@@@@@@@@
@@ FUN_INIT_COMBATSTAT
@@ %0 = name
@@ %1 = type
@@ %2 = vehicle
@@ %3 = weapon
@@ @@@@@@@@@@@@@@@@@@@@@
&FUN_INIT_COMBATSTAT  FS3Combat Instance Parent=set(me,stat_[unprettify(%0)]:[lcstr(trim(%1))]|[prettify(%2)]|%3|normal||0|||0|pass||

@@ @@@@@@@@@@@@@@@@@@@@@
@@ FUN_COMBAT_ATTR
@@ Gets the combat stat attribute for a name.  Blank if not in combat.
@@ %0 = name
@@ @@@@@@@@@@@@@@@@@@@@@

&FUN_COMBAT_ATTR FS3Combat Instance Parent=v(stat_[unprettify(%0)])

@@ @@@@@@@@@@@@@@@@@@@@@
@@ FUN_IS_PARTICIPANT
@@ FUN_IS_COMBATANT
@@ FUN_IS_TARGETABLE
@@ Given a name, returns whether they're a combatant, participant or targetable.
@@ 'participant' refers to everyone who's joined the combat 
@@ 'combatant' refers to everyone EXCEPT observers 
@@ 'targetable' refers to everyone EXCEPT observers and passengers
@@ @@@@@@@@@@@@@@@@@@@@@

&FUN_IS_PARTICIPANT FS3Combat Instance Parent=t(u(fun_combat_attr,%0))
&FUN_IS_COMBATANT FS3Combat Instance Parent=switch(u(fun_combatstat,%0,type),,0,observer,0,1)
&FUN_IS_TARGETABLE FS3Combat Instance Parent=switch(u(fun_combatstat,%0,type),,0,observer,0,passenger,0,1)

@@ @@@@@@@@@@@@@@@@@@@@@
@@ FUN_PARTICIPANTS
@@ Gets an (unpretty) list of the participants.
@@ @@@@@@@@@@@@@@@@@@@@@

&FUN_PARTICIPANTS FS3Combat Instance Parent=iter(lattr(me/stat_*),after(##,_))

@@ @@@@@@@@@@@@@@@@@@@@@
@@ FUN_COMBAT_MSG
@@ Sends a message to all participants.
@@ %0 = message
@@ @@@@@@@@@@@@@@@@@@@@@

&FUN_COMBAT_MSG FS3Combat Instance Parent=iter(u(fun_participants),switch(isplayer(##),1,pemit(pmatch(##),[ansi(hb,<COMBAT>)] %0))


@create FS3Combat Action Commands
@set FS3Combat Action Commands = WIZARD
@set FS3Combat Action Commands = SAFE
@set FS3Combat Action Commands = !NO_COMMAND

&CMD-+COMBAT-ALL FS3Combat Action Commands=$+combat/all:@pemit %#=[line()]%R[ansi(h,Combat #%B%BOrganizer%B%B%B%BParticipants)][iter(children(#806),%R[ljust(##,9)] [padstr(name(xget(##,organizer)),12)] [iter(u(##/fun_participants),prettify(lcstr(itext(0))))])]%R[line()]

&CMD-+COMBAT-START FS3Combat Action Commands=$+combat/start *:@pemit %#=switch(0,not(u(FUN_IS_IN_COMBAT,%#)),ansi(hr,You are already in combat.),match(mock real,%0),ansi(hr,Combat type must be mock or real.),ansi(hg,You start a combat.  Your combat number is [setq(0,create(Combat Instance))]%q0.[setq(1,parent(%q0,#806))][set(%q0,organizer:%#)][set(%q0,mock:[switch(%0,mock,1,0)])]))

&CMD-+COMBAT-STOP FS3Combat Action Commands=$+combat/stop *:@pemit %#=switch(0,u(FUN_IS_VALID_COMBAT,%0),ansi(hr,That is not a valid combat number.),or(isstaff(%#),strmatch(xget(%0,organizer),%N)),ansi(hr,Only staff or the organizer can stop a combat.),ansi(hg,You stop combat %0.)[u(%0/fun_combat_msg,%N has stopped the combat.)][iter(u(%0/fun_participants),[switch(isplayer(##),1,wipe([pmatch(##)]/combat))][clearmockdmg(##)])][setq(0,1)]);@switch %q0=1,{@nuke %0;@nuke %0}

&CMD-+COMBAT-JOIN FS3Combat Action Commands=$+combat/join *=*:@pemit %#=u(FUN_DO_JOIN,%N,%0,%1)

&CMD-+COMBAT-FORCEJOIN FS3Combat Action Commands=$+combat/forcejoin *=*/*:@pemit %#=[switch(0,isnpc(%1),[setq(0,createnpc(%1,first(shuffle(1 2 2 3 3 3 4 4 4 4 4 4 4 4 4 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 6 6 6 7 7 8 8 9 10 11 12))))][switch(%q0,0|*,ansi(hr,after(%q0,|)))])][switch(isnpc(%1),1,u(FUN_DO_JOIN,%1,%0,%2))]


@@ @@@@@@@@@@@@@@@@@@
@@ FUN_DO_JOIN
@@ Handles joining someone (PC or NPC) to combat.
@@ %0 = name
@@ %1 = combat #
@@ %2 = type/vehicle  (split into %q0=type, %q1=vehicle)
@@ 
@@ NOTE! Uses %qv for temporary vehicle results register
@@ Returns message to display to enactor.
@@ @@@@@@@@@@@@@@@@@@

&FUN_DO_JOIN FS3Combat Action Commands=localize([setq(0,before(%2,/))][setq(1,after(%2,/))][switch(0,u(FUN_IS_VALID_COMBAT,%1),ansi(hr,That is not a valid combat number.),not(u(FUN_IS_IN_COMBAT,%0)),ansi(hr,%0 is already in combat.),match(v(types),%q0),ansi(hr,That is not a valid type.  Pick one of: [v(types)].),[setq(v,u(fun_join_vehicle_stuff,%q0,%q1))][eq(%qv,1)],ansi(hr,after(%qv,|)),ansi(hg,<COMBAT> %0 joins combat %1 as a %q0[switch(t(%q1),1,%Bin [prettify(%q1)])].%R< Weapon set to: [u(fun_default_weapon,%q0,%q1)])[u(%1/fun_combat_msg,%0 has joined the combat as a %q0[switch(t(%q1),1,%Bin [prettify(%q1)])].)][u(%1/fun_init_combatstat,%0,%q0,%q1,[u(fun_default_weapon,%q0,%q1)])][set(pmatch(%0),combat:%1)])

@@ @@@@@@@@@@@@@@@@@@
@@ FUN_DEFAULT_WEAPON
@@ %0 = type
@@ %1 = vehicle
@@ @@@@@@@@@@@@@@@@@@

&FUN_DEFAULT_WEAPON FS3Combat Action Commands=switch(%0,soldier,Rifle,pilot,first(vehtypestat(vehiclestat(%1,type),weapons)),)

@@ @@@@@@@@@@@@@@@@@@
@@ FUN_JOIN_VEHICLE_STUFF
@@ Figures out all the vehicle stuff when joining combat.  Checks to make sure the vehicle names are valid and such.  Creates a random vehicle
@@ if needed.  
@@ %0 = type
@@ %1 = vehicle
@@
@@ Returns either 1 or 0|error
@@ NOTE!!  Overrides %q1 if vehicle type was specified and a random vehicle created.  DO NOT LOCALIZE
@@ @@@@@@@@@@@@@@@@@@

&FUN_JOIN_VEHICLE_STUFF FS3Combat Action Commands=switch(1,not(match(pilot passenger,%0)),1,isvehicle(%1),1,not(%1),0|You must specify a vehicle if you are a pilot or passenger.,not(match(vehicletypes(),%1)),0|%1 is not a valid vehicle name or type,t(match(vehicletypes(),%1)),1[setq(1,randvehicle(%1))],0|Unknown error.)

&TYPES FS3Combat Action Commands=soldier pilot passenger observer

&FUN_IS_IN_COMBAT FS3Combat Action Commands=switch(t(pmatch(%0)),1,t(xget(pmatch(%0),combat)),t(squish(iter(children(#806),lattr(##/stat_[unprettify(%0)])))))
&FUN_IS_VALID_COMBAT FS3Combat Action Commands=match(children(#806),%0)


@STARTUP FS3Combat Action Commands=@set me=!no_command



