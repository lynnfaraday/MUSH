@@ TODO: Sort combat status by team


@create FS3Combat Action Commands
@set FS3Combat Action Commands = WIZARD
@set FS3Combat Action Commands = SAFE
@set FS3Combat Action Commands = !NO_COMMAND

@@ ================================================================================================================
@@ ---------------   Combat Status -----------------
@@ ================================================================================================================

&CMD-+COMBAT-ALL FS3Combat Action Commands=$+combat/all:@pemit %#=[line()]%R[ansi(h,Combat #%B%BOrganizer%B%B%B%BParticipants)][iter(children(#806),%R[ljust(##,9)] [padstr(name(xget(##,organizer)),12)] [iter(u(##/fun_participants),prettify(lcstr(itext(0))))])]%R[line()]

&CMD-+COMBAT-STATUS FS3Combat Action Commands=$+combat:@pemit %#=[setq(0,u(fun_combat_num,%N))]switch(0,t(%q0),ansi(hr,You are not in combat.),[iter(u(%q0/fun_combatants),%R[padstr(prettify(lcstr(##)),15)][setq(1,u(%q0/fun_combatstat,##,type))][padstr([switch(%q1,observer,obs,pilot,plt,passenger,pas,soldier,sol)] [switch(t(match(pilot passenger,%q1)),1,\([u(%q0/fun_combatstat,##,vehicle)]\))],15)][u(fun_dmg_meter,u(%q0/fun_damagemod,##))])]%R%R[ansi(h,Observers:)] %R[iter(setdiff(u(%q0/fun_participants),u(%q0/fun_combatants)),prettify(lcstr(##)))])


@@ @@@@@@@@@@@@@@@@@@
@@ FUN_IS_IN_COMBAT
@@ Returns 1 if the person is in combat.
@@ %0 = name
@@ @@@@@@@@@@@@@@@@@@
&FUN_IS_IN_COMBAT FS3Combat Action Commands=t(u(FUN_COMBAT_NUM,%0))

@@ @@@@@@@@@@@@@@@@@@
@@ FUN_IS_VALID_COMBAT
@@ Returns 1 if there is a combat by that name.
@@ %0 = combat #
@@ @@@@@@@@@@@@@@@@@@
&FUN_IS_VALID_COMBAT FS3Combat Action Commands=match(children(#806),%0)

@@ @@@@@@@@@@@@@@@@@@
@@ FUN_COMBAT_NUM
@@ Returns the combat # of the person, if they're in combat.  Otherwise blank.
@@ %0 = name
@@ @@@@@@@@@@@@@@@@@@
&FUN_COMBAT_NUM  FS3Combat Action Commands=switch(t(pmatch(%0)),1,xget(pmatch(%0),combat),squish(iter(children(#806),switch(words(lattr(##/stat_[unprettify(%0)])),>0,##))))

@@ @@@@@@@@@@@@@@@@@@
@@ FUN_DMG_METER
@@ %0 = damage modifier
@@ Displays a damage meter that fills up as you become more hurt.
@@ @@@@@@@@@@@@@@@@@@
&FUN_DMG_METER FS3Combat Action Commands=switch(%0,>80,ansi(hR,XXXXX),>60,ansi(hr,XXXX.),>40,ansi(hy,XXX..),>20,ansi(hb,XX...),>0,ansi(hg,X....),....)


@@ ================================================================================================================
@@ ---------------   Combat Start/Stop/Join/Leave -----------------
@@ ================================================================================================================


&CMD-+COMBAT-START FS3Combat Action Commands=$+combat/start *:@pemit %#=switch(0,not(u(FUN_IS_IN_COMBAT,%#)),ansi(hr,You are already in combat.),match(mock real,%0),ansi(hr,Combat type must be mock or real.),ansi(hg,You start a combat.  Your combat number is [setq(0,create(Combat Instance))]%q0.[setq(1,parent(%q0,#806))][set(%q0,organizer:%#)][set(%q0,mock:[switch(%0,mock,1,0)])]))

&CMD-+COMBAT-STOP FS3Combat Action Commands=$+combat/stop *:@pemit %#=switch(0,u(FUN_IS_VALID_COMBAT,%0),ansi(hr,That is not a valid combat number.),or(isstaff(%#),strmatch(xget(%0,organizer),%N)),ansi(hr,Only staff or the organizer can stop a combat.),ansi(hg,You stop combat %0.)[u(%0/fun_combat_msg,%N has stopped the combat.)][iter(u(%0/fun_participants),u(FUN_DO_LEAVE,##,%0))][setq(0,1)]);@switch %q0=1,{@nuke %0;@nuke %0}


&CMD-+COMBAT-JOIN FS3Combat Action Commands=$+combat/join *=*:@pemit %#=u(FUN_DO_JOIN,%N,%0,%1)

&CMD-+COMBAT-FORCEJOIN FS3Combat Action Commands=$+combat/forcejoin *=*/*:@pemit %#=u(FUN_DO_JOIN,%1,%0,%2)

&CMD-+COMBAT-LEAVE FS3Combat Action Commands=$+combat/leave:@pemit %#=[setq(0,u(fun_combat_num,%N))]switch(0,t(%q0),ansi(hr,You are not in combat.),[u(%q0/fun_combat_msg,%N has left the combat.)][u(fun_do_leave,%N,%q0)])

&CMD-+COMBAT-FORCELEAVE FS3Combat Action Commands=$+combat/leave *:@pemit %#=[setq(0,u(fun_combat_num,%0))]switch(0,t(%q0),ansi(hr,%0 is not in combat.),[u(%q0/fun_combat_msg,[prettify(%0)] has left the combat. \(%N\))][u(fun_do_leave,%0,%q0)])


@@ @@@@@@@@@@@@@@@@@@
@@ FUN_DO_JOIN
@@ Handles joining someone (PC or NPC) to combat.
@@ %0 = name
@@ %1 = combat #
@@ %2 = type/vehicle  (split into %q0=type, %q1=vehicle)
@@ 
@@ NOTE! Uses %qv for temporary vehicle results register and %qn for temp npc register
@@ Returns message to display to enactor.
@@ @@@@@@@@@@@@@@@@@@

&FUN_DO_JOIN FS3Combat Action Commands=localize([setq(0,before(%2,/))][setq(1,after(%2,/))][switch(0,u(FUN_IS_VALID_COMBAT,%1),ansi(hr,That is not a valid combat number.),not(u(FUN_IS_IN_COMBAT,%0)),ansi(hr,%0 is already in combat.),match(v(types),%q0),ansi(hr,That is not a valid type.  Pick one of: [v(types)].),[setq(v,u(fun_join_vehicle_stuff,%q0,%q1))][eq(%qv,1)],ansi(hr,after(%qv,|)),[setq(n,u(fun_join_npc_stuff,%0))][eq(%qn,1)],ansi(hr,after(%qn,|)),ansi(hg,<COMBAT> %0 joins combat %1.%R< Weapon set to: [u(fun_default_weapon,%q0,%q1)])[u(%1/fun_init_combatstat,%0,%q0,%q1,[u(fun_default_weapon,%q0,%q1)])][set(pmatch(%0),combat:%1)][u(%1/fun_combat_msg,%0 has joined the combat as a %q0[switch(t(%q1),1,%Bin [prettify(%q1)])] on team [u(%1/fun_combatstat,%0,team)].)])

@@ @@@@@@@@@@@@@@@@@@
@@ FUN_JOIN_VEHICLE_STUFF
@@ Figures out all the vehicle stuff when joining combat.  Checks to make sure the vehicle names are valid and such.  Creates a random vehicle
@@ if needed.  
@@ %0 = type
@@ %1 = vehicle
@@
@@ Returns either 1 or 0|error
@@ NOTE!!  Overrides %q1 if vehicle type was specified and a random vehicle created.  DO NOT LOCALIZE
@@ @@@@@@@@@@@@@@@@@@

&FUN_JOIN_VEHICLE_STUFF FS3Combat Action Commands=switch(1,not(match(pilot passenger,%0)),1,isvehicle(%1),1,not(%1),0|You must specify a vehicle if you are a pilot or passenger.,not(match(vehicletypes(),%1)),0|%1 is not a valid vehicle name or type,t(match(vehicletypes(),%1)),1[setq(1,randvehicle(%1))],0|Unknown error.)

@@ @@@@@@@@@@@@@@@@@@
@@ FUN_JOIN_NPC_STUFF
@@ Figures out all the npc stuff when joining combat.  Checks to make sure the npc names are valid and such.  
@@ Creates a random npc if needed.  
@@ %0 = name
@@
@@ Returns either 1 or 0|error
@@ @@@@@@@@@@@@@@@@@@

&FUN_JOIN_NPC_STUFF FS3Combat Action Commands=switch(1,isplayer(%0),1,isnpc(%0),1,createnpc(%0,u(fun_rand_npc_skill)))
&FUN_RAND_NPC_SKILL FS3Combat Action Commands=first(shuffle(1 2 2 3 3 3 4 4 4 4 4 4 4 4 4 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 6 6 6 7 7 8 8 9 10 11 12))

@@ @@@@@@@@@@@@@@@@@@
@@ FUN_DEFAULT_WEAPON
@@ %0 = type
@@ %1 = vehicle
@@ @@@@@@@@@@@@@@@@@@
&FUN_DEFAULT_WEAPON FS3Combat Action Commands=switch(%0,soldier,defaultrifle(),pilot,first(vehtypestat(vehiclestat(%1,type),weapons)),)


@@ @@@@@@@@@@@@@@@@@@
@@ FUN_DO_LEAVE
@@ %0 = name
@@ %1 = combat #
@@ @@@@@@@@@@@@@@@@@@
&FUN_DO_LEAVE FS3Combat Action Commands=[switch(isplayer(%0),1,wipe([pmatch(%0)]/combat))][clearmockdmg(%0)][wipe(%1/stat_[unprettify(%0)])][ansi(hg,switch(%0,%N,You leave the combat.,You make %0 leave the combat.))]




&TYPES FS3Combat Action Commands=soldier pilot passenger observer

@STARTUP FS3Combat Action Commands=@set me=!no_command






@create FS3Combat Preferences
@set FS3Combat Preferences = !NO_COMMAND
@set FS3Combat Preferences = WIZARD

&HERO_FACTOR FS3Combat Preferences=0
&DEFAULT_RIFLE FS3Combat Preferences=Rifle

@STARTUP FS3Combat Preferences=@set me=!no_command;@fun herofactor=me,hero_factor;@fun defaultrifle=me,default_rifle




