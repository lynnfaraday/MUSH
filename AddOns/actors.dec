@@ Faraday's Actor System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hg,Beginning Actors Installation.  Please wait until the Installation Complete message appears...)]%R

@pemit/silent %#=[ansi(hg,Creating Objects...)]%R

think install_create(ACTORS_CMD,Actor Commands,1,0,5.0,Actor tracker.)
think install_create(ACTORS_DB,Actor DB,0,1,5.0,Actor database.)

&CMD-+ACTOR Actor Commands=$+actor *:think pemit(%#,switch(t(u(fun_find_actors,%0)),1,ansi(hr,That actor is already taken.),ansi(hg,You set your actor to %0.)[u(fun_set_actor,%N,%0)])))

&CMD-+ACTOR-CLEAR Actor Commands=$+actor/clear:think pemit(%#,switch(t([setq(0,u(fun_find_players,%N))]%q0),0,ansi(hr,You don't have an actor set.),ansi(hg,You clear your actor.)[wipe(#ACTORS_DB/%q0)])))

&CMD-+ACTORS Actor Commands=$+actors*:think switch(%0,/*,,%B*,,pemit(%#,[setq(1,switch(%0,,1,%0))][setq(2,[add(div(words(lattr(#ACTORS_DB/actor_*)),25),1)])][line()]%R[ansi(h,Characters with Actors Set \(Page %q1 of %q2\))]%R[iter(extract(sortby(fun_sort_actors,lattr(#ACTORS_DB/ACTOR_*)),add(1,mul(sub(%q1,1),25)),25),%R[setq(0,before(xget(#ACTORS_DB,##),|))][padstr([switch(t(pmatch(%q0)),1,%q0,ansi(hx,%q0))],30)][padstr(after(xget(#ACTORS_DB,##),|),30)])][switch(lt(%q1,%q2),1,%R%RSee [ansi(h,+actors[inc(%q1)])] for more pages.)]%R[line()]))
&CMD-+ACTOR-NPC Actor Commands=$+actor/npc *=*:think pemit(%#,switch(1,t(u(fun_find_actors,%1)),ansi(hr,That actor is already taken.),pmatch(%0),ansi(hr,%0 is a player.  Use the +actor command instead.),ansi(hg,You set %0's actor to %1.)[u(fun_set_actor,trim(capstr(%0)),%1)])))
&CMD-+ACTORS-SEARCH Actor Commands=$+actors *:think pemit(%#,[line()]%R[ansi(h,Characters with Actors Matching "[capstr(%0)]")]%R[iter(sortby(fun_sort_actors,u(fun_find_actors,%0)),%R[ljust(before(xget(#ACTORS_DB,##),|),30)][after(xget(#ACTORS_DB,##),|)])]%R[line()])
@STARTUP Actor Commands=@set me=!no_command
@set Actor Commands/STARTUP=no_command
&FUN_SORT_ACTORS Actor Commands=comp(before(xget(#ACTORS_DB,%0),|),before(xget(#ACTORS_DB,%1),|))
&FUN_FIND_ACTORS Actor Commands =squish(iter(lattr(#ACTORS_DB/actor_*),switch(strmatch(after(xget(#ACTORS_DB,##),|),*%0*),1,##)))
&FUN_FIND_PLAYERS Actor Commands =squish(iter(lattr(#ACTORS_DB/actor_*),switch(and(strmatch(before(xget(#ACTORS_DB,##),|),*%0*),strmatch(after(xget(#ACTORS_DB,##),|),xget(pmatch(%0),actor))),1,##)))
&FUN_SET_ACTOR Actor Commands =localize([setq(0,u(fun_find_players,%0))][switch(1,eq(words(%q0),1),[set(#ACTORS_DB,%q0:%0|%1)][set(pmatch(%0),actor:%1)],[set(pmatch(%0),actor:%1)][set(#ACTORS_DB,actor_[xget(#ACTORS_DB,next_actor)]:%0|%1)][set(#ACTORS_DB,next_actor:[inc(xget(#ACTORS_DB,next_actor))])])
&HELP_+ACTOR Actor Commands =This command lets you see which actors/actresses people have selected to represent their characters, or to see if a particular actor is "taken".%R%R[ansi(h,+actors)] - Lists all actors%R[ansi(h,+actors <search>)] - Search for all or part of an actor's name.%R%R[ansi(h,+actor <name>)] - Sets your character's actor.%R[ansi(h,+actor/clear)] - Erases your actor.%R[ansi(h,+actor/npc <character name>=<actor name>)] - Adds a NPC actor.
&FUN_GET_ACTOR Actor Commands=localize([setq(0,u(FUN_FIND_PLAYERS,%0))][switch(0,t(%q0),,after(xget(#ACTORS_DB,%q0),|))])
&HATTR_ACTOR Actor Commands=
@STARTUP Actor Commands=@set me=!no_command;@function actor=[num(me)],FUN_GET_ACTOR

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       DATA SECTION           *****
@@    *****   CHECK BEFORE UPGRADING     *****
@@    *****   REMOVE OF MODIFY AS NEEDED *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&NEXT_ACTOR Actor DB=1

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       END DATA SECTION      *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

@pemit/silent %#=[ansi(hg,Setting up database numbers...)]%R

@wait 2=+install/updategroup ACTORS

@wait 5=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!)]

@wait 5=@set me=!quiet

