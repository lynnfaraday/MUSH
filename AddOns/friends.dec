@@ Faraday's Friends System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hg,Beginning +friends Installation.  Please wait until the Installation Complete message appears...)]%R


@pemit/silent %#=[ansi(hg,Creating Objects...)]%R

think install_create(FRIENDS_CMD,Friends System,1,0,5.0,Friends tracker.)

@ACONNECT Friends System=think switch(hasflag(%#,dark),0,iter(lwho(),u(fun_notify_connect,##,%N))
@ADISCONNECT Friends System=think switch(hasflag(%#,dark),0,iter(lwho(),u(fun_notify_disconnect,##,%N))
&CMD-+FRIEND-ADD Friends System=$+friend/add *:think [setq(0,pmatch(%0))]pemit(%#,switch(0,t(%q0),ansi(hr,That is not a valid player.),not(match(xget(%#,list_friends),%0)),ansi(hr,That person is already on your friends list.),not(match(%N,%0)),ansi(hr,You can't add yourself!),ansi(hg,You add %0 to your friends list.)[set(%#,list_friends:[xget(%#,list_friends)][name(%q0)]%B)]))
&CMD-+FRIEND-REMOVE Friends System=$+friend/remove *:think [setq(0,pmatch(%0))]pemit(%#,switch(0,t(%q0),ansi(hr,That is not a valid player.),match(xget(%#,list_friends),%0),ansi(hr,That person is not on your friends list.),ansi(hg,You remove %0 from your friends list.)[set(%#,list_friends:[remove(xget(%#,list_friends),name(%q0))])]))
&CMD-+FRIENDS Friends System=$+friends:think pemit(%#,[line()]%R[ansi(hg,Your Friends)]%R%R[ansi(h,Name)][space(14)][ansi(h,Last Online)][space(12)][ansi(h,Location)]%R[line2()][iter(xget(%#,list_friends),[setq(0,pmatch(##))]%R[padstr(##,18)][padstr(switch(1,hasflag(%q0,dark),u(fun_format_last_time,xget(%q0,last)),hasflag(%q0,connected),ansi(h,<CONNECTED>),u(fun_format_last_time,xget(%q0,last))),23)][switch(1,hasflag(%q0,unfind),<UNFINDABLE>,[name(loc(%q0))])])]%R[line()])
&FUN_FORMAT_LAST_TIME Friends System=timefmt($x - $X,convtime(%0))
&FUN_NOTIFY_CONNECT Friends System=switch(1,t(match(xget(%0,list_friends),%1)),pemit(%0,[ansi(hb,<FRIENDS>)] %1 has connected.))
&FUN_NOTIFY_DISCONNECT Friends System=switch(1,t(match(xget(%0,list_friends),%1)),pemit(%0,[ansi(hb,<FRIENDS>)] %1 has disconnected.))
&FUN_FRIENDS Friends System=xget(%0,list_friends)

&HELP_+FRIENDS Friends System=The +friends command lets you keep track of your friends.  It will tell you when they log in and out, and you can see a list of their locations and last connection times.%R%R[ansi(h,+friend/add <player>)] - Add a friend.%R[ansi(h,+friend/remove <player>)] - Remove a friend.%R[ansi(h,+friends)] - Show a list of your friends.%R%RYou can also use the +mail system to send a mail to all your friends.  Just use [ansi(h,+mail friends=<subject>/<message>)].
@STARTUP Friends System=@set me=!no_command;@fun FRIENDS=me,FUN_FRIENDS,1,1,admin


@pemit/silent %#=[ansi(hg,Setting up database numbers...)]%R

@wait 2=+install/updategroup FRIENDS

@wait 5=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!)]

@wait 5=@set me=!quiet
