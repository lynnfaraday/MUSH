@@ Faraday's Friends System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hr,Beginning +friends Installation.  Please wait until the Installation Complete message appears...)]%R

@pemit/silent %#=[ansi(hr,Creating Objects...)]%R



@create Friends System
@set Friends System = WIZARD
@set Friends System = SAFE
@set Friends System = !NO_COMMAND
@ACONNECT Friends System=think switch(hasflag(%#,dark),0,iter(lwho(),u(fun_notify_connect,##,%N))
@ADISCONNECT Friends System=think switch(hasflag(%#,dark),0,iter(lwho(),u(fun_notify_disconnect,##,%N))
&CMD-+FRIEND-ADD Friends System=$+friend/add *:think [setq(0,pmatch(%0))]pemit(%#,switch(0,t(%q0),ansi(hr,That is not a valid player.),not(match(xget(%#,list_friends),%0)),ansi(hr,That person is already on your friends list.),not(match(%N,%0)),ansi(hr,You can't add yourself!),ansi(hg,You add %0 to your friends list.)[set(%#,list_friends:[xget(%#,list_friends)][name(%q0)]%B)]))
&CMD-+FRIEND-REMOVE Friends System=$+friend/remove *:think [setq(0,pmatch(%0))]pemit(%#,switch(0,t(%q0),ansi(hr,That is not a valid player.),match(xget(%#,list_friends),%0),ansi(hr,That person is not on your friends list.),ansi(hg,You remove %0 from your friends list.)[set(%#,list_friends:[remove(xget(%#,list_friends),name(%q0))])]))
&CMD-+FRIENDS Friends System=$+friends:think pemit(%#,[line()]%R[ansi(hg,Your Friends)]%R%R[ansi(h,Name)][space(14)][ansi(h,Last Online)][space(12)][ansi(h,Location)]%R[line2()][iter(xget(%#,list_friends),[setq(0,pmatch(##))]%R[padstr(##,18)][padstr(switch(1,hasflag(%q0,dark),u(fun_format_last_time,xget(%q0,last)),hasflag(%q0,connected),ansi(h,<CONNECTED>),u(fun_format_last_time,xget(%q0,last))),23)][switch(1,hasflag(%q0,unfind),<UNFINDABLE>,[name(loc(%q0))])])]%R[line()])
@DESCRIBE Friends System=Allows you to keep track of your friends.%R[u(credits)]
&FUN_FORMAT_LAST_TIME Friends System=timefmt($x - $X,convtime(%0))
&FUN_NOTIFY_CONNECT Friends System=switch(1,t(match(xget(%0,list_friends),%1)),pemit(%0,[ansi(hb,<FRIENDS>)] %1 has connected.))
&FUN_NOTIFY_DISCONNECT Friends System=switch(1,t(match(xget(%0,list_friends),%1)),pemit(%0,[ansi(hb,<FRIENDS>)] %1 has disconnected.))
&HELP_+FRIENDS Friends System=The +friends command lets you keep track of your friends.  It will tell you when they log in and out, and you can see a list of their locations and last connection times.%R%R[ansi(h,+friend/add <player>)] - Add a friend.%R[ansi(h,+friend/remove <player>)] - Remove a friend.%R[ansi(h,+friends)] - Show a list of your friends.%R%RYou can also use the +mail system to send a mail to all your friends.  Just use [ansi(h,+mail friends=<subject>/<message>)].
@STARTUP Friends System=@set me=!no_command
&CREDITS Friends System=Version [v(version)]%R%RCoded by Faraday ~ http://www.wordsmyth.org/faraday
&VERSION Friends System=3.0

@wait 3=@pemit/silent %#=[ansi(hr,Copying help files...)]%R
@wait 3=@fo %#=+copyhelp [num(Friends System)]

@wait 4=@pemit/silent %#=[ansi(hr,Moving objects to their appropriate places...)]%R
@wait 4=@tel Friends System=#2

@wait 5=@pemit/silent %#=%R%R[ansi(hr,Installation Complete!!)]

@wait 5=@set me=!quiet

