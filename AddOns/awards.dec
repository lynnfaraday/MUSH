@@ Faraday's Awards System
@@ Coded by Linda Naughton O'Meara 
@@ http://www.wordsmyth.org/faraday
@@
@@ ===================
@@ OVERVIEW
@@ ===================
@@ The MUSH awards system is used to reward outstanding achievement by players on the MUSH. There are different @@ types of awards for different things (roleplaying, staffing, etc.) and different levels of awards.
@@
@@ ===================
@@ FEATURES
@@ ===================
@@ * Votes are tallied automatically, but require a comment field explaining why you feel the person 
@@   deserves an award. This allows staff review of comments to prevent abuse of the system. The player 
@@   receiving the award gets the comments anonymously.
@@ * Automatically notifies staff when a player is eligible for an award, through the Staff Jobs System 
@@   (though the award still has to be given manually).
@@ * Automatically @mails a person's vote comments to them when they get an award, and then clears them. 
@@   Any "extra" votes (i.e. if they only needed 10 votes for the next award and had 12) are kept, not erased.
@@ * When an award is given, posts a message to an awards bulletin board, if one exists. This allows lasting 
@@   recognition.
@@ * Stores votes and awards on the player rather than on a central object. Nuked players' votes disappear 
@@   along with the players automatically, and FC votes can be reset with a simple +vote/reset command.
@@ * If the comments get too long for the attribute, they are sent to the player so none are lost.
@@ * Configuration options allow you to set up the types and levels of awards, enable/disable the ability 
@@   to see who's voted for you, and choose how multiple votes from the same person will be counted.
@@ * You can also configure special actions to be taken upon an award being given (like handing out XP). 
@@
@@ ===================
@@ REQUIREMENTS
@@ ===================
@@
@@ * PennMUSH
@@ * Faraday's Global Utility Functions
@@
@@ * Faraday's Staff Jobs system (to notify you when someone is eligible for awards)
@@     Alternative:  Replace job stuff with @mail
@@ * Myrddin's Bulletin Board (for posting awards to the BBS)
@@     Alternative:  Just don't have an awards BBS.  Nothing will be posted.
@@
@@ ===================
@@ INSTALLATION
@@ ===================
@@ 
@@ Simply /quote this script to your MUSH via a MUSH client.  You can also cut and paste, but beware
@@ of linebreaks.  There is no need to create any objects or do any manual editing of this script unless
@@ your master room is something other than #2.
@@
@@ ===================
@@ CREDITS
@@ ===================
@@ This awards system was originally coded for B5MUSH, and was designed with input from numerous players 
@@ and staff members from that game.
@@
@@ ===================
@@ SETUP
@@ ===================
@@
@@ Once the system is installed, you will need to configure some options and set up a few other things:
@@   +vote/confighelp
@@
@@ New players will need to have the FUN_SETUP_ATTRS on the function object called when they are created.
@@
@@ ===================
@@ USAGE
@@ ===================
@@ 
@@ See +vote/help for usage instructions.
@@
@@ ===================
@@ UPDATES
@@ ===================
@@
@@ Join the mailing list (http://groups.yahoo.com/group/faradaycode) to keep up with the latest news.
@@
@@ * Version 2.0 - 6/12/04
@@    Modified the system to require the global utility functions. 


@set me=quiet

@pemit/silent %#=[ansi(hr,Beginning Awards System Installation.  Please wait until you see the 'Installation Complete' message...)]%R

@pemit/silent %#=[ansi(hr,Creating Objects...)]%R


@create Awards System
@set Awards System = WIZARD
@set Awards System = SAFE
@set Awards System = !NO_COMMAND
&CMD-+VOTE-AWARD Awards System=$+vote/award *=*:@switch/first [setq(0,pmatch(%0))][setq(1,u(fun_next_award_level,%q0,trim(ucstr(%1))))]0=isstaff(%#),think pemit(%#,ansi(hr,This command is restricted to staff members only!)),t(%q0),think pemit(%#,ansi(hr,That is not a valid player.)),match(v(dat_award_types),%1),think pemit(%#,ansi(hr,That is not a valid award type.)),u(fun_has_enough_votes,%q0,%1),think pemit(%#,ansi(hr,%0 does not have enough votes yet for a %q1 award!)),{think pemit(%#,ansi(hg,You award %0 a %q1 award!))[set(%q0,award_notified_[trim(%1)]:0)][u(fun_add_award,%q0,%q1)];think [create_job(AWARD,Award Given,[name(%q0)] has been awarded a %q1 award.,%#)];@mail [name(%q0)]=Award!/Congratulations [name(%q0)]!  You have just received a %q1 award!  Below are your vote comments:[u(fun_get_vote_comments,%q0,trim(ucstr(%1)))];@trigger me/TRG_GIVE_AWARD=%0,%q1;+bbpost awards/%q1 Award!/Congratulations to [name(%q0)] for [poss(%q0)] recent %q1 award!;think [set(%q0,votedetails-[trim(%1)]:[iter(lnum(sub(u(fun_num_votes,%q0,trim(%1)),u(fun_votes_for_level,sub(u(fun_next_award_num,%q0,trim(%1)),1)))),OldVote## - [time()] - Carried over|)])][set(%q0,votes-[trim(%1)]:[iter(lnum(sub(u(fun_num_votes,%q0,trim(%1)),u(fun_votes_for_level,sub(u(fun_next_award_num,%q0,trim(%1)),1)))),OldVote##%b)])]}
&CMD-+VOTE-CONFIG-COUNT Awards System=$+vote/config count=*:think switch(0,isstaff(%#),pemit(%#,ansi(hr,Sorry - this command is restricted to staff.)),hasattr(me,fun_count_votes_[trim(%0)]),pemit(%#,ansi(hr,Sorry that is not a valid vote-counting method.  Choose one of these: [iter(lattr(me/fun_count_votes*),last(##,_))])),pemit(%#,ansi(hg,You set the vote counting method to "%0".))[set(me,dat_count_setting:[trim(%0)])])
&CMD-+VOTE-CONFIGHELP Awards System=$+vote/confighelp:think pemit(%#,[line_with_text(Awards System)]%RThere are several options that may be configured on the awards system via the +vote/config command.  This configuration may only be performed by staff.  The syntax for the award config command is:%R[center(+vote/config <option>=<setting>,78)]%RThe options are described here. %R%R[ansi(h,+vote/me)]%RThe first option is [ansi(hr,me)].  This controls whether players are allowed to use the +vote/me command.  Valid settings are "1" (to allow the command) and "0" (to disable the command).%R%RContinued in +vote/confighelp2%R[line_with_text(+vote/help)]
&CMD-+VOTE-CONFIGHELP2 Awards System=$+vote/confighelp2:think pemit(%#,[line_with_text(Awards System)]%R[ansi(h,Vote Counting)]%RAnother config option is [ansi(hr,count)] which determines how multiple votes from the same person are tallied.  The various options are described below:%R%R- ALL - Every vote is counted, even multiple votes from the same person.%R- MAX1 - Only one vote is counted per person.%R- MAX3 - Each vote is counted, up to a maximum of 3 from a single person.%R- MAX5 - Each vote is counted, up to a maximum of 5 from a single person.%R- 1FOR2 - The first vote is always counted normally.  After that, only half the votes from the same person count.%R- 1FOR4 - The first vote is always counted normally.  After that, only a fourth of the votes from the same person count.%R%RContinued in +vote/confighelp3%R[line_with_text(+vote/help)]
&CMD-+VOTE-CONFIGHELP3 Awards System=$+vote/confighelp3:think pemit(%#,[line_with_text(Awards System)]%RIn addition to these fixed options, there are a couple other things you may wish to edit by changing the attributes on the object itself.%R%R[ansi(h,DAT_AWARD_LEVELS)] - Edit this to change the names of the awards, or to add a new award level.  This must be kept in the same order as DAT_AWARD_COSTS.  To check your order and levels, use [ansi(h,+vote/awardinfo)].%R%R[ansi(h,DAT_AWARD_TYPES)] - Edit this to add a new type of award.  If you do this, you [ansi(hr,must re-run +vote/setup)] to properly initialize all the attributes.To check your order and levels, use [ansi(h,+vote/awardinfo)].%R%R[ansi(h,DAT_AWARD_COSTS)] - Edit this to change the number of votes required for each award level, or to add a new level.  This must be kept in the same order as DAT_AWARD_LEVELS. To check your order and levels, use [ansi(h,+vote/awardinfo)].%R%R[ansi(h,TRG_GIVE_AWARD)] - If you want to do something extra when an award is given (like giving out XP) you can put the actions into this attribute.  It will be called via an @trigger, with the following parameters: \%0 = Player DB#, \%1 = Award level and type (i.e. BRONZE-RPER)%R[line_with_text(+vote/help)]
&CMD-+VOTE-CONFIG-ME Awards System=$+vote/config me=*:think switch(0,isstaff(%#),pemit(%#,ansi(hr,Sorry - this command is restricted to staff.)),match(1 0,%0),pemit(%#,ansi(hr,Please use 1 or 0 for the option.  1 enables the command, 0 disables it.)),pemit(%#,ansi(hg,You [switch(%0,1,enable,0,disable)] the +vote/me command.))[set(me,dat_allow_me:%0)])
&CMD-+VOTE-SETUP Awards System=$+vote/setup:@dolist lsearch(all,type,player)=think [u(fun_setup_attrs,##)];@pemit %#=Award setup complete.
&CMD-+VOTE Awards System=$+vote *=*/*:think [setq(0,pmatch(%0))]switch(1,match(%#,%q0),pemit(%#,ansi(hr,You may not vote for yourself  silly.)),not(%q0),pemit(%#,ansi(hr,That is not a player.)),match(%2,*|*),pemit(%#,ansi(hr,Please do not use the | character in your vote comments.)),and(match(lcstr(%1),staff),not(orflags(%q0,WrJ))),pemit(%#,ansi(hr,[capstr(%0)] cannot receive a staff award because [subj(%q0)] is not on the staff.)),not(match(v(dat_award_types),ucstr(%1))),pemit(%#,ansi(hr,That is not a valid award type.  Use one of these: [v(dat_award_types)].)),pemit(%#,ansi(hc,You vote for %0 for a [u(fun_next_award_level,%q0,trim(ucstr(%1)))] award.))[u(fun_add_vote,%q0,trim(ucstr(%1)),%#,%2)]);@tr me/trg_check_award=%q0,trim(ucstr(%1))
&CMD-+VOTE-AWARDINFO Awards System=$+vote/awardinfo:think pemit(%#,[line_with_text(Awards System)]%R[ansi(hr,Award Information)]%R%R[ansi(r,Award Types)][iter(v(dat_award_types),%R##)]%R%R[ansi(r,Award Levels and Votes Required)][iter(v(dat_award_levels),%R##%T[extract(v(dat_award_costs),#@,1)])]%R[line_with_text(+vote/help)])
&CMD-+VOTE-HELP Awards System=$+vote/help:think pemit(%#,[line_with_text(Awards System)]%R[u(help_+vote)]%R[switch(isstaff(%#),1,%RUse [ansi(h,+vote/staffhelp)] to see staff-only commands.%R)]%R[line_with_text(+vote/help)])
&CMD-+VOTE-LIST Awards System=$+vote/list *:think [setq(0,pmatch(%0))]switch(0,t(%q0),pemit(%#,ansi(hr,There is no such player.)),pemit(%#,[line_with_text(Awards System)]%R[ansi(hr,Your vote comments for [name(%q0)]:)][iter(lattr(%q0/votedetails-*),%R%R[ansi(r,after(##,-))][u(fun_get_list_comments,%N,%q0,##)])]%R[line_with_text(+vote/help)]))
&CMD-+VOTE-LIST-ALL Awards System=$+vote/list:think pemit(%#,[line_with_text(Awards System)]%R[ansi(hr,Your have voted for the following people:)]%R[iter(lsearch(all,type,player),[switch(words(grepi(##,votes-*,%N)),>0,%R[name(##)])],,)]%R%RUse [ansi(h,+vote/list <player>)] to see your comments.%R[line_with_text(+vote/help)]))
&CMD-+VOTE-ME Awards System=$+vote/me:think switch(0,v(dat_allow_me),pemit(%#,ansi(hr,Sorry but the staff has disabled this command.)),pemit(%#,[line_with_text(Awards System)]%R[ansi(hm,Vote Comments You Have Received:)][iter(lattr(%#/votedetails-*),%R%R[ansi(r,after(##,-))][u(fun_get_vote_comments,%#,after(##,-))])]%R[line_with_text(+vote/help)]))
&CMD-+VOTE-RESET Awards System=$+vote/reset *:think [setq(0,pmatch(%0))]switch(0,isstaff(%#),pemit(%#,ansi(hr,This command is restricted to staff only.)),t(%q0),pemit(%#,ansi(hr,That is not a valid player.)),pemit(%#,ansi(hg,You reset %0's votes.))[u(fun_setup_attrs,%q0)])
&CMD-+VOTE-SHOW Awards System=$+vote/show *=*:think [setq(0,pmatch(%0))]switch(0,isstaff(%#),pemit(%#,ansi(hr,This command is restricted to staff only.)),t(%q0),pemit(%#,ansi(hr,That is not a valid player.)),match(v(dat_award_types),%1),pemit(%#,ansi(hr,That is not a valid award type.)),pemit(%#,[line_with_text(Awards System)]%R[ansi(hg,Votes for [name(%q0)] for [u(fun_next_award_level,%q0,trim(ucstr(%1)))]:)][iter(rest(revwords(xget(%q0,votedetails-[trim(ucstr(%1))]),|),|),%r-+- ##,|)]%R%R[ansi(h,[u(fun_num_votes,%q0,trim(%1))] votes counted -- [u(fun_votes_for_level,u(fun_next_award_num,%q0,trim(%1)))] needed for next level)]%R[line_with_text(+vote/help)]))
&cmd-+vote-credits Awards System=$+vote/credits:think pemit(%#,[line()]%RAwards System Version [v(version)]%R%RCoded by Faraday ~ http://www.wordsmyth.org/faraday%R[line()])
&CMD-+VOTE-STAFFHELP Awards System=$+vote/staffhelp:think pemit(%#,[line_with_text(Awards System)]%R[u(help_awards_staff)]%R[line_with_text(+vote/help)])
&DAT_ALLOW_ME Awards System=1
&DAT_AWARD_COSTS Awards System=5 10 15 20 25 30 35 40 45 50 55 60
&DAT_AWARD_LEVELS Awards System=BRONZE SILVER GOLD PLATINUM QUARTZ SAPPHIRE EMERALD TOPAZ RUBY OBSIDIAN COBALT DIAMOND
&DAT_AWARD_TYPES Awards System=RPER STORYTELLER STAFF
&DAT_AWARD_XP Awards System=1
&DAT_COUNT_SETTING Awards System=all
@DESCRIBE Awards System=See +vote/help and +vote/confighelp
@set Awards System/DESCRIBE=no_command visual
&FUN_ADD_AWARD Awards System=[set(%0,awards:[switch(match(xget(%0,awards),*-[after(%1,-)]),0,[xget(%0,awards)] %1,replace(xget(%0,awards),match(xget(%0,awards),*-[after(%1,-)]),%1))])][set(%q0/awards,wiz)]
&FUN_ADD_VOTE Awards System=[set(%0,votes-%1:[xget(%0,votes-%1)][name(%2)]%b)][set(%0,votedetails-%1:[xget(%0,votedetails-%1)][name(%2)] - [time()] - %3|)]
&FUN_CONVERT_NUM_TO_NAME Awards System=[iter(%0,switch(isplayer(##),1,name(##),NonPlayerOldVote#@))]%B
&FUN_COUNT_VOTES_1FOR3 Awards System=[trim([setq(9,0)][iter(sort(setunion(%0,%b)),[setq(9,add(%q9,add(1,div(words(graball(%0,##)),3))))])]%q9)]
&FUN_COUNT_VOTES_1FOR5 Awards System=[trim([setq(9,0)][iter(sort(setunion(%0,%b)),[setq(9,add(%q9,add(1,div(words(graball(%0,##)),5))))])]%q9)]
&FUN_COUNT_VOTES_ALL Awards System=words(%0)
&FUN_COUNT_VOTES_MAX1 Awards System=[trim([setq(9,0)][iter(sort(setunion(%0,%b)),[setq(9,add(%q9,switch(words(graball(%0,##)),>1,1,words(graball(%0,##)))))])]%q9)]
&FUN_COUNT_VOTES_MAX3 Awards System=[trim([setq(9,0)][iter(sort(setunion(%0,%b)),[setq(9,add(%q9,switch(words(graball(%0,##)),>3,3,words(graball(%0,##)))))])]%q9)]
&FUN_COUNT_VOTES_MAX5 Awards System=[trim([setq(9,0)][iter(sort(setunion(%0,%b)),[setq(9,add(%q9,switch(words(graball(%0,##)),>5,5,words(graball(%0,##)))))])]%q9)]
&FUN_GET_LIST_COMMENTS Awards System=iter(edit(xget(%1,%2),\(,\\\(),switch(match(trim(before({##},-)]),%0*),0,,%r-+- [after({##},-)]),|)
&FUN_GET_VOTE_COMMENTS Awards System=iter(rest(revwords([edit(xget(%0,votedetails-%1),\(,\\\()],|)),%R%R-+- [after([after({##},-)],-)],|)
&FUN_HAS_ENOUGH_VOTES Awards System=gte(u(fun_num_votes,%0,%1),u(fun_votes_for_level,u(fun_next_award_num,%0,%1)))
&FUN_NEXT_AWARD_LEVEL Awards System=[extract(v(dat_award_levels),u(fun_next_award_num,%0,%1),1)]-%1
&FUN_NEXT_AWARD_NUM Awards System=switch(0,match(xget(%0,awards),*-%1),1,[add(1,match(v(dat_award_levels),before(extract(xget(%0,awards),match(xget(%0,awards),*-%1),1),-)))])
&FUN_NUM_VOTES Awards System=u(FUN_COUNT_VOTES_[v(dat_count_setting)],xget(%0,votes-%1))
&FUN_SETUP_ATTRS Awards System=[iter(v(dat_award_types),[switch(hasattr(%0,votes-##),0,[set(%0,award_notified_##:0)][set(%0,votes-##:)][set(%0,votedetails-##:)])][set(%0/award_notified_##,mortal_dark)][set(%0/votes-##,mortal_dark)][set(%0/votedetails-##,mortal_dark)][atrlock(%0/award_notified_##,on)][atrlock(%0/votes-##,on)][atrlock(%0/votedetails-##,on)])][switch(hasattr(%0,awards),0,[set(%0,awards:)])][set(%0/awards,mortal_dark)][atrlock(%0/awards,on)]
&FUN_VOTES_FOR_LEVEL Awards System=extract(v(dat_award_costs),%0,1)
&HELP_AWARDS_STAFF Awards System=The Awards system will automatically notify you when a player is eligible for an award.  Someone must then review the votes that player has received (to make sure everything is in order) and then manually give the award.  Staff award commands include:%R%R[ansi(h,+vote/show <player>=<award type>)] - Reviews a player's votes.%R%R[ansi(h,+vote/award <player>=<award type>)] - Awards the player the next level in a given award type.  Any extra votes will be carried over, not lost.  The player will receive an @mail notifying them of the award.  If an awards bulletin board exists, a message will be posted to it.%R%R[ansi(h,+vote/reset <player>)] - Resets a player's votes, for whatever reason.%R%R[ansi(h,+vote/confighelp)] - Shows configuration help info%R[ansi(h,+vote/config <config option>=<setting>)] - Configures an option on the system%R[ansi(h,+vote/setup)] - Sets up the necessary attributes.  This is run automatically at install time, and must be run manually any time you add a new award type.
&HELP_+VOTE Awards System=The MUSH awards system is used to reward outstanding achievement by players on the MUSH.  There are different types of awards for different things (roleplaying, staffing, etc.) and different levels of awards.%R%RVotes will be tallied automatically, and the staff notified when a person has enough votes to receive an award.  The staff must then approve the award.  The staff can see who cast each vote, but the player receiving the award sees the vote comments with the names removed.  NOTE: Multiple votes from the same person may or may not count, depending on how the staff has configured the system.%R%R[ansi(h,Usage:)]%R%R[ansi(h,+vote <player>=<award type>/<reason you're voting>)] - Cast a vote for a player.%R%R[ansi(h,+vote/list)] - Shows who all you've voted for.%R[ansi(h,+vote/list <player>)] - Show what votes and comments you've given for a player.%R%R[ansi(h,+vote/me)] - Show the comments that others have made when voting for you, minus their names.  Note that this is not always an accurate gauge of how close you are to an award since some votes may be from the same person and may not all count.%R%R[ansi(h,+vote/awardinfo)] - Shows the valid types of awards, the levels, and how many votes are required for each level.%R%R[ansi(h,+vote/credits)] - Shows version and credit info.
@QUEUE Awards System=
@set Awards System/QUEUE=no_inherit no_clone wizard mortal_dark
@STARTUP Awards System=@set me=!no_command
@set Awards System/STARTUP=no_command
&TRG_CHECK_AWARD Awards System=@switch t(%0)=0,,{@switch gt(strlen(xget(%0,votedetails-%1)),5000)=1,{@mail [name(%0)]=Vote Comments Overflow/Your +vote comments for the [u(fun_next_award_level,%0,%1)] award have reached the limit.  We're sending them to you now to make room for more!  Note that all the old votes will still count towards your next award - only the comments are being reset.%R%R[u(fun_get_vote_comments,%0,%1)];think set(%0,votedetails-%1:[time()] - Auto - Comments cleared due to overflow.|)};@switch [and(u(fun_has_enough_votes,%0,%1),not(xget(%0,award_notified_[trim(%1)])))]=1,{[create_job(AWARD,Award Eligible,[name(%0)] now has enough votes for a [u(fun_next_award_level,%0,%1)] award!,%#)];think set(%0,award_notified_[trim(%1)]:1)}}
&TRG_GIVE_AWARD Awards System=
&VERSION Awards System=2.0

@pemit/silent %#=[ansi(hr,Setting up attributes...)]%R

@wait 2=@fo me=+jobs/newcat AWARD
@wait 2=@fo me=+vote/setup

@wait 5=@pemit/silent %#=[ansi(hr,Moving objects to their appropriate places...)]%R

@wait 6=@tel Awards System=#2

@wait 9=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!  Please read the important setup information on the web page http://www.wordsmyth.org/faraday/awards.shtml)]%R

@wait 10=@set me=!quiet


