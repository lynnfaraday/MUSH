@@ Faraday's Alts System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hr,Beginning Alts System Installation.  Please wait until you see the 'Installation Complete' message...)]%R

@pemit/silent %#=[ansi(hr,Creating Objects...)]%R


@create Alts Commands
@set Alts Commands = WIZARD
@set Alts Commands = !NO_COMMAND

@DESCRIBE Alts Commands=Commands for registering and unregistering alternate characters.%R%R[u(credits)]
&HELP_+ALTS Alts Commands=The +alts command lets you record who your alternate characters are.  Alts must be registered so that staff knows who's playing who.  Alt information may only be seen by staff.  If you want people to know your alts, put it in +finger in the PUBLICALTS attribute.%R%R[ansi(h,+alts/register <character>)] - Registers a character as your alt.%R[ansi(h,+alts/unregister <character>)] - Use if a character is no longer your alt.%R%TUsually you'd only use this if your char was @nuked or something.%R%R[ansi(h,+alts/mail)] - Shows if your alts have unread messages.
&SHELP_+ALTS Alts Commands=Use the +alts command to view a character's alts.%R%R[ansi(h,+alts <character>)] - Lists alts.%r%rSet the NOTIFY_STAFF attribute to 1 to submit a job every time someone registers or unregisters an alt. 

&CMD-+ALTS Alts Commands=$+alts *:think switch(0,isstaff(%#),pemit(%#,ansi(hr,This command is for staff only.)),t(pmatch(%0)),pemit(%#,ansi(hr,That is not a valid player!)),hasattr(pmatch(%0),alts),pemit(%#,ansi(hy,[capstr(%0)] has no alternate characters.)),pemit(%#,ansi(hc,Alts for [capstr(%0)]: [iter(xget(pmatch(%0),alts),name(##))])))
&CMD-+ALTS-MAIL Alts Commands=$+alts/mail:think pemit(%#,[line()]%R[ansi(h,Alt Mail Report)][iter(%# [xget(%#,alts)],%R[ljust(name(##),15)][extract(mail(pmatch(##)),2,1)] unread messages)]%R[line()])
&CMD-+ALTS-REGISTER Alts Commands=$+alts/register *:think [setq(0,pmatch(%0))][switch(0,t(%q0),pemit(%#,ansi(hr,[name(%q0)] is not a valid character.)),not(match(xget(%#,alts),%q0)),pemit(%#,ansi(hr,That person is already your alt.)),[setq(1,xget(%#,alts))][setq(2,xget(%q0,alts))][iter(setdiff(%q2,%q1), set(##,alts:[trim([xget(##,alts)] %#)]))][iter(setdiff(%q1,%q2), set(##,alts:[trim([xget(##,alts)] %q0)]))][set(%q0,alts:[trim([xget(%q0,alts)] [trim(%q1)] %#)])][set(%#,alts:[trim([xget(%#,alts)] [trim(%q2)] %q0)])][pemit(%#,You register [name(%q0)] as your alt.)][switch(v(notify_staff),1,create_job(REQ,Alts - %N,%N has registered [name(%q0)] as %p alt.%R%ROther alts:%R%N: [xget(%#,position)][iter(xget(%#,alts),%R[name(##)]: [xget(##,position)])],%#))])]
&CMD-+ALTS-UNREGISTER Alts Commands=$+alts/unregister *:think [setq(0,#-1)]switch(0,t(pmatch(%0)),pemit(%#,ansi(hr,%0 is not a valid character.)),t(match(xget(%#,alts),pmatch(%0))),pemit(%#,ansi(hr,[name(pmatch(%0))] has already been unregistered as an alt of yours.)),[setq(0,setunion([xget(%#,alts)] %#,))][iter(%q0,set(##,alts:[remove(xget(##,alts),pmatch(%0))]))][set(pmatch(%0),alts:)][pemit(%#,You un-register [name(pmatch(%0))] as your alt.)][switch(v(notify_staff),1,create_job(REQ,Alts - %N,%N has UN-registered [name(pmatch(%0))] as %p alt.,%#))])
&NOTIFY_STAFF Alts Commands=0

@STARTUP Alts Commands=@set me=!no_command
&CREDITS Alts Commands=Version [v(version)]%R%RCoded by Faraday ~ http://www.wordsmyth.org/faraday
&VERSION Alts Commands=4.xx

@wait 4=@pemit/silent %#=[ansi(hr,Copying help files...)]%R
@wait 4=@fo %#=+copyhelp [num(Alts Commands)]

@wait 4=think setup_attr(h,alts,)

@wait 5=@pemit/silent %#=[ansi(hr,Moving objects to their appropriate places...)]%R

@wait 6=@tel Alts Commands=#2

@wait 9=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!)]

@wait 10=@set me=!quiet

