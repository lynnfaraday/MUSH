@@ Faraday's Weather System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hg,Beginning Weather System Installation.  Please wait until you see the 'Installation Complete' message...)]%R

@pemit/silent %#=[ansi(hg,Creating Objects...)]%R


think install_create(WEATHER_CMD,Weather Generator,1,0,5.0,Weather Generator.)
think install_create(WEATHER_HOLDER,Weather Seasons,0,1,5.0,Weather descs.)
think install_create(WEATHER_WINTER,Winter,0,1,5.0,Weather descs.,WEATHER_HOLDER)
think install_create(WEATHER_SPRING,Spring,0,1,5.0,Weather descs.,WEATHER_HOLDER)
think install_create(WEATHER_SUMMER,Summer,0,1,5.0,Weather descs.,WEATHER_HOLDER)
think install_create(WEATHER_FALL,Fall,0,1,5.0,Weather descs.,WEATHER_HOLDER)

@set Weather Generator = VISUAL
&CMD-+WEATHER Weather Generator=$+weather:think pemit(%#,u(fun_weather))
&CMD-+WEATHER-CLEAR Weather Generator=$+weather/clear:think [pemit(%#,switch(0,isstaff(%#),ansi(hr,This command is for staff only.),ansi(hg,You clear the weather override.)[set(me,override:0)][set(me,type:[u(fun_random_type)])][set(me,temp:[u(fun_random_temp)])]))]
&CMD-+WEATHER-OVERRIDE Weather Generator=$+weather/override *=*:think
&CMD-+WEATHER-SET Weather Generator=$+weather/set * and *:think [pemit(%#,switch(0,isstaff(%#),ansi(hr,This command is for staff only.),ansi(hg,You set the weather to %0 and %1.)[set(me,override:1)][set(me,type:[lcstr(trim(%1))])][set(me,temp:[lcstr(trim(%0))])]))]
&FUN_COMPUTE_TIME Weather Generator=switch(first(extract(ictime(),4,1),:),<5,night,<11,morning,<18,day,<21,evening,night)
&FUN_DETERMINE_SEASON Weather Generator=switch(extract(time(),2,1),Jan,winter,Feb,winter,Mar,switch(extract(time(),3,1),>20,spring,winter),Apr,spring,May,spring,Jun,switch(extract(time(),3,1),>20,summer,spring),Jul,summer,Aug,summer,Sep,switch(extract(time(),3,1),>20,fall,summer),Oct,fall,Nov,fall,Dec,switch(extract(time(),3,1),>20,winter,fall))

&FUN_FIND_SEASON Weather Generator=iter(lcon(#WEATHER_HOLDER),switch(name(##),%0,##),,)

&FUN_RANDOM_TEMP Weather Generator=[lcstr(first(shuffle(xget([u(fun_find_season,v(season))],temp))))]
&FUN_RANDOM_TYPE Weather Generator=[lcstr(after(first(shuffle(lattr([u(fun_find_season,v(season))]/type*))),-))]
&FUN_WEATHER Weather Generator=It is a [v(season)] [v(time)].  The weather is [v(temp)] and [v(type)].
&OVERRIDE Weather Generator=0
&SHELP_WEATHER Weather Generator=Weather is updated every half hour.  If you want it to show up in the room descriptions, add a call to weather() in your room parent to get the current weather.  This assumes the entire game has the same weather.  Multiple weather zones are not permitted in this version of the system.%R%RIf your months/seasons/times are the same as RL (Northern Hemisphere) then the system requires no special setup. If you're using a custom IC time system, you'll need to tweak some of the settings as described in the support article on the wiki (http://code.google.com/p/faramushcode/wiki/WeatherSystem)%R%RYou can temporarily override the weather if you need to for a special plot or something.  The weather will stay at what you set until you clear the override.%R%R[ansi(h,+weather/set <temperature> and <weather>)] - Overrides the weather.%R%TExample: +weather/set cold and raining%R%R[ansi(h,+weather/clear)] - Clears the weather override.
@STARTUP Weather Generator=@set me=!no_command;@tr me/tr-changeweather;@fun weather=me/fun_weather

&TR-CHANGEWEATHER Weather Generator=think [u(fun_change_weather)];@wait v(UPDATE_WEATHER_SECS)=@tr me/tr-changeweather

&FUN_CHANGE_WEATHER Weather Generator=[set(me,season:[u(fun_determine_season)])][set(me,time:[u(fun_compute_time)])][switch(v(override),0,[set(me,type:[u(fun_random_type)])][set(me,temp:[u(fun_random_temp)])])][iter(lwho(),switch(xget(%l,show_weather),1,pemit(##,[ansi(hb,<WEATHER>)] [u(FUN_WEATHER)])))]

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       DATA SECTION           *****
@@    *****   CHECK BEFORE UPGRADING     *****
@@    *****   REMOVE OF MODIFY AS NEEDED *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


&SEASON Weather Generator=winter
&TEMP Weather Generator=cold
&TIME Weather Generator=evening
&TYPE Weather Generator=flurrying
&UPDATE_WEATHER_SECS Weather Generator=1800



&TEMP Winter=cold cold cold cold cold cold cold cool cool freezing freezing
&TYPE-CLEAR Winter=clear clear clear fair fair fair flurrying flurrying overcast snowing
&TYPE-FAIR Winter=fair fair fair flurrying flurrying flurrying clear clear overcast overcast snowing
&TYPE-FLURRYING Winter=flurrying flurrying flurrying fair fair fair overcast clear snowing snowing
&TYPE-OVERCAST Winter=overcast overcast overcast flurrying flurrying flurrying fair fair clear clear snowing
&TYPE-SNOWING Winter=snowing snowing snowing overcast overcast fair fair flurrying flurrying flurrying

&TEMP Fall=cool cool cool cool cool cool cool freezing warm warm
&TYPE-CLEAR Fall=clear clear clear fair fair fair drizzling drizzling overcast raining
&TYPE-DRIZZLING Fall=drizzling drizzling drizzling fair fair fair overcast overcast clear stormy
&TYPE-FAIR Fall=fair fair fair drizzling drizzling drizzling clear clear overcast overcast raining
&TYPE-OVERCAST Fall=overcast overcast overcast drizzling drizzling drizzling fair fair clear clear raining
&TYPE-RAINING Fall=raining raining raining overcast overcast overcast stormy stormy fair fair
&TYPE-STORMY Fall=stormy stormy stormy raining raining raining clear overcast drizzling fair

&TEMP Summer=hot hot hot hot hot warm warm warm warm cool cool
&TYPE-CLEAR Summer=clear clear clear fair fair fair drizzling drizzling overcast raining
&TYPE-DRIZZLING Summer=drizzling drizzling drizzling fair fair fair overcast overcast clear stormy
&TYPE-FAIR Summer=fair fair fair drizzling drizzling drizzling clear clear overcast overcast raining
&TYPE-OVERCAST Summer=overcast overcast overcast drizzling drizzling drizzling fair fair clear clear raining
&TYPE-RAINING Summer=raining raining raining overcast overcast overcast stormy stormy fair fair
&TYPE-STORMY Summer=stormy stormy stormy raining raining raining clear overcast drizzling fair

&TEMP Spring=cool cool cool cool cool warm warm warm warm warm
&TYPE-CLEAR Spring=clear clear clear fair fair fair drizzling drizzling overcast raining
&TYPE-DRIZZLING Spring=drizzling drizzling drizzling fair fair fair overcast overcast clear stormy
&TYPE-FAIR Spring=fair fair fair drizzling drizzling drizzling clear clear overcast overcast raining
&TYPE-OVERCAST Spring=overcast overcast overcast drizzling drizzling drizzling fair fair clear clear raining
&TYPE-RAINING Spring=raining raining raining overcast overcast overcast stormy stormy fair fair
&TYPE-STORMY Spring=stormy stormy stormy raining raining raining clear overcast drizzling fair

@wait 5=+jobs/create CODE=Weather/Customize weather system.  See +shelp weather.


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@    *****       END DATA SECTION      *****
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

@pemit/silent %#=[ansi(hg,Setting up database numbers...)]%R

@wait 2=+install/updategroup WEATHER

@wait 5=@set me=!quiet


@wait 6=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!)]

@wait 8=@set me=!quiet

