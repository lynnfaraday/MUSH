@create FS3Combat NPC DB
@set FS3Combat NPC DB = SAFE


@create FS3Combat NPC Commands
@set FS3Combat NPC Commands = WIZARD
@set FS3Combat NPC Commands = SAFE
@set FS3Combat NPC Commands = !NO_COMMAND


&NPCSTATS FS3Combat NPC Commands=skill owner notes

&CMD-+NPCS FS3Combat NPC Commands=$+npcs:@pemit %#=[line()]%R[ansi(h,Name)][space(16)][ansi(h,Owner)][space(15)]Injured?[iter(sort(lattr(#816/npc_*)),[setq(0,after(##,_))]%R[padstr(prettify(lcstr(%q0)),20)][padstr(npcstat(%q0,owner),20)][switch(damagemod(%q0),0,--,[ansi(hr,YES)])])]%R%RUse [ansi(h,+npc <name>)] to view details.%R[line()]

&CMD-+NPC FS3Combat NPC Commands=$+npc *:@pemit %#=switch(0,isnpc(%0),ansi(hr,There is no NPC by that name.),[setq(0,unprettify(%0))][setq(1,damagemod(%0))][line()]%R[ansi(h,Name:)] [prettify(%0)]%R[ansi(h,Owner:)] [npcstat(%q0,owner)][switch(u(fun_controls_npc,%0,%#),1,%R[ansi(h,Skill:)] [npcstat(%q0,skill)])]%R[ansi(h,Damage Modifier:)] %q1 [switch(%q1,>0,ansi(hr,<INJURED>))]%R[ansi(h,Notes:)]%R[replace_cr(npcstat(%q0,notes))]%R[line()]

&CMD-+NPC-CREATE FS3Combat NPC Commands=$+npc/create *=*:@pemit %#=[setq(0,u(fun_create_npc,%0,%1))]switch(%q0,1,ansi(hg,You create NPC %0.),ansi(hr,Error creating NPC: [after(%q0,|)]))

&CMD-+NPC-DELETE FS3Combat NPC Commands=$+npc/delete *:@pemit %#=switch(0,isnpc(%0),ansi(hr,There is no NPC by that name.),u(fun_controls_npc,%0,%#),ansi(hr,Only staff or the owner can delete an NPC.),ansi(hg,You delete NPC %0)[wipe(#816/npc_[unprettify(%0)])])

&CMD-+NPC-SKILL FS3Combat NPC Commands=$+npc/skill *=*:@pemit %#=switch(0,isnpc(%0),ansi(hr,There is no NPC by that name.),u(fun_controls_npc,%0,%#),ansi(hr,Only staff or the owner can change a NPC's skill.),u(fun_is_valid_skill,%1),ansi(hr,%1 is not a valid skill.),ansi(hg,You change NPC %0's skill to %1.)[u(fun_mod_npcstat,%0,skill,trim(%1))])

&CMD-+NPC-NOTES FS3Combat NPC Commands=$+npc/notes *=*:@pemit %#=switch(0,isnpc(%0),ansi(hr,There is no NPC by that name.),u(fun_controls_npc,%0,%#),ansi(hr,Only staff or the owner can change a NPC's notes.),not(match(%1,*|*)),ansi(hr,Notes may not contain the '|' character.),ansi(hg,You change NPC %0's notes to %1.)[u(fun_mod_npcstat,%0,notes,escape_cr(%1))])

&CMD-+NPC-ADDNOTE FS3Combat NPC Commands=$+npc/addnote *=*:@pemit %#=switch(0,isnpc(%0),ansi(hr,There is no NPC by that name.),u(fun_controls_npc,%0,%#),ansi(hr,Only staff or the owner can change a NPC's notes.),not(match(%1,*|*)),ansi(hr,Notes may not contain the '|' character.),ansi(hg,You add a note to NPC %0.)[u(fun_mod_npcstat,%0,notes,[npcstat(%0,notes)]%R[escape_cr(%1)])])


@@ @@@@@@@@@@@@@@@@@
@@ FUN_CONTROLS_NPC
@@  %0 = NPC name
@@  %1 = Player DB
@@  Returns 1 if they're staff or owner
@@ @@@@@@@@@@@@@@@@@
&FUN_CONTROLS_NPC FS3Combat NPC Commands=or(strmatch(npcstat(unprettify(%0),owner),name(%1)),isstaff(%1)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_MOD_NPCSTAT
@@  Modifies a NPC statistic.
@@  %0 = NPC name
@@  %1 = The stat to set, from NPCSTATS.
@@  %2 = The new value.
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_MOD_NPCSTAT FS3Combat NPC Commands=set(#816,npc_[unprettify(%0)]:[replace(xget(#816,npc_[unprettify(%0)]),match(v(NPCSTATS),%1),%2,|)])

&FUN_IS_VALID_SKILL FS3Combat NPC Commands=and(isnum(%0),lte(%0,12),gte(%0,1))

&FUN_NPCSTAT FS3Combat NPC Commands=switch(0,isstaff(%!),ansi(hr,You are not allowed to do that.),extract(xget(#816,npc_[unprettify(%0)]),match(v(npcstats),%1),1,|))
&FUN_ISNPC FS3Combat NPC Commands=hasattr(#816,npc_[unprettify(%0)])

&FUN_CREATE_NPC FS3Combat NPC Commands=switch(1,not(isstaff(%!)),0|You are not allowed to do that.,isnpc(%0),0|There is already a NPC by that name.,isplayer(%0),0|There is already a player by that name.,isvehicle(%0),0|There is already a vehicle by that name.,strmatch(%0,*|*),0|NPC names may not contain the '|' character.,not(u(fun_is_valid_skill,%1)),0|%1 is not a valid skill level.,1[set(#816,npc_[unprettify(%0)]:[trim(%1)]|%N|)])

@STARTUP FS3Combat NPC Commands=@set me=!no_command;@fun npcstat=me,fun_npcstat;@fun isnpc=me,fun_isnpc;@fun createnpc=me,fun_create_npc
