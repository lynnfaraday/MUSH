@@ Faraday's Map System
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hr,Beginning Map System Installation.  Please wait until you see the 'Installation Complete' message...)]%R

@pemit/silent %#=[ansi(hr,Creating Objects...)]%R


@create Map Commands
@set Map Commands = !NO_COMMAND
@set Map Commands = WIZARD
&CMD-+MAP Map Commands=$+map:think pemit(%#,[line_with_text(xget(%l,area))]%r[u(#MAPDB/map_[xget(%l,area)])][switch(hasattr(#MAPDB,legend_[xget(%l,area)]),1,%r%rType [ansi(h,+map/legend)] for a legend to the numbers or abbreviations on the map.)]%r[line()])
&CMD-+MAP-AREA Map Commands=$+map *:think switch(0,hasattr(#MAPDB,map_[trim(%0)]),pemit(%#,ansi(hr,That is not a valid area. Use one of these instead: [iter(sort(lattr(#MAPDB/map_*)),after(##,_))])),pemit(%#,[line_with_text([capstr(%0)])]%r[u(#MAPDB/map_[trim(%0)])][switch(hasattr(#MAPDB,legend_[trim(%0)]),1,%r%rType [ansi(h,+map/legend %0)] for a legend to the numbers or abbreviations on the map.)]%r[line()]))
&CMD-+MAP-AREA-LEGEND Map Commands=$+map/legend *:think switch(0,hasattr(#MAPDB,legend_[trim(%0)]),pemit(%#,ansi(hr,That is not a valid area.  Use one of these instead: [iter(sort(lattr(#MAPDB/legend_*)),after(##,_))])),pemit(%#,[line()]%r[ansi(h,Map Legend For [capstr(%0)])]%R%R[u(#MAPDB/legend_[trim(%0)])]%r[line()])
&CMD-+MAP-AREAS Map Commands=$+map/areas:think pemit(%#,[line()]%R[ansi(hg,Areas covered by +map include:)]%r[iter(sort(lattr(#MAPDB/map_*)),after(##,_))]%R%RType [ansi(g,+map <area>)] for a map of a particular area.%R[line()])
&CMD-+MAP-legend Map Commands=$+map/legend:think pemit(%#,[switch(hasattr(#MAPDB,legend_[xget(%l,area)]),0,[ansi(hr,There is no map legend for this area.)],[line()]%R[ansi(h,Map Legend For [xget(%l,area)])]%R%R[u(#MAPDB/legend_[xget(%l,area)])]%R[line()])])
&HELP_+MAP Map Commands=The +map system helps you find your way around the MUSH.%R%R[ansi(h,+map)] - Views a map for your current area.%R[ansi(h,+map/legend)] - Views a legend for your current area's map, if there is one.%R[ansi(h,+map/areas)] - Views a list of other areas that have +maps.%R[ansi(h,+map <area>)] - Views another area's map.%R[ansi(h,+map/legend <area>)] - Views another area's legend.
&SHELP_+MAP Map Commands=Maps are stored on object #MAPDB in attributes named MAP_AREA and LEGEND_AREA.  A legend is not required.  You only need to use it if you want supplemental information to appear.  The system expects the area to match an AREA attribute on the room.
@DESCRIBE Map Commands=Commands for IC Maps.%R%R[u(credits)]
@STARTUP Map Commands=@set me=!no_command
&CREDITS Map Commands=Version [v(version)]%R%RCoded by Faraday ~ http://www.wordsmyth.org/faraday
&VERSION Map Commands=3.0

@create Map DB
@describe Map DB=Stores the actual maps.
@set Map DB = NO_COMMAND

@pemit/silent %#=[ansi(hr,Setting up database numbers...)]%R

@wait 2=@fo me=@edit Map Commands/*=#MAPDB,[num(Map DB)]

@wait 4=@pemit/silent %#=[ansi(hr,Copying help files...)]%R
@wait 4=@fo %#=+copyhelp [num(Map Commands)]

@wait 5=@pemit/silent %#=[ansi(hr,Moving objects to their appropriate places...)]%R

@wait 5=@tel Map DB=Map Commands
@wait 6=@tel Map Commands=#2

@wait 9=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!)]

@wait 10=@set me=!quiet

