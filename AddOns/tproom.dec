@@ Faraday's MUSH Tinyplot Room Parent
@@ Coded by Linda Naughton O'Meara
@@ http://www.wordsmyth.org/faraday
@@
@@ ===================
@@ OVERVIEW
@@ ===================
@@ The TP Room Parent is designed to let you create "TP Rooms" - rooms with changeable descs 
@@ that can represent everything from a back alley to an ornate ballroom.
@@
@@ ===================
@@ FEATURES
@@ ===================
@@
@@ * Store descs on a central parent and access them from any TP room.
@@ * Players can submit their own descs.
@@ * Rooms can be locked for privacy.
@@
@@ ===================
@@ REQUIREMENTS
@@ ===================
@@
@@ * PennMUSH
@@ * Faraday's Global Utility Functions
@@
@@ ===================
@@ INSTALLATION
@@ ===================
@@ 
@@ Simply /quote this script to your MUSH via a MUSH client.  You can also cut and paste, but beware
@@ of linebreaks.  There is no need to create any objects or do any manual editing of this script unless
@@ your master room is something other than #2.
@@
@@ ===================
@@ SETUP
@@ ===================
@@
@@ The installation merely creates a room parent. You will need to create actual TP rooms yourself and parent @@ them to the parent object. There is special setup required for the exits in order to make the locks work 
@@ properly. Once you've got a room created, check out +lhelp setup for more info.
@@
@@ ===================
@@ USAGE
@@ ===================
@@ 
@@ See +lhelp in a Tinyplot room for usage instructions.
@@
@@ ===================
@@ UPDATES
@@ ===================
@@
@@ Join the mailing list (http://groups.yahoo.com/group/faradaycode) to keep up with the latest news.
@@
@@     * Version 1.0 - 6/12/04 - Initial version

@set me=quiet

@create TP Room Parent
@set TP Room Parent = SAFE
@set TP Room Parent = !NO_COMMAND
@ALEAVE TP Room Parent=think switch(words(lcon(me)),0,[set(v(my_exit),islocked:0)][wipe(me/desc2)])
@set TP Room Parent/ALEAVE=no_command prefixmatch
&CMD-DESC-CREATE TP Room Parent=$desc/create *=*:think switch(1,hasattr(me,tpdesc_[edit(%0,%b,-)]),pemit(%#,ansi(hr,There is already a desc with that name.)),t(match(%1,*\[*)),pemit(%#,ansi(hr,Sorry - your request could not be completed.  Do not use MUSH function calls in your descriptions.  Contact staff to request a special exception.)),pemit(%#,ansi(hc,You create a desc entited "[edit(%0,%b,-)]".))[set(#TPROOM,tpdesc_[edit(%0,%b,-)]:%1)])
&CMD-DESC-DELETE TP Room Parent=$desc/delete *:think switch(hasattr(#TPROOM,tpdesc_[trim(%0)]),0,pemit(%#,ansi(hr,There is no desc by that name.)),pemit(%#,ansi(hg,You delete desc [trim(%0)].))[set(#TPROOM,old_[trim(%0)]:[xget(#TPROOM,tpdesc_[trim(%0)])])][wipe(#TPROOM/tpdesc_[trim(%0)])])
&CMD-DESC-LIST TP Room Parent=$desc/list:think pemit(%#,[line()]%R[ansi(h,Available TP Room Descriptions)]%R[iter(lattr(#TPROOM/tpdesc_*),%R[capstr(lcstr(after(##,_)))])]%R[line()])
&CMD-DESC-SET TP Room Parent=$desc/set *:think switch(hasattr(#TPROOM,tpdesc_[trim(%0)]),0,pemit(%#,ansi(hr,Sorry there is no desc by that name.)),pemit(%#,ansi(hy,You set the room's description to [trim(%0)].))[set(%l,desc2:[xget(#TPROOM,tpdesc_[trim(%0)])])])
&CMD-DESC-VIEW TP Room Parent=$desc/view *:think pemit(%#,switch(hasattr(#TPROOM,tpdesc_[trim(%0)]),0,ansi(hr,There is no such desc.),[line()]%R[xget(#TPROOM,tpdesc_[trim(%0)])]%R[line()]))
&CMD-LOCK TP Room Parent=$lock/*:think switch(%0,on,pemit(%#,ansi(hg,You lock the room, ensuring your privacy.  Please remember to unlock the room when you leave.))[set(v(my_exit),islocked:1)],off,pemit(%#,ansi(hg,You unlock the room.))[set(v(my_exit),islocked:0)],pemit(%#,ansi(hr,That is not a valid option.)))
&DESC2 TP Room Parent=The walls of the room shimmer.  They are shapeless, malleable, almost waiting to be given form.  With a little imagination, the room can become anything, from an abandoned mining tunnel to an ornate ballroom.%R%RThis is a [ansi(hg,TinyPlot Room)] which you can use to simulate any room not actually coded on the MUSH.  The room's commands allow you to change the description to suit your needs, even storing multiple descs if you so desire.  You can also lock the room for privacy, and request that it be linked to the IC world for special TPs.
@DESCRIBE TP Room Parent=[u(desc2)]%R%RThis room is currently [switch(xget(v(my_exit),islocked),1,[ansi(hc,LOCKED)],[ansi(hy,UNLOCKED)])].%R%RSee [ansi(h,+lhelp)] for local help files.
@set TP Room Parent/DESCRIBE=no_command visual prefixmatch
&help_setup TP Room Parent=To set up a new TP room, you need to do a few things:%R%R1.  Dig the room as normal.%R%R2.  @parent the room to #TPROOM.%R%R3. Set a MY_EXIT attribute on the new room equal to the DB# of the exit leading INTO the room (from the outside).  This will enable the locks to work.%R%R4. Lock the exit leading INTO the room to islocked/0 (go outside to do this).  For example: @lock Room1=islocked/0%R%R5. Set an @fail on the exit saying that the room is in use.
&HELP_DESC TP Room Parent=This command allows you to create and store descriptions for use in the TP Rooms.%R%R[ansi(hg,Usage:)]  [ansi(h,desc/list)] - Lists existing descs%R[space(7)][ansi(h,desc/view <descname>)] - Views an existing desc.%R[space(7)][ansi(h,desc/set <descname>)] - Sets the room desc to an existing desc%R[space(7)][ansi(h,desc/delete <descname>)] - Deletes an existing desc%R[space(32)]Please do not delete other people's descs.%R[space(7)][ansi(h,desc/create <descname>=<description>)] - Creates a new desc
&HELP_LINKING TP Room Parent=It is possible for TP Rooms to be linked to the IC World.  To request this, simply +mail staff.
&HELP_LOCK TP Room Parent=For private scenes, it is possible to lock the door to prevent anyone from entering the room and disturbing you.  Just please remember to unlock the door when you're finished.%R%R[ansi(hg,Usage:)] lock/on or lock/off to lock or unlock the door
@STARTUP TP Room Parent=@set me=!no_command
@set TP Room Parent/STARTUP=no_command prefixmatch
&TPDESC_DEFAULT TP Room Parent=The walls of the room shimmer.  They are shapeless, malleable, almost waiting to be given form.  With a little imagination, the room can become anything, from an abandoned mining tunnel to an ornate ballroom.%R%RThis is a [ansi(hg,TinyPlot Room)] which you can use to simulate any room not actually coded on the MUSH.  The room's commands allow you to change the description to suit your needs, even storing multiple descs if you so desire.  You can also lock the room for privacy, and request that it be linked to the IC world for special TPs.
&cmd-+lhelp TP Room Parent=$+lhelp*:think switch(words([lattr(me/help_*)]%B[lattr([parent(me)]/help_*)]),0,pemit(%#,ansi(c,There are no local help files here.)),switch(%0,,pemit(%#,[line()]%rLocal Help Topics::%r%r[setq(0,iter([lattr(me/help_*)]%B[lattr([parent(me)]/help_*)],after(##,_)))][table([sort(%q0)],15,78,,)]%r%rType [ansi(hb,+lhelp <topic>)] for more information about a given topic.%r[line()]),pemit(%#,[line()]%rLocal Help Topic : [ansi(hr,[ucstr(%0)])]%r%r[u(help_[trim(%0)])]%r[line()])))

@wait 2=@fo me=@edit TP Room Parent/*=#TPROOM,[num(TP Room Parent)]

@wait 3=@pemit/silent %#=[ansi(hr,Make sure you have also installed the Global Utility Functions system.)]

@wait 4=@pemit/silent %#=%R%R[ansi(hr,Installation Complete!!  Put the room parent in a safe place.)]%R


@set me=!quiet