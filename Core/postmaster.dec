@@ Faraday's MUSH Postmaster System
@@ Coded by Linda Naughton O'Meara
@@ http://www.wordsmyth.org/faraday
@@
@@ ===================
@@ OVERVIEW
@@ ===================
@@ A simple +mail system that implements mailing lists and multi-party sends. It uses the basic PennMUSH
@@ @mail system, and just expands it a little.
@@
@@ ===================
@@ FEATURES
@@ ===================
@@
@@ * Supports a MUX-like style of piece-by-piece message composition.
@@ * Mailing lists can be created easily, and items sent to the list will show the names that the 
@@   message was sent to.
@@ * Personal mailing lists can be created by individual players. This can double as a 'friends' 
@@   list to see who's online.
@@ * List members can set up comments to show jobs/responsibilities/etc. when people view the lists.
@@ * Lists can be restricted, so only list members can see who's on it. Good for secret organizations.
@@ * There is an optional sent-mail feature that lets you get a copy of all messages you send. 
@@
@@ ===================
@@ REQUIREMENTS
@@ ===================
@@
@@ * PennMUSH
@@ * Faraday's Global Utility Functions
@@
@@ ===================
@@ INSTALLATION
@@ ===================
@@ 
@@ Simply /quote this script to your MUSH via a MUSH client.  You can also cut and paste, but beware
@@ of linebreaks.  There is no need to create any objects or do any manual editing of this script unless
@@ your master room is something other than #2.
@@
@@ ===================
@@ SETUP
@@ ===================
@@
@@ You'll need to set up the global mailing lists.  See +mail/help staff.
@@
@@ ===================
@@ USAGE
@@ ===================
@@ 
@@ See +mail/help for usage instructions.
@@
@@ ===================
@@ UPDATES
@@ ===================
@@
@@ Join the mailing list (http://groups.yahoo.com/group/faradaycode) to keep up with the latest news.
@@
@@     * Version 1.0 - 6/12/04 - Initial version
@@     * Version 1.1 - 6/6/05 - Fixed a minor bug in +mail/send, which wouldn't fill in the "Sent To" list. 


@set me=quiet

@pemit/silent %#=[ansi(hr,Beginning Postmaster System Installation.  Please wait until the Installation Complete message appears...)]%R

@pemit/silent %#=[ansi(hr,Creating Objects...)]%R


@create MUSH Postmaster
@set MUSH Postmaster = WIZARD
@set MUSH Postmaster = !NO_COMMAND
&CMD-+LIST MUSH Postmaster=$+list:think pemit(%#,[setq(0,%#)][line()]%R[ansi(h[rand_color()],Mailing Lists)]%R%R[ansi(h,Title[space(15)]Description)]%r[repeat(-,78)][iter(sort([lattr(#MAILFUN/list_*)]%B[lattr(%q0/list_*)]),%R[padstr(capstr(lcstr(after(##,_))),20)][switch(t(match(lattr(%q0/list_*),##)),1,Private List,u(#MAILFUN/comment_[after(##,_)]))])]%R%RUse [ansi(h,+mail <list title>=<subject>/<message>)] to send a message to a mailing list.%RUse [ansi(h,+list <list title>)] to view the members of a list.%R[line()]
&CMD-+LIST-DETAIL MUSH Postmaster=$+list *:think [setq(0,%#)][switch(1,not(or(hasattr(%q0,list_[trim(%0)]),hasattr(#MAILFUN,list_[trim(%0)]))),pemit(%#,ansi(hr,[capstr(%0)] is not a valid list.  Type +list to see what lists are available.)),and(match(xget(#MAILFUN,restricted),[trim(%0)]),not(or(isstaff(%#),match(xget(#MAILFUN,list_[trim(%0)]),%#)))),pemit(%#,ansi(hr,[capstr(%0)] is a restricted list and you are not on it.  You can still send messages to it but you can't see who's on it.)),pemit(%#,[line()]%R[center(ansi(h,[capstr(lcstr(trim(%0)))] List),78)]%R [center(switch(t(match(lattr(%q0/list_*),list_[trim(%0)])),1,xget(%q0,comment_[trim(%0)]),xget(#MAILFUN,comment_[trim(%0)])),78)]%R%RName[space(12)]Comment[space(30)]Last Online%r[repeat(-,78)][iter(switch(t(match(lattr(%q0/list_*),list_[trim(%0)])),1,xget(%q0,list_[trim(%0)]),xget(#MAILFUN,list_[trim(%0)])),%R[padstr(##,14,left,.)]%B%B[padstr(xget(pmatch(##),comment_[trim(%0)]),35,left,.)]%B%B[padstr(switch(and(hasflag(pmatch(##),connected),not(hasflag(pmatch(##),dark))),1,CONNECTED,xget(pmatch(##),last)),24,left,.)])]%R[line()]))]
&CMD-+MAIL MUSH Postmaster=$+mail *=*/*:@fo %#=@mail [setq(0,u(#MAILFUN/fun_build_to_list,%0,%#))]%q0=%1/[ansi(h,Sent To:)] [iter(%0,capstr(##))]%R%R[edit(%2,\;,\\\;)]
&CMD-+MAIL-++ MUSH Postmaster=$++*:think switch(0,hasattr(%#,mailsub),pemit(%#,You are not composing a mail message.),pemit(%#,%r[line()]%r[ansi(hg,You add to your mail message.[set(%#,mailbody:[xget(%#,mailbody)]%b[edit(%0,\;,\\\;)])] It now reads:)]%r[u(%#/mailbody)]%r%RUse +mail/send to send your message or ++<text> to add to it.%R[line()]%r)
&CMD-+MAIL2 MUSH Postmaster=$+mail *=*:@switch %1=*/*,,@fo %#=+mail/start %0=%1
&CMD-+MAIL-ABORT MUSH Postmaster=$+mail/abort:think pemit(%#,You abort your mail composition.[iter(mailto mailsub mailbody,wipe(%#/##))])
&CMD-+MAIL-ANON MUSH Postmaster=$+mail/anon */*=*/*:@switch/first 0=hasattr(#MAILFUN,list_%0),@pemit %#=That is not a valid list name.,t(match(xget(#MAILFUN,list_%0),%N)),@pemit %#=You are not a member of that list.,t(match(xget(#MAILFUN,restricted),%0)),@pemit %#=This command can only be used for restricted lists.,{@pemit %#=Message sent.;@fo #MAILCMD=@mail [setq(0,u(#MAILFUN/fun_build_to_list,%1,%#))]%q0=%2/[ansi(h,From:)]%B%B%B [capstr(%0)] \(Anonymous\)%R[ansi(h,Sent To:)] [iter(%1,capstr(##))] [capstr(%0)] Staff%R%R[edit(%3,\;,\\\;)];@fo %#=+mail staff %0=%2/[ansi(h,From:)] [capstr(%0)] \(Anonymous - %N\)%R[ansi(h,Originally Sent To:)] [iter(%1,capstr(##))]%R%R[edit(%3,\;,\\\;)]}
&CMD-+MAIL-CREDITS MUSH Postmaster=$+mail/credits:think pemit(%#,[line()]%RMUSH Postmaster System Version [xget(#MAILFUN,version)]%R%RCoded by Faraday ~ http://www.wordsmyth.org/faraday%R[line()])
&CMD-+MAIL-EDIT MUSH Postmaster=$+mail/edit *=*:think switch(hasattr(%#,mailbody),0,pemit(%#,There is no text in the mail message to edit.),[set(%#,mailbody:[edit(xget(%#,mailbody),{%0},{%1})])][pemit(%#,%r[line(+mail System,Composition)]%rYou edit your mail message. It now reads:%r%r[u(%#/mailbody)]%r[line(The Babylon Project,+mail/send to send your message)]%r)])
&CMD-+MAIL-FWD MUSH Postmaster=$+mail/fwd *=*/*:+mail %0=%2/[mail(%#,%1
&CMD-+MAIL-HELP MUSH Postmaster=$+mail/help*:think pemit(%#,[line_with_text(Mail Help)]%R[switch(%0,,u(#MAILFUN/help_+mail),u(#MAILFUN/help_+mail_[trim(%0)]))]%R[line()])
&CMD-+MAIL-PROOF MUSH Postmaster=$+mail/proof:think switch(hasattr(%#,mailto),0,pemit(%#,You are not composing a mail message.),pemit(%#,%r[line(+mail System,Composition)]%r[ansi(h,To:)] [xget(%#,mailto)]%r[ansi(h,From:)] %N%R[ansi(h,Subject:)]  [xget(%#,mailsub)]%r[repeat(-,30)]%r[u(%#/mailbody)]%r[line(The Babylon Project,+mail/send to send your message)]%r))
&CMD-+MAIL-SEND MUSH Postmaster=$+mail/send:@fo %#=@mail [setq(0,u(#MAILFUN/fun_build_to_list,xget(%#,mailto),%#))]%q0=[xget(%#,mailsub)]/{[ansi(h,Sent To:)] [iter(%q0,capstr(##))]%R%R[u(%#/mailbody)]};@dolist mailto mailsub mailbody=@wipe %#/##
&CMD-+MAIL-START MUSH Postmaster=$+mail/start *=*:think switch(1,hasattr(%#,mailto),pemit(%#,You are already composing a mail message.),match(%1,*/*),pemit(%#,You can't use a '/' in your subject.),pemit(%#,%r[line()]%rYou begin composing your mail message to %0 with subject %1. Use ++<text> to add text to your mail message.[set(%#,mailto:%0)][set(%#,mailsub:%1)]%r%RUse +mail/send to send your message.%R[line()]))
@DESCRIBE MUSH Postmaster=+mail/help
@set MUSH Postmaster/DESCRIBE=no_command visual prefixmatch
@set MUSH Postmaster/MAILCURF=no_command wizard locked prefixmatch
@STARTUP MUSH Postmaster=@set me=!no_command
@set MUSH Postmaster/STARTUP=no_command prefixmatch


@create Mail Functions and Lists
@set Mail Functions and Lists = NO_COMMAND
@set Mail Functions and Lists = WIZARD
@DESCRIBE Mail Functions and Lists=+mail/help
@set Mail Functions and Lists/DESCRIBE=no_command visual prefixmatch
&FUN_BUILD_TO_LIST Mail Functions and Lists=setunion(switch(xget(%1,cc_sent_mail),1,[name(%1)]),iter(%0,switch(1,isplayer(##),capstr(##),hasattr(%1,list_##),xget(%1,list_##),hasattr(#MAILFUN,list_##),xget(#MAILFUN,list_##))),%B)
&HELP_+MAIL Mail Functions and Lists=The +mail command allows you to send messages through the @mail system.  There are a few commands that let you use a MUX-like syntax for composing messages a little at a time, rather than all at once.%R%RThe other benefit of the +mail system is the ability to send to mailing lists.  The "to-list" can be any combination of mailing lists and player names.%R%RYou can set up the system to automatically carbon copy you on all messages you sent.  To do this, set:  [ansi(h,&cc_sent_mail me=1)]%R%R[ansi(hg,Quick Mail Usage:)]%R[ansi(h,+mail <to-list>=<subject>/<message>)] - send a message%R[ansi(h,+mail/credits)] - shows version and credit info%R%R[ansi(hg,Other help topics:)]%R[ansi(h,+mail/help compose)] - MUX-mail style composition%R[ansi(h,+mail/help list)] - Mailing list help%R[ansi(h,+mail/help staff)] - Staff setup help
&HELP_+MAIL_COMPOSE Mail Functions and Lists=The following commands show how to create mail messages by composing them a little at a time.%R%R[ansi(hg,Compose (MUX) Style Usage:)]%R[ansi(h,+mail/start <to-list>=<subject>)] [ansi(hb,-or-)]%r[ansi(h,+mail <to-list>=<subject, which may not contain a />)]%r%tbegins composing your mail message%r[ansi(h,++<text>)] - Adds text to the body of your mail message%r[ansi(h,+mail/proof)] - Reviews the message%r[ansi(h,+mail/edit <text to find>=<text to replace>)] - %R%TEdits the body of your mail message.%R%TYou can also use the @edit command on your MAILBODY attribute.%r[ansi(h,+mail/abort)] - Cancels your message in progress%r[ansi(h,+mail/send)] - Sends the mail message
&HELP_+MAIL_LIST Mail Functions and Lists=The +mail system supports sending messages to mailing lists.  There are certain public lists, created by staff, and private lists that you can create for your own purposes.%R%RPublic lists may be restricted, meaning that only members of the list (or staff) can see who's on it.  You can still send messages to those lists - you just won't be able to tell who it's going to.  Players who receive the messages can then reply anonymously so they can answer the mail without revealing their membership on the list.%R%R[ansi(hg,Mailing List Usage:)]%R[ansi(h,+list)] - Shows all lists%R[ansi(h,+list <list name>)] - Views members of a list.%R%R[ansi(h,&list_<name> me=Player1 Player2 etc.)] - Create your own personal mailing list.  You can edit or delete it later using the standard MUSH attribute commands.%R[ansi(h,&comment_<listname> me=<comment>)] - Change the comment that shows up beside your name if you're on a list.%R%R[ansi(h,+mail/anon <listName>/<to-list>=<subject>/<message>)] - Replies anonymously to a message.  You must be a member of 'listname' and it must be a restricted list.  Both staff and the list will get a copy of the message automatically, and it will show who sent it.
&HELP_+MAIL_STAFF Mail Functions and Lists=To set up a global mailing list, add an attribute to the #MAILFUN object.  For example, to add a "TEST" list with Joe and Frank on it, you would type:%R&LIST_TEST #MAILFUN=Joe Frank%R%RTo restrict a list so that only members can see who's on it, simply add the list name to the RESTRICTED attribute on #MAILFUN. For example:%R&RESTRICTED #MAILFUN=TEST%R%RNote that if a player uses the +mail/anon command to send messages anonymously from a restricted list, a copy will be sent to the list and to the "staff" mailing list (if there is one).  This allows staff members and list members to see who sent what.
&LIST_STAFF Mail Functions and Lists=
&COMMENT_STAFF Mail Functions and Lists=Staff members.
&RESTRICTED Mail Functions and Lists=
&VERSION Mail Functions and Lists=1.1



@pemit/silent %#=[ansi(hr,Setting up database numbers...)]%R

@wait 4=@fo me=@edit MUSH Postmaster/*=#MAILFUN,[num(Mail Functions and Lists)]
@wait 4=@fo me=@edit Mail Functions and Lists/*=#MAILFUN,[num(Mail Functions and Lists)]
@wait 4=@fo me=@edit MUSH Postmaster/*=#MAILCMD,[num(MUSH Postmaster)]
@wait 4=@fo me=@edit Mail Functions and Lists/*=#MAILCMD,[num(MUSH Postmaster)]


@pemit/silent %#=[ansi(hr,Moving objects to their appropriate places...)]%R

@wait 5=@tel Mail Functions and Lists=MUSH Postmaster
@wait 6=@tel MUSH Postmaster=#2

@pemit/silent %#=[ansi(hg,Make sure you have also installed the Global Utility Functions system.)]


@wait 7=@pemit/silent %#=%R%R[ansi(hr,Installation Complete!!)]%R

@wait 7=@set me=!quiet
