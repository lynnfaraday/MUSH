@@ Faraday's Install Manager
@@ Author:  Linda O'Meara
@@ Website: http://code.google.com/p/faramushcode/
@@
@@ Visit the website for documentation, installation instructions, bug reports,
@@ and more.

@set me=quiet

@pemit/silent %#=[ansi(hg,Beginning Installation.  Please wait until the Installation Complete message appears...)]%R

@pemit/silent %#=[ansi(hg,Creating Objects...)]%R

think [setq(0,#-1)][squish(iter([lcon(%l)] [lcon(%#)],switch(name(##),Install Manager,setq(0,##))))][switch(%q0,#-1,Install Manager created: DB# [create(Install Manager)][set(Install Manager,Wizard)][set(Install Manager,!NO_COMMAND)][set(Install Manager,Safe)].  %RInstall DB created: DB# [create(Install DB)].   %RInstall Object Storage created: DB# [create(Install Object Storage)],Install Manager already exists.[setq(1,num(Install Object Storage))][setq(2,num(Install DB))][iter(lcon(Install Object Storage),switch(name(##),Install DB,[tel(##,%#)][setq(2,##)]))][switch(or(match(%q1,#-1),match(%q2,#-1)),1,%R[ansi(hR,ERROR! Couldn't find the Install Manager objects!  Make sure that they are in your inventory\, the master room\, or the install object storage.)])])]

@pemit/silent %#=[ansi(hg,Setting Object Attributes...)]%R

@set Install Manager = SAFE
@set Install Manager = !NO_COMMAND

&CMD-+install-update Install Manager=$+install/update *:@switch/first [setq(0,u(fun_get_dbref,%0))]0=[u(fun_is_registered,%0)],@pemit %#=ansi(hr,I can't find a object named %0.),{@pemit %#=ansi(c,Updating database references on object %0.);think switch(u(fun_is_installed_properly,%0),0,pemit(%#,ansi(hr,It appears object %q0 \([name(%q0)]\) may not be properly installed.  Please double-check.)));@dolist [lattr(#INSTALL_DB/obj_*)] ALLDONE=@switch ##=ALLDONE,{@fo %#=+install/finalize %0},{think u(FUN_UPDATE,%q0,#[after(##,_)],xget(#INSTALL_DB,##))}}

&CMD-+install-updategroup Install Manager=$+install/updategroup *:@switch [setq(0,u(fun_get_group_attrs,%0))][words(%q0)]=0,@pemit %#=[ansi(hr,Can't find a system group named %0.  Make sure it's been installed properly.)],@dolist %q0={@fo %#=+install/update [after(##,_)]}

&CMD-+install-finalize Install Manager=$+install/finalize *:think [setq(0,u(fun_get_dbref,%0))];@tr %q0/startup;think pemit(%#,[u(fun_INSTALL_CRON,%q0)]%R[u(fun_INSTALL_HELP,%q0)]);@fo %#=+install/attr %q0;@tel %q0=u(fun_get_destination,%0);@pemit %#=ansi(hc,%0 installed.)

&CMD-+INSTALL_ATTR Install Manager=$+install/attr *:@dolist lattr(%0/?ATTR_*)=think pemit(%#,setup_attr(mid(##,0,1),after(##,_),xget(%0,##)))

&CMD-+UNINSTALL Install Manager=$+uninstall *:@pemit %#=[setq(0,u(fun_get_group_attrs,%0))]switch(0,words(%q0),ansi(hr,I cannot find a group named %0.),ansi(hg,Are you sure you really want to uninstall the %0 system?  Type +uninstall/confirm to confirm.)[set(%#,uninstall:[trim(%0)])])

&CMD-+UNINSTALL-CONFIRM Install Manager=$+uninstall/confirm:@switch 0[setq(0,u(fun_get_group_attrs,xget(%#,uninstall)))]=t(xget(%#,uninstall)),@pemit %#=ansi(hr,You are not uninstalling anything.),{@pemit %#=ansi(hc,You uninstall the [xget(%#,uninstall)] system.  Before reinstalling - you need to run the @dbck command to clean up purged objects.)[wipe(%#/uninstall)];@dolist %q0={think setq(1,u(fun_get_dbref,after(##,_)));@wipe #INSTALL_DB/##;@set %q1=!safe;@nuke %q1;think pemit(%#,UNINSTALL_HELP(%q1))}}

@@ --------------------------
@@  FUN_GET_DESTINATION
@@  Figure out where the object should be placed when it's created.
@@  Normally command objects go in the master room and other objects go into the install storage object
@@  but you can override this per object by setting the INSTALL_DESTINATION attribute to be the *ALIAS*
@@  of another known system object.  For instance, set the INSTALL_DESTINATION to HELP_CMD to place it
@@  into the help commands object.
@@  %0 = Object alias
@@ --------------------------

&FUN_GET_DESTINATION Install Manager=localize([setq(0,u(fun_get_dbref,%0))]switch(1,hasattr(%q0,install_destination),u(fun_get_dbref,xget(%q0,install_destination)),match(extract(%0,2,1,_),CMD),v(MASTER_ROOM),#INSTALL_STORAGE)

@@ --------------------------
@@  FUN_GET_GROUP_ATTRS
@@  Gets a list of the attributes corresponding to the objects in a group.
@@  %0 = Group prefix
@@ --------------------------
&FUN_GET_GROUP_ATTRS Install Manager=lattr(#INSTALL_DB/OBJ_[trim(%0)]_*)

@@ --------------------------
@@  FUN_INSTALL_CREATE
@@  Will either create a new object or find the existing one and move it to your inventory.
@@  %0 = Alias
@@  %1 = Object name
@@  %2 = Wizard Flag (0 or 1)
@@  %3 = No Command Flag (0 or 1)
@@  %4 = Version
@@  %5 = Description
@@  %6 = Install Destination (may be blank)
@@ --------------------------
&FUN_INSTALL_CREATE Install Manager=pemit(%#,[switch(0,isstaff(%#),ansi(hr,This function is restricted.),u(fun_is_registered,%0),[setq(0,create(%1))][u(fun_register,%0,%q0)][tel(%q0,%#)][ansi(hc,Created object %0 with DB# %q0.)],[setq(0,u(fun_get_dbref,%0))][tel(%q0,%#)][ansi(hc,Found object %0.)][switch(strmatch(name(%q0),%1),0,ansi(hR,Object names do not match!  Install will fail!  Rename the old object to %1 and try again.))])][set(%q0,SAFE)][switch(%2,1,set(%q0,wizard))][switch(%3,0,[set(%q0,!NO_COMMAND)][set(%q0,startup:@set me=!no_command)],set(%q0,NO_COMMAND))][set(%q0,version:%4)][set(%q0,describe:%5%R\[u(credits)\])][switch(%6,,,set(%q0,install_destination:%6))][set(%q0,credits:[v(credits)])])

&CREDITS Install Manager=Version [v(version)]%R%RCoded by Faraday ~ http://www.wordsmyth.org/faraday
&MASTER_ROOM install manager=#2

@@ --------------------------
@@  FUN_INSTALL_ATTRS
@@  Installs player setup attributes (named SETUPATTR_*) to the player setup object
@@  %0 = Object DB#
@@ --------------------------
&FUN_INSTALL_ATTRS Install Manager=switch(1,t(match(functions(),INSTALL_ATTRs)),INSTALL_ATTRs(%0))

@@ --------------------------
@@  FUN_INSTALL_CRON
@@  Installs cron jobs (named CRON*) to the cron system.
@@  %0 = Object DB#
@@ --------------------------
&FUN_INSTALL_CRON Install Manager=switch(1,t(match(functions(),INSTALL_CRON)),INSTALL_CRON(%0))

@@ --------------------------
@@  FUN_INSTALL_HELP
@@  Installs help files (named HELP*) to the help system.
@@  %0 = Object DB#
@@ --------------------------
&FUN_INSTALL_HELP Install Manager=switch(1,t(match(functions(),INSTALL_HELP)),INSTALL_HELP(%0))

@@ --------------------------
@@  FUN_UPDATE
@@  Updates the DB references for all attributes on an object.
@@  %0 = Object DB#
@@  %1 = Alias to replace (e.g., #FOO_DB)
@@  %2 = DB# to use in place of the alias
@@ --------------------------
&FUN_UPDATE Install Manager=iter(lattr(%0),set(%0,##:[edit(xget(%0,##),%1,%2)])

@@ --------------------------
@@  FUN_IS_INSTALLED_PROPERLY
@@  Checks to make sure the object in question has at least one attribute, thus indicating that its data was actually installed.
@@  %0 = Object DB#
@@ --------------------------
&FUN_IS_INSTALLED_PROPERLY Install Manager=t(words(lattr([u(fun_get_dbref,%0)]/*)))

@@ --------------------------
@@  FUN_REGISTER
@@  Maps an alias to its DB# in the database
@@  %0 = Alias
@@  %1 = DB#
@@ --------------------------
&FUN_REGISTER Install Manager=localize([setq(0,OBJ_[trim(%0)])][set(#INSTALL_DB,%q0:%1)])

@@ --------------------------
@@  FUN_IS_REGISTERED
@@  Checks if an alias is registered with the database.
@@  %0 = Alias
@@ --------------------------
&FUN_IS_REGISTERED Install Manager=hasattr(#INSTALL_DB,OBJ_[trim(%0)])

@@ --------------------------
@@  FUN_GET_DBREF
@@  Gets the DB# for a registered alias.
@@  %0 = Alias
@@ --------------------------
&FUN_GET_DBREF Install Manager=switch(u(fun_is_registered,%0),0,#-1,xget(#INSTALL_DB,OBJ_[trim(%0)]))

@STARTUP Install Manager=@set me=!no_command;@fun INSTALL_CREATE=me,FUN_INSTALL_CREATE;@fun INSTALL_ALIASDB=me,FUN_GET_DBREF

@set Install DB = NO_COMMAND


@pemit/silent %#=[ansi(hg,Setting up database numbers...)]%R
@fo me=@edit Install Manager/*=#INSTALL_DB,[num(Install DB)]
@fo me=@edit Install Manager/*=#INSTALL_STORAGE,[num(Install Object Storage)]
@wait 2=@tel Install DB=Install Object Storage
@tr Install Manager/Startup

@wait 5=@pemit/silent %#=%R%R[ansi(hg,Installation Complete!!)]%R

@wait 5=@set me=!quiet