
@create FS3Combat Damage Commands
@set FS3Combat Damage Commands = WIZARD
@set FS3Combat Damage Commands = SAFE

@@  @@@@@@@@@@@@@@@@@@@@@@
@@  @ DAMAGE VIEW COMMANDS
@@  @@@@@@@@@@@@@@@@@@@@@@

&CMD-+DAMAGE FS3Combat Damage Commands=$+damage:@pemit %#=[line()]%R[ansi(hr,Damage Report for %N)]%R[u(fun_dmglist,%N)]%R%R[ansi(h,Total Wound Modifier:)] -[damagemod(%N)] [u(fun_display_restricted,%N)]%RSee [ansi(h,+damage <#>)] for details on a given wound.%R[line()]

&CMD-+DAMAGE-DETAIL FS3Combat Damage Commands=$+damage *:@pemit %#=switch(0,isnum(%0),,gt(%0,0),ansi(hr,That is not a valid wound number.),lte(%0,words(u(FUN_ALL_DMG,%N))),ansi(hr,You don't have that many wounds.),[line()]%R[ansi(hr,Damage Report for %N wound %0)]%R[u(fun_dmg_detail,u(fun_get_dmgnum,%N,%0))]%R[line()]

&CMD-+DAMAGE-PERSON FS3Combat Damage Commands=$+damage *:@pemit %#=switch(1,isnum(%0),,strmatch(%0,*/*),,eq(words(u(FUN_ALL_DMG,%0)),0),ansi(hr,No damage report available.  Make sure %0 is a valid PC\, NPC\, or vehicle name.  It could be they have never taken any damage.),[line()]%R[ansi(hr,Damage Report for [prettify(%0)])]%R[u(fun_dmglist,%0)]%R%R[ansi(h,Total Wound Modifier:)] -[damagemod(%0)] [u(fun_display_restricted,%0)]%RSee [ansi(h,+damage <name>/<#>)] for details on a given wound.%R[line()]

&CMD-+DAMAGE-PERSON-DETAIL FS3Combat Damage Commands=$+damage */*:@pemit %#=switch(0,isnum(%1),ansi(hr,That is not a valid wound number.),gt(%1,0),ansi(hr,That is not a valid wound number.),lte(%1,words(u(FUN_ALL_DMG,%0))),ansi(hr,%0 doesn't have that many wounds.),[line()]%R[ansi(hr,Damage Report for [prettify(%0)] wound %1)]%R[u(fun_dmg_detail,u(fun_get_dmgnum,%0,%1))]%R[line()]

@@  @@@@@@@@@@@@@@@@@@@@@@
@@   DAMAGE MOD COMMANDS
@@  @@@@@@@@@@@@@@@@@@@@@@

&CMD-+DAMAGE-MODLIST FS3Combat Damage Commands=$+damage/mod:@pemit %#=[line()]%R[ansi(h,Damage values that can be modified:)]%R[ljust(real,12)] 1 for real, 0 for mock%R[ljust(hitloc,12)] Hit location%R[ljust(weapon,12)] Weapon used%R[ljust(type,12)] Physical or stun%R[ljust(initsev,12)] Initial severity (C/S/M/L/H)%R[ljust(currsev,12)] Current severity (C/S/M/L/H)%R[ljust(healing,12)] Healing points%R[ljust(lasttreat,12)] Last treated (secs)%R[ljust(lastheal,12)] Last healed (secs)%R[ljust(notes,12)] Wound notes%R%RC=Critical, S=Serious, M=Moderate, L=Light, H=Healed%RUse [ansi(h,+damage/mod <name>/<#>=<stat, from list above>/<newvalue>)]%R[line()]

&CMD-+DAMAGE-MOD FS3Combat Damage Commands=$+damage/mod */*=*/*:@pemit %#=[setq(0,u(fun_get_dmgnum,%0,%1))]switch(0,isstaff(%#),ansi(hr,This command is for staff only.),t(%q0),ansi(hr,%0/%1 is not a valid damage value.),match(v(dmgstats),trim(%2)),ansi(hr,%2 is not a valid damage stat to modify.),u(fun_valid_dmgstat_[trim(%2)],%3),ansi(hr,%3 is not a valid value for the %2 statistic.),ansi(hg,You modify [prettify(%0)]'s damage.)[u(fun_mod_dmgstat,%q0,trim(%2),prettify(escape_cr(%3)))])

&CMD-+DAMAGE-MOVE FS3Combat Damage Commands=$+damage/move *=*:@pemit %#=[setq(0,u(fun_get_dmgnum,%N,%0))][switch(0,t(%q0),ansi(hr,That is not a valid damage number.),gt(xget(%#,luck_points),0),ansi(hr,You do not have any luck points.  Contact staff.),lte(u(fun_minutes_since_injury,%q0),10),ansi(hr,You only have 10 minutes to move a wound using this command.  Contact staff.),ansi(hg,You spend a luck point to move wound %0 to hit location %1.)[set(%#,luck_points:[sub(xget(%#,luck_points),1)])][u(fun_mod_dmgstat,%q0,hitloc,prettify(%1))])]

&CMD-+DAMAGE-NOTES FS3Combat Damage Commands=$+damage/notes *=*:@pemit %#=[setq(0,u(fun_get_dmgnum,%N,%0))][switch(0,t(%q0),ansi(hr,That is not a valid damage number.),ansi(hg,You modify the notes on wound %0.)[u(fun_mod_dmgstat,%q0,notes,escape_cr(prettify(%1)))])]

&CMD-+DAMAGE-DELETE FS3Combat Damage Commands=$+damage/delete */*:@pemit %#=[setq(0,u(fun_get_dmgnum,%0,%1))]switch(0,isstaff(%#),ansi(hr,This command is for staff only.),t(%q0),ansi(hr,%0/%1 is not a valid damage value.),ansi(hg,You delete [prettify(%0)]'s damage.)[wipe(#731/%q0)])

&CMD-+DAMAGE-CLEAR FS3Combat Damage Commands=$+damage/clear *:@pemit %#=switch(0,isstaff(%#),ansi(hr,This command is for staff only.),gt(words(u(FUN_ALL_DMG,%0)),0),ansi(hr,No damage report available.  Make sure %0 is a valid PC\, NPC\, or vehicle name.  It could be they have never taken any damage.),ansi(hg,You clear all of [prettify(%0)]'s damage.)[u(fun_clear_damage,%0)])


@@  %0 = Name 
@@  %1 = Real (1) or Mock (0) Damage
@@  %2 = Hit location
@@  %3 = Weapon
@@  %4 = Physical or Stun damage
@@  %5 = Severity (C/S/M/L), applied to both initial and current severity

&CMD-+DAMAGE-INFLICT FS3Combat Damage Commands=$+damage/inflict *=*/*/*/*/*:@pemit %#=switch(0,isstaff(%#),ansi(hr,This command is only for staff.),match(real mock,%1),ansi(hr,Damage type must be mock or real.),match(physical stun,%4),ansi(hr,Damage type must be physical or stun.),match(C S M L,%5),ansi(hr,Damage severity must be C S M L.),ansi(hg,You inflict damage upon %0)[dodamage(%0,[switch(%1,real,1,0)],prettify(%2),prettify(%3),prettify(%4),capstr(%5))])

&CMD-+DAMAGE-INFLICT-CATCH FS3Combat Damage Command=$+damage/inflict *=*:@pemit %#=switch(words(%1,/),5,,[ansi(h,<COMBAT>)] [ansi(hr,The syntax is +damage/inflict <name>=<real or mock>/<hit location>/<weapon>/<physical or stun>/<C S M or L for severity>)])

@@  @@@@@@@@@@@@@@@@@@@@@@
@@   HEALING COMMANDS
@@  @@@@@@@@@@@@@@@@@@@@@@

&CMD-+DAMAGE-SCAN FS3Combat Damage Commands=$+damage/scan:@pemit %#=[line()]%R[ansi(h,Injured Characters In [name(%l)])]%R[ansi(h,Name[space(16)]Damage Mod[space(5)]Status)][iter(lcon(%l),[setq(0,name(##))]switch(0,or(isplayer(##),isvehicle(%q0)),,damagemod(%q0),,%R[ljust(name(##),20)][ljust(-[damagemod(%q0)],15)][u(fun_display_restricted,%q0)]))]%R%RSee [ansi(h,+damage <name>)] for a detailed damage report.%R[line()]

&CMD-+TREAT FS3Combat Damage Commands=$+treat */*:@pemit %#=[setq(0,u(fun_get_dmgnum,%0,%1))][setq(1,xget(#731,treat_skill))][setq(2,roll_ability(%#,%q1))][setq(3,u(fun_dmgstat,%q0,healing))][switch(0,t(%q0),ansi(hr,%0/%1 is not a valid damage combination.),u(fun_can_be_treated,%q0),ansi(hr,That wound cannot be treated.),or(isplayer(%0),isnpc(%0)),ansi(hr,This command may only be used on PCs or NPCs.),ability_level(%#,%q1),ansi(hr,You don't have the %q1 skill.),[remit(%l,[ansi(h,%N treats [prettify(%0)])] [switch(%q2,>0,[ansi(hg,successfully)],[ansi(hr,unsuccessfully)])].)][u(fun_mod_dmgstat,%q0,lasttreat,secs())][u(fun_mod_dmgstat,%q0,healing,add(%q3,switch(%q2,-2,-0.5,-1,-0.25,0,0,1,1.5,2,2,3,2.5,0)))])]

&CMD-+TREAT-ERROR  FS3Combat Damage Commands=$+treat *:@pemit %#=switch(%0,*/*,,ansi(hr,You have to specify a wound number from the damage list:  +treat <name>/<wound#>)

&CMD-+HEAL-ERROR  FS3Combat Damage Commands=$+heal *:@pemit %#=switch(%0,*/*,,ansi(hr,You have to specify a wound number from the damage list:  +heal <name>/<wound#>)

&CMD-+REPAIR-ERROR  FS3Combat Damage Commands=$+repair *:@pemit %#=switch(%0,*/*,,ansi(hr,You have to specify a wound number from the damage list:  +repair <name>/<wound#>)

&CMD-+HEAL FS3Combat Damage Commands=$+heal */*:@pemit %#=[setq(0,u(fun_get_dmgnum,%0,%1))][setq(1,xget(#731,heal_skill))][setq(2,roll_ability(%#,%q1))][setq(3,u(fun_dmgstat,%q0,healing))][switch(0,t(%q0),ansi(hr,%0/%1 is not a valid damage combination.),not(match(%0,%N)),ansi(hr,You can't heal yourself!),u(FUN_CAN_BE_HEALED,%q0),ansi(hr,That wound cannot be healed today.),or(isplayer(%0),isnpc(%0)),ansi(hr,This command may only be used on PCs or NPCs.),ability_level(%#,%q1),ansi(hr,You don't have the %q1 skill.),[remit(%l,[ansi(h,%N heals [prettify(%0)])] [switch(%q2,>0,[ansi(hg,successfully)],[ansi(hr,unsuccessfully)])].))][u(fun_mod_dmgstat,%q0,lastheal,secs())][u(fun_mod_dmgstat,%q0,healing,add(%q3,switch(%q2,-2,-1,-1,-0.5,0,0,1,1.75,2,2.25,3,2.75,0)))])]

&CMD-+REPAIR FS3Combat Damage Commands=$+repair */*:@pemit %#=[setq(0,u(fun_get_dmgnum,%0,%1))][setq(1,xget(#731,repair_skill))][setq(2,roll_ability(%#,%q1))][setq(3,u(fun_dmgstat,%q0,healing))][switch(0,t(%q0),ansi(hr,%0/%1 is not a valid damage combination.),isvehicle(%0),ansi(hr,This command may only be used on vehicles.),u(FUN_CAN_BE_HEALED,%q0),ansi(hr,That damage cannot be repaired today.),ability_level(%#,%q1),ansi(hr,You don't have the %q1 skill.),ansi(hg,You repair [prettify(%0)].)[remit(%l,[ansi(h,%N repairs [prettify(%0)])] [switch(%q2,>0,[ansi(hg,successfully)],[ansi(hr,unsuccessfully)])].)][u(fun_mod_dmgstat,%q0,lastheal,secs())][u(fun_mod_dmgstat,%q0,healing,add(%q3,switch(%q2,-2,-1,-1,-0.5,0,0,1,.75,2,1.25,3,1.75,0)))])]


@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@   TR_DAILY_HEALING
@@   Goes through every unhealed wound in the database and applies daily healing points.
@@   This is based on a 'toughness' skill roll, with a bonus if you're in a designated
@@   hospital.  NPCs roll their rating.  Vehicles don't have a rating so they just use the
@@   default NPC rating.
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&TR_DAILY_HEALING FS3Combat Damage Commands=@dolist lattr(#731/DMG_*)=think [setq(0,before(after(##,_),_))][setq(1,switch(1,isplayer(%q0),ability_level(pmatch(%q0),xget(#731,toughness_skill)),npc_rating(%q0)))][setq(2,u(fun_dmgstat,##,healing))][setq(3,add(%q2,switch(roll_ability(%q1),-2,0.75,-1,1,0,1,1,1,2,1.25,3,1.5,1),switch(0,match(xget(#731,hospitals),loc(pmatch(%q0))),0,0.5)))][setq(4,u(FUN_HEALPOINTS_WOUND,##))][switch(u(fun_dmgstat,##,currsev),H,,[u(fun_mod_dmgstat,##,healing,%q3)][switch(gte(%q3,%q4),1,[u(fun_mod_dmgstat,##,currsev,switch(u(fun_dmgstat,##,currsev),C,S,S,M,M,L,L,H))][u(fun_mod_dmgstat,##,healing,sub(%q3,%q4))])])])

@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  @@  NOTE: Most +damage functions expect to be passed in the damage attribute name  @@@@
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&SEVERITIES FS3Combat Damage Commands=C S M L H

@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@   FUN_ALL_DMG
@@   Gets a list of attr names for all the damage suffered by a char/vehicle.
@@   Damage is sorted by date/time, oldest first.
@@   %0 = Name, may be PC, NPC or Vehicle
@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_ALL_DMG FS3Combat Damage Commands=sortkey(KEY_SORT_DMG,lattr(#731/DMG_[trim(edit(%0,_,.,%B,.))]_*))
&KEY_SORT_DMG FS3Combat Damage Commands=after(after(%0,_),_)

@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@   FUN_GET_DMGNUM
@@  Gets a specific damage attr name from the 'all damage' list for a char/vehicle.
@@  %0 = Name
@@  %1 = Index, 1-based
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_GET_DMGNUM FS3Combat Damage Commands=extract(u(FUN_ALL_DMG,%0),%1,1)

@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@   FUN_CLEAR_MOCK
@@  Clears out any mock damage suffered by someone.  Should be done after a combat. 
@@  Because it's global, must check for wiz to prevent unauth access.
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_CLEAR_MOCK FS3Combat Damage Commands=switch(0,isstaff(%!),ansi(hr,You are not allowed to do that.),iter(u(fun_all_dmg,%0),switch(u(fun_dmgstat,##,real),0,wipe(#731/##)),,))


@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_CLEAR_DAMAGE
@@  Clears out ALL damage suffered by someone.
@@  Because it's global, must check for wiz to prevent unauth access.
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_CLEAR_DAMAGE FS3Combat Damage Commands=switch(0,isstaff(%!),ansi(hr,You are not allowed to do that.),[wipe(#731/dmg_[unprettify(%0)]_*)])

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DMGSTAT
@@  Gets a piece of damage info from a given damage attribute.
@@  %0 = Damage attr name 
@@  %1 = The stat to get, from DMGSTATS.
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&DMGSTATS FS3Combat Damage Commands=real hitloc weapon type initsev currsev healing lasttreat lastheal notes
&FUN_DMGSTAT FS3Combat Damage Commands=extract(xget(#731,%0),match(v(DMGstats),%1),1,|)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_VALID_DMGSTAT_xxx
@@  Tells whether a value is valid for the xxx damage statistic.
@@  %0 = The value
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_VALID_DMGSTAT_REAL FS3Combat Damage Commands=match(0 1,%0)
&FUN_VALID_DMGSTAT_HITLOC FS3Combat Damage Commands=1
&FUN_VALID_DMGSTAT_WEAPON FS3Combat Damage Commands=1
&FUN_VALID_DMGSTAT_TYPE FS3Combat Damage Commands=match(physical stun,%0)
&FUN_VALID_DMGSTAT_INITSEV FS3Combat Damage Commands=match(v(severities),%0)
&FUN_VALID_DMGSTAT_CURRSEV FS3Combat Damage Commands=match(v(severities),%0)
&FUN_VALID_DMGSTAT_HEALING FS3Combat Damage Commands=isnum(%0)
&FUN_VALID_DMGSTAT_LASTTREAT FS3Combat Damage Commands=isnum(%0)
&FUN_VALID_DMGSTAT_LASTHEAL FS3Combat Damage Commands=isnum(%0)
&FUN_VALID_DMGSTAT_NOTES FS3Combat Damage Commands=1

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_MOD_DMGSTAT
@@  Modifies a piece of damage info from a given damage attribute.  This is stored back 
@@  on the damage database.
@@  %0 = Damage attr name 
@@  %1 = The stat to set, from DMGSTATS.
@@  %2 = The new value.
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_MOD_DMGSTAT FS3Combat Damage Commands=set(#731,%0:[replace(xget(#731,%0),match(v(DMGstats),%1),%2,|)])

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_NAME_FROM_ATTR
@@  Gets the person/vehicle name from a damage attr.
@@  %0 = Damage attr name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_NAME_FROM_ATTR FS3Combat Damage Commands=unprettify(before(after(%0,_),_))


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_MINUTES_SINCE_INJURY
@@  How long it's been since the injury occurred.
@@  %0 = Damage attr name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_MINUTES_SINCE_INJURY FS3Combat Damage Commands=div(sub(secs(),last(%0,_)),60)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_CAN_BE_TREATED
@@  Whether the char/vehicle can be treated right now.  True if they haven't already been treated,
@@  and the damage is not more than an hour old.
@@  %0 = Damage attr name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_CAN_BE_TREATED FS3Combat Damage Commands=not(or(u(FUN_DMGSTAT,%0,lasttreat),gt(u(FUN_MINUTES_SINCE_INJURY,%0),60)))

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_CAN_BE_HEALED
@@  Whether the char/vehicle can be healed/repaired right now.  They can only be healed once
@@  per day, and obviously not if the damage is already fully healed.
@@  %0 = Damage attr name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_CAN_BE_HEALED FS3Combat Damage Commands=and(not(u(FUN_HEALED_TODAY,%0)),switch(u(FUN_DMGSTAT,%0,currsev),H,0,1))

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DISPLAY_TIME
@@  Shows a damage time, -- if not set.
@@  %0 = Seconds
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_DISPLAY_TIME FS3Combat Damage Commands=switch(%0,,--,convsecs(%0))

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_HEALED_TODAY
@@  Whether the char/vehicle was already healed today.
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_HEALED_TODAY FS3Combat Damage Commands=switch(sub(secs(),u(FUN_DMGSTAT,%0,lastheal)),>86400,0,1)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DMG_DETAIL
@@  Shows detail for a given damage element.
@@  %0 = Damage attr name.
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_DMG_DETAIL FS3Combat Damage Commands=%R[ansi(h,Wound)]: [u(fun_dmgstat,%0,weapon)] to [u(fun_dmgstat,%0,hitloc)] [switch(u(fun_dmgstat,%0,real),0,%R[ansi(hg,*** MOCK DAMAGE ***)] %R\(will be removed when combat ends\)%R)]  %R[ansi(h,Time of Injury)]: [convsecs(after(after(%0,_),_))]%R[ansi(h,Type)]: [u(fun_dmgstat,%0,type)] %R[ansi(h,Initial Severity)]: [u(fun_print_wound,u(fun_dmgstat,%0,initsev))] %R[ansi(h,Current Severity)]: [u(fun_print_wound,u(fun_dmgstat,%0,currsev))] %R[ansi(h,Healing Points)]: [u(fun_dmgstat,%0,healing)] %R[ansi(h,Last Treated)]: [u(fun_display_time,u(fun_dmgstat,%0,lasttreat))] %R[ansi(h,Last Healed)]: [u(fun_display_time,u(fun_dmgstat,%0,lastheal))] %R[ansi(h,Notes)]:%R[replace_cr(u(fun_dmgstat,%0,notes))]

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DMGLIST
@@  Lists damage summary for a char/vehicle.
@@  %0 = Name
@@
@@ %q0 = current severity, %q1 = time to heal
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_DMGLIST FS3Combat Damage Commands=[ansi(h,%b%b%b%b%bSeverity[space(41)]Can%b%bCan)]%r[ansi(h,[space(5)]\(Current\)[space(6)]Type[space(6)]Wound[space(18)]Treat Heal%B%BEst Heal)][iter(u(FUN_ALL_DMG,%0),[setq(0,u(FUN_DMGSTAT,##,currsev))[setq(1,u(FUN_TIME_TO_HEAL,##))]]%R[ljust(#@.,4)] [ljust(ansi(u(fun_wound_color,%q0),[u(fun_wound_name,%q0)]),14)] [ljust(u(FUN_DMGSTAT,##,type),10)][padstr([u(FUN_DMGSTAT,##,weapon)] to [u(FUN_DMGSTAT,##,hitloc)],25)][switch(u(FUN_CAN_BE_TREATED,##),1,+,-)]%B%B%B%B[switch(u(FUN_CAN_BE_HEALED,##),1,+,-)]%B%B%B%B[switch(%q1,0,-,%q1 days)])]

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DMGMOD_TOTAL
@@  Total damage modifier for a char/vehicle, counting all wounds.
@@  %0 = Name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_DMGMOD_TOTAL FS3Combat Damage Commands=localize([setq(0,0)][iter(u(FUN_ALL_DMG,%0),setq(0,add(%q0,u(FUN_DMGMOD_WOUND,##))),,)][min(%q0,95)])

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DMGMOD_WOUND
@@  Damage modifier for an individual wound.
@@  Modifier is 1/2 for wounds that have been treated/healed and another 1/2 for stun.
@@  This is cumulative.
@@  %0 = Damage attr
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&WOUND_DMGMOD FS3Combat Damage Commands=L:5 M:10 S:50 C:80
&FUN_DMGMOD_WOUND FS3Combat Damage Commands=localize([setq(0,after(grab(v(WOUND_DMGMOD),[u(FUN_DMGSTAT,%0,currsev)]:*),:))][switch(u(FUN_DMGSTAT,%0,healing),>0,setq(0,div(%q0,2)))][switch(u(FUN_DMGSTAT,%0,type),stun,setq(0,div(%q0,2)))]%q0)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_PRINT_WOUND
@@  FUN_WOUND_COLOR
@@  FUN_WOUND_NAME
@@  These functions are used to show a pretty printed version of a wound.
@@  %0 = Wound level (C/S/M/L)
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_PRINT_WOUND FS3Combat Damage Commands=ansi(u(FUN_WOUND_COLOR,%0),u(FUN_WOUND_NAME,%0))
&FUN_WOUND_COLOR FS3Combat Damage Commands=switch(%0,C,hR,S,hr,M,hy,L,hg,hx)
&FUN_WOUND_NAME FS3Combat Damage Commands=switch(%0,C,Critical,S,Serious,M,Moderate,L,Light,Healed)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_DO_DMG
@@  Inflicts damage upon a char/vehicle.
@@  %0 = Name 
@@  %1 = Real (1) or Mock (0) Damage
@@  %2 = Hit location
@@  %3 = Weapon
@@  %4 = Physical or Stun damage
@@  %5 = Severity (C/S/M/L), applied to both initial and current severity
@@  Healing points defaulted to 0, notes to empty, and last treated/healed to empty
@@ Because this is global, must check for staff.
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_DO_DMG FS3Combat Damage Commands=switch(0,isstaff(%!),ansi(hr,You aren't allowed to do that.),set(#731,DMG_[edit(%0,_,.,%B,.)]_[secs()]:%1|%2|%3|%4|%5|%5|0|||))

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_HEALPOINTS_WOUND
@@  How many healing points are required to heal a particular wound down to the next
@@  lower level.  
@@  %0 = Damage attr name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&HEALING_POINTS FS3Combat Damage Commands=C:15 S:10 M:10 L:5 H:0
&FUN_HEALPOINTS_WOUND FS3Combat Damage Commands=u(fun_healpoints_sev,u(fun_name_from_attr,%0),[u(fun_dmgstat,%0,currsev)],u(fun_dmgstat,%0,type))

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_HEALPOINTS_TOTAL
@@  How many healing points are required to heal a particular wound down to 0.
@@  lower level.  
@@  %0 = Damage attr name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_HEALPOINTS_TOTAL FS3Combat Damage Commands=localize([setq(0,0)][iter(switch([u(fun_dmgstat,%0,currsev)],C,C S M L,S,S M L,M,M L,L,L,0),setq(0,add(%q0,u(fun_healpoints_sev,u(FUN_NAME_FROM_ATTR,%0),##,u(fun_dmgstat,%0,type)))),,)]%q0)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ FUN_HEALPOINTS_SEV
@@ How many healing points are required to heal a wound of a given severity/type.
@@ %0 = name
@@ %1 = severity
@@ %2 = type (stun or physical)
@@  Note that stun wounds heal 5x faster and vehicles heal 5x faster.
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_HEALPOINTS_SEV FS3Combat Damage Commands=div(after(grab(v(healing_points),%1:*),:),switch(1,isvehicle(%0),5,strmatch(%2,Stun),5,1))

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ FUN_IS_RESTRICTED
@@ Determine whether someone is hospitalized (2), restricted duty (1) or OK (0).
@@ Thresholds are somewhat arbitrary.  Keeping in mind that L5,M10,S50,C80 but halved 
@@ for wounds that are healed, they will be restricted if they have anything at mod or
@@ higher (or enough light added up), and hospitalized if they have anything serious or 
@@ higher (or enough mods added up).
@@ %0 = name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_IS_RESTRICTED FS3Combat Damage Commands=switch(damagemod(%0),>24,2,>4,1,0)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@ FUN_DISPLAY_RESTRICTED
@@ Displays an appropriate message based on someone's restricted status.
@@ %0 = name
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

&FUN_DISPLAY_RESTRICTED FS3Combat Damage Commands=switch(u(FUN_IS_RESTRICTED,%0),0,[ansi(hg,<FIT FOR DUTY>)],1,[ansi(hm,switch(isvehicle(%0),0,<LIGHT DUTY>,<DOWN FOR REPAIRS>))],2,[ansi(hr,switch(isvehicle(%0),0,<HOSPITALIZED>,<DOWN FOR REPAIRS>))])


@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_CHECK_KO
@@  Determines if someone should be ko'd by rolling a random # and comparing to their wound
@@  level.  They get a bonus/penalty based on their 'toughness' stat.  PCs get an additional
@@  bonus based on the Hero Factor.  (Note this does not apply to vehicles piloted by PCs.)
@@ 
@@  They must roll OVER the specified target number, which will never be lower than 2 
@@  or higher than 95.  Thus there's always a chance of getting KO'd or staying OK no
@@  matter how hurt you are.
@@
@@  Returns 1 if they FAIL their KO check.
@@  %0 = name
@@
@@ %q0=damagemod    %q1=ability level  %q2=random roll  %q3=hero  %q4=ability mod
@@ %q5=roll target  %q6=result
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_CHECK_KO FS3Combat Damage Commands=localize([setq(0,damagemod(%0))][setq(1,switch(1,isnpc(%0),npcstat(%0,skill),isvehicle(%0),vehtypestat(vehiclestat(%0,type),toughness),isplayer(%0),ability_level(pmatch(%0),xget(#731,toughness_skill)),5))][setq(2,rand(100))][setq(3,switch(isplayer(%0),1,herofactor(),0))][setq(4,mul(sub(%q1,5),5))][setq(5,max(min(sub(sub(%q0,%q4),%q3),95),2))][setq(6,switch(1,eq(%q0,0),0,lte(%q2,%q5),1,0))][pemit(*faraday,%0 checking KO : dmgmod=%q0 ability=%q1 abilmodifier=%q4 randroll=%q2 hero=%q3 rolltarget=%q5%B%B%q6)]%q6)

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@  FUN_TIME_TO_HEAL
@@  Calculates estimated time to heal, cumulative for the current level + all under.
@@  %0 = damage attr
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
&FUN_TIME_TO_HEAL FS3Combat Damage Commands=max(round(sub(u(FUN_HEALPOINTS_TOTAL,%0),u(fun_dmgstat,%0,healing)),0),0)

@STARTUP FS3Combat Damage Commands=@set me=!no_command;@fun damagemod=me,fun_dmgmod_total;@fun dodamage=me,fun_do_dmg;@fun clearmockdmg=me,fun_clear_mock;@fun checkko=me,fun_check_ko;@fun dmgrestricted=me,fun_display_restricted;@fun cleardmg=me,fun_clear_damage


&CRON_TIME_DAILYHEAL FS3Combat Damage Commands=|||23|57|
&CRON_JOB_DAILYHEAL FS3Combat Damage Commands=@tr #730/tr_daily_healing

@create FS3Combat Damage Database
@set FS3Combat Damage Database = NO_COMMAND

&TREAT_SKILL FS3Combat Damage Database=First_Aid
&HEAL_SKILL FS3Combat Damage Database=Medicine
&REPAIR_SKILL FS3Combat Damage Database=Repair
&TOUGHNESS_SKILL FS3Combat Damage Database=Athletic
&HOSPITALS FS3Combat Damage Database=#-1
